USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MM_STOCK_PO_RPT_S2]    Script Date: 2019-03-26 오전 11:13:37 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [NEOE].[SP_CZ_MM_STOCK_PO_RPT_S2]
(
    @P_CD_COMPANY   NVARCHAR(7),
	@P_QT_VESSEL	INT,
	@P_NM_MODEL		NVARCHAR(100)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DECLARE @V_QT_VESSEL INT

PRINT ('임시테이블 생성')
CREATE TABLE #빈도분석
(
	CD_SPEC NVARCHAR(20),
	QT_ITEM NUMERIC(17, 0)
)

CREATE NONCLUSTERED INDEX UCODE ON #빈도분석 (CD_SPEC)

CREATE TABLE #대상도면번호
(
	EZCODE	   NVARCHAR(20)
)

CREATE NONCLUSTERED INDEX 도면번호 ON #대상도면번호 (EZCODE)

CREATE TABLE #사용도면번호
(
	CLS_L		   NVARCHAR(4),
	CLS_M		   NVARCHAR(4),
	CLS_S		   NVARCHAR(4),
	EZCODE	   NVARCHAR(20)
)

CREATE NONCLUSTERED INDEX 사용도면번호 ON #사용도면번호 (CLS_L, CLS_M, CLS_S, EZCODE)

CREATE TABLE #엔진유형
(
	CLS_L		NVARCHAR(4),
	CLS_M		NVARCHAR(4),
	CLS_S		NVARCHAR(4),
	CLS_S1		NVARCHAR(4),
	NO_IMO		NVARCHAR(10),
	NO_ENGINE	INT
)

CREATE NONCLUSTERED INDEX 엔진유형 ON #엔진유형 (CLS_L, CLS_S, NO_IMO, NO_ENGINE)

CREATE TABLE #공장품목
(
	CD_ITEM			 NVARCHAR(20),
	STND_DETAIL_ITEM NVARCHAR(200),
	UCODE2			 NVARCHAR(500),
	NO_STND			 NVARCHAR(20)
)

CREATE NONCLUSTERED INDEX 공장품목 ON #공장품목 (CD_ITEM, STND_DETAIL_ITEM)
CREATE NONCLUSTERED INDEX 공장품목2 ON #공장품목 (CD_ITEM, NO_STND)

CREATE TABLE #대치자재
(
	NO_PRIME			 NVARCHAR(20),
	CD_ITEM_PRIME		 NVARCHAR(20),
	NO_MATERIAL			 NVARCHAR(20),
	CD_ITEM_MATERIAL	 NVARCHAR(20),
	NO_ALTERNATIVE		 NVARCHAR(20),
	CD_ITEM_ALTERNATIVE	 NVARCHAR(20)
)

CREATE NONCLUSTERED INDEX 대치자재 ON #대치자재 (NO_PRIME, NO_MATERIAL, NO_ALTERNATIVE)

CREATE TABLE #기부속정보
(
	NO_IMO		NVARCHAR(10),
	NO_ENGINE	INT,
	CLS_L		NVARCHAR(4),
	CLS_M		NVARCHAR(4),
	CLS_S		NVARCHAR(4),
	CLS_S1		NVARCHAR(4),
	UCODE		NVARCHAR(20), 
	EZCODE		NVARCHAR(20),
	NO_PLATE	NVARCHAR(100),
	NM_PLATE	NVARCHAR(200)
)

CREATE NONCLUSTERED INDEX 기부속정보1 ON #기부속정보 (CLS_L, CLS_S, UCODE)
CREATE NONCLUSTERED INDEX 기부속정보2 ON #기부속정보 (CLS_L, CLS_S, EZCODE)

CREATE TABLE #대상품목
(
	CLS_L		    NVARCHAR(4),
	CLS_M			NVARCHAR(4),
	CLS_S			NVARCHAR(4),
	CLS_S1			NVARCHAR(4),
	NO_IMO			NVARCHAR(10),
	NO_ENGINE		INT,
	NO_PLATE		NVARCHAR(100),
	NM_PLATE		NVARCHAR(200),
	CD_SPEC			NVARCHAR(20),
	EZCODE			NVARCHAR(20),
	CD_ITEM			NVARCHAR(20),
	QT_QTN			NUMERIC(15, 4),
	QT_SO			NUMERIC(15, 4),
	UM_KR_P_AVG		NUMERIC(17, 4)
)

CREATE TABLE #소분류
(
	CD_SYSDEF NVARCHAR(4),
	CD_SYSDEF1 NVARCHAR(4)
)

CREATE NONCLUSTERED INDEX 소분류 ON #소분류 (CD_SYSDEF, CD_SYSDEF1)

PRINT ('빈도분석 유코드 기준으로 5회 이상')
INSERT INTO #빈도분석
SELECT QL.CD_SPEC,
	   COUNT(1) AS QT_QTN
FROM CZ_SA_QTNH QH
JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = QH.CD_COMPANY AND QL.NO_FILE = QH.NO_FILE
WHERE QH.CD_COMPANY = @P_CD_COMPANY
AND QH.DT_INQ BETWEEN CONVERT(CHAR(8), DATEADD(YEAR, -1, GETDATE()), 112) AND CONVERT(CHAR(8), GETDATE(), 112)
AND EXISTS (SELECT 1 
	        FROM MA_CODEDTL MC
		    WHERE MC.CD_COMPANY = QH.CD_COMPANY
		    AND MC.CD_FIELD = 'CZ_SA00023'
		    AND MC.CD_SYSDEF NOT IN ('00', 'HB', 'ZZ')
			AND QH.NO_FILE LIKE MC.CD_SYSDEF +  '%')
AND QL.CD_SPEC IS NOT NULL
AND QL.CD_SPEC <> ''
GROUP BY QL.CD_SPEC
HAVING COUNT(1) >= 5

PRINT ('유코드 기준으로 도면번호 검색')
INSERT INTO #대상도면번호
SELECT A.EZCODE
FROM (SELECT ROW_NUMBER() OVER (PARTITION BY A.CD_SPEC ORDER BY B.CNT DESC) AS IDX,
	  	     A.CD_SPEC,
	  	     B.EZCODE
	  FROM #빈도분석 A
	  JOIN (SELECT QL.CD_SPEC, EI.KCODE AS EZCODE,
	  			   COUNT(1) AS CNT 
	  		FROM CZ_SA_QTNH QH
	  		JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = QH.CD_COMPANY AND QL.NO_FILE = QH.NO_FILE
	  		JOIN CZ_MA_HULL_ENGINE_ITEM EI ON EI.NO_IMO = QH.NO_IMO AND EI.NO_ENGINE = QL.NO_ENGINE AND EI.NO_PLATE = QL.CD_ITEM_PARTNER
	  		WHERE QH.CD_COMPANY = @P_CD_COMPANY
	  		AND QL.NO_ENGINE IS NOT NULL -- 현대엑셀 업로드 항목
	  		AND EI.KCODE IS NOT NULL -- 도면번호 있는 항목
	  		GROUP BY QL.CD_SPEC, EI.KCODE) B
	  ON B.CD_SPEC = A.CD_SPEC) A
WHERE A.IDX = 1

PRINT ('메인엔진 통합')
INSERT INTO #소분류
SELECT MC.CD_SYSDEF,
	   'M01' AS CD_SYSDEF1
FROM MA_CODEDTL MC
WHERE MC.CD_COMPANY = 'K100'
AND MC.CD_FIELD = 'MA_B000032' 
AND MC.CD_FLAG1 = 'HME'
AND MC.NM_SYSDEF <> 'ETC'

PRINT ('도면번호 기준으로 호선선정 1')
INSERT INTO #엔진유형
SELECT A.CLS_L, 
	   A.CLS_M,
	   A.CLS_S,
	   A.CLS_S1,
	   A.NO_IMO,
	   A.NO_ENGINE
FROM (SELECT HE.CLS_L, 
			 HE.CLS_M,
			 HE.CLS_S,
			 HE.CLS_S1, 
			 HE.NO_IMO, 
			 HE.NO_ENGINE,
	  	     ROW_NUMBER() OVER(PARTITION BY HE.CLS_L, HE.CLS_M, HE.CLS_S ORDER BY EI.CNT DESC) AS IDX
	  FROM (SELECT HE.CLS_L,
				   HE.CLS_M,
				   ISNULL(CD.CD_SYSDEF1, HE.CLS_S) AS CLS_S,
				   HE.CLS_S AS CLS_S1,
				   HE.NO_IMO,
				   HE.NO_ENGINE
		    FROM CZ_MA_HULL_ENGINE HE
			LEFT JOIN #소분류 AS CD ON CD.CD_SYSDEF = HE.CLS_S
			WHERE HE.CLS_M LIKE 'H%' 
			AND (ISNULL(@P_NM_MODEL, '') = '' OR HE.NM_MODEL LIKE '%' + @P_NM_MODEL + '%')) HE
	  JOIN (SELECT EI.NO_IMO, EI.NO_ENGINE,
				   COUNT(1) AS CNT 
			FROM CZ_MA_HULL_ENGINE_ITEM EI
			WHERE EI.KCODE IN (SELECT EZCODE FROM #대상도면번호)
			GROUP BY EI.NO_IMO, EI.NO_ENGINE) EI 
	  ON EI.NO_IMO = HE.NO_IMO AND EI.NO_ENGINE = HE.NO_ENGINE) A
WHERE A.IDX = 1

INSERT INTO #사용도면번호
SELECT A.CLS_L, 
	   A.CLS_M,
	   A.CLS_S,
	   B.KCODE AS EZCODE 
FROM #엔진유형 A
JOIN CZ_MA_HULL_ENGINE_ITEM B ON B.NO_IMO = A.NO_IMO AND B.NO_ENGINE = A.NO_ENGINE
WHERE B.KCODE IN (SELECT EZCODE FROM #대상도면번호)

SET @V_QT_VESSEL = 2

WHILE @V_QT_VESSEL <= @P_QT_VESSEL
BEGIN
	PRINT ('도면번호 기준으로 호선선정 ' + CONVERT(CHAR, @V_QT_VESSEL))
	INSERT INTO #엔진유형
	SELECT A.CLS_L,
		   A.CLS_M, 
		   A.CLS_S,
		   A.CLS_S1,
		   A.NO_IMO,
		   A.NO_ENGINE
	FROM (SELECT HE.CLS_L,
				 HE.CLS_M, 
				 HE.CLS_S,
				 MAX(HE.CLS_S1) AS CLS_S1, 
				 HE.NO_IMO, 
				 HE.NO_ENGINE,
		  	     ROW_NUMBER() OVER(PARTITION BY HE.CLS_L, HE.CLS_M, HE.CLS_S ORDER BY COUNT(1) DESC) AS IDX
		  FROM (SELECT HE.CLS_L,
					   HE.CLS_M,
					   ISNULL(CD.CD_SYSDEF1, HE.CLS_S) AS CLS_S,
					   HE.CLS_S AS CLS_S1,
					   HE.NO_IMO,
					   HE.NO_ENGINE 
			    FROM CZ_MA_HULL_ENGINE HE
				LEFT JOIN #소분류 AS CD ON CD.CD_SYSDEF = HE.CLS_S
				WHERE HE.CLS_M LIKE 'H%'
				AND (ISNULL(@P_NM_MODEL, '') = '' OR HE.NM_MODEL LIKE '%' + @P_NM_MODEL + '%')) HE
		  JOIN CZ_MA_HULL_ENGINE_ITEM EI ON EI.NO_IMO = HE.NO_IMO AND EI.NO_ENGINE = HE.NO_ENGINE
		  WHERE EXISTS (SELECT 1 
						FROM #대상도면번호 A
						WHERE A.EZCODE = EI.KCODE)
		  AND NOT EXISTS (SELECT 1 
						  FROM #사용도면번호 A
						  WHERE A.CLS_L = HE.CLS_L
						  AND A.CLS_M = HE.CLS_M
						  AND A.CLS_S = HE.CLS_S
						  AND A.EZCODE = EI.KCODE)
		  GROUP BY HE.CLS_L, HE.CLS_M, HE.CLS_S, HE.NO_IMO, HE.NO_ENGINE) A
	WHERE A.IDX = 1
	
	IF @V_QT_VESSEL < @P_QT_VESSEL
	BEGIN
		DELETE FROM #사용도면번호
	
	    INSERT INTO #사용도면번호
	    SELECT A.CLS_L, 
	    	   A.CLS_M,
	    	   A.CLS_S,
	    	   B.KCODE AS EZCODE 
	    FROM #엔진유형 A
	    JOIN CZ_MA_HULL_ENGINE_ITEM B ON B.NO_IMO = A.NO_IMO AND B.NO_ENGINE = A.NO_ENGINE
	    WHERE B.KCODE IN (SELECT EZCODE FROM #대상도면번호)	
	END
	
	SET @V_QT_VESSEL = @V_QT_VESSEL + 1
END

PRINT ('공장품목 데이터 조회')
INSERT INTO #공장품목
SELECT MI.CD_ITEM,
	   MI.STND_DETAIL_ITEM,
	   MI.UCODE2,
	   MI.NO_STND
FROM MA_PITEM MI
WHERE MI.CD_COMPANY = @P_CD_COMPANY
AND MI.YN_USE = 'Y'
AND MI.CLS_ITEM = '009'
AND (MI.STND_DETAIL_ITEM IS NOT NULL OR MI.UCODE2 IS NOT NULL OR MI.NO_STND IS NOT NULL)
AND (MI.CD_ITEM LIKE 'B%' OR MI.CD_ITEM LIKE 'H%' OR MI.CD_ITEM LIKE 'SA%')

PRINT ('대치자재 데이터 조회')
INSERT INTO #대치자재
SELECT AI.NO_PRIME,
	   AI1.CD_ITEM AS CD_ITEM_PRIME,
	   AI.NO_MATERIAL,
	   MI.CD_ITEM AS CD_ITEM_MATERIAL,
	   AI.NO_ALTERNATIVE,
	   MI1.CD_ITEM AS CD_ITEM_ALTERNATIVE
FROM CZ_MA_ALTERNATIVE_ITEM AI
LEFT JOIN (SELECT AI.NO_PRIME,
				  MAX(MI.CD_ITEM) AS CD_ITEM 
		   FROM CZ_MA_ALTERNATIVE_ITEM AI
		   LEFT JOIN (SELECT NO_STND,
					  		 MAX(CD_ITEM) AS CD_ITEM 
					  FROM #공장품목
					  GROUP BY NO_STND) MI 
		   ON MI.NO_STND = AI.NO_MATERIAL
		   GROUP BY AI.NO_PRIME) AI1
ON AI1.NO_PRIME = AI.NO_PRIME
LEFT JOIN (SELECT NO_STND,
		   		  MAX(CD_ITEM) AS CD_ITEM 
		   FROM #공장품목
		   GROUP BY NO_STND) MI
ON MI.NO_STND = AI.NO_MATERIAL
LEFT JOIN (SELECT NO_STND,
		   		  MAX(CD_ITEM) AS CD_ITEM 
		   FROM #공장품목
		   GROUP BY NO_STND) MI1 
ON MI1.NO_STND = AI.NO_ALTERNATIVE
WHERE (AI1.CD_ITEM IS NOT NULL OR MI.CD_ITEM IS NOT NULL OR MI1.CD_ITEM IS NOT NULL)

PRINT ('기부속 정보 조회')
INSERT INTO #기부속정보
SELECT A.NO_IMO,
	   A.NO_ENGINE,
	   A.CLS_L,
	   A.CLS_M,
	   A.CLS_S,
	   A.CLS_S1,
	   EI.UCODE, 
	   EI.KCODE AS EZCODE,
	   EI.NO_PLATE,
	   EI.NM_PLATE
FROM #엔진유형 A
JOIN CZ_MA_HULL_ENGINE_ITEM EI ON EI.NO_IMO = A.NO_IMO AND EI.NO_ENGINE = A.NO_ENGINE
WHERE ((EI.UCODE IS NOT NULL AND EI.UCODE <> '') OR 
	   (EI.KCODE IS NOT NULL AND EI.KCODE <> ''))

PRINT ('빈도분석 5회 이상');
WITH A1 AS
(
	SELECT * 
	FROM (SELECT CLS_L,
				 CLS_M,
				 CLS_S,
				 CLS_S1,
				 UCODE,
		  	     NO_IMO,
		  	     NO_ENGINE,
		  	     NO_PLATE,
		  	     NM_PLATE,
		  	     ROW_NUMBER() OVER (PARTITION BY CLS_L, CLS_M, CLS_S, UCODE ORDER BY NO_IMO) AS IDX
		  FROM #기부속정보
		  WHERE (UCODE IS NOT NULL AND UCODE <> '')) A
	WHERE A.IDX = 1
),
A2 AS
(
	SELECT * 
	FROM (SELECT CLS_L, 
				 CLS_M,
				 CLS_S,
				 CLS_S1,
				 EZCODE,
		  	     NO_IMO,
		  	     NO_ENGINE,
		  	     NO_PLATE,
		  	     NM_PLATE,
		  	     ROW_NUMBER() OVER (PARTITION BY CLS_L, CLS_M, CLS_S, EZCODE ORDER BY NO_IMO) AS IDX
		  FROM #기부속정보
		  WHERE (EZCODE IS NOT NULL AND EZCODE <> '')) A
	WHERE A.IDX = 1
)
INSERT INTO #대상품목
SELECT A1.CLS_L,
	   A1.CLS_M,
	   A1.CLS_S,
	   A1.CLS_S1,
	   A1.NO_IMO,
	   A1.NO_ENGINE,
	   A1.NO_PLATE,
	   A1.NM_PLATE,
	   B.CD_SPEC,
	   NULL AS EZCODE,
	   NULL AS CD_ITEM,
	   B.QT_QTN,
	   B.QT_SO,
	   B.UM_KR_P_AVG 
FROM A1
JOIN (SELECT QL.CD_SPEC,
      	     COUNT(1) AS QT_QTN,
			 SUM(SL.QT_SO) AS QT_SO,
			 AVG(QL.UM_KR_P) AS UM_KR_P_AVG
      FROM CZ_SA_QTNH QH
      JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = QH.CD_COMPANY AND QL.NO_FILE = QH.NO_FILE
	  LEFT JOIN SA_SOL SL ON SL.CD_COMPANY = QL.CD_COMPANY AND SL.NO_SO = QL.NO_FILE AND SL.SEQ_SO = QL.NO_LINE
      WHERE QH.CD_COMPANY = @P_CD_COMPANY
      AND QH.DT_INQ BETWEEN CONVERT(CHAR(8), DATEADD(YEAR, -1, GETDATE()), 112) AND CONVERT(CHAR(8), GETDATE(), 112)
      AND QL.CD_SPEC IS NOT NULL
      AND QL.CD_SPEC <> ''
      AND EXISTS (SELECT 1 
                  FROM MA_CODEDTL MC
	              WHERE MC.CD_COMPANY = QH.CD_COMPANY
	              AND MC.CD_FIELD = 'CZ_SA00023'
	              AND MC.CD_SYSDEF NOT IN ('00', 'HB', 'ZZ')
		          AND QH.NO_FILE LIKE MC.CD_SYSDEF +  '%')
      GROUP BY QL.CD_SPEC) B
ON B.CD_SPEC = A1.UCODE 
WHERE B.QT_QTN >= 5
UNION ALL
SELECT A2.CLS_L,
	   A2.CLS_M,
	   A2.CLS_S,
	   A2.CLS_S1,
	   A2.NO_IMO,
	   A2.NO_ENGINE,
	   A2.NO_PLATE,
	   A2.NM_PLATE,
	   (SELECT TOP 1 A.UCODE
	    FROM #기부속정보 AS A
		WHERE A.CLS_L = A2.CLS_L
		AND A.CLS_M = A2.CLS_M
		AND A.CLS_S = A2.CLS_S 
		AND A.EZCODE = A2.EZCODE) AS CD_SPEC,
	   B.NO_DRAWING AS EZCODE,
	   NULL AS CD_ITEM,
	   B.QT_QTN,
	   B.QT_SO,
	   B.UM_KR_P_AVG 
FROM A2
JOIN (SELECT QL.NO_DRAWING,
      	     COUNT(1) AS QT_QTN,
			 SUM(SL.QT_SO) AS QT_SO,
			 AVG(QL.UM_KR_P) AS UM_KR_P_AVG
      FROM CZ_SA_QTNH QH
      JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = QH.CD_COMPANY AND QL.NO_FILE = QH.NO_FILE
	  LEFT JOIN SA_SOL SL ON SL.CD_COMPANY = QL.CD_COMPANY AND SL.NO_SO = QL.NO_FILE AND SL.SEQ_SO = QL.NO_LINE
      WHERE QH.CD_COMPANY = N'K100'
      AND QH.DT_INQ BETWEEN CONVERT(CHAR(8), DATEADD(YEAR, -1, GETDATE()), 112) AND CONVERT(CHAR(8), GETDATE(), 112)
      AND QL.NO_DRAWING IS NOT NULL
      AND QL.NO_DRAWING <> ''
      AND EXISTS (SELECT 1 
                  FROM MA_CODEDTL MC
	              WHERE MC.CD_COMPANY = QH.CD_COMPANY
	              AND MC.CD_FIELD = 'CZ_SA00023'
	              AND MC.CD_SYSDEF NOT IN ('00', 'HB', 'ZZ')
		          AND QH.NO_FILE LIKE MC.CD_SYSDEF +  '%')
      GROUP BY QL.NO_DRAWING) B
ON B.NO_DRAWING = A2.EZCODE
AND B.QT_QTN >= 5

PRINT ('유코드, 도면번호 둘다 없는 건 제거')
DELETE B
FROM #대상품목 B
WHERE (B.CD_SPEC IS NULL OR B.CD_SPEC = '')
AND (B.EZCODE IS NULL OR B.EZCODE = '')

PRINT ('유코드 기준으로 중복 제거')
DELETE B 
FROM (SELECT *,
			 ROW_NUMBER() OVER (PARTITION BY B.CLS_L, B.CLS_M, B.CLS_S, (CASE WHEN (B.CD_SPEC IS NULL OR B.CD_SPEC = '') THEN B.EZCODE ELSE B.CD_SPEC END) ORDER BY B.QT_QTN DESC) AS IDX  
	  FROM #대상품목 AS B) AS B
WHERE B.IDX <> 1

PRINT ('유코드 기준으로 도면번호 업데이트')
UPDATE B
SET B.EZCODE = (SELECT TOP 1 EZCODE 
					FROM #기부속정보 AS A
					WHERE A.CLS_L = B.CLS_L
					AND A.CLS_M = B.CLS_M
					AND A.CLS_S = B.CLS_S 
					AND A.UCODE = B.CD_SPEC)
FROM #대상품목 AS B
WHERE B.EZCODE IS NULL

PRINT ('셋트품 제외')
DELETE B 
FROM #대상품목 AS B
WHERE B.EZCODE IN ('KB9105615100',
					   'KB9001220591',
					   'KB1919454920',
					   'KB9902456060',
					   'KA1016903086',
					   'KB9900176462',
					   'KB9900184023',
					   'KB9902020543',
					   'KY1000286488',
					   'KY1000286490',
					   'KB1011745793',
					   'KA1143365863',
					   'KA1118864143',
					   'KA1018864023',
					   'KB9902116681',
					   'KB9400421351',
					   'KY1000083565',
					   'KY1000083568',
					   'KY1000083579',
					   'KB9900277940',
					   'KB1412191940',
					   'KB1310066380',
					   'KB1412547750',
					   'KY1000286501',
					   'KY1000286509',
					   'KB9909288870',
					   'KYB000000002',
					   'KB1900145594',
					   'KA1148382570',
					   'KB9902458810')

PRINT ('도면번호 기준으로 중복 제거')
DELETE B 
FROM (SELECT *,
		     ROW_NUMBER() OVER (PARTITION BY B.CLS_L, B.CLS_M, B.CLS_S, B.EZCODE ORDER BY B.QT_QTN DESC) AS IDX  
	  FROM #대상품목 AS B) AS B
WHERE B.EZCODE IS NOT NULL 
AND B.IDX <> 1

PRINT ('재고코드 업데이트 : U코드')
UPDATE B
SET B.CD_ITEM = (SELECT TOP 1 CD_ITEM 
				 FROM #공장품목 MI 
				 WHERE MI.STND_DETAIL_ITEM = B.CD_SPEC)
FROM #대상품목 AS B
WHERE B.CD_SPEC IS NOT NULL
AND B.CD_SPEC <> ''
AND B.CD_ITEM IS NULL

PRINT ('재고코드 업데이트 : U코드2')
UPDATE B
SET B.CD_ITEM = (SELECT TOP 1 CD_ITEM 
				 FROM #공장품목 MI 
				 WHERE MI.UCODE2 = B.CD_SPEC)
FROM #대상품목 AS B
WHERE B.CD_SPEC IS NOT NULL
AND B.CD_SPEC <> ''
AND B.CD_ITEM IS NULL

PRINT ('재고코드 업데이트 : 도면번호')
UPDATE B
SET B.CD_ITEM = (SELECT TOP 1 CD_ITEM 
				 FROM #공장품목 MI 
				 WHERE MI.NO_STND = B.EZCODE)
FROM #대상품목 AS B
WHERE B.EZCODE IS NOT NULL
AND B.EZCODE <> ''
AND B.CD_ITEM IS NULL

PRINT ('재고코드 업데이트 : 대치자재1')
UPDATE B
SET B.CD_ITEM = (SELECT TOP 1 (CASE WHEN CD_ITEM_PRIME IS NOT NULL THEN CD_ITEM_PRIME
				 				    WHEN CD_ITEM_MATERIAL IS NOT NULL THEN CD_ITEM_MATERIAL
				 				    ELSE CD_ITEM_ALTERNATIVE END) 
				 FROM #대치자재 AI 
				 WHERE AI.NO_PRIME = B.EZCODE)
FROM #대상품목 AS B
WHERE B.EZCODE IS NOT NULL
AND B.EZCODE <> ''
AND B.CD_ITEM IS NULL

PRINT ('재고코드 업데이트 : 대치자재2')
UPDATE B
SET B.CD_ITEM = (SELECT TOP 1 (CASE WHEN CD_ITEM_PRIME IS NOT NULL THEN CD_ITEM_PRIME
				 				    WHEN CD_ITEM_MATERIAL IS NOT NULL THEN CD_ITEM_MATERIAL
				 				    ELSE CD_ITEM_ALTERNATIVE END) 
				 FROM #대치자재 AI 
				 WHERE AI.NO_MATERIAL = B.EZCODE)
FROM #대상품목 AS B
WHERE B.EZCODE IS NOT NULL
AND B.EZCODE <> ''
AND B.CD_ITEM IS NULL

PRINT ('재고코드 업데이트 : 대치자재3')
UPDATE B
SET B.CD_ITEM = (SELECT TOP 1 (CASE WHEN CD_ITEM_PRIME IS NOT NULL THEN CD_ITEM_PRIME
				 				    WHEN CD_ITEM_MATERIAL IS NOT NULL THEN CD_ITEM_MATERIAL
				 				    ELSE CD_ITEM_ALTERNATIVE END) 
				 FROM #대치자재 AI 
				 WHERE AI.NO_ALTERNATIVE = B.EZCODE)
FROM #대상품목 AS B
WHERE B.EZCODE IS NOT NULL
AND B.EZCODE <> ''
AND B.CD_ITEM IS NULL

PRINT ('재고수량 산정 및 마무리');
WITH C AS
(
	SELECT B.NO_IMO,
		   B.NO_ENGINE,
		   B.CLS_L,
		   B.CLS_M,
		   B.CLS_S,
		   B.CLS_S1,
		   B.CD_SPEC,
		   B.EZCODE,
		   B.NO_PLATE,
		   B.NM_PLATE,
		   B.CD_ITEM,
		   B.QT_QTN,
		   B.QT_SO,
		   B.UM_KR_P_AVG, 
		   (ISNULL(ML.QT_OPEN, 0) + ISNULL(OM.QT_MONTH, 0)) AS QT_INV,
		   PL.QT_NOT_IN,
		   SB.QT_BOOK,
		   SB.QT_HOLD
	FROM #대상품목 AS B
	LEFT JOIN (SELECT OL.CD_COMPANY, OL.CD_ITEM,
		  	   	      SUM(ISNULL(OL.QT_GOOD_INV, 0) + ISNULL(OL.QT_REJECT_INV, 0) + ISNULL(OL.QT_INSP_INV, 0) + ISNULL(OL.QT_TRANS_INV, 0)) AS QT_OPEN
		  	   FROM MM_OPENQTL OL
		  	   WHERE OL.YM_STANDARD = CONVERT(NVARCHAR, YEAR(GETDATE())) + '00'
			   AND OL.CD_SL = 'SL02'
		  	   GROUP BY OL.CD_COMPANY, OL.CD_ITEM) ML
	ON ML.CD_COMPANY = @P_CD_COMPANY AND ML.CD_ITEM = B.CD_ITEM
	LEFT JOIN (SELECT CD_COMPANY, CD_ITEM,
			   		  SUM(ISNULL(QT_GOOD_GR, 0) - ISNULL(QT_GOOD_GI, 0)) AS QT_MONTH 
			   FROM MM_OHSLINVM
			   WHERE CD_SL = 'SL02'
			   AND YY_INV = YEAR(GETDATE())
			   GROUP BY CD_COMPANY, CD_ITEM) OM
	ON OM.CD_COMPANY = @P_CD_COMPANY AND OM.CD_ITEM = B.CD_ITEM
	LEFT JOIN (SELECT PL.CD_COMPANY, PL.CD_ITEM,
					  SUM(PL.QT_PO - ISNULL(PL.QT_RCV, 0)) AS QT_NOT_IN 
			   FROM PU_POL PL
			   WHERE PL.FG_RCV IN ('160', '170', '180', '190') 
			   AND PL.QT_PO > PL.QT_RCV
			   GROUP BY PL.CD_COMPANY, PL.CD_ITEM) PL
	ON PL.CD_COMPANY = @P_CD_COMPANY AND PL.CD_ITEM = B.CD_ITEM
	LEFT JOIN (SELECT SB.CD_COMPANY, SB.CD_ITEM,
					  SUM(CASE WHEN SB.QT_BOOK > ISNULL(GL.QT_GIR_STOCK_ALL, 0) THEN SB.QT_BOOK - (ISNULL(GL.QT_GIR_STOCK_OLD, 0) + ISNULL(QL.QT_GI, 0)) ELSE 0 END) AS QT_BOOK,
					  SUM(SB.QT_HOLD) AS QT_HOLD
			   FROM CZ_SA_STOCK_BOOK SB
			   LEFT JOIN (SELECT GL.CD_COMPANY, GL.NO_SO, GL.SEQ_SO,
								 SUM(GL.QT_GIR_STOCK) AS QT_GIR_STOCK_ALL,
								 SUM(CASE WHEN GL.YN_GI_STOCK = 'Y' THEN GL.QT_GIR_STOCK ELSE 0 END) AS QT_GIR_STOCK_OLD 
		                  FROM SA_GIRL GL
		                  WHERE GL.QT_GIR_STOCK > 0
						  AND GL.QT_GI > 0
		                  GROUP BY GL.CD_COMPANY, GL.NO_SO, GL.SEQ_SO) GL
			   ON GL.CD_COMPANY = SB.CD_COMPANY AND GL.NO_SO = SB.NO_FILE AND GL.SEQ_SO = SB.NO_LINE
			   LEFT JOIN (SELECT CD_COMPANY, NO_SO, SEQ_SO, 
								 SUM(QT_GI) AS QT_GI
						  FROM MM_GIREQL
						  WHERE CD_COMPANY = @P_CD_COMPANY
						  AND CD_SL = 'SL02'
						  AND CD_GRSL = 'SL03'
						  AND QT_GI > 0
						  GROUP BY CD_COMPANY, NO_SO, SEQ_SO) QL
			   ON QL.CD_COMPANY = SB.CD_COMPANY AND QL.NO_SO = SB.NO_FILE AND QL.SEQ_SO = SB.NO_LINE
			   GROUP BY SB.CD_COMPANY, SB.CD_ITEM) SB
	ON SB.CD_COMPANY = @P_CD_COMPANY AND SB.CD_ITEM = B.CD_ITEM
)
SELECT C.CLS_L,
	   C.CLS_M,
	   C.CLS_S,
	   C.CLS_S1,
       MC.NM_SYSDEF AS NM_CLS_L,
	   MC1.NM_SYSDEF AS NM_CLS_M,
	   MC2.NM_SYSDEF AS NM_CLS_S,
	   (SELECT NM_SYSDEF 
		FROM MA_CODEDTL 
		WHERE CD_COMPANY = @P_CD_COMPANY 
		AND CD_FIELD = 'MA_B000032' 
		AND CD_SYSDEF = C.CLS_S1) AS NM_CLS_S1,
	   C.NO_IMO,
       C.NO_ENGINE,
       C.NO_PLATE,
       C.NM_PLATE,
       C.CD_SPEC,
       C.EZCODE,
       C.CD_ITEM,
       ISNULL(C.QT_QTN, 0) AS QT_QTN,
       ISNULL(C.QT_SO, 0) AS QT_SO,
       ISNULL(C.QT_INV, 0) AS QT_INV,
       ISNULL(C.QT_NOT_IN, 0) AS QT_NOT_IN,
       ISNULL(C.QT_BOOK, 0) AS QT_BOOK,
       ISNULL(C.QT_HOLD, 0) AS QT_HOLD,
       ROUND(C.UM_KR_P_AVG, 0) AS UM_KR_P_AVG,
       (C.QT_SO / 12) AS QT_SO_MONTH,
       0.0 AS QT_SO_MONTH1,
       (((ISNULL(C.QT_INV, 0) - ISNULL(C.QT_BOOK, 0)) + ISNULL(C.QT_NOT_IN, 0)) - ISNULL(C.QT_HOLD, 0)) AS QT_REMAIN,
       0.0 AS QT_NEED,
       0.0 AS QT_NEED_ROUND,
       0.0 AS AM_PO
FROM C
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = @P_CD_COMPANY AND MC.CD_FIELD = 'MA_B000030' AND MC.CD_SYSDEF = C.CLS_L
LEFT JOIN MA_CODEDTL MC1 ON MC1.CD_COMPANY = @P_CD_COMPANY AND MC1.CD_FIELD = 'MA_B000031' AND MC1.CD_SYSDEF = C.CLS_M
LEFT JOIN MA_CODEDTL MC2 ON MC2.CD_COMPANY = @P_CD_COMPANY AND MC2.CD_FIELD = 'MA_B000032' AND MC2.CD_SYSDEF = C.CLS_S

PRINT ('임시테이블 제거')
DROP TABLE #빈도분석
DROP TABLE #대상도면번호
DROP TABLE #사용도면번호
DROP TABLE #엔진유형
DROP TABLE #공장품목
DROP TABLE #대치자재
DROP TABLE #기부속정보
DROP TABLE #대상품목
DROP TABLE #소분류

GO

