USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_FI_TAX_REG_S]    Script Date: 2015-08-11 오전 9:59:56 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



ALTER PROCEDURE [NEOE].[SP_CZ_FI_TAX_REG_S] 
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_LANGUAGE		    NVARCHAR(5) = 'KR',
	@P_ST_VAT			NVARCHAR(1),
	@P_DT_SHIPPING_FROM	NVARCHAR(8),
	@P_DT_SHIPPING_TO	NVARCHAR(8),
	@P_DT_PROCESS_FROM	NVARCHAR(8),
	@P_DT_PROCESS_TO	NVARCHAR(8),
	@P_NO_IO			NVARCHAR(20),
	@P_NO_SO			NVARCHAR(20)
) 
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT 'N' AS S,
	   IH.NO_IV,
	   IL.NO_IO,
	   IL.NO_SO,
	   IH.DT_PROCESS,
	   FT.DT_VAT,
	   MH.NM_VESSEL,
	   MP.LN_PARTNER AS NM_PARTNER,
	   FT.NO_TO,
	   FT.DT_TO,
	   IH.CD_EXCH AS CD_EXCH_IV,
	   (SELECT (CASE @P_LANGUAGE WHEN 'KR' THEN NM_SYSDEF
								 WHEN 'US' THEN NM_SYSDEF_E
								 WHEN 'JP' THEN NM_SYSDEF_JP
								 WHEN 'CH' THEN NM_SYSDEF_CH END)
		FROM MA_CODEDTL
		WHERE CD_COMPANY = IH.CD_COMPANY
		AND CD_FIELD = 'MA_B000005'
		AND CD_SYSDEF = IH.CD_EXCH) AS NM_EXCH_IV,
	   IH.RT_EXCH AS RT_EXCH_IV,
	   ISNULL(IL.AM_EX_IV, 0) AS AM_EX_IV,
	   ISNULL(IL.AM_IV, 0) AS AM_IV,
	   ISNULL(IL.AM_EX_IV, 0) - ISNULL(FT.AM_EX, 0) AS AM_EX_DIF, 
	   FT.CD_EXCH AS CD_EXCH_TAX,
	   (SELECT (CASE @P_LANGUAGE WHEN 'KR' THEN NM_SYSDEF
								 WHEN 'US' THEN NM_SYSDEF_E
								 WHEN 'JP' THEN NM_SYSDEF_JP
								 WHEN 'CH' THEN NM_SYSDEF_CH END)
		FROM MA_CODEDTL
		WHERE CD_COMPANY = IH.CD_COMPANY
		AND CD_FIELD = 'MA_B000005'
		AND CD_SYSDEF = FT.CD_EXCH) AS NM_EXCH_TAX,
	   ISNULL(FT.RT_EXCH, 0) AS RT_EXCH_TAX,
	   ISNULL(FT.AM_EX, 0) AS AM_EX_TAX,
	   ROUND(ISNULL(FT.AM, 0), 0) AS AM_TAX,
	   FT.DT_SHIPPING,
	   FT.DC_RMK
FROM SA_IVH IH
JOIN (SELECT IL.CD_COMPANY, IL.NO_IV, IL.NO_IO, IL.NO_SO,
			 SUM(CASE WHEN ISNULL(IL.YN_RETURN, 'N') = 'Y' THEN -IL.AM_EX_CLS ELSE IL.AM_EX_CLS END) AS AM_EX_IV,
	         SUM(CASE WHEN ISNULL(IL.YN_RETURN, 'N') = 'Y' THEN -IL.AM_CLS ELSE IL.AM_CLS END) AS AM_IV
	  FROM SA_IVL IL
	  GROUP BY IL.CD_COMPANY, IL.NO_IV, IL.NO_IO, IL.NO_SO) IL
ON IL.CD_COMPANY = IH.CD_COMPANY AND IL.NO_IV = IH.NO_IV 
LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = IL.CD_COMPANY AND SH.NO_SO = IL.NO_SO
LEFT JOIN CZ_FI_TAX FT ON FT.CD_COMPANY = IL.CD_COMPANY AND FT.NO_IV = IL.NO_IV AND FT.NO_IO = IL.NO_IO AND FT.NO_SO = IL.NO_SO
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = SH.NO_IMO
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = SH.CD_COMPANY AND MP.CD_PARTNER = SH.CD_PARTNER
WHERE IH.CD_COMPANY = @P_CD_COMPANY
AND IH.FG_TRANS <> '001'
AND ((@P_ST_VAT = 'A') OR (@P_ST_VAT = 'S' AND ISNULL(FT.DT_VAT, '') <> '') OR (@P_ST_VAT = 'M' AND ISNULL(FT.DT_VAT, '') = ''))
AND ((ISNULL(@P_DT_SHIPPING_FROM, '') = '' AND ISNULL(@P_DT_SHIPPING_TO, '') = '') OR (FT.DT_SHIPPING BETWEEN @P_DT_SHIPPING_FROM AND @P_DT_SHIPPING_TO))
AND ((ISNULL(@P_DT_PROCESS_FROM, '') = '' AND ISNULL(@P_DT_PROCESS_TO, '') = '') OR (IH.DT_PROCESS BETWEEN @P_DT_PROCESS_FROM AND @P_DT_PROCESS_TO))
AND (ISNULL(@P_NO_IO, '') = '' OR IL.NO_IO = @P_NO_IO)
AND (ISNULL(@P_NO_SO, '') = '' OR IL.NO_SO = @P_NO_SO)

GO