USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_FI_TAX_EXPORT_EXCEL]    Script Date: 2016-06-28 오후 1:28:30 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_FI_TAX_EXPORT_EXCEL] 
(
	@P_CD_COMPANY	NVARCHAR(7),
	@P_XML			XML, 
	@P_ID_USER		NVARCHAR(10)
) 
AS 

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DECLARE @V_DOC INT

EXEC SP_XML_PREPAREDOCUMENT @V_DOC OUTPUT, @P_XML 

SELECT * INTO #XML
FROM OPENXML (@V_DOC, '/XML/ROW', 2) WITH 
(
	신고번호							NVARCHAR(20),
	송품장번호						NVARCHAR(200),
	[B/L]							NVARCHAR(20),
	수리일자							NVARCHAR(10),
	선적일자							NVARCHAR(10),
	통화부호							NVARCHAR(3),
	환율								NUMERIC(15, 4),
	[수출금액(미화)]					NUMERIC(19, 4),
	[수출금액(원화)] 				NUMERIC(19, 4),
	[수출자구분/거래구분/결제방법]	NVARCHAR(20),
	결재금액							NUMERIC(19, 4)
)

EXEC SP_XML_REMOVEDOCUMENT @V_DOC

-- ================================================== INSERT
INSERT INTO CZ_FI_TAX_EXPORT
(
	CD_COMPANY,
	NO_TAX,
	NO_REF,
	NO_BL,
	DT_TAX,
	DT_LOADING,
	CD_EXCH,
	RT_EXCH,
	AM_TAX_EX,
	AM_TAX,
	TP_GUBUN,
	AM_PAY,
	YN_CHECK,
	ID_INSERT,
	DTS_INSERT
)
SELECT @P_CD_COMPANY,
	   A.신고번호,
	   A.송품장번호,
	   A.[B/L],
	   REPLACE(A.수리일자, '-', ''),
	   REPLACE(A.선적일자, '-', ''),
	   A.통화부호,
	   A.환율,
	   A.[수출금액(미화)],
	   A.[수출금액(원화)],
	   A.TP_GUBUN,
	   A.결재금액,
	   (CASE WHEN A.TP_GUBUN IN ('90', '94', '92') THEN 'Y' ELSE NULL END) AS YN_CHECK,
	   @P_ID_USER, 
	   NEOE.SF_SYSDATE(GETDATE())
FROM (SELECT ROW_NUMBER() OVER (PARTITION BY A.신고번호, A.[B/L] ORDER BY REPLACE(A.선적일자, '-', '') DESC) AS IDX,
			 A.신고번호, 
			 A.송품장번호,
			 A.[B/L],
			 A.수리일자,
			 A.선적일자,
			 A.통화부호,
			 A.환율,
			 A.[수출금액(미화)],
			 A.[수출금액(원화)],
			 SUBSTRING(A.[수출자구분/거래구분/결제방법], CHARINDEX(' ', A.[수출자구분/거래구분/결제방법]) + 3, 2) AS TP_GUBUN,
			 A.결재금액
	  FROM #XML A) A
WHERE A.IDX = 1
AND NOT EXISTS (SELECT 1 
			    FROM CZ_FI_TAX_EXPORT B
				WHERE B.CD_COMPANY = @P_CD_COMPANY
				AND B.NO_TAX = A.신고번호
				AND B.NO_BL = A.[B/L])

INSERT INTO CZ_FI_TAX_EXPORT_D
(
	CD_COMPANY,
	NO_TAX,
	NO_IO,
	ID_INSERT,
	DTS_INSERT
)
SELECT @P_CD_COMPANY,
	   A.신고번호,
	   A.송품장번호,
	   @P_ID_USER, 
	   NEOE.SF_SYSDATE(GETDATE())
FROM (SELECT A.신고번호, A.송품장번호 
	  FROM #XML A
	  GROUP BY A.신고번호, A.송품장번호) A
WHERE EXISTS (SELECT 1 
			  FROM MM_QTIOH OH
			  WHERE OH.CD_COMPANY = @P_CD_COMPANY
			  AND OH.NO_IO = A.송품장번호)
AND NOT EXISTS (SELECT 1 
				FROM CZ_FI_TAX_EXPORT_D ED
				WHERE ED.CD_COMPANY = @P_CD_COMPANY
				AND ED.NO_TAX = A.신고번호
				AND ED.NO_IO = A.송품장번호)

;WITH A AS
(
	SELECT TE.CD_COMPANY,
		   IL.NO_IV,
		   IL.NO_IO,
		   IL.NO_SO,
		   TE.NO_TAX,
		   TE.DT_TAX,
		   TE.DT_LOADING AS DT_SHIPPING,
		   TE.CD_EXCH,
		   TE.RT_EXCH,
		   TE.AM_TAX_EX,
		   TE.AM_TAX,
		   ROW_NUMBER() OVER (PARTITION BY TE.CD_COMPANY, IL.NO_IV, IL.NO_IO, IL.NO_SO ORDER BY TE.AM_TAX_EX DESC, TE.DT_LOADING DESC) AS IDX
	FROM CZ_FI_TAX_EXPORT TE
	JOIN CZ_FI_TAX_EXPORT_D TD ON TD.CD_COMPANY = TE.CD_COMPANY AND TD.NO_TAX = TE.NO_TAX
	JOIN (SELECT IL.CD_COMPANY, IL.NO_IV, IL.NO_IO, IL.NO_SO 
		  FROM SA_IVL IL
		  GROUP BY IL.CD_COMPANY, IL.NO_IV, IL.NO_IO, IL.NO_SO) IL
	ON IL.CD_COMPANY = TD.CD_COMPANY AND IL.NO_IO = TD.NO_IO
	WHERE TE.CD_COMPANY = @P_CD_COMPANY
	AND EXISTS (SELECT 1 
				FROM #XML A
				WHERE A.신고번호 = TE.NO_TAX)
)
INSERT INTO CZ_FI_TAX
(
	CD_COMPANY,
	NO_IV,
	NO_IO,
	NO_SO,
	NO_TO,
	DT_TO,
	DT_SHIPPING,
	CD_EXCH,
	RT_EXCH,
	AM_EX,
	AM,
	ID_INSERT,
	DTS_INSERT
)
SELECT A.CD_COMPANY,
	   A.NO_IV,
	   A.NO_IO,
	   A.NO_SO,
	   A.NO_TAX,
	   A.DT_TAX,
	   A.DT_SHIPPING,
	   A.CD_EXCH,
	   A.RT_EXCH,
	   A.AM_TAX_EX,
	   A.AM_TAX,
	   @P_ID_USER AS ID_INSERT,
	   NEOE.SF_SYSDATE(GETDATE()) AS DTS_INSERT 
FROM A
WHERE A.IDX = 1
AND NOT EXISTS (SELECT 1 
				FROM CZ_FI_TAX FT
				WHERE FT.CD_COMPANY = A.CD_COMPANY
				AND FT.NO_IV = A.NO_IV
				AND FT.NO_IO = A.NO_IO
				AND FT.NO_SO = A.NO_SO)

-- ================================================== UPDATE
UPDATE FT
SET FT.DT_SHIPPING = TE.DT_SHIPPING
FROM CZ_FI_TAX FT
JOIN (SELECT TE.CD_COMPANY, TE.NO_TAX,
			 MAX(TE.DT_LOADING) AS DT_SHIPPING
	  FROM CZ_FI_TAX_EXPORT TE
	  GROUP BY TE.CD_COMPANY, TE.NO_TAX) TE
ON TE.CD_COMPANY = FT.CD_COMPANY AND TE.NO_TAX = FT.NO_TO
WHERE FT.CD_COMPANY = @P_CD_COMPANY
AND ISNULL(FT.DT_VAT, '') = ''
AND EXISTS (SELECT 1 
			FROM #XML A
			WHERE A.신고번호 = FT.NO_TO)

GO