USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_HR_EMP_WORK_TIME_RPT_WEEK_S]    Script Date: 2015-11-17 오전 10:07:39 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

  
ALTER PROCEDURE [NEOE].[SP_CZ_HR_EMP_WORK_TIME_RPT_WEEK_S]  
(  
	@P_CD_COMPANY	NVARCHAR(7),
	@P_DT_MONTH		NVARCHAR(6),
	@P_NO_EMP		NVARCHAR(10),
	@P_QT_INTERVAL	INT
)  
AS  

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

WITH A AS
(
	SELECT ((DATEPART(DD, UL.DT_TIME) + (6 - DATEPART(DW, UL.DT_TIME))) / 7) + 1 AS NO_WEEK,
		   DATEDIFF(MINUTE, LAG(UL.DT_TIME) OVER (PARTITION BY UL.CD_COMPANY, UL.DT_WORK, UL.ID_USER ORDER BY UL.DT_TIME), UL.DT_TIME) AS QT_MINUTE
	FROM CZ_MA_USER_LOG UL
	WHERE UL.CD_COMPANY = @P_CD_COMPANY
	AND UL.ID_USER = @P_NO_EMP
	AND UL.DT_WORK LIKE @P_DT_MONTH + '%'	
),
B AS
(
	SELECT A.NO_WEEK,
		   SUM(A.QT_MINUTE) AS QT_WORK_MINUTE 
	FROM A
	WHERE A.QT_MINUTE <= @P_QT_INTERVAL
	GROUP BY A.NO_WEEK	
)
SELECT CONVERT(VARCHAR, B.NO_WEEK) + ' 주차' AS NM_WEEK,
	   B.QT_WORK_MINUTE,
	   RIGHT('00' + CONVERT(VARCHAR, (B.QT_WORK_MINUTE / 60)), 2) + ':' + RIGHT('00' + CONVERT(VARCHAR, (B.QT_WORK_MINUTE % 60)), 2) AS QT_WORK_HOUR 
FROM B
ORDER BY B.NO_WEEK ASC

GO