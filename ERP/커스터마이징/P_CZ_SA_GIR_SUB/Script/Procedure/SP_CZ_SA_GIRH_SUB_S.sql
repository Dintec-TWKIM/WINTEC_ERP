USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_GIRH_SUB_S]    Script Date: 2015-06-01 오후 5:55:03 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [NEOE].[SP_CZ_SA_GIRH_SUB_S]
(
	@P_CD_COMPANY       NVARCHAR(7),
	@P_DT_SO_FROM       NCHAR(8),
	@P_DT_SO_TO         NCHAR(8),
	@P_CD_PLANT         NVARCHAR(7),
	@P_TP_BUSI          NVARCHAR(3),
	@P_CD_PARTNER       NVARCHAR(20),
	@P_NO_EMP           NVARCHAR(10),
	@P_DT_GUBUN         NVARCHAR(4), --'SO' 수주일자 조회, 'IN' 입고일자 조회, 'DU' 납기일자 조회 
	@P_NO_SO			NVARCHAR(20),
	@P_NO_IMO			NVARCHAR(10),
	@P_CD_SUPPLIER      NVARCHAR(20),
	@P_IN_STATE			NVARCHAR(3), -- 001 -> 입고, 002 -> 미입고, 003 -> 전체
	@P_OUT_STATE		NVARCHAR(3), -- 001 -> 출고, 002 -> 미출고, 003 -> 전체
	@P_YN_PACK			NVARCHAR(1)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT DISTINCT 'N' AS S,
				SH.NO_SO,
				SH.NO_PO_PARTNER,
				SH.DT_SO,
				SH.NO_IMO,
				MH.NO_HULL,
				MH.NM_VESSEL,
				SH.CD_PARTNER,
				MP.LN_PARTNER,
				SH.NO_EMP,
				ME.NM_KOR,
				SH.TP_SO,
				TS.NM_SO,
				TS.TP_BUSI,
				SH.CD_SALEGRP,
				SG.NM_SALEGRP,
				SH.TP_TRANS,
				SH.PORT_LOADING AS TP_TRANSPORT, 
				SH.COND_PAY,
				SH.CD_EXCH,
				MC.NM_SYSDEF AS NM_EXCH
FROM SA_SOH SH
JOIN CZ_SA_QTNH QH ON QH.CD_COMPANY = SH.CD_COMPANY AND QH.NO_FILE = SH.NO_SO
JOIN (SELECT SL.CD_COMPANY, SL.NO_SO, SL.SEQ_SO, SL.CD_ITEM,
	  		 SUM(SL.QT_SO) AS QT_SO,
	  		 SUM(ISNULL(PL.QT_PO, 0) + ISNULL(SB.QT_STOCK, 0)) AS QT_PO,	 
			 SUM(ISNULL(PL.QT_IO, 0) + ISNULL(SB.QT_BOOK, 0)) AS QT_IN,
			 SUM(ISNULL(PL.QT_IO, 0) + ISNULL(SB.QT_BOOK, 0) - ISNULL(SL.QT_GIR, 0)) AS QT_IN_REMAIN,
			 SUM(SL.QT_GIR) AS QT_GIR,
	  		 SUM(SL.QT_GI) AS QT_OUT,
			 MAX(SL.CD_USERDEF1) AS CD_USERDEF1
	  FROM SA_SOL SL
	  LEFT JOIN CZ_SA_STOCK_BOOK SB ON SB.CD_COMPANY = SL.CD_COMPANY AND SB.NO_FILE = SL.NO_SO AND SB.NO_LINE = SL.SEQ_SO
	  JOIN (SELECT SL.CD_COMPANY, SL.NO_SO, SL.SEQ_SO,
				   SUM(PL.QT_PO) AS QT_PO,
	  			   SUM(CASE WHEN IH.YN_RETURN = 'Y' THEN 0 ELSE IL.QT_IO END) AS QT_IO
	  		FROM SA_SOL SL 
			LEFT JOIN PU_POL PL ON PL.CD_COMPANY = SL.CD_COMPANY AND PL.NO_SO = SL.NO_SO AND PL.NO_SOLINE = SL.SEQ_SO
			LEFT JOIN PU_POH PH ON PH.CD_COMPANY = PL.CD_COMPANY AND PH.NO_PO = PL.NO_PO
	  		LEFT JOIN MM_QTIO IL ON IL.CD_COMPANY = PL.CD_COMPANY AND IL.NO_PSO_MGMT = PL.NO_PO AND IL.NO_PSOLINE_MGMT = PL.NO_LINE
	  		LEFT JOIN MM_QTIOH IH ON IH.CD_COMPANY = IL.CD_COMPANY AND IH.NO_IO = IL.NO_IO
			WHERE (ISNULL(@P_CD_SUPPLIER, '') = '' OR PH.CD_PARTNER = @P_CD_SUPPLIER)
			AND (ISNULL(@P_DT_GUBUN, '') <> 'IN' OR (ISNULL(@P_DT_GUBUN, '') = 'IN' AND IH.DT_IO BETWEEN @P_DT_SO_FROM AND @P_DT_SO_TO))
	  		GROUP BY SL.CD_COMPANY, SL.NO_SO, SL.SEQ_SO) PL 
	  ON PL.CD_COMPANY = SL.CD_COMPANY AND PL.NO_SO = SL.NO_SO AND PL.SEQ_SO = SL.SEQ_SO
	  WHERE SL.CD_COMPANY = @P_CD_COMPANY
	  AND (ISNULL(SL.STA_SO, 'O') <> 'O' OR SL.CD_USERDEF1 = '재고픽업')
	  AND (ISNULL(@P_CD_PLANT, '') = '' OR SL.CD_PLANT = @P_CD_PLANT)
	  AND (ISNULL(@P_TP_BUSI, '') = '' OR SL.TP_BUSI = @P_TP_BUSI)
	  AND (ISNULL(@P_DT_GUBUN, '') <> 'DU' OR (ISNULL(@P_DT_GUBUN, '') = 'DU' AND SL.DT_DUEDATE BETWEEN @P_DT_SO_FROM AND @P_DT_SO_TO))
	  GROUP BY SL.CD_COMPANY, SL.NO_SO, SL.SEQ_SO, SL.CD_ITEM) SL 
ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
LEFT JOIN FI_GWDOCU GW ON GW.CD_COMPANY = 'K100' AND GW.CD_PC = '010000' AND GW.NO_DOCU = SH.NO_DOCU
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = SH.NO_IMO
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = SH.CD_COMPANY AND MP.CD_PARTNER = SH.CD_PARTNER
LEFT JOIN CZ_MA_PARTNER MP1 ON MP1.CD_COMPANY = MP.CD_COMPANY AND MP1.CD_PARTNER = MP.CD_PARTNER
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = SH.CD_COMPANY AND ME.NO_EMP = SH.NO_EMP
LEFT JOIN SA_TPSO TS ON TS.CD_COMPANY = SH.CD_COMPANY AND TS.TP_SO = SH.TP_SO
LEFT JOIN MA_SALEGRP SG ON SG.CD_COMPANY = SH.CD_COMPANY AND SG.CD_SALEGRP = SH.CD_SALEGRP
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = SH.CD_COMPANY AND MC.CD_FIELD = 'MA_B000005' AND MC.CD_SYSDEF = SH.CD_EXCH
WHERE SH.CD_COMPANY = @P_CD_COMPANY
AND (ISNULL(@P_DT_GUBUN, '') <> 'SO' OR (ISNULL(@P_DT_GUBUN, '') = 'SO' AND SH.DT_SO BETWEEN @P_DT_SO_FROM AND @P_DT_SO_TO))
AND (ISNULL(SH.YN_CLOSE, 'N') = 'N' OR GW.ST_STAT <> '1' OR SL.CD_USERDEF1 = '재고픽업')
AND	(ISNULL(@P_NO_SO, '') = '' OR SH.NO_SO = @P_NO_SO)
AND	(ISNULL(@P_NO_IMO, '') = '' OR SH.NO_IMO = @P_NO_IMO)
AND	(ISNULL(@P_CD_PARTNER, '') = '' OR SH.CD_PARTNER = @P_CD_PARTNER)
AND	(ISNULL(@P_NO_EMP, '') = '' OR SH.NO_EMP = @P_NO_EMP)
AND (@P_YN_PACK = 'Y' OR (@P_YN_PACK = 'N' AND SL.QT_SO > SL.QT_GIR))
AND (SL.CD_ITEM LIKE 'SD%' OR
	 (@P_IN_STATE = '001' AND ISNULL(SL.QT_IN_REMAIN, 0) > 0) OR
     (@P_IN_STATE = '002' AND ISNULL(SL.QT_PO, 0) > ISNULL(SL.QT_IN, 0)) OR
	 (@P_IN_STATE = '003' OR ISNULL(@P_IN_STATE, '') = ''))
AND ((@P_OUT_STATE = '001' AND ISNULL(SL.QT_OUT, 0) > 0) OR
	 (@P_OUT_STATE = '002' AND ISNULL(SL.QT_SO, 0) > ISNULL(SL.QT_OUT, 0)) OR
	 (@P_OUT_STATE = '003' OR ISNULL(@P_OUT_STATE, '') = ''))
ORDER BY NO_SO
OPTION (USE HINT('QUERY_OPTIMIZER_COMPATIBILITY_LEVEL_100'))

GO