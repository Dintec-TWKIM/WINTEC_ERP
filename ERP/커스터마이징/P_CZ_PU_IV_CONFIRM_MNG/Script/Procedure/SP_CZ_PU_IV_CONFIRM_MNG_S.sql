USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_PU_IV_CONFIRM_MNG_S]    Script Date: 2016-11-17 오후 5:00:32 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO
    
ALTER PROCEDURE [NEOE].[SP_CZ_PU_IV_CONFIRM_MNG_S]        
(             
	@P_CD_COMPANY           NVARCHAR(7),
	@P_NO_EMP				NVARCHAR(15),
	@P_QT_DAY				INT,
	@P_YN_DONE				NVARCHAR(1),
	@P_YN_END				NVARCHAR(1),
	@P_YN_RETURN			NVARCHAR(1)
)        
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

;WITH A AS
(
	SELECT IC.CD_COMPANY,
		   IC.SEQ,
		   IC.TP_CONFIRM,
		   IC.NO_IO,
		   IC.NO_ETAX,
		   IC.NO_PO,
		   IC.DT_END,
		   IC.YN_DONE,
		   IC.NO_EMP,
		   IC.AM_EX,
		   IC.YN_SEND,
		   IC.YN_RETURN,
		   IC.DC_RMK,
		   IC.DC_RMK_WF,
		   IC.ID_INSERT,
		   IC.DTS_INSERT,
		   CONVERT(DATETIME, LEFT(IC.DTS_INSERT, 8) + ' ' + 
							 SUBSTRING(IC.DTS_INSERT, 9, 2) + ':' + 
							 SUBSTRING(IC.DTS_INSERT, 11, 2) + ':' + 
							 SUBSTRING(IC.DTS_INSERT, 13, 2)) AS DTS_INSERT1,
		   (SELECT COUNT(1) 
			FROM MA_CALENDAR
			WHERE CD_COMPANY = IC.CD_COMPANY
			AND FG1_HOLIDAY = 'H'
			AND DT_CAL BETWEEN LEFT(IC.DTS_INSERT, 8) AND CONVERT(CHAR(8), GETDATE(), 112)) AS QT_HOLIDAY
	FROM CZ_PU_IV_CONFIRM IC
	WHERE IC.CD_COMPANY = @P_CD_COMPANY
	AND (ISNULL(@P_NO_EMP, '') = '' OR IC.ID_INSERT = @P_NO_EMP)
	AND (ISNULL(@P_YN_DONE, 'N') = 'N' OR ISNULL(IC.YN_DONE, 'N') = 'N')
	AND (ISNULL(@P_YN_RETURN, 'N') = 'N' OR IC.YN_RETURN = 'Y')
	AND (ISNULL(@P_YN_END, 'N') = 'N' OR  DATEDIFF(DAY, IC.DT_END, GETDATE()) > 0)
),
B AS
(
	SELECT A.CD_COMPANY,
		   A.SEQ,
		   A.TP_CONFIRM,
		   A.NO_IO,
		   A.NO_ETAX,
		   A.NO_PO,
		   A.DT_END,
		   A.YN_DONE,
		   A.NO_EMP,
		   A.AM_EX,
		   A.YN_SEND,
		   A.YN_RETURN,
		   A.DC_RMK,
		   A.DC_RMK_WF,
		   A.ID_INSERT,
		   A.DTS_INSERT,
		   (DATEDIFF(SECOND, A.DTS_INSERT1, GETDATE()) / 86400) - A.QT_HOLIDAY AS QT_DAY,
		   CONVERT(VARCHAR, DATEADD(S, DATEDIFF(SECOND, A.DTS_INSERT1, GETDATE()) % 86400, ''), 8) AS DT_HHMMSS
	FROM A
)
SELECT 'N' AS S,
	   B.SEQ,
	   B.TP_CONFIRM,
	   B.NO_IO,
	   B.NO_ETAX,
	   B.NO_PO,
	   B.DT_END,
	   MP.LN_PARTNER,
	   B.YN_DONE,
	   B.NO_EMP,
	   B.AM_EX,
	   B.YN_SEND,
	   B.YN_RETURN,
	   PL.QT_PO,
	   PL.QT_RCV,
	   B.DC_RMK,
	   B.DC_RMK_WF,
	   B.ID_INSERT,
	   MU.NM_USER AS NM_INSERT,
	   B.DTS_INSERT,
	   (TRIM(CONVERT(CHAR(4), B.QT_DAY)) + '일 ' + B.DT_HHMMSS) AS DT_ELAPSE
FROM B
LEFT JOIN MA_USER MU ON MU.CD_COMPANY = B.CD_COMPANY AND MU.ID_USER = B.ID_INSERT
LEFT JOIN PU_POH PH ON PH.CD_COMPANY = B.CD_COMPANY AND PH.NO_PO = B.NO_PO
LEFT JOIN (SELECT PL.CD_COMPANY, PL.NO_PO,
		   	      SUM(PL.QT_PO) AS QT_PO,
		   	      SUM(PL.QT_RCV) AS QT_RCV
		   FROM PU_POL PL
		   GROUP BY PL.CD_COMPANY, PL.NO_PO) PL
ON PL.CD_COMPANY = PH.CD_COMPANY AND PL.NO_PO = PH.NO_PO
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = PH.CD_COMPANY AND MP.CD_PARTNER = PH.CD_PARTNER
WHERE (@P_QT_DAY = 0 OR B.QT_DAY >= @P_QT_DAY)

GO