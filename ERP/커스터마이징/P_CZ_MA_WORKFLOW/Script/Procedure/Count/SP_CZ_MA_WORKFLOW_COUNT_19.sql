USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_WORKFLOW_COUNT_19]    Script Date: 2018-08-21 오후 4:50:05 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO




-- 장기미출고
ALTER PROCEDURE [NEOE].[SP_CZ_MA_WORKFLOW_COUNT_19]  
(
	@P_CD_COMPANY			NVARCHAR(7),
	@P_ID_USER				NVARCHAR(15),
	@P_NO_KEY				NVARCHAR(20),
	@P_CD_SALEORG			NVARCHAR(7),		
	@P_CD_SALEGRP			NVARCHAR(500),
	@P_DAY_OUTSTANDING		INT
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT COUNT(1) AS CNT
FROM SA_SOH SH
JOIN (SELECT SL.CD_COMPANY, SL.NO_SO,
			 SUM(SL.QT_SO) AS QT_SO,
			 SUM(CASE WHEN QL.TP_BOM = 'P' THEN ISNULL(IL1.QT_BOM, 0) 
										   ELSE ISNULL(IL.QT_IO, 0) + ISNULL(SB.QT_BOOK, 0) END) AS QT_IN,
			 SUM(OL.QT_IO) AS QT_OUT,
			 DATEDIFF(DAY, ISNULL(MAX(IL.DT_IO), GETDATE()), GETDATE()) AS DT_DELAY 
	  FROM SA_SOL SL
	  LEFT JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = SL.CD_COMPANY AND QL.NO_FILE = SL.NO_SO AND QL.NO_LINE = SL.SEQ_SO
	  LEFT JOIN CZ_SA_STOCK_BOOK SB ON SB.CD_COMPANY = SL.CD_COMPANY AND SB.NO_FILE = SL.NO_SO AND SB.NO_LINE = SL.SEQ_SO
	  LEFT JOIN (SELECT PL.CD_COMPANY, PL.NO_SO, PL.NO_SOLINE,
						MAX(IH.DT_IO) AS DT_IO,
						SUM(IL.QT_IO) AS QT_IO 
				 FROM PU_POL PL
				 JOIN MM_QTIO IL ON IL.CD_COMPANY = PL.CD_COMPANY AND IL.NO_PSO_MGMT = PL.NO_PO AND IL.NO_PSOLINE_MGMT = PL.NO_LINE
				 JOIN MM_QTIOH IH ON IH.CD_COMPANY = IL.CD_COMPANY AND IH.NO_IO = IL.NO_IO
				 WHERE (IH.YN_RETURN IS NULL OR IH.YN_RETURN = 'N')
				 GROUP BY PL.CD_COMPANY, PL.NO_SO, PL.NO_SOLINE) IL
	  ON IL.CD_COMPANY = SL.CD_COMPANY AND IL.NO_SO = SL.NO_SO AND IL.NO_SOLINE = SL.SEQ_SO
	  LEFT JOIN (SELECT IL.CD_COMPANY, IL.NO_PSO_MGMT, IL.NO_PSOLINE_MGMT,
						SUM(IL.QT_IO) AS QT_BOM 
				 FROM MM_QTIO IL
				 WHERE IL.FG_PS = '1'
				 AND IL.FG_IO = '002'
				 GROUP BY IL.CD_COMPANY, IL.NO_PSO_MGMT, IL.NO_PSOLINE_MGMT) IL1
	  ON IL1.CD_COMPANY = SL.CD_COMPANY AND IL1.NO_PSO_MGMT = SL.NO_SO AND IL1.NO_PSOLINE_MGMT = SL.SEQ_SO
	  LEFT JOIN (SELECT OL.CD_COMPANY, OL.NO_PSO_MGMT, OL.NO_PSOLINE_MGMT,
				 	    SUM(OL.QT_IO) AS QT_IO,
				 	    MAX(OH.DT_IO) AS DT_IO
				 FROM MM_QTIO OL
				 JOIN MM_QTIOH OH ON OH.CD_COMPANY = OL.CD_COMPANY AND OH.NO_IO = OL.NO_IO
				 WHERE (OH.YN_RETURN IS NULL OR OH.YN_RETURN = 'N')
				 AND OL.FG_PS = '2'
				 AND OL.FG_IO IN ('010', '075')
				 GROUP BY OL.CD_COMPANY, OL.NO_PSO_MGMT, OL.NO_PSOLINE_MGMT) OL
	  ON OL.CD_COMPANY = SL.CD_COMPANY AND OL.NO_PSO_MGMT = SL.NO_SO AND OL.NO_PSOLINE_MGMT = SL.SEQ_SO
	  GROUP BY SL.CD_COMPANY, SL.NO_SO) SL
ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
LEFT JOIN CZ_MA_WORKFLOWH WH ON WH.CD_COMPANY = SH.CD_COMPANY AND WH.NO_KEY = SH.NO_SO AND WH.TP_STEP = '08'
WHERE SL.QT_SO > ISNULL(SL.QT_OUT, 0)
AND ISNULL(SL.QT_IN, 0) > ISNULL(SL.QT_OUT, 0)
AND SL.DT_DELAY > @P_DAY_OUTSTANDING
AND (SH.YN_CLOSE IS NULL OR SH.YN_CLOSE = 'N')
AND (ISNULL(@P_CD_COMPANY, '') = '' OR SH.CD_COMPANY = @P_CD_COMPANY)
AND (ISNULL(@P_NO_KEY, '') = '' OR SH.NO_SO = @P_NO_KEY)
AND (ISNULL(@P_ID_USER, '') = '' OR WH.ID_LOG = @P_ID_USER)
AND (ISNULL(@P_CD_SALEGRP, '') = '' OR SH.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP)))
AND (ISNULL(@P_CD_SALEORG, '') = '' OR EXISTS (SELECT 1 
											   FROM MA_SALEGRP
											   WHERE CD_COMPANY = SH.CD_COMPANY
											   AND CD_SALEGRP = SH.CD_SALEGRP
											   AND CD_SALEORG = @P_CD_SALEORG))


GO

