USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_WORKFLOW_DONE]    Script Date: 2015-11-25 오전 10:59:54 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_MA_WORKFLOW_DONE]  
(
	@P_CD_COMPANY			NVARCHAR(7),
	@P_STEP					NVARCHAR(2),
	@P_NO_KEY				NVARCHAR(20),
	@P_ID_USER				NVARCHAR(15)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DECLARE	@V_YN_DONE VARCHAR(1)
DECLARE @V_ID_SALES NVARCHAR(15)
DECLARE @V_DC_MSG NVARCHAR(500)

IF ISNULL(@P_NO_KEY, '') = ''
BEGIN	
	SET @V_DC_MSG = '[SP_CZ_MA_WORKFLOW_DONE] 파일번호가 없습니다.' + ' [단계 : ' + @P_STEP + ']'

	RAISERROR(@V_DC_MSG, 16, 1)
	RETURN
END

IF NOT EXISTS (SELECT 1 
			   FROM CZ_SA_QTNH QH
			   WHERE QH.CD_COMPANY = @P_CD_COMPANY 
			   AND QH.NO_FILE = @P_NO_KEY)
BEGIN
	SET @V_DC_MSG = '등록 된 견적 데이터가 없습니다.'

	RAISERROR(@V_DC_MSG, 16, 1)
	RETURN
END

IF @P_STEP = '12' AND NOT EXISTS (SELECT 1 
								  FROM CZ_MA_WORKFLOWH 
								  WHERE CD_COMPANY = @P_CD_COMPANY 
								  AND TP_STEP = @P_STEP 
								  AND NO_KEY = @P_NO_KEY 
								  AND YN_DONE = 'F')
	SET @V_YN_DONE = 'F'
ELSE
	SET @V_YN_DONE = 'Y'

IF @P_STEP = '12'
BEGIN
	IF EXISTS (SELECT 1 FROM CZ_SA_CONTRACT_RPTH WHERE CD_COMPANY = @P_CD_COMPANY AND NO_SO = @P_NO_KEY)
	BEGIN
		IF @V_YN_DONE = 'F'
		BEGIN
			UPDATE CZ_SA_CONTRACT_RPTH
			SET ID_FIRST_APPROVAL = @P_ID_USER,
				DTS_FIRST_APPROVAL = NEOE.SF_SYSDATE(GETDATE()),
				ID_UPDATE = @P_ID_USER,
				DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
			WHERE CD_COMPANY = @P_CD_COMPANY
			AND NO_SO = @P_NO_KEY
		END
		ELSE
		BEGIN
			UPDATE CZ_SA_CONTRACT_RPTH
			SET ID_SECOND_APPROVAL = @P_ID_USER,
				DTS_SECOND_APPROVAL = NEOE.SF_SYSDATE(GETDATE()),
				ID_UPDATE = @P_ID_USER,
				DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
			WHERE CD_COMPANY = @P_CD_COMPANY
			AND NO_SO = @P_NO_KEY
		END
	END
	ELSE
	BEGIN
		IF @V_YN_DONE = 'F'
		BEGIN
			INSERT INTO CZ_SA_CONTRACT_RPTH
			(
				CD_COMPANY,
				NO_SO,
				ID_FIRST_APPROVAL,
				DTS_FIRST_APPROVAL,
				ID_INSERT,
				DTS_INSERT
			)
			VALUES
			(
				@P_CD_COMPANY,
				@P_NO_KEY,
				@P_ID_USER,
				NEOE.SF_SYSDATE(GETDATE()),
				@P_ID_USER,
				NEOE.SF_SYSDATE(GETDATE())
			)
		END
		ELSE
		BEGIN
			INSERT INTO CZ_SA_CONTRACT_RPTH
			(
				CD_COMPANY,
				NO_SO,
				ID_SECOND_APPROVAL,
				DTS_SECOND_APPROVAL,
				ID_INSERT,
				DTS_INSERT
			)
			VALUES
			(
				@P_CD_COMPANY,
				@P_NO_KEY,
				@P_ID_USER,
				NEOE.SF_SYSDATE(GETDATE()),
				@P_ID_USER,
				NEOE.SF_SYSDATE(GETDATE())
			)
		END
	END
END

IF EXISTS (SELECT 1 
		   FROM CZ_MA_WORKFLOWH 
		   WHERE CD_COMPANY = @P_CD_COMPANY 
		   AND TP_STEP = @P_STEP 
		   AND NO_KEY = @P_NO_KEY)
BEGIN
	IF (SELECT ISNULL(YN_DONE, 'N') 
		FROM CZ_MA_WORKFLOWH 
		WHERE CD_COMPANY = @P_CD_COMPANY 
		AND TP_STEP = @P_STEP 
		AND NO_KEY = @P_NO_KEY) <> @V_YN_DONE
	BEGIN
		UPDATE CZ_MA_WORKFLOWH
	    SET YN_DONE = @V_YN_DONE,
	    	DTS_DONE = NEOE.SF_SYSDATE(GETDATE()),
	    	UPDATE_HIST = 'SP_CZ_MA_WORKFLOW_DONE',
	    	ID_UPDATE = @P_ID_USER,
	    	DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
	    WHERE CD_COMPANY = @P_CD_COMPANY
	    AND TP_STEP = @P_STEP
	    AND NO_KEY = @P_NO_KEY
	END
END
ELSE IF @P_STEP <> '21'
BEGIN
	SELECT @V_ID_SALES = ID_SALES 
	FROM CZ_MA_WORKFLOWH 
	WHERE CD_COMPANY = @P_CD_COMPANY 
	AND TP_STEP = '01' 
	AND NO_KEY = @P_NO_KEY

	IF ISNULL(@V_ID_SALES, '') = ''
	BEGIN
		SELECT @V_ID_SALES = NO_EMP 
		FROM CZ_SA_QTNH 
		WHERE CD_COMPANY = @P_CD_COMPANY  
		AND NO_FILE = @P_NO_KEY
	END

	INSERT INTO CZ_MA_WORKFLOWH
	(
		CD_COMPANY,
		TP_STEP,
		NO_KEY,
		ID_SALES,
		ID_TYPIST,
		ID_LOG,
		ID_PUR,
		YN_DONE,
		DTS_DONE,
		INSERT_HIST,
		ID_INSERT,
		DTS_INSERT
	)
	VALUES
	(
		@P_CD_COMPANY,
		@P_STEP,
		@P_NO_KEY,
		@V_ID_SALES,
		(SELECT ID_TYPIST FROM CZ_MA_WORKFLOWH WHERE CD_COMPANY = @P_CD_COMPANY AND TP_STEP = '01' AND NO_KEY = @P_NO_KEY),
		(SELECT ID_LOG FROM CZ_MA_WORKFLOWH WHERE CD_COMPANY = @P_CD_COMPANY AND TP_STEP = '08' AND NO_KEY = @P_NO_KEY),
		(SELECT ID_PUR FROM CZ_MA_WORKFLOWH WHERE CD_COMPANY = @P_CD_COMPANY AND TP_STEP = '08' AND NO_KEY = @P_NO_KEY),
		@V_YN_DONE,
		NEOE.SF_SYSDATE(GETDATE()),
		'SP_CZ_MA_WORKFLOW_DONE',
		@P_ID_USER,
		NEOE.SF_SYSDATE(GETDATE())
	)
END

GO