USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_WORKFLOWH_S_21]    Script Date: 2018-08-13 오전 9:48:52 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



-- 재고견적
ALTER PROCEDURE [NEOE].[SP_CZ_MA_WORKFLOWH_S_21]  
(
	@P_CD_COMPANY			NVARCHAR(7),
	@P_ID_USER				NVARCHAR(15),
	@P_NO_KEY				NVARCHAR(20),
	@P_CD_SALEORG			NVARCHAR(7),		
	@P_CD_SALEGRP			NVARCHAR(500)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

WITH WH AS
(
	SELECT WH.CD_COMPANY,
		   WH.NO_KEY,
		   SRM.QT_SRM,
		   SRM.TP_SRM,
		   QH.NO_REF,
		   QH.CD_PARTNER,
		   QH.NO_IMO,
		   WH.ID_SALES,
		   MU.NM_USER AS NM_SALES,
		   WH.ID_TYPIST,
		   WH.ID_PUR,
		   WH.ID_LOG,
		   WH.DC_RMK,
		   LEFT(WH.DTS_INSERT, 8) AS DT_INSERT,
		   CONVERT(DATETIME, LEFT(WH.DTS_INSERT, 8) + ' ' + 
			   				     SUBSTRING(WH.DTS_INSERT, 9, 2) + ':' + 
			   				     SUBSTRING(WH.DTS_INSERT, 11, 2) + ':' + 
			   				     SUBSTRING(WH.DTS_INSERT, 13, 2)) AS DTS_INSERT,
		   (SELECT COUNT(1) 
			FROM MA_CALENDAR
			WHERE CD_COMPANY = WH.CD_COMPANY
			AND FG1_HOLIDAY = 'H'
			AND DT_CAL BETWEEN LEFT(WH.DTS_INSERT, 8) AND CONVERT(CHAR(8), GETDATE(), 112)) AS QT_HOLIDAY,
		   (SELECT LEFT(MIN(WL.DTS_INSERT), 8) AS DT_QUTATION
		  	FROM CZ_MA_WORKFLOWL WL
		  	WHERE WL.CD_COMPANY = WH.CD_COMPANY
			AND WL.NO_KEY = WH.NO_KEY 
			AND TP_STEP = '05') AS DT_QUTATION,
		   (SELECT COUNT(1)
		    FROM CZ_PU_QTNH PH
			WHERE PH.CD_COMPANY = WH.CD_COMPANY
			AND PH.NO_FILE = WH.NO_KEY) AS QT_SUPPLIER,
		   (SELECT COUNT(DISTINCT (CASE WHEN PL.AM_KR IS NOT NULL THEN PL.CD_PARTNER ELSE NULL END)) 
			FROM CZ_PU_QTNL PL
			WHERE PL.CD_COMPANY = WH.CD_COMPANY
			AND PL.NO_FILE = WH.NO_KEY) AS QT_QUOTED,
		   (SELECT COUNT(DISTINCT PH.CD_PARTNER)
			FROM CZ_PU_QTNH PH
			WHERE PH.CD_COMPANY = WH.CD_COMPANY
			AND PH.NO_FILE = WH.NO_KEY
			AND EXISTS (SELECT 1 
						FROM CZ_MA_WORKFLOWL WL 
					    WHERE WL.CD_COMPANY = PH.CD_COMPANY 
						AND WL.NO_KEY = PH.NO_FILE 
						AND WL.CD_SUPPLIER = PH.CD_PARTNER
						AND WL.TP_STEP = '04'
						AND WL.NM_FILE_REAL IS NOT NULL
						AND (WL.YN_INCLUDED IS NULL OR WL.YN_INCLUDED = 'N'))) AS QT_FILE,
		   (SELECT MAX(CD_PARTNER) 
		    FROM CZ_SA_QTNH QH1 
			WHERE QH1.CD_COMPANY = 'S100' 
			AND (QH1.NO_FILE = QH.NO_REF OR QH1.NO_FILE = QH.NO_FILE)) AS CD_PARTNER_SP,
		   STUFF((SELECT DISTINCT ',' + MP.LN_PARTNER + ' (' + PH.DT_INQ + ')'
			      FROM CZ_PU_QTNH PH
			      LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = PH.CD_COMPANY AND MP.CD_PARTNER = PH.CD_PARTNER
			      WHERE PH.CD_COMPANY = WH.CD_COMPANY
			      AND PH.NO_FILE = WH.NO_KEY
			      FOR XML PATH(''), TYPE).value('.', 'nvarchar(max)'), 1, 1, '') AS NM_SUPPLIER,
		    (SELECT SUM(AM_KR) 
			 FROM CZ_PU_QTNL
			 WHERE CD_COMPANY = WH.CD_COMPANY
			 AND NO_FILE = WH.NO_KEY
			 AND YN_CHOICE = 'Y') AS AM_PUR_KR,
			(SELECT COUNT(1) 
			 FROM CZ_SA_QTNL
			 WHERE CD_COMPANY = WH.CD_COMPANY
			 AND NO_FILE = WH.NO_KEY) AS QT_QTN,
			(CASE WHEN EXISTS(SELECT 1 
							  FROM CZ_MA_WORKFLOWH
							  WHERE CD_COMPANY = WH.CD_COMPANY
							  AND NO_KEY = WH.NO_KEY
							  AND TP_STEP = '06') THEN 'Y' ELSE 'N' END) AS YN_SEND,
			(SELECT CONVERT(VARCHAR, (WH1.DTS_INSERT / 86400)) + ' 일 ' +
				    CONVERT(VARCHAR, (WH1.DTS_INSERT - ((WH1.DTS_INSERT / 86400) * 86400)) / 3600) + ' 시간' AS DTS_ELAPSE
			 FROM (SELECT DATEDIFF(SECOND, CONVERT(DATETIME, LEFT(DTS_INSERT, 8) + ' ' + 
										   SUBSTRING(DTS_INSERT, 9, 2) + ':' + 
										   SUBSTRING(DTS_INSERT, 11, 2) + ':' + 
										   SUBSTRING(DTS_INSERT, 13, 2)), GETDATE()) AS DTS_INSERT
				   FROM CZ_MA_WORKFLOWH
			       WHERE CD_COMPANY = WH.CD_COMPANY
			       AND TP_STEP = '01'
			       AND NO_KEY = WH.NO_KEY) WH1) AS DTS_ELAPSE
	FROM CZ_MA_WORKFLOWH WH
	LEFT JOIN CZ_SA_QTNH QH ON QH.CD_COMPANY = WH.CD_COMPANY AND QH.NO_FILE = WH.NO_KEY
	LEFT JOIN MA_USER MU ON MU.CD_COMPANY = WH.CD_COMPANY AND MU.ID_USER = WH.ID_SALES
	LEFT JOIN (SELECT SRM.CD_COMPANY, SRM.NO_FILE,
					  COUNT(1) AS QT_SRM,
					  (CASE WHEN MAX(CD_STATUS) = 'S' THEN '가능' ELSE '완료' END) AS TP_SRM
			   FROM CZ_SRM_QTNH SRM
			   WHERE SRM.CD_STATUS IN ('S', 'C')
			   AND EXISTS (SELECT 1 
			   			   FROM CZ_PU_QTNH PH
			   			   WHERE PH.CD_COMPANY = SRM.CD_COMPANY
			   			   AND PH.NO_FILE = SRM.NO_FILE
			   			   AND PH.CD_PARTNER = SRM.CD_PARTNER)
			   GROUP BY SRM.CD_COMPANY, SRM.NO_FILE) SRM
	ON SRM.CD_COMPANY = WH.CD_COMPANY AND SRM.NO_FILE = WH.NO_KEY
	WHERE WH.TP_STEP = '21'
	AND (WH.YN_DONE IS NULL OR WH.YN_DONE = 'N')
	AND (QH.YN_CLOSE IS NULL OR QH.YN_CLOSE = 'N')
	AND (ISNULL(@P_CD_COMPANY, '') = '' OR WH.CD_COMPANY = @P_CD_COMPANY)
    AND (ISNULL(@P_ID_USER, '') = '' OR (WH.ID_PUR = @P_ID_USER OR WH.ID_SALES = @P_ID_USER))
    AND (ISNULL(@P_NO_KEY, '') = '' OR WH.NO_KEY = @P_NO_KEY)
	AND (ISNULL(@P_CD_SALEGRP, '') = '' OR MU.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP)))
	AND (ISNULL(@P_CD_SALEORG, '') = '' OR EXISTS (SELECT 1 
												   FROM MA_SALEGRP
												   WHERE CD_COMPANY = MU.CD_COMPANY
												   AND CD_SALEGRP = MU.CD_SALEGRP
												   AND CD_SALEORG = @P_CD_SALEORG))
),
WH1 AS
(
	SELECT WH.CD_COMPANY,
		   WH.NO_KEY,
		   (CASE WHEN WH.QT_FILE > 0 THEN 'Y' ELSE 'N' END) AS YN_FILE,
		   (CASE WHEN WH.DT_QUTATION IS NULL AND (WH.QT_QUOTED > 0 OR WH.QT_FILE > 0 OR WH.QT_SRM > 0) THEN '002' -- 견적가능
				 WHEN WH.DT_QUTATION IS NOT NULL AND (WH.QT_QUOTED > 0 OR WH.QT_FILE > 0 OR WH.QT_SRM > 0) THEN '003' -- 견적완료
				 ELSE '001' END) AS TP_STATE,
		   WH.TP_SRM,
		   WH.NO_REF,
		   WH.CD_PARTNER,
		   WH.CD_PARTNER_SP,
		   WH.NO_IMO,
		   WH.NM_SUPPLIER,
		   WH.DT_INSERT,
		   WH.DT_QUTATION,
		   WH.AM_PUR_KR,
		   WH.QT_QTN,
	       WH.QT_SUPPLIER,
	       WH.QT_QUOTED,
		   WH.ID_SALES,
		   WH.NM_SALES,
		   WH.ID_TYPIST,
		   WH.ID_PUR,
		   WH.ID_LOG,
		   WH.DC_RMK,
		   WH.YN_SEND,
		   WH.DTS_ELAPSE,
		   (DATEDIFF(SECOND, WH.DTS_INSERT, GETDATE()) / 86400) - WH.QT_HOLIDAY AS QT_DAY,
		   CONVERT(VARCHAR, DATEADD(S, DATEDIFF(SECOND, WH.DTS_INSERT, GETDATE()) % 86400, ''), 8) AS DT_HHMMSS,
		   CONVERT(VARCHAR, DATEADD(S, DATEDIFF(SECOND, CONVERT(CHAR(8), GETDATE(), 112) + ' 00:00:00', GETDATE()) % 86400, ''), 8) AS DT_HHMMSS1
	FROM WH	
)
SELECT 'N' AS S,
	   WH1.CD_COMPANY,
	   WH1.NO_KEY,
	   WH1.TP_STATE,
	   WH1.TP_SRM,
	   WH1.NO_REF,
	   MP.LN_PARTNER AS NM_PARTNER,
	   MH.NM_VESSEL,
	   WH1.NM_SUPPLIER,
	   WH1.ID_LOG AS CD_SUPPLIER1,
	   MP1.LN_PARTNER AS NM_SUPPLIER1,
	   WH1.DT_INSERT,
	   WH1.DT_QUTATION,
	   WH1.AM_PUR_KR,
	   QL.AM_PO,
	   QL.AM_SO,
	   (ISNULL(QL.AM_SO, 0) - ISNULL(QL.AM_PO, 0)) AS AM_PROFIT,
	   ROUND(CASE WHEN QL.AM_SO IS NULL OR QL.AM_SO = 0 THEN NULL 
														ELSE ((ISNULL(QL.AM_SO, 0) - ISNULL(QL.AM_PO, 0)) / ISNULL(QL.AM_SO, 0)) * 100 END, 2) AS RT_PROFIT,
	   WH1.QT_QTN,
	   WH1.QT_SUPPLIER,
	   WH1.QT_QUOTED,
	   WH1.ID_SALES,
	   WH1.NM_SALES,
	   MU.NM_USER AS NM_TYPIST,
	   WH1.ID_PUR,
	   MU1.NM_USER AS NM_PUR,
	   WH1.DC_RMK,
	   WH1.YN_SEND,
	   WH1.DTS_ELAPSE,
	   ISNULL(QH.YN_SPO, 'N') AS YN_SPO,
	   WH1.YN_FILE,
	   (CASE WHEN WH1.QT_DAY < 0 THEN '0일 ' + WH1.DT_HHMMSS1 
								 ELSE (TRIM(CONVERT(CHAR(4), WH1.QT_DAY)) + '일 ' + WH1.DT_HHMMSS) END) AS DTS_ELAPSE1,
	   QH.DT_SEND_MAIL
FROM WH1
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = WH1.CD_COMPANY AND MP.CD_PARTNER = WH1.CD_PARTNER
LEFT JOIN MA_PARTNER MP1 ON MP1.CD_COMPANY = WH1.CD_COMPANY AND MP1.CD_PARTNER = WH1.ID_LOG
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = WH1.NO_IMO
LEFT JOIN MA_USER MU ON MU.CD_COMPANY = WH1.CD_COMPANY AND MU.ID_USER = WH1.ID_TYPIST
LEFT JOIN MA_USER MU1 ON MU1.CD_COMPANY = WH1.CD_COMPANY AND MU1.ID_USER = WH1.ID_PUR
LEFT JOIN CZ_PU_QTNH QH ON QH.CD_COMPANY = WH1.CD_COMPANY AND QH.NO_FILE = WH1.NO_KEY AND QH.CD_PARTNER = WH1.ID_LOG
LEFT JOIN (SELECT CD_COMPANY, NO_FILE,
				  SUM(AM_KR_P) AS AM_PO,
				  SUM(AM_KR_S) AS AM_SO 
		   FROM CZ_SA_QTNL
		   GROUP BY CD_COMPANY, NO_FILE) QL
ON QL.CD_COMPANY = QH.CD_COMPANY AND QL.NO_FILE = QH.NO_FILE
ORDER BY WH1.TP_STATE, WH1.NO_KEY

GO

