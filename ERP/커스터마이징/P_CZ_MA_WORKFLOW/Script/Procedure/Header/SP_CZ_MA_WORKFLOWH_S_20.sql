USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_WORKFLOWH_S_20]    Script Date: 2018-08-13 오전 9:48:45 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



-- 클레임종결
ALTER PROCEDURE [NEOE].[SP_CZ_MA_WORKFLOWH_S_20]  
(
	@P_CD_COMPANY			NVARCHAR(7),
	@P_ID_USER				NVARCHAR(15),
	@P_NO_KEY				NVARCHAR(20),
	@P_CD_SALEORG			NVARCHAR(7),		
	@P_CD_SALEGRP			NVARCHAR(500)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DECLARE @P_AM_FSIZE				 NUMERIC(3, 0)   -- 소수점    

IF @P_CD_COMPANY = 'S100'
	SET @P_AM_FSIZE = 2
ELSE
	SET @P_AM_FSIZE = 0

SELECT CH.CD_COMPANY,
	   CH.NO_CLAIM AS NO_KEY,
	   CH.NO_SO,
	   ME.NM_KOR AS NM_EMP,
	   CH.DT_INPUT,
	   MC.NM_SYSDEF AS NM_STATUS,
	   MC1.NM_SYSDEF AS NM_GW_STATUS,
	   MP.LN_PARTNER AS NM_PARTNER,
	   MH.NM_VESSEL,
	   MC2.NM_SYSDEF AS NM_CLAIM,
	   MC3.NM_SYSDEF AS NM_CAUSE,
	   MC4.NM_SYSDEF AS NM_ITEM,
	   DATEDIFF(MONTH, CH.DT_INPUT, GETDATE()) AS QT_PROGRESS,
	   ISNULL(CL.AM_CLAIM, 0) AS AM_CLAIM,
	   CH.DC_RMK
FROM CZ_SA_CLAIMH CH
JOIN (SELECT CL.CD_COMPANY, CL.NO_CLAIM,
	  	     SUM(ISNULL(CL.QT_CLAIM, 0) * ISNULL(SL.UM_KR_S, ROUND(SL.UM_SO * SH.RT_EXCH, @P_AM_FSIZE))) AS AM_CLAIM
	  FROM CZ_SA_CLAIML CL
	  LEFT JOIN SA_SOL SL ON SL.CD_COMPANY = CL.CD_COMPANY AND SL.NO_SO = CL.NO_SO AND SL.SEQ_SO = CL.SEQ_SO
	  LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = SL.CD_COMPANY AND SH.NO_SO = SL.NO_SO
	  GROUP BY CL.CD_COMPANY, CL.NO_CLAIM) CL
ON CL.CD_COMPANY = CH.CD_COMPANY AND CL.NO_CLAIM = CH.NO_CLAIM
LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = CH.CD_COMPANY AND SH.NO_SO = CH.NO_SO
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = CH.CD_COMPANY AND ME.NO_EMP = CH.NO_EMP
LEFT JOIN FI_GWDOCU GW ON GW.CD_COMPANY = 'K100' AND GW.CD_PC = '010000' AND GW.NO_DOCU = CH.CD_COMPANY + '-' + CH.NO_CLAIM + '-' + CH.CD_STATUS
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = CH.CD_COMPANY AND MP.CD_PARTNER = CH.CD_PARTNER
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = CH.NO_IMO
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = CH.CD_COMPANY AND MC.CD_FIELD = 'CZ_SA00004' AND MC.CD_SYSDEF = CH.CD_STATUS
LEFT JOIN MA_CODEDTL MC1 ON MC1.CD_COMPANY = GW.CD_COMPANY AND MC1.CD_FIELD = 'PU_C000065' AND MC1.CD_SYSDEF = GW.ST_STAT
LEFT JOIN MA_CODEDTL MC2 ON MC2.CD_COMPANY = CH.CD_COMPANY AND MC2.CD_FIELD = 'CZ_SA00001' AND MC2.CD_SYSDEF = CH.TP_CLAIM
LEFT JOIN MA_CODEDTL MC3 ON MC3.CD_COMPANY = CH.CD_COMPANY AND MC3.CD_FIELD = 'CZ_SA00002' AND MC3.CD_SYSDEF = CH.TP_CAUSE
LEFT JOIN MA_CODEDTL MC4 ON MC4.CD_COMPANY = CH.CD_COMPANY AND MC4.CD_FIELD = 'CZ_SA00003' AND MC4.CD_SYSDEF = CH.TP_ITEM
WHERE (CH.CD_STATUS IN ('0', '1', '2') OR 
	   (CH.CD_STATUS = '3' AND (GW.ST_STAT IS NULL OR GW.ST_STAT IN ('0', '-1', '2', '3')))) 
AND (ISNULL(@P_CD_COMPANY, '') = '' OR CH.CD_COMPANY = @P_CD_COMPANY)
AND (ISNULL(@P_NO_KEY, '') = '' OR CH.NO_SO = @P_NO_KEY)
AND (ISNULL(@P_ID_USER, '') = '' OR CH.NO_EMP = @P_ID_USER)
AND (ISNULL(@P_CD_SALEGRP, '') = '' OR SH.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP)))
AND (ISNULL(@P_CD_SALEORG, '') = '' OR EXISTS (SELECT 1 
											   FROM MA_SALEGRP
											   WHERE CD_COMPANY = SH.CD_COMPANY
											   AND CD_SALEGRP = SH.CD_SALEGRP
											   AND CD_SALEORG = @P_CD_SALEORG)) 
ORDER BY CH.DT_INPUT

GO

