USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_WORKFLOWH_S_23]    Script Date: 2018-08-13 오전 9:48:52 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



-- 계산서확인
ALTER PROCEDURE [NEOE].[SP_CZ_MA_WORKFLOWH_S_23]  
(
	@P_CD_COMPANY			NVARCHAR(7),
	@P_ID_USER				NVARCHAR(15),
	@P_NO_KEY				NVARCHAR(20),
	@P_CD_SALEORG			NVARCHAR(7),		
	@P_CD_SALEGRP			NVARCHAR(500)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

CREATE TABLE #MA_EMP
(
	CD_COMPANY	NVARCHAR(7),
	CD_CC		NVARCHAR(12),
	NO_EMP		NVARCHAR(10),
	NM_KOR		NVARCHAR(40)
)

CREATE NONCLUSTERED INDEX IDX_MA_EMP ON #MA_EMP (CD_COMPANY, CD_CC)

INSERT INTO #MA_EMP
(
	CD_COMPANY,
	CD_CC,
	NO_EMP,
	NM_KOR
)
SELECT ME.CD_COMPANY,
	   ME.CD_CC,
	   ME.NO_EMP,
	   ME.NM_KOR
FROM MA_EMP ME
WHERE ME.CD_COMPANY = @P_CD_COMPANY
AND ME.CD_DUTY_RESP = '400'
AND ISNULL(ME.DT_RETIRE, '00000000') = '00000000'
UNION ALL
SELECT 'K100',
	   '010900',
	   'S-495',
	   '김태연'

;WITH A AS
(
	SELECT IC.CD_COMPANY,
		   IC.SEQ,
		   IC.NO_IO,
	       IC.NO_PO,
		   IC.NO_ETAX,
		   IC.DT_END,
		   IC.AM_EX,
		   IC.AM_EX_IO,
		   IC.DC_RMK,
		   IC.DC_RMK_WF,
		   IC.ID_INSERT,
		   IC.DTS_INSERT,
		   PH.CD_PARTNER,
		   IC.NO_EMP,
		   IC.YN_RETURN,
		   IC.DTS_RETURN,
		   CONVERT(DATETIME, LEFT(IC.DTS_INSERT, 8) + ' ' + 
							 SUBSTRING(IC.DTS_INSERT, 9, 2) + ':' + 
							 SUBSTRING(IC.DTS_INSERT, 11, 2) + ':' + 
							 SUBSTRING(IC.DTS_INSERT, 13, 2)) AS DTS_INSERT1,
		   (SELECT COUNT(1) 
			FROM MA_CALENDAR
			WHERE CD_COMPANY = IC.CD_COMPANY
			AND FG1_HOLIDAY = 'H'
			AND DT_CAL BETWEEN LEFT(IC.DTS_INSERT, 8) AND CONVERT(CHAR(8), GETDATE(), 112)) AS QT_HOLIDAY
	FROM CZ_PU_IV_CONFIRM IC
	LEFT JOIN PU_POH PH ON PH.CD_COMPANY = IC.CD_COMPANY AND PH.NO_PO = IC.NO_PO
	WHERE IC.CD_COMPANY = @P_CD_COMPANY
	AND ISNULL(IC.YN_DONE, 'N') = 'N'
	AND (ISNULL(@P_ID_USER, '') = '' OR EXISTS (SELECT 1 
												FROM STRING_SPLIT(IC.NO_EMP, '|') A
												WHERE A.VALUE = @P_ID_USER) OR EXISTS (SELECT 1 
																					   FROM #MA_EMP ME
																					   WHERE EXISTS (SELECT 1 
																					   			     FROM STRING_SPLIT(IC.NO_EMP, '|') A
																					   			     JOIN MA_EMP ME1 ON ME1.CD_COMPANY = IC.CD_COMPANY AND ME1.NO_EMP = A.value
																					   			     WHERE ME1.CD_COMPANY = ME.CD_COMPANY
																					   			     AND ME1.CD_CC = ME.CD_CC)
																					   AND ME.NO_EMP =  @P_ID_USER) OR IC.ID_INSERT = @P_ID_USER)
	AND (ISNULL(@P_NO_KEY, '') = '' OR IC.NO_PO = @P_NO_KEY)
	AND (ISNULL(@P_CD_SALEGRP, '') = '' OR EXISTS (SELECT 1 
												   FROM SA_SOH SH
												   WHERE SH.CD_COMPANY = PH.CD_COMPANY
												   AND SH.NO_SO = PH.CD_PJT
												   AND SH.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP))))
	AND (ISNULL(@P_CD_SALEORG, '') = '' OR EXISTS (SELECT 1 
												   FROM SA_SOH SH
												   JOIN MA_SALEGRP SG ON SG.CD_COMPANY = SH.CD_COMPANY AND SG.CD_SALEGRP = SH.CD_SALEGRP
												   WHERE SH.CD_COMPANY = PH.CD_COMPANY
												   AND SH.NO_SO = PH.CD_PJT
												   AND SG.CD_SALEORG = @P_CD_SALEORG))
),
B AS
(
	SELECT A.CD_COMPANY,
		   A.SEQ,
		   A.NO_IO,
	       A.NO_PO,
		   A.NO_ETAX,
		   A.DT_END,
		   A.AM_EX,
		   A.AM_EX_IO,
		   A.DC_RMK,
		   A.DC_RMK_WF,
		   A.ID_INSERT,
		   A.DTS_INSERT,
		   A.CD_PARTNER,
		   A.NO_EMP,
		   A.YN_RETURN,
		   A.DTS_RETURN,
		   (DATEDIFF(SECOND, A.DTS_INSERT1, GETDATE()) / 86400) - A.QT_HOLIDAY AS QT_DAY,
		   CONVERT(VARCHAR, DATEADD(S, DATEDIFF(SECOND, A.DTS_INSERT1, GETDATE()) % 86400, ''), 8) AS DT_HHMMSS
	FROM A
)
SELECT 'N' AS S,
	   B.YN_RETURN,
	   B.DTS_RETURN,
	   B.CD_COMPANY,
	   B.SEQ AS NO_KEY,
	   B.NO_IO,
	   B.NO_PO,
	   B.NO_ETAX,
	   EH.DT_SEND,
	   B.DT_END,
	   MP.LN_PARTNER,
	   B.NO_EMP,
	   (SELECT STRING_AGG(ME.NM_KOR, ',') 
	    FROM MA_EMP ME
		WHERE ME.CD_COMPANY = B.CD_COMPANY
		AND EXISTS (SELECT 1 
					FROM STRING_SPLIT(B.NO_EMP, '|') A
					WHERE A.VALUE = ME.NO_EMP)) AS NM_EMP,
	   (SELECT STRING_AGG(ME.NO_EMP, ',')
		FROM #MA_EMP ME
		WHERE EXISTS (SELECT 1 
					  FROM STRING_SPLIT(B.NO_EMP, '|') A
					  JOIN MA_EMP ME1 ON ME1.CD_COMPANY = B.CD_COMPANY AND ME1.NO_EMP = A.value
					  WHERE ME1.CD_COMPANY = ME.CD_COMPANY
					  AND ME1.CD_CC = ME.CD_CC)) AS NO_EMP_TEAM,
	   (SELECT STRING_AGG(ME.NM_KOR, ',')
		FROM #MA_EMP ME
		WHERE EXISTS (SELECT 1 
					  FROM STRING_SPLIT(B.NO_EMP, '|') A
					  JOIN MA_EMP ME1 ON ME1.CD_COMPANY = B.CD_COMPANY AND ME1.NO_EMP = A.value
					  WHERE ME1.CD_COMPANY = ME.CD_COMPANY
					  AND ME1.CD_CC = ME.CD_CC)) AS NM_EMP_TEAM,
	   B.AM_EX AS AM_IV,
	   B.AM_EX_IO AS AM_IO,
	   B.ID_INSERT,
	   MU.NM_USER AS NM_INSERT,
	   (TRIM(CONVERT(CHAR(4), B.QT_DAY)) + '일 ' + B.DT_HHMMSS) AS DT_ELAPSE,
	   (CASE WHEN ISNULL(B.DT_END, '') = '' THEN NULL ELSE DATEDIFF(DAY, B.DT_END, GETDATE()) END) AS QT_END,
	   B.DC_RMK,
	   B.DC_RMK_WF
FROM B
LEFT JOIN CZ_PU_ETAXH EH ON EH.CD_COMPANY = B.CD_COMPANY AND EH.NO_ETAX = B.NO_ETAX AND ISNULL(B.NO_ETAX, '') <> ''
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = B.CD_COMPANY AND MP.CD_PARTNER = B.CD_PARTNER
LEFT JOIN MA_USER MU ON MU.CD_COMPANY = B.CD_COMPANY AND MU.ID_USER = B.ID_INSERT

GO