USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_WORKFLOWH_S_21]    Script Date: 2018-08-13 오전 9:48:52 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



-- 인수증확인
ALTER PROCEDURE [NEOE].[SP_CZ_MA_WORKFLOWH_S_22]  
(
	@P_CD_COMPANY			NVARCHAR(7),
	@P_ID_USER				NVARCHAR(15),
	@P_NO_KEY				NVARCHAR(20),
	@P_CD_SALEORG			NVARCHAR(7),		
	@P_CD_SALEGRP			NVARCHAR(500)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

CREATE TABLE #MM_QTIO
(
	CD_COMPANY	   NVARCHAR(7),
	NO_IO		   NVARCHAR(20),
	DT_IO		   NVARCHAR(8),
	NO_ISURCV	   NVARCHAR(20),
	NO_PSO_MGMT	   NVARCHAR(20),
	AM_OUT		   NUMERIC(17, 4),
	AM_IV		   NUMERIC(17, 4)
)

CREATE NONCLUSTERED INDEX NO_PSO_MGMT ON #MM_QTIO (CD_COMPANY, NO_IO, NO_PSO_MGMT)

INSERT INTO #MM_QTIO
SELECT OL.CD_COMPANY, 
       OL.NO_IO, 
	   OL.DT_IO,
       OL.NO_ISURCV,
	   OL.NO_PSO_MGMT,
	   SUM(CASE WHEN OH.YN_RETURN = 'Y' THEN -OL.AM ELSE OL.AM END) AS AM_OUT,
	   SUM(CASE WHEN OH.YN_RETURN = 'Y' THEN -OL.AM_CLS ELSE OL.AM_CLS END) AS AM_IV
FROM MM_QTIOH OH
JOIN MM_QTIO OL ON OL.CD_COMPANY = OH.CD_COMPANY AND OL.NO_IO = OH.NO_IO
WHERE (ISNULL(@P_CD_COMPANY, '') = '' OR OH.CD_COMPANY = @P_CD_COMPANY)
AND OH.DT_IO > '20151231'
AND (OH.YN_RETURN IS NULL OR OH.YN_RETURN = 'N')
AND OL.FG_PS = '2'
AND OL.YN_AM = 'Y'
AND OL.QT_IO > OL.QT_CLS
GROUP BY OL.CD_COMPANY, OL.NO_IO, OL.DT_IO, OL.NO_ISURCV, OL.NO_PSO_MGMT;

WITH OH AS
(
	SELECT OL.CD_COMPANY,
		   OL.NO_IO,
		   OL.DT_IO,
		   OL.NO_ISURCV,
		   SUM(OL.AM_OUT) AS AM_OUT,
		   SUM(OL.AM_IV) AS AM_IV,
		   GH.NO_EMP AS NO_EMP_GIR,
		   GH.DT_GIR,
		   GD.DC_RMK2,
		   GD.NO_IMO,
		   GD.DT_LOADING,
		   GD.DT_BILL,
		   GD.CD_MAIN_CATEGORY,
		   GD.CD_SUB_CATEGORY,
		   MAX(SH.NO_EMP) AS NO_EMP_SA,
		   MP.LN_PARTNER AS NM_PARTNER,
		   MF.CD_FILE
	FROM #MM_QTIO OL
	JOIN SA_GIRH GH ON GH.CD_COMPANY = OL.CD_COMPANY AND GH.NO_GIR = OL.NO_ISURCV
	JOIN CZ_SA_GIRH_WORK_DETAIL GD ON GD.CD_COMPANY = GH.CD_COMPANY AND GD.NO_GIR = GH.NO_GIR
	JOIN (SELECT CD_COMPANY, CD_FILE
		  FROM MA_FILEINFO 
		  WHERE CD_MODULE = 'SA'
		  AND ID_MENU = 'P_CZ_SA_GIM_REG'
		  GROUP BY CD_COMPANY, CD_FILE) MF
	ON MF.CD_COMPANY = GH.CD_COMPANY AND MF.CD_FILE = GH.NO_GIR + '_' + GH.CD_COMPANY
	LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = OL.CD_COMPANY AND SH.NO_SO = OL.NO_PSO_MGMT
	LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = GH.CD_COMPANY AND MP.CD_PARTNER = GH.CD_PARTNER
	WHERE GD.NO_IMO IS NOT NULL
	AND GD.DT_LOADING IS NOT NULL 
	AND GD.DT_LOADING <> ''
	AND (GD.YN_BILL IS NULL OR GD.YN_BILL = 'N')
	AND (MP.CD_AREA = '100' OR MP.CD_AREA IS NULL OR MP.CD_AREA = '' OR EXISTS (SELECT 1 
																			    FROM MM_QTIO OL1
																				WHERE OL1.CD_COMPANY = OL.CD_COMPANY
																				AND OL1.NO_IO = OL.NO_IO
																				AND OL1.NO_PSO_MGMT LIKE 'DB%'))
	AND (ISNULL(@P_ID_USER, '') = '' OR GH.NO_EMP = @P_ID_USER OR SH.NO_EMP = @P_ID_USER)
	AND (ISNULL(@P_NO_KEY, '') = '' OR GH.NO_GIR = @P_NO_KEY)
	AND (ISNULL(@P_CD_SALEGRP, '') = '' OR EXISTS (SELECT 1 
												   FROM SA_GIRL GL
												   WHERE GL.CD_COMPANY = GH.CD_COMPANY
												   AND GL.NO_GIR = GH.NO_GIR
												   AND GL.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP))))
	AND (ISNULL(@P_CD_SALEORG, '') = '' OR EXISTS (SELECT 1 
												   FROM SA_GIRL GL
												   JOIN MA_SALEGRP SG ON SG.CD_COMPANY = GL.CD_COMPANY AND SG.CD_SALEGRP = GL.CD_SALEGRP
												   WHERE GL.CD_COMPANY = GH.CD_COMPANY
												   AND GL.NO_GIR = GH.NO_GIR
												   AND SG.CD_SALEORG = @P_CD_SALEORG))
	GROUP BY OL.CD_COMPANY, OL.NO_IO, OL.DT_IO, OL.NO_ISURCV, 
			 GH.NO_EMP, GH.DT_GIR,
			 GD.DC_RMK2, GD.NO_IMO, GD.DT_LOADING, GD.DT_BILL, GD.CD_MAIN_CATEGORY, GD.CD_SUB_CATEGORY,
			 MP.LN_PARTNER, MF.CD_FILE
)
SELECT 'N' AS S,
	   OH.CD_COMPANY,
	   OH.NO_IO,
	   OH.NO_ISURCV AS NO_KEY,
	   ME.NM_KOR AS NM_EMP_SALES,
	   ME1.NM_KOR AS NM_EMP_GIR,
	   OH.NM_PARTNER,
	   MH.NM_VESSEL,
	   OH.DT_GIR,
	   OH.DT_IO,
	   OH.DT_LOADING,
	   OH.DT_BILL,
	   MD1.NM_SYSDEF AS NM_TYPE1,
	   MD.NM_SYSDEF AS NM_TYPE2,
	   MD2.NM_SYSDEF AS NM_TYPE3,
	   STUFF((SELECT CHAR(10) + SH.NO_PO_PARTNER
		      FROM SA_SOH SH
			  WHERE SH.CD_COMPANY = OH.CD_COMPANY
			  AND EXISTS (SELECT 1 
						  FROM #MM_QTIO OL
						  WHERE OL.CD_COMPANY = OH.CD_COMPANY
						  AND OL.NO_IO = OH.NO_IO
						  AND OL.NO_PSO_MGMT = SH.NO_SO)
			  GROUP BY SH.NO_SO, SH.NO_PO_PARTNER
			  ORDER BY SH.NO_SO
			  FOR XML PATH('')),1,1,'') AS NO_PO_PARTNER,
	   STUFF((SELECT CHAR(10) + SH.NO_SO + '(' + (CASE WHEN SUM(SL.QT_SO) = SUM(SL.QT_GI) AND (SELECT COUNT(DISTINCT OL.NO_IO) 
																							   FROM MM_QTIO OL
																							   WHERE OL.CD_COMPANY = OH.CD_COMPANY
																							   AND OL.NO_PSO_MGMT = SH.NO_SO
																							   AND OL.FG_PS = '2'
																							   AND YN_PURSALE = 'Y'
																							   AND CD_QTIOTP IN ('200', '240', '250', '210', '241', '251')) = 1 THEN 'C' ELSE 'P' END) + ':' + CONVERT(NVARCHAR, CONVERT(INT, SUM(SL.QT_GI))) + '/' + CONVERT(NVARCHAR, CONVERT(INT, SUM(SL.QT_SO))) + ')' 
		      FROM SA_SOH SH
			  JOIN SA_SOL SL ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
			  WHERE SH.CD_COMPANY = OH.CD_COMPANY
			  AND EXISTS (SELECT 1 
					      FROM #MM_QTIO OL
					      WHERE OL.CD_COMPANY = OH.CD_COMPANY
					      AND OL.NO_IO = OH.NO_IO
					      AND OL.NO_PSO_MGMT = SH.NO_SO)
			   GROUP BY SH.NO_SO
			   ORDER BY SH.NO_SO
			   FOR XML PATH('')),1,1,'') AS NO_SO_STATUS,
	   OH.AM_OUT,
	   OH.AM_IV,
	   (ISNULL(OH.AM_OUT, 0) - ISNULL(OH.AM_IV, 0)) AS AM_REMAIN,
	   OH.CD_FILE,
	   OH.DC_RMK2  
FROM OH OH
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = OH.CD_COMPANY AND ME.NO_EMP = OH.NO_EMP_SA
LEFT JOIN MA_EMP ME1 ON ME1.CD_COMPANY = OH.CD_COMPANY AND ME1.NO_EMP = OH.NO_EMP_GIR
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = OH.NO_IMO
LEFT JOIN MA_CODEDTL MD ON MD.CD_COMPANY = OH.CD_COMPANY AND MD.CD_FIELD = 'CZ_SA00006' AND MD.CD_SYSDEF = OH.CD_MAIN_CATEGORY
LEFT JOIN MA_CODEDTL MD1 ON MD1.CD_COMPANY = OH.CD_COMPANY AND MD1.CD_FIELD = 'CZ_SA00021' AND MD1.CD_SYSDEF = '001'
LEFT JOIN MA_CODEDTL MD2 ON MD2.CD_COMPANY = OH.CD_COMPANY AND MD2.CD_FIELD = MD.CD_FLAG1 AND MD2.CD_SYSDEF = OH.CD_SUB_CATEGORY

GO