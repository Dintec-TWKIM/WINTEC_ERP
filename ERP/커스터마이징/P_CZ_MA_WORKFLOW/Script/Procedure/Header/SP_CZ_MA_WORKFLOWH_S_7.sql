USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_WORKFLOWH_S_7]    Script Date: 2018-08-13 오전 9:46:34 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



-- 고객문의클로징
ALTER PROCEDURE [NEOE].[SP_CZ_MA_WORKFLOWH_S_7]  
(
	@P_CD_COMPANY			NVARCHAR(7),
	@P_ID_USER				NVARCHAR(15),
	@P_NO_KEY				NVARCHAR(20),
	@P_CD_SALEORG			NVARCHAR(7),		
	@P_CD_SALEGRP			NVARCHAR(500)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT 'N' AS S,
	   WH.CD_COMPANY,
	   WH.TP_STATE,
	   WH.NO_KEY,
	   MP.LN_PARTNER AS NM_PARTNER,
	   MH.NM_VESSEL,
	   WH.NM_SUPPLIER,
	   WH.ID_SALES,
	   WH.NM_USER,
	   QH.DT_INQ,
	   LEFT(QH.DT_CLOSE, 8) AS DT_CLOSE,
	   QH.TP_CLOSE,
	   QH.DC_CLOSE
FROM (SELECT WH.CD_COMPANY, 
			 (CASE WHEN WL.DT_QUTATION IS NULL AND (PH.QT_QUOTED > 0 OR PH.QT_FILE > 0 OR SRM.QT_SRM > 0) THEN '002' -- 견적가능
				   WHEN WL.DT_QUTATION IS NOT NULL AND (PH.QT_QUOTED > 0 OR PH.QT_FILE > 0 OR SRM.QT_SRM > 0) THEN '003' -- 견적완료
				   ELSE '001' END) AS TP_STATE,
			 WH.NO_KEY,
			 STUFF((SELECT DISTINCT ',' + MP.LN_PARTNER
					FROM CZ_PU_QTNH PH
					LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = PH.CD_COMPANY AND MP.CD_PARTNER = PH.CD_PARTNER
					WHERE PH.CD_COMPANY = WH.CD_COMPANY
					AND PH.NO_FILE = WH.NO_KEY
					FOR XML PATH(''), TYPE).value('.', 'nvarchar(max)'), 1, 1, '') AS NM_SUPPLIER,
			 WH.ID_SALES,
			 MU.NM_USER
	  FROM CZ_MA_WORKFLOWH WH
	  LEFT JOIN MA_USER MU ON MU.CD_COMPANY = WH.CD_COMPANY AND MU.ID_USER = WH.ID_SALES
	  LEFT JOIN (SELECT WL.CD_COMPANY, WL.NO_KEY,
	  				    LEFT(MIN(WL.DTS_INSERT), 8) AS DT_QUTATION
	  		     FROM CZ_MA_WORKFLOWL WL
	  		     WHERE TP_STEP = '05'
	  		     GROUP BY WL.CD_COMPANY, WL.NO_KEY) WL
	  ON WL.CD_COMPANY = WH.CD_COMPANY AND WL.NO_KEY = WH.NO_KEY
	  LEFT JOIN (SELECT PH.CD_COMPANY, PH.NO_FILE,
		   				COUNT(1) AS QT_SUPPLIER,
		   				SUM(CASE WHEN PL.CD_COMPANY IS NULL THEN 0 ELSE 1 END) AS QT_QUOTED,
						SUM(CASE WHEN WL.CD_COMPANY IS NULL THEN 0 ELSE 1 END) AS QT_FILE
				 FROM CZ_PU_QTNH PH
				 LEFT JOIN (SELECT CD_COMPANY, NO_FILE, CD_PARTNER 
				 		    FROM CZ_PU_QTNL
				 		    WHERE AM_KR IS NOT NULL
				 		    GROUP BY CD_COMPANY, NO_FILE, CD_PARTNER) PL
				 ON PL.CD_COMPANY = PH.CD_COMPANY AND PL.NO_FILE = PH.NO_FILE AND PL.CD_PARTNER = PH.CD_PARTNER
				 LEFT JOIN (SELECT CD_COMPANY, NO_KEY, CD_SUPPLIER
				 		    FROM CZ_MA_WORKFLOWL
				 		    WHERE TP_STEP = '04'
				 		    AND NM_FILE_REAL IS NOT NULL
							AND (YN_INCLUDED IS NULL OR YN_INCLUDED = 'N')
				 		    GROUP BY CD_COMPANY, NO_KEY, CD_SUPPLIER) WL
				 ON WL.CD_COMPANY = PH.CD_COMPANY AND WL.NO_KEY = PH.NO_FILE AND WL.CD_SUPPLIER = PH.CD_PARTNER
				 GROUP BY PH.CD_COMPANY, PH.NO_FILE) PH
	  ON PH.CD_COMPANY = WH.CD_COMPANY AND PH.NO_FILE = WH.NO_KEY
	  LEFT JOIN (SELECT SRM.CD_COMPANY, SRM.NO_FILE,
		   				COUNT(1) AS QT_SRM
				 FROM CZ_SRM_QTNH SRM
				 WHERE SRM.CD_STATUS IN ('S', 'C')
				 AND EXISTS (SELECT 1 
							 FROM CZ_PU_QTNH
							 WHERE CD_COMPANY = SRM.CD_COMPANY
							 AND NO_FILE = SRM.NO_FILE
							 AND CD_PARTNER = SRM.CD_PARTNER)
				 GROUP BY SRM.CD_COMPANY, SRM.NO_FILE) SRM
	  ON SRM.CD_COMPANY = WH.CD_COMPANY AND SRM.NO_FILE = WH.NO_KEY
	  WHERE WH.TP_STEP = '07'
	  AND (WH.YN_DONE IS NULL OR WH.YN_DONE = 'N')
	  AND (ISNULL(@P_CD_COMPANY, '') = '' OR WH.CD_COMPANY = @P_CD_COMPANY)
      AND (ISNULL(@P_ID_USER, '') = '' OR WH.ID_SALES = @P_ID_USER)
      AND (ISNULL(@P_NO_KEY, '') = '' OR WH.NO_KEY = @P_NO_KEY)
	  AND (ISNULL(@P_CD_SALEGRP, '') = '' OR MU.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP)))
	  AND (ISNULL(@P_CD_SALEORG, '') = '' OR EXISTS (SELECT 1 
													 FROM MA_SALEGRP
													 WHERE CD_COMPANY = MU.CD_COMPANY
													 AND CD_SALEGRP = MU.CD_SALEGRP
													 AND CD_SALEORG = @P_CD_SALEORG))) WH
LEFT JOIN CZ_SA_QTNH QH ON QH.CD_COMPANY = WH.CD_COMPANY AND QH.NO_FILE = WH.NO_KEY
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = QH.CD_COMPANY AND MP.CD_PARTNER = QH.CD_PARTNER
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = QH.NO_IMO

GO

