USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_WORKFLOWH_S_5]    Script Date: 2018-08-13 오전 9:46:10 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



-- 견적작성
ALTER PROCEDURE [NEOE].[SP_CZ_MA_WORKFLOWH_S_5]  
(
	@P_CD_COMPANY			NVARCHAR(7),
	@P_ID_USER				NVARCHAR(15),
	@P_NO_KEY				NVARCHAR(20),
	@P_CD_SALEORG			NVARCHAR(7),		
	@P_CD_SALEGRP			NVARCHAR(500)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

CREATE TABLE #CZ_MA_WORKFLOWH
(
	CD_COMPANY  NVARCHAR(7),
	NO_KEY      NVARCHAR(20)
)

CREATE NONCLUSTERED INDEX CZ_MA_WORKFLOWH ON #CZ_MA_WORKFLOWH (CD_COMPANY, NO_KEY)

CREATE TABLE #CZ_PU_QTNH
(
	CD_COMPANY  NVARCHAR(7),
	NO_FILE     NVARCHAR(20),
    CD_PARTNER  NVARCHAR(20)
)

CREATE NONCLUSTERED INDEX CZ_PU_QTNH ON #CZ_PU_QTNH (CD_COMPANY, NO_FILE, CD_PARTNER)

INSERT INTO #CZ_MA_WORKFLOWH
SELECT WH.CD_COMPANY,
       WH.NO_KEY
FROM CZ_MA_WORKFLOWH WH
WHERE (ISNULL(@P_CD_COMPANY, '') = '' OR WH.CD_COMPANY = @P_CD_COMPANY)
AND (ISNULL(@P_NO_KEY, '') = '' OR WH.NO_KEY = @P_NO_KEY)
AND WH.TP_STEP = '05'
AND (WH.YN_DONE IS NULL OR WH.YN_DONE = 'N')
AND NOT EXISTS (SELECT 1 
                FROM CZ_SA_QTNH QH 
                WHERE QH.CD_COMPANY = WH.CD_COMPANY 
                AND QH.NO_FILE = WH.NO_KEY
                AND (ISNULL(QH.YN_CLOSE, 'N') <> 'N'))
ORDER BY WH.DTS_INSERT DESC

INSERT INTO #CZ_PU_QTNH
SELECT QH.CD_COMPANY,
       QH.NO_FILE,
       QH.CD_PARTNER
FROM CZ_PU_QTNH QH
WHERE EXISTS (SELECT 1 
              FROM #CZ_MA_WORKFLOWH WH1
              WHERE WH1.CD_COMPANY = QH.CD_COMPANY
              AND WH1.NO_KEY = QH.NO_FILE)

;WITH A AS
(
    SELECT WH.CD_COMPANY,
           WH.NO_KEY,
           SRM.QT_SRM,
    	   SRM.TP_SRM,
    	   QH.NO_REF,
    	   QH.CD_PARTNER,
    	   QH.NO_IMO,
           WH.ID_SALES,
    	   MU.NM_USER AS NM_SALES,
    	   WH.ID_TYPIST,
    	   QH.NO_EMP_QTN,
    	   WH.DC_RMK,
    	   LEFT(WH.DTS_INSERT, 8) AS DT_INSERT,
    	   (CASE WHEN WH1.YN_DONE = 'Y' THEN '완료' 
    	   	     WHEN WH1.YN_DONE IS NOT NULL THEN '요청'
    	   	     ELSE NULL END) AS TP_CHECK,
    	   (SELECT LEFT(MIN(WL.DTS_INSERT), 8) AS DT_QUTATION
    	   	FROM CZ_MA_WORKFLOWL WL
    	   	WHERE WL.CD_COMPANY = WH.CD_COMPANY
    	    AND WL.NO_KEY = WH.NO_KEY 
    	    AND TP_STEP = '05') AS DT_QUTATION,
    	   (SELECT COUNT(1)
    	    FROM #CZ_PU_QTNH PH
    	    WHERE PH.CD_COMPANY = WH.CD_COMPANY
    	    AND PH.NO_FILE = WH.NO_KEY) AS QT_SUPPLIER,
    	   (SELECT COUNT(DISTINCT (CASE WHEN PL.AM_KR IS NOT NULL THEN PL.CD_PARTNER ELSE NULL END)) 
    	    FROM CZ_PU_QTNL PL
    	    WHERE PL.CD_COMPANY = WH.CD_COMPANY
    	    AND PL.NO_FILE = WH.NO_KEY) AS QT_QUOTED,
    	   (SELECT COUNT(DISTINCT PH.CD_PARTNER)
    	    FROM #CZ_PU_QTNH PH
    	    WHERE PH.CD_COMPANY = WH.CD_COMPANY
    	    AND PH.NO_FILE = WH.NO_KEY
    	    AND EXISTS (SELECT 1 
    	   			    FROM CZ_MA_WORKFLOWL WL 
    	   		        WHERE WL.CD_COMPANY = PH.CD_COMPANY 
    	   			    AND WL.NO_KEY = PH.NO_FILE 
    	   			    AND WL.CD_SUPPLIER = PH.CD_PARTNER
    	   			    AND WL.TP_STEP = '04'
    	   			    AND WL.NM_FILE_REAL IS NOT NULL
    	   			    AND (WL.YN_INCLUDED IS NULL OR WL.YN_INCLUDED = 'N'))) AS QT_FILE,
    	   (SELECT MAX(CD_PARTNER) 
    	    FROM CZ_SA_QTNH QH1 
    	    WHERE QH1.CD_COMPANY = 'S100' 
    	    AND (QH1.NO_FILE = QH.NO_REF OR QH1.NO_FILE = QH.NO_FILE)) AS CD_PARTNER_SP,
    	   (SELECT SUM(AM_KR) 
    	    FROM CZ_PU_QTNL
    	    WHERE CD_COMPANY = WH.CD_COMPANY
    	    AND NO_FILE = WH.NO_KEY
    	    AND YN_CHOICE = 'Y') AS AM_PUR_KR,
    	   (SELECT COUNT(1) 
    	    FROM CZ_SA_QTNL
    	    WHERE CD_COMPANY = WH.CD_COMPANY
    	    AND NO_FILE = WH.NO_KEY) AS QT_QTN,
    	   (CASE WHEN EXISTS(SELECT 1 
    	   			         FROM CZ_MA_WORKFLOWH
    	   			         WHERE CD_COMPANY = WH.CD_COMPANY
    	   			         AND NO_KEY = WH.NO_KEY
    	   			         AND TP_STEP = '06') THEN 'Y' ELSE 'N' END) AS YN_SEND,
    	   (SELECT CONVERT(VARCHAR, (WH1.DTS_INSERT / 86400)) + ' 일 ' +
    	    	   CONVERT(VARCHAR, (WH1.DTS_INSERT - ((WH1.DTS_INSERT / 86400) * 86400)) / 3600) + ' 시간' AS DTS_ELAPSE
    	    FROM (SELECT DATEDIFF(SECOND, CONVERT(DATETIME, LEFT(DTS_INSERT, 8) + ' ' + 
    	    							  SUBSTRING(DTS_INSERT, 9, 2) + ':' + 
    	    							  SUBSTRING(DTS_INSERT, 11, 2) + ':' + 
    	    							  SUBSTRING(DTS_INSERT, 13, 2)), GETDATE()) AS DTS_INSERT
    	    	  FROM CZ_MA_WORKFLOWH
    	          WHERE CD_COMPANY = WH.CD_COMPANY
    	          AND TP_STEP = '01'
    	          AND NO_KEY = WH.NO_KEY) WH1) AS DTS_ELAPSE
    FROM CZ_MA_WORKFLOWH WH
    LEFT JOIN CZ_MA_WORKFLOWH WH1 ON WH1.CD_COMPANY = WH.CD_COMPANY AND WH1.NO_KEY = WH.NO_KEY AND WH1.TP_STEP = '21'
    LEFT JOIN CZ_SA_QTNH QH ON QH.CD_COMPANY = WH.CD_COMPANY AND QH.NO_FILE = WH.NO_KEY
    LEFT JOIN MA_USER MU ON MU.CD_COMPANY = WH.CD_COMPANY AND MU.ID_USER = WH.ID_SALES
    LEFT JOIN (SELECT SRM.CD_COMPANY, SRM.NO_FILE,
    				  COUNT(1) AS QT_SRM,
    				  (CASE WHEN MAX(CD_STATUS) = 'S' THEN '가능' ELSE '완료' END) AS TP_SRM
    		   FROM CZ_SRM_QTNH SRM
    		   WHERE SRM.CD_STATUS IN ('S', 'C')
    		   AND EXISTS (SELECT 1 
    		   			   FROM #CZ_PU_QTNH PH
    		   			   WHERE PH.CD_COMPANY = SRM.CD_COMPANY
    		   			   AND PH.NO_FILE = SRM.NO_FILE
    		   			   AND PH.CD_PARTNER = SRM.CD_PARTNER)
    		   GROUP BY SRM.CD_COMPANY, SRM.NO_FILE) SRM
    ON SRM.CD_COMPANY = WH.CD_COMPANY AND SRM.NO_FILE = WH.NO_KEY
    WHERE WH.TP_STEP = '05'
    AND EXISTS (SELECT 1 
                FROM #CZ_MA_WORKFLOWH WH1
                WHERE WH1.CD_COMPANY = WH.CD_COMPANY
                AND WH1.NO_KEY = WH.NO_KEY)
    AND (ISNULL(@P_ID_USER, '') = '' OR 
    	 (ISNULL(QH.NO_EMP_QTN, WH.ID_SALES) = @P_ID_USER) OR 
    	 (WH1.YN_DONE = 'Y' AND WH.ID_SALES = @P_ID_USER))
    AND (ISNULL(@P_CD_SALEGRP, '') = '' OR MU.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP)))
    AND (ISNULL(@P_CD_SALEORG, '') = '' OR EXISTS (SELECT 1 
    	 										   FROM MA_SALEGRP
    	 										   WHERE CD_COMPANY = MU.CD_COMPANY
    	 										   AND CD_SALEGRP = MU.CD_SALEGRP
    	 										   AND CD_SALEORG = @P_CD_SALEORG))
),
B AS
(
	SELECT WH.CD_COMPANY,
		   WH.NO_KEY,
		   (CASE WHEN WH.DT_QUTATION IS NULL AND (WH.QT_QUOTED > 0 OR WH.QT_FILE > 0 OR WH.QT_SRM > 0) THEN '002' -- 견적가능
				 WHEN WH.DT_QUTATION IS NOT NULL AND (WH.QT_QUOTED > 0 OR WH.QT_FILE > 0 OR WH.QT_SRM > 0) THEN '003' -- 견적완료
				 ELSE '001' END) AS TP_STATE,
		   WH.TP_CHECK,
		   WH.TP_SRM,
		   WH.NO_REF,
		   WH.CD_PARTNER,
		   WH.CD_PARTNER_SP,
		   WH.NO_IMO,
		   WH.DT_INSERT,
		   WH.DT_QUTATION,
		   WH.AM_PUR_KR,
		   WH.QT_QTN,
	       WH.QT_SUPPLIER,
	       WH.QT_QUOTED,
		   WH.ID_SALES,
		   WH.NM_SALES,
		   WH.ID_TYPIST,
		   WH.DC_RMK,
		   WH.YN_SEND,
		   WH.DTS_ELAPSE,
		   WH.NO_EMP_QTN
	FROM A WH	
)
SELECT 'N' AS S,
	   WH1.CD_COMPANY,
	   WH1.NO_KEY,
	   WH1.TP_STATE,
	   WH1.TP_CHECK,
	   WH1.TP_SRM,
	   WH1.NO_REF,
	   MP.LN_PARTNER AS NM_PARTNER,
	   MP1.LN_PARTNER AS NM_PARTNER_SP,
	   MH.NM_VESSEL,
	   PH.LN_PARTNER AS NM_SUPPLIER,
	   WH1.DT_INSERT,
	   WH1.DT_QUTATION,
	   WH1.AM_PUR_KR,
	   WH1.QT_QTN,
	   WH1.QT_SUPPLIER,
	   WH1.QT_QUOTED,
	   WH1.ID_SALES,
	   WH1.NM_SALES,
	   MU.NM_USER AS NM_TYPIST,
	   WH1.DC_RMK,
	   WH1.YN_SEND,
	   WH1.DTS_ELAPSE,
	   WH1.NO_EMP_QTN,
	   ME.NM_KOR AS NM_EMP_QTN
FROM B WH1
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = WH1.CD_COMPANY AND MP.CD_PARTNER = WH1.CD_PARTNER
LEFT JOIN MA_PARTNER MP1 ON MP1.CD_COMPANY = WH1.CD_COMPANY AND MP1.CD_PARTNER = WH1.CD_PARTNER_SP
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = WH1.NO_IMO
LEFT JOIN MA_USER MU ON MU.CD_COMPANY = WH1.CD_COMPANY AND MU.ID_USER = WH1.ID_TYPIST
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = WH1.CD_COMPANY AND ME.NO_EMP = WH1.NO_EMP_QTN
LEFT JOIN (SELECT PH.CD_COMPANY, PH.NO_FILE,
				  STRING_AGG(MP.LN_PARTNER, ',') AS LN_PARTNER
		   FROM #CZ_PU_QTNH PH
		   LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = PH.CD_COMPANY AND MP.CD_PARTNER = PH.CD_PARTNER
		   GROUP BY PH.CD_COMPANY, PH.NO_FILE) PH
ON PH.CD_COMPANY = WH1.CD_COMPANY AND PH.NO_FILE = WH1.NO_KEY
ORDER BY WH1.TP_STATE, WH1.NO_KEY

DROP TABLE #CZ_MA_WORKFLOWH
DROP TABLE #CZ_PU_QTNH

GO