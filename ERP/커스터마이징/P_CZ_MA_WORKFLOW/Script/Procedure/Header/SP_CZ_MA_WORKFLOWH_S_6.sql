USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_WORKFLOWH_S_6]    Script Date: 2018-08-13 오전 9:46:27 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



-- 견적제출
ALTER PROCEDURE [NEOE].[SP_CZ_MA_WORKFLOWH_S_6]  
(
	@P_CD_COMPANY			NVARCHAR(7),
	@P_ID_USER				NVARCHAR(15),
	@P_NO_KEY				NVARCHAR(20),
	@P_CD_SALEORG			NVARCHAR(7),		
	@P_CD_SALEGRP			NVARCHAR(500)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT 'N' AS S,
	   WH.CD_COMPANY,
	   WH.NO_KEY,
	   QH.NO_REF,
	   (SELECT TOP 1 NM_FILE 
	    FROM CZ_MA_WORKFLOWL 
		WHERE CD_COMPANY = WH.CD_COMPANY 
		AND TP_STEP = '05' 
		AND NO_KEY = WH.NO_KEY 
		ORDER BY DTS_INSERT DESC) AS NM_FILE,
	   (SELECT TOP 1 NM_FILE_REAL 
	    FROM CZ_MA_WORKFLOWL 
		WHERE CD_COMPANY = WH.CD_COMPANY 
		AND TP_STEP = '05' 
		AND NO_KEY = WH.NO_KEY 
		ORDER BY DTS_INSERT DESC) AS NM_FILE_REAL,
	   WH.DTS_INSERT,
	   WH.ID_SALES,
	   MU.NM_USER AS NM_SALES,
	   MU1.NM_USER AS NM_TYPIST,
	   MP.LN_PARTNER AS NM_PARTNER,
	   MH.NM_VESSEL,
	   MC.NM_SYSDEF AS SUBMISSION_METHOD,
	   MC2.NM_SYSDEF AS NM_EXCH,
	   MC1.NM_SYSDEF AS NM_COND_PAY,
	   (CASE WHEN (QH.CD_EXCH IS NOT NULL AND QH.CD_EXCH <> '000' AND QH.CD_EXCH <> '001') 
			   OR (QH.COND_PAY IS NOT NULL AND QH.COND_PAY <> '001') THEN 'Y' ELSE 'N' END) AS YN_WARNING,
	   WH.DC_RMK,
	   ('ORIGIN : ' + QH.ORIGIN + CHAR(13) + CHAR(10) +
        'TERMS OF PAYMENT : ' + MC1.NM_SYSDEF + CHAR(13) + CHAR(10) +
	    'TERMS OF DELIVERY : ' + (QH.TP_TRANS + ' ' + QH.PORT_LOADING)) AS DC_RMK1,
	   (CASE WHEN EXISTS (SELECT 1 FROM FT_CZ_MA_WORKFLOWL(WH.CD_COMPANY, WH.NO_KEY) WHERE TP_STEP = '58') THEN 'Y' ELSE 'N' END) AS YN_ATTACH,
	   (CASE WHEN WQ.YN_DONE = 'Y' THEN '완료'
			 WHEN WQ.YN_DONE = 'N' AND ISNULL(WQ.DTS_DONE, '') <> '' THEN '실패'
			 WHEN WQ.YN_READ = 'Y' THEN '실행중'
			 WHEN WQ.YN_READ = 'N' THEN '실행대상'
			 ELSE NULL END) AS ST_RPA,
	   (SELECT QL.DC_JOB 
		FROM (SELECT QL.DC_JOB,
			  	     ISNULL(ROW_NUMBER() OVER(PARTITION BY QL.CD_COMPANY, QL.SEQ ORDER BY QL.DTS_INSERT DESC), 1) AS IDX
			  FROM CZ_RPA_WORK_QUEUE_LOG QL
			  WHERE QL.CD_COMPANY = WQ.CD_COMPANY
			  AND QL.SEQ = WQ.SEQ
			  AND QL.CD_JOB NOT IN ('READ', 'END', 'QUEUE', '시작')) QL
		WHERE QL.IDX = 1) AS DC_RPA,
	    QH.NO_EMP_QTN,
		ME.NM_KOR AS NM_EMP_QTN,
		WQ.SEQ,
		WQ.YN_DONE
FROM CZ_MA_WORKFLOWH WH
LEFT JOIN CZ_SA_QTNH QH ON QH.CD_COMPANY = WH.CD_COMPANY AND QH.NO_FILE = WH.NO_KEY
LEFT JOIN MA_USER MU ON MU.CD_COMPANY = WH.CD_COMPANY AND MU.ID_USER = WH.ID_SALES
LEFT JOIN MA_USER MU1 ON MU1.CD_COMPANY = WH.CD_COMPANY AND MU1.ID_USER = WH.ID_TYPIST
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = QH.CD_COMPANY AND ME.NO_EMP = QH.NO_EMP_QTN
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = QH.CD_COMPANY AND MP.CD_PARTNER = QH.CD_PARTNER
LEFT JOIN CZ_MA_PARTNER MP1 ON MP1.CD_COMPANY = MP.CD_COMPANY AND MP1.CD_PARTNER = MP.CD_PARTNER
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = QH.NO_IMO
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = MP1.CD_COMPANY AND MC.CD_FIELD = 'CZ_SA00017' AND MC.CD_SYSDEF = MP1.TP_QTN
LEFT JOIN MA_CODEDTL MC1 ON MC1.CD_COMPANY = QH.CD_COMPANY AND MC1.CD_FIELD = 'CZ_SA00013' AND MC1.CD_SYSDEF = QH.COND_PAY
LEFT JOIN MA_CODEDTL MC2 ON MC2.CD_COMPANY = QH.CD_COMPANY AND MC2.CD_FIELD = 'MA_B000005' AND MC2.CD_SYSDEF = QH.CD_EXCH
LEFT JOIN (SELECT A.CD_COMPANY,
				  A.SEQ,
		          A.NO_FILE,
				  A.CD_PARTNER,
		   	      A.YN_READ,
		   	      A.YN_DONE,
				  A.DTS_DONE
		   FROM (SELECT WQ.CD_COMPANY,
						WQ.SEQ,
		   	  	        WQ.NO_FILE,
						WQ.CD_PARTNER,
		   	  	        WQ.YN_READ,
		   	  	        WQ.YN_DONE,
						WQ.DTS_DONE,
		   	  	        ROW_NUMBER() OVER(PARTITION BY WQ.CD_COMPANY, WQ.NO_FILE, WQ.CD_PARTNER ORDER BY WQ.YN_DONE DESC, WQ.DTS_INSERT DESC) AS IDX
		   	     FROM CZ_RPA_WORK_QUEUE WQ
				 WHERE WQ.CD_PARTNER NOT IN ('11823')
				 AND WQ.CD_RPA LIKE '%QTN%') A
		   WHERE A.IDX = 1) WQ
ON WQ.CD_COMPANY = WH.CD_COMPANY AND WQ.NO_FILE = WH.NO_KEY AND WQ.CD_PARTNER = QH.CD_PARTNER
WHERE WH.TP_STEP = '06'
AND (WH.YN_DONE IS NULL OR WH.YN_DONE = 'N')
AND (QH.YN_CLOSE IS NULL OR QH.YN_CLOSE = 'N')
AND (ISNULL(@P_CD_COMPANY, '') = '' OR WH.CD_COMPANY = @P_CD_COMPANY)
AND (ISNULL(@P_ID_USER, '') = '' OR WH.ID_TYPIST = @P_ID_USER)
AND (ISNULL(@P_NO_KEY, '') = '' OR WH.NO_KEY = @P_NO_KEY)
AND (ISNULL(@P_CD_SALEGRP, '') = '' OR MU.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP)))
AND (ISNULL(@P_CD_SALEORG, '') = '' OR EXISTS (SELECT 1 
											   FROM MA_SALEGRP SG
											   WHERE SG.CD_COMPANY = MU.CD_COMPANY
											   AND SG.CD_SALEGRP = MU.CD_SALEGRP
											   AND SG.CD_SALEORG = @P_CD_SALEORG))
ORDER BY WH.CD_COMPANY, WH.NO_KEY

GO

