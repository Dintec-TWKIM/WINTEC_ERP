USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_WORKFLOWH_S_19]    Script Date: 2018-08-13 오전 9:48:36 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



-- 장기미출고
ALTER PROCEDURE [NEOE].[SP_CZ_MA_WORKFLOWH_S_19]  
(
	@P_CD_COMPANY			NVARCHAR(7),
	@P_ID_USER				NVARCHAR(15),
	@P_NO_KEY				NVARCHAR(20),
	@P_CD_SALEORG			NVARCHAR(7),		
	@P_CD_SALEGRP			NVARCHAR(500),
	@P_DAY_OUTSTANDING		INT
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT SH.CD_COMPANY,
	   SH.NO_SO AS NO_KEY,
	   MP.LN_PARTNER,
	   MH.NM_VESSEL,
	   SL.QT_SO,
	   SL.QT_IN,
	   SL.QT_OUT,
	   (ISNULL(SL.QT_IN, 0) - ISNULL(SL.QT_OUT, 0)) AS QT_REMAIN,
	   SH.NO_EMP AS ID_SALES,
	   ME.NM_KOR AS NM_SALES,
	   MU.NM_USER AS NM_LOG,
	   SL.DT_IN,
	   SL.DT_OUT,
	   SL.DT_DELAY,
	   SL.AM_SO,
	   SL.AM_REMAIN,
	   (SELECT SUM(RH.AM_RCP_A)
		FROM SA_RCPH RH
		WHERE RH.CD_COMPANY = SH.CD_COMPANY
		AND RH.NO_PROJECT = SH.NO_SO) AS AM_RCP_A,
	   SH.DC_RMK_TEXT,
	   SH.DC_RMK_TEXT2,
	   WH.CD_RMK,
	   WH.DC_RMK
FROM SA_SOH SH
JOIN (SELECT SL.CD_COMPANY, SL.NO_SO,
			 SUM(SL.QT_SO) AS QT_SO,
			 SUM(CASE WHEN QL.TP_BOM = 'P' THEN ISNULL(IL1.QT_BOM, 0) 
										   ELSE ISNULL(IL.QT_IO, 0) END) AS QT_IN,
			 SUM(OL.QT_IO) AS QT_OUT,
			 SUM(ISNULL(SL.QT_SO, 0) * ISNULL(SL.UM_KR_S, 0)) AS AM_SO,
			 SUM(((CASE WHEN QL.TP_BOM = 'P' THEN ISNULL(IL1.QT_BOM, 0) 
										     ELSE ISNULL(IL.QT_IO, 0) END) - ISNULL(OL.QT_IO, 0)) * ISNULL(SL.UM_KR_S, 0)) AS AM_REMAIN,
			 MAX(IL.DT_IO) AS DT_IN,
			 MAX(OL.DT_IO) AS DT_OUT,
			 (CASE WHEN MAX(IL.DT_IO) IS NULL THEN 0
											  ELSE DATEDIFF(DAY, MAX(IL.DT_IO), GETDATE()) END) AS DT_DELAY
	  FROM SA_SOL SL
	  LEFT JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = SL.CD_COMPANY AND QL.NO_FILE = SL.NO_SO AND QL.NO_LINE = SL.SEQ_SO
	  LEFT JOIN (SELECT A.CD_COMPANY, A.NO_SO, A.NO_SOLINE,
				 	    MAX(A.DT_IO) AS DT_IO,
				 	    SUM(A.QT_IO) AS QT_IO
				 FROM (SELECT PL.CD_COMPANY, PL.NO_SO, PL.NO_SOLINE,
				 	  	      MAX(IH.DT_IO) AS DT_IO,
				 	  	      SUM(IL.QT_IO) AS QT_IO 
				 	   FROM PU_POL PL
				 	   JOIN MM_QTIO IL ON IL.CD_COMPANY = PL.CD_COMPANY AND IL.NO_PSO_MGMT = PL.NO_PO AND IL.NO_PSOLINE_MGMT = PL.NO_LINE
				 	   JOIN MM_QTIOH IH ON IH.CD_COMPANY = IL.CD_COMPANY AND IH.NO_IO = IL.NO_IO
				 	   WHERE (IH.YN_RETURN IS NULL OR IH.YN_RETURN = 'N')
				 	   GROUP BY PL.CD_COMPANY, PL.NO_SO, PL.NO_SOLINE
				 	   UNION ALL
				 	   SELECT SB.CD_COMPANY, SB.NO_FILE, SB.NO_LINE,
				 	   	      ISNULL(LEFT(SB.DTS_UPDATE, 8), LEFT(SB.DTS_INSERT, 8)) AS DT_IO,
				 	   	      SB.QT_BOOK AS QT_IO
				 	   FROM CZ_SA_STOCK_BOOK SB) A
				 GROUP BY A.CD_COMPANY, A.NO_SO, A.NO_SOLINE) IL
	  ON IL.CD_COMPANY = SL.CD_COMPANY AND IL.NO_SO = SL.NO_SO AND IL.NO_SOLINE = SL.SEQ_SO
	  LEFT JOIN (SELECT IL.CD_COMPANY, IL.NO_PSO_MGMT, IL.NO_PSOLINE_MGMT,
						SUM(IL.QT_IO) AS QT_BOM 
				 FROM MM_QTIO IL
				 WHERE IL.FG_PS = '1'
				 AND IL.FG_IO = '002'
				 GROUP BY IL.CD_COMPANY, IL.NO_PSO_MGMT, IL.NO_PSOLINE_MGMT) IL1
	  ON IL1.CD_COMPANY = SL.CD_COMPANY AND IL1.NO_PSO_MGMT = SL.NO_SO AND IL1.NO_PSOLINE_MGMT = SL.SEQ_SO
	  LEFT JOIN (SELECT OL.CD_COMPANY, OL.NO_PSO_MGMT, OL.NO_PSOLINE_MGMT,
				 	    SUM(OL.QT_IO) AS QT_IO,
				 	    MAX(OH.DT_IO) AS DT_IO
				 FROM MM_QTIO OL
				 JOIN MM_QTIOH OH ON OH.CD_COMPANY = OL.CD_COMPANY AND OH.NO_IO = OL.NO_IO
				 WHERE (OH.YN_RETURN IS NULL OR OH.YN_RETURN = 'N')
				 AND OL.FG_PS = '2'
				 AND OL.FG_IO IN ('010', '075')
				 GROUP BY OL.CD_COMPANY, OL.NO_PSO_MGMT, OL.NO_PSOLINE_MGMT) OL
	  ON OL.CD_COMPANY = SL.CD_COMPANY AND OL.NO_PSO_MGMT = SL.NO_SO AND OL.NO_PSOLINE_MGMT = SL.SEQ_SO
	  GROUP BY SL.CD_COMPANY, SL.NO_SO) SL
ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
LEFT JOIN CZ_MA_WORKFLOWH WH ON WH.CD_COMPANY = SH.CD_COMPANY AND WH.NO_KEY = SH.NO_SO AND WH.TP_STEP = '19'
LEFT JOIN CZ_MA_WORKFLOWH WH1 ON WH1.CD_COMPANY = SH.CD_COMPANY AND WH1.NO_KEY = SH.NO_SO AND WH1.TP_STEP = '08'
LEFT JOIN MA_USER MU ON MU.CD_COMPANY = WH1.CD_COMPANY AND MU.ID_USER = WH1.ID_LOG
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = SH.CD_COMPANY AND ME.NO_EMP = SH.NO_EMP
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = SH.CD_COMPANY AND MP.CD_PARTNER = SH.CD_PARTNER
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = SH.NO_IMO
WHERE SL.QT_SO > ISNULL(SL.QT_OUT, 0)
AND ISNULL(SL.QT_IN, 0) > ISNULL(SL.QT_OUT, 0)
AND SL.DT_DELAY > @P_DAY_OUTSTANDING
AND (SH.YN_CLOSE IS NULL OR SH.YN_CLOSE = 'N')
AND (ISNULL(@P_CD_COMPANY, '') = '' OR SH.CD_COMPANY = @P_CD_COMPANY)
AND (ISNULL(@P_NO_KEY, '') = '' OR SH.NO_SO = @P_NO_KEY)
AND (ISNULL(@P_ID_USER, '') = '' OR WH1.ID_LOG = @P_ID_USER)
AND (ISNULL(@P_CD_SALEGRP, '') = '' OR SH.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP)))
AND (ISNULL(@P_CD_SALEORG, '') = '' OR EXISTS (SELECT 1 
											   FROM MA_SALEGRP
											   WHERE CD_COMPANY = SH.CD_COMPANY
											   AND CD_SALEGRP = SH.CD_SALEGRP
											   AND CD_SALEORG = @P_CD_SALEORG))
ORDER BY SH.NO_SO
OPTION(RECOMPILE)


GO

