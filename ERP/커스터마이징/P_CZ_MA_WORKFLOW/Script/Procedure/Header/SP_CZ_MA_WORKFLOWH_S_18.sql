USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_WORKFLOWH_S_18]    Script Date: 2018-08-13 오전 9:48:29 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



-- 계산서발행
ALTER PROCEDURE [NEOE].[SP_CZ_MA_WORKFLOWH_S_18]  
(
	@P_CD_COMPANY			NVARCHAR(7),
	@P_ID_USER				NVARCHAR(15),
	@P_NO_KEY				NVARCHAR(20),
	@P_CD_SALEORG			NVARCHAR(7),		
	@P_CD_SALEGRP			NVARCHAR(500)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

CREATE TABLE #MM_QTIO
(
	CD_COMPANY	   NVARCHAR(7),
	NO_IO		   NVARCHAR(20),
	DT_IO		   NVARCHAR(8),
	YN_RETURN	   NVARCHAR(1),
	FG_TRANS	   NVARCHAR(3),
	YN_STATUS1	   NVARCHAR(1),
	NO_ISURCV	   NVARCHAR(20),
	NO_PSO_MGMT	   NVARCHAR(20),
	NO_PO_PARTNER  NVARCHAR(50),
	NO_EMP		   NVARCHAR(10),
	QT_OUT		   NUMERIC(17, 4),
	QT_IV		   NUMERIC(17, 4),
	AM_OUT		   NUMERIC(17, 4),
	AM_IV		   NUMERIC(17, 4)
)

CREATE NONCLUSTERED INDEX NO_PSO_MGMT ON #MM_QTIO (CD_COMPANY, NO_IO, NO_PSO_MGMT)
CREATE NONCLUSTERED INDEX NO_PSO_MGMT1 ON #MM_QTIO (NO_PSO_MGMT, NO_PO_PARTNER)

INSERT INTO #MM_QTIO
SELECT OH.CD_COMPANY,
	   OH.NO_IO,
	   OH.DT_IO,
	   OH.YN_RETURN,
	   OH.FG_TRANS,
	   OH.YN_STATUS1,
       OL.NO_ISURCV,
	   OL.NO_PSO_MGMT,
	   MAX(SH.NO_PO_PARTNER) AS NO_PO_PARTNER,
	   MAX(SH.NO_EMP) AS NO_EMP,
	   SUM(CASE WHEN OH.YN_RETURN = 'Y' THEN -OL.QT_IO ELSE OL.QT_IO END) AS QT_OUT,
	   SUM(CASE WHEN OH.YN_RETURN = 'Y' THEN -OL.QT_CLS ELSE OL.QT_CLS END) AS QT_IV,
	   SUM(CASE WHEN OH.YN_RETURN = 'Y' THEN -OL.AM ELSE OL.AM END) AS AM_OUT,
	   SUM(CASE WHEN OH.YN_RETURN = 'Y' THEN -OL.AM_CLS ELSE OL.AM_CLS END) AS AM_IV
FROM MM_QTIOH OH
JOIN MM_QTIO OL ON OL.CD_COMPANY = OH.CD_COMPANY AND OL.NO_IO = OH.NO_IO
LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = OL.CD_COMPANY AND SH.NO_SO = OL.NO_PSO_MGMT
LEFT JOIN CZ_SA_GIRH_WORK_DETAIL GD ON GD.CD_COMPANY = OL.CD_COMPANY AND GD.NO_GIR = OL.NO_ISURCV
WHERE (ISNULL(@P_CD_COMPANY, '') = '' OR OH.CD_COMPANY = @P_CD_COMPANY)
AND OL.FG_PS = '2' 
AND OL.YN_AM = 'Y' 
AND OL.QT_IO > OL.QT_CLS
AND (ISNULL(@P_NO_KEY, '') = '' OR OL.NO_IO = @P_NO_KEY)
AND (ISNULL(@P_CD_SALEGRP, '') = '' OR OL.CD_GROUP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP)))
AND (ISNULL(@P_CD_SALEORG, '') = '' OR EXISTS (SELECT 1 
											   FROM MA_SALEGRP
											   WHERE CD_COMPANY = OL.CD_COMPANY
											   AND CD_SALEGRP = OL.CD_GROUP
											   AND CD_SALEORG = @P_CD_SALEORG))
AND (OL.CD_ITEM NOT LIKE 'SD%' OR ISNULL(GD.TP_CHARGE, '001') = '001' OR NOT EXISTS (SELECT 1 
													                                 FROM CZ_SA_GIRH_CHARGE GC
													                                 WHERE GC.CD_COMPANY = OL.CD_COMPANY
													                                 AND GC.NO_GIR = OL.NO_ISURCV
													                                 AND GC.CD_ITEM = OL.CD_ITEM))
GROUP BY OH.CD_COMPANY, OH.NO_IO, OH.DT_IO, OH.YN_RETURN, OH.FG_TRANS, OH.YN_STATUS1, OL.NO_ISURCV, OL.NO_PSO_MGMT

SELECT 'N' AS S,
	   OH.CD_COMPANY,
	   OH.NO_IO AS NO_KEY,
	   OH.NO_ISURCV AS NO_GIR,
	   STUFF((SELECT CHAR(10) + OL.NO_PO_PARTNER
		      FROM #MM_QTIO OL
			  WHERE OL.CD_COMPANY = OH.CD_COMPANY
			  AND OL.NO_IO = OH.NO_IO
			  GROUP BY OL.NO_PSO_MGMT, OL.NO_PO_PARTNER
			  ORDER BY OL.NO_PSO_MGMT
			  FOR XML PATH('')),1,1,'') AS NO_PO_PARTNER,
	   STUFF((SELECT CHAR(10) + SH.NO_SO + '(' + (CASE WHEN SUM(SL.QT_SO) = SUM(SL.QT_GI) AND (SELECT COUNT(DISTINCT OL.NO_IO) 
																							   FROM MM_QTIO OL
																							   WHERE OL.CD_COMPANY = OH.CD_COMPANY
																							   AND OL.NO_PSO_MGMT = SH.NO_SO
																							   AND OL.FG_PS = '2'
																							   AND YN_PURSALE = 'Y'
																							   AND CD_QTIOTP IN ('200', '240', '250', '210', '241', '251')) = 1 THEN 'C' ELSE 'P' END) + ':' + CONVERT(NVARCHAR, CONVERT(INT, SUM(SL.QT_GI))) + '/' + CONVERT(NVARCHAR, CONVERT(INT, SUM(SL.QT_SO))) + ')' 
		      FROM SA_SOH SH
			  JOIN SA_SOL SL ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
			  WHERE SH.CD_COMPANY = OH.CD_COMPANY
			  AND EXISTS (SELECT 1 
					      FROM #MM_QTIO OL
					      WHERE OL.CD_COMPANY = OH.CD_COMPANY
					      AND OL.NO_IO = OH.NO_IO
					      AND OL.NO_PSO_MGMT = SH.NO_SO)
			   GROUP BY SH.NO_SO
			   ORDER BY SH.NO_SO
			   FOR XML PATH('')),1,1,'') AS NO_SO_STATUS,
	   MP.LN_PARTNER,
	   MH.NM_VESSEL,
	   ME.NM_KOR AS NM_LOG_EMP,
	   ME1.NM_KOR AS NM_SALES_EMP,
	   MC.NM_SYSDEF AS NM_MAIN_CATEGORY,
	   MC1.NM_SYSDEF AS NM_SUB_CATEGORY,
	   OH.QT_OUT,
	   OH.QT_IV,
	   OH.AM_OUT,
	   OH.AM_IV,
	   (ISNULL(AM_OUT, 0) - ISNULL(AM_IV, 0)) AS AM_REMAIN,
	   (CASE WHEN EXISTS (SELECT 1 
						  FROM #MM_QTIO OL
						  WHERE OL.CD_COMPANY = OH.CD_COMPANY
					      AND OL.NO_IO = OH.NO_IO
						  AND EXISTS (SELECT 1 
									  FROM SA_SOH SH
									  WHERE SH.CD_COMPANY = OH.CD_COMPANY
									  AND SH.NO_SO = OL.NO_PSO_MGMT
									  AND SH.COND_PAY = '101')) THEN 'Y' ELSE 'N' END) AS YN_PROFORMA,
	   (SELECT ISNULL(SUM(RH.AM_RCP_A - RH.AM_RCPS), 0) 
	    FROM SA_RCPH RH
		WHERE RH.CD_COMPANY = OH.CD_COMPANY
		AND RH.AM_RCP_A > RH.AM_RCPS
		AND EXISTS (SELECT 1 
					FROM #MM_QTIO OL
					WHERE OL.CD_COMPANY = OH.CD_COMPANY
					AND OL.NO_IO = OH.NO_IO
					AND OL.NO_PSO_MGMT = RH.NO_PROJECT)) AS AM_RCP_A,
	   GH.DT_GIR,
	   WD.DT_START,
	   OH.DT_IO,
	   WD.DT_LOADING,
	   MF.CD_FILE,
	   WD.YN_BILL,
	   WD.DT_BILL,
	   WD.DC_RMK,
	   WD.DC_RMK2,
	   MP.CD_AREA,
	   OH.YN_STATUS1,
	   (CASE WHEN OH.FG_TRANS = '005' AND 
		          OH.YN_RETURN <> 'Y' AND
		          MP.YN_JEONJA <> '4' AND
		          NOT EXISTS (SELECT 1 
		          		      FROM MM_QTIO OL
							  JOIN SA_GIRL GL ON GL.CD_COMPANY = OL.CD_COMPANY AND GL.NO_GIR = OL.NO_ISURCV AND GL.SEQ_GIR = OL.NO_ISURCVLINE
							  JOIN SA_SOH SH ON SH.CD_COMPANY = OL.CD_COMPANY AND SH.NO_SO = OL.NO_PSO_MGMT
							  LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = SH.CD_COMPANY AND MC.CD_FIELD = 'CZ_SA00013' AND MC.CD_SYSDEF = SH.COND_PAY
		          		      WHERE OL.CD_COMPANY = OH.CD_COMPANY
		          		      AND OL.NO_IO = OH.NO_IO
		          		      AND ((SH.NO_SO NOT LIKE 'DS%' AND ISNULL(GL.TXT_USERDEF1, '') = '') OR 
								   LEFT(SH.NO_SO, 2) IN ('TE', 'BS', 'BE') OR
								   ISNULL(MC.CD_FLAG1, '') <> '' OR
								   SH.COND_PAY = '102' OR
								   (SH.CD_COMPANY = 'K100' AND (SH.CD_PARTNER_GRP IN ('53', '54') OR SH.NO_SO LIKE 'DB%')) OR 
								   (SH.CD_COMPANY = 'K200' AND (SH.CD_PARTNER_GRP = '82' OR SH.NO_SO LIKE 'D-%')) OR
								   (SH.TP_SO IN ('2000', '2100') AND ISNULL(SH.TP_VAT, '') <> '15') OR
								   (SH.CD_PARTNER = '09737' AND LEFT(SH.NO_SO, 2) = 'SB'))) THEN 'Y' ELSE 'N' END) AS YN_AUTO
FROM (SELECT OL.CD_COMPANY, 
	  	     OL.NO_IO, 
	  	     OL.DT_IO,
			 OL.YN_RETURN,
			 OL.FG_TRANS,
			 OL.YN_STATUS1,
	  	     OL.NO_ISURCV,
			 MAX(OL.NO_EMP) AS NO_EMP,
	  	     SUM(OL.QT_OUT) AS QT_OUT,
	  	     SUM(OL.QT_IV) AS QT_IV,
	  	     SUM(OL.AM_OUT) AS AM_OUT,
	  	     SUM(OL.AM_IV) AS AM_IV,
			 SUM(SL.QT_SO) AS QT_SO,
			 SUM(SL.QT_GI) AS QT_GI,
			 SUM(OL1.QT_IO) AS QT_IO,
			 COUNT(1) AS QT_SO1
	  FROM #MM_QTIO OL
	  JOIN (SELECT SL.CD_COMPANY, SL.NO_SO,
	               SUM(SL.QT_SO) AS QT_SO,
	               SUM(SL.QT_GI) AS QT_GI
	        FROM SA_SOL SL
	        WHERE SL.CD_ITEM NOT LIKE 'SD%'
	        GROUP BY SL.CD_COMPANY, SL.NO_SO) SL
	  ON SL.CD_COMPANY = OL.CD_COMPANY AND SL.NO_SO = OL.NO_PSO_MGMT
	  LEFT JOIN (SELECT OL.CD_COMPANY, OL.NO_PSO_MGMT,
	                    COUNT(DISTINCT OL.NO_IO) AS QT_IO
	  		     FROM MM_QTIO OL
	  		     WHERE OL.FG_PS = '2'
	  		     AND OL.YN_PURSALE = 'Y'
	  		     AND OL.CD_QTIOTP IN ('200', '240', '250', '210', '241', '251')
	             GROUP BY OL.CD_COMPANY, OL.NO_PSO_MGMT) OL1
	  ON OL1.CD_COMPANY = SL.CD_COMPANY AND OL1.NO_PSO_MGMT = SL.NO_SO
	  GROUP BY OL.CD_COMPANY, OL.NO_IO, OL.DT_IO, OL.YN_RETURN, OL.FG_TRANS, OL.YN_STATUS1, OL.NO_ISURCV) OH
JOIN SA_GIRH GH ON GH.CD_COMPANY = OH.CD_COMPANY AND GH.NO_GIR = OH.NO_ISURCV
LEFT JOIN CZ_SA_GIRH_WORK_DETAIL WD ON WD.CD_COMPANY = GH.CD_COMPANY AND WD.NO_GIR = GH.NO_GIR
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = GH.CD_COMPANY AND MP.CD_PARTNER = GH.CD_PARTNER
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = WD.NO_IMO
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = GH.CD_COMPANY AND ME.NO_EMP = GH.NO_EMP
LEFT JOIN MA_EMP ME1 ON ME1.CD_COMPANY = OH.CD_COMPANY AND ME1.NO_EMP = OH.NO_EMP
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = WD.CD_COMPANY AND MC.CD_FIELD = 'CZ_SA00006' AND MC.CD_SYSDEF = WD.CD_MAIN_CATEGORY
LEFT JOIN MA_CODEDTL MC1 ON MC1.CD_COMPANY = WD.CD_COMPANY AND MC1.CD_FIELD = MC.CD_FLAG1 AND MC1.CD_SYSDEF = WD.CD_SUB_CATEGORY
LEFT JOIN (SELECT MF.CD_COMPANY, MF.CD_FILE 
		   FROM MA_FILEINFO MF
		   WHERE MF.CD_MODULE = 'SA'
		   AND MF.ID_MENU = 'P_CZ_SA_GIM_REG'
		   GROUP BY MF.CD_COMPANY, MF.CD_FILE) MF
ON MF.CD_COMPANY = OH.CD_COMPANY AND MF.CD_FILE = OH.NO_ISURCV + '_' + OH.CD_COMPANY


DROP TABLE #MM_QTIO

GO