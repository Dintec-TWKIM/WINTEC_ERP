USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_AUTO_MAIL_READY_PACK]    Script Date: 2016-01-13 오후 9:02:15 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO
 
ALTER PROCEDURE [NEOE].[SP_CZ_SA_AUTO_MAIL_READY_PACK]    
(            
	@P_CD_COMPANY		NVARCHAR(7),
    @P_DT_FROM          NVARCHAR(8),
    @P_DT_TO            NVARCHAR(8),
    @P_NO_SO		    NVARCHAR(20)
)            
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

;WITH A AS
(
    SELECT PH.CD_COMPANY,
           PH.NO_GIR,
           PH.NO_PACK,
           LEFT(ISNULL(PH.DTS_UPDATE, PH.DTS_INSERT), 8) AS DT_PACK,
           PH.CD_TYPE,
           MC.NM_SYSDEF AS NM_TYPE,
           PH.QT_GROSS_WEIGHT,
           (FORMAT(PH.QT_WIDTH, 'g15') + ' X ' + FORMAT(QT_LENGTH, 'g15') + ' X '+ FORMAT(QT_HEIGHT, 'g15') + ' (MM)') AS DC_SIZE,
           PL.NO_FILE,
           PL.QT_ITEM,
           PL.QT_PACK,
           CONVERT(NUMERIC(17, 2), ((PH.QT_WIDTH * 0.001) * (PH.QT_HEIGHT * 0.001) * (PH.QT_LENGTH * 0.001))) AS QT_CBM,
           PH.YN_OUTSOURCING,
           (CASE WHEN PH.DTS_UPDATE IS NOT NULL AND LEFT(PH.DTS_INSERT, 8) <> LEFT(PH.DTS_UPDATE, 8) THEN 'Y' ELSE NULL END) AS YN_REVISED,
           IM.DC_URL
    FROM CZ_SA_PACKH PH
    JOIN CZ_SA_GIRH_PACK GH ON GH.CD_COMPANY = PH.CD_COMPANY AND GH.NO_GIR = PH.NO_GIR
    LEFT JOIN CZ_SA_GIRH_PACK_DETAIL PD ON PD.CD_COMPANY = PH.CD_COMPANY AND PD.NO_GIR = PH.NO_GIR
    LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = PH.CD_COMPANY AND MC.CD_FIELD = 'CZ_SA00026' AND MC.CD_SYSDEF = PH.CD_TYPE
    JOIN (SELECT PL.CD_COMPANY, PL.NO_GIR, PL.NO_PACK, PL.NO_FILE,
                 SUM(1.0) AS QT_ITEM,
                 SUM(PL.QT_PACK) AS QT_PACK
          FROM CZ_SA_PACKL PL
          WHERE PL.CD_ITEM NOT LIKE 'SD%'
          GROUP BY PL.CD_COMPANY, PL.NO_GIR, PL.NO_PACK, PL.NO_FILE) PL 
    ON PL.CD_COMPANY = PH.CD_COMPANY AND PL.NO_GIR = PH.NO_GIR AND PL.NO_PACK = PH.NO_PACK
    LEFT JOIN (SELECT IM.CD_COMPANY,
                      IM.NO_GIR,
                      STRING_AGG(DC_URL, ',') AS DC_URL
               FROM (SELECT SUBSTRING(DATA, CHARINDEX('/', DATA, 8) + 1, 4) AS CD_COMPANY,
                            SUBSTRING(DATA, CHARINDEX('/', DATA, 25) + 1, 10) AS NO_GIR,
                            'http://dint.ec/' + ENCODED AS DC_URL 
                     FROM CZ_DX_URL_BASE62
                     WHERE DATA LIKE 'Upload/CZ_SA_PACKH_FILE/%') IM
               GROUP BY IM.CD_COMPANY, IM.NO_GIR) IM
    ON IM.CD_COMPANY = PH.CD_COMPANY AND IM.NO_GIR = PH.NO_GIR
    WHERE PH.CD_COMPANY = @P_CD_COMPANY
    AND LEFT(PL.NO_FILE, 2) NOT IN ('HB')
    AND LEFT(ISNULL(PH.DTS_UPDATE, PH.DTS_INSERT), 8) BETWEEN @P_DT_FROM AND @P_DT_TO
    AND (ISNULL(@P_NO_SO, '') = '' OR PL.NO_FILE = @P_NO_SO)
    AND (PD.CD_PACK_CATEGORY <> '001' OR PD.CD_SUB_CATEGORY <> '007')
)
SELECT SH.CD_PARTNER,
       MP.LN_PARTNER,
       MH.NM_VESSEL,
       SH.NO_PO_PARTNER,
       SH.NO_SO,
       A.DT_PACK,
       A.NM_TYPE,
       A.DC_SIZE,
       CONVERT(NVARCHAR, CONVERT(NUMERIC(10, 2), A.QT_GROSS_WEIGHT)) + ' KG' AS GROSS_WEIGHT,
       A.NO_GIR,
       A.NO_PACK,
       ISNULL(ME1.NO_EMAIL, '') AS FROM_EMAIL,
	   AM.TO_EMAIL,
	   (ISNULL(ME.NO_EMAIL, '') + ';' + ISNULL(ME1.NO_EMAIL, '')) AS CC_EMAIL,
       (CASE WHEN A.CD_TYPE = '003' AND A.YN_OUTSOURCING = 'Y' AND MC1.CD_FLAG1 = 'Y' THEN MC.NM_SYSDEF + ' ' + CONVERT(NVARCHAR, CONVERT(NUMERIC(10, 2), ROUND(((CASE WHEN A.QT_CBM >= 1 THEN A.QT_CBM * TC.UM ELSE TC.UM END) / SH.RT_EXCH), 2)))
                                                                                      ELSE NULL END) AS AM_WOODEN,
       (CASE WHEN A.QT_ITEM = SL.QT_ITEM AND A.QT_PACK = SL.QT_SO THEN (CASE WHEN YN_REVISED = 'Y' THEN 'Revised Complete' ELSE 'Complete' END) 
                                                                  ELSE (CASE WHEN YN_REVISED = 'Y' THEN 'Revised Partial (' ELSE 'Partial (' END) + CONVERT(NVARCHAR, CONVERT(NUMERIC(5, 2), ROUND((A.QT_PACK / SL.QT_SO) * 100, 2))) + '%)' END) AS DC_STATUS,
       A.DC_URL
FROM A
JOIN SA_SOH SH ON SH.CD_COMPANY = A.CD_COMPANY AND SH.NO_SO = A.NO_FILE
LEFT JOIN (SELECT SL.CD_COMPANY, SL.NO_SO,
                  SUM(1.0) AS QT_ITEM,
                  SUM(SL.QT_SO) AS QT_SO
           FROM SA_SOL SL
           WHERE SL.CD_ITEM NOT LIKE 'SD%'
           GROUP BY SL.CD_COMPANY, SL.NO_SO) SL
ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = SH.NO_IMO
LEFT JOIN (SELECT TH.CD_COMPANY,
	  	          TL.SIZE,
	  	          TL.UM 
	  	   FROM CZ_MA_TARIFF_CBM_H TH
	  	   JOIN CZ_MA_TARIFF_CBM_L TL ON TL.CD_COMPANY = TH.CD_COMPANY AND TL.CD_PARTNER = TH.CD_PARTNER
	  	   WHERE ISNULL(TH.YN_USE, 'N') = 'Y'
	  	   AND CONVERT(NVARCHAR(8), GETDATE(), 112) BETWEEN TL.DT_START AND TL.DT_END) TC
ON TC.CD_COMPANY = A.CD_COMPANY AND TC.SIZE = (CASE WHEN A.QT_CBM >= 1 THEN 1 ELSE A.QT_CBM END)
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = SH.CD_COMPANY AND MP.CD_PARTNER = SH.CD_PARTNER
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = SH.CD_COMPANY AND MC.CD_FIELD = 'MA_B000005' AND MC.CD_SYSDEF = SH.CD_EXCH
LEFT JOIN MA_CODEDTL MC1 ON MC1.CD_COMPANY = SH.CD_COMPANY AND MC1.CD_FIELD = 'TR_IM00011' AND MC1.CD_SYSDEF = SH.TP_PACKING
LEFT JOIN V_CZ_SA_QTN_LOG_EMP VE ON VE.CD_COMPANY = SH.CD_COMPANY AND VE.NO_FILE = SH.NO_SO
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = VE.CD_COMPANY AND ME.NO_EMP = VE.CD_FLAG1
LEFT JOIN MA_EMP ME1 ON ME1.CD_COMPANY = VE.CD_COMPANY AND ME1.NO_EMP = VE.CD_FLAG2
LEFT JOIN (SELECT A.CD_COMPANY, A.NO_GIR,
                  STRING_AGG(A.NM_EMAIL, ';') AS TO_EMAIL 
           FROM (SELECT AM.CD_COMPANY, AM.NO_GIR, FP.NM_EMAIL
                 FROM CZ_SA_GIR_AUTO_MAIL_PTR AM
                 JOIN FI_PARTNERPTR FP ON FP.CD_COMPANY = AM.CD_COMPANY AND FP.CD_PARTNER = AM.CD_PARTNER AND FP.SEQ = AM.SEQ
                 UNION ALL
                 SELECT AM.CD_COMPANY, AM.NO_GIR, AM.DC_ETC AS NM_EMAIL
                 FROM CZ_SA_GIR_AUTO_MAIL_PTR AM
                 WHERE AM.SEQ < 0) A
           GROUP BY A.CD_COMPANY, A.NO_GIR) AM
ON AM.CD_COMPANY = A.CD_COMPANY AND AM.NO_GIR = A.NO_GIR
WHERE ISNULL(SH.YN_CLOSE, 'N') <> 'Y'
ORDER BY A.NO_GIR, A.NO_PACK

GO