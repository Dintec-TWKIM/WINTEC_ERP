USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_AUTO_MAIL_PU_S]    Script Date: 2016-01-13 오후 9:02:15 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO
 
ALTER PROCEDURE [NEOE].[SP_CZ_SA_AUTO_MAIL_PU_S]    
(            
	@P_CD_COMPANY		NVARCHAR(7),
	@P_CD_PARTNER		NVARCHAR(20),
    @P_NO_PO            NVARCHAR(20),
    @P_NO_EMP           NVARCHAR(20),
    @P_YN_SEND          NVARCHAR(1),
    @P_YN_NOT_GR        NVARCHAR(1),
    @P_YN_SEND_DATA     NVARCHAR(1)
)            
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT PH.CD_COMPANY, PH.CD_PARTNER,
       MAX(CASE WHEN DATEDIFF(DAY, PH.DT_PO, GETDATE()) >= 5
                     AND ISNULL(DD.YN_EXCLUDE, 'N') <> 'Y'
                     AND (ISNULL(DD.DT_EXPECT, '') = '' OR DATEDIFF(DAY, DD.DT_EXPECT, GETDATE()) >= 2)
                     AND (ISNULL(DD.DT_REPLY, '') = '' OR DATEDIFF(DAY, DD.DT_REPLY, GETDATE()) >= 2)
                     AND (ISNULL(DD.DTS_SEND, '') = '' OR DATEDIFF(DAY, LEFT(DD.DTS_SEND, 8), GETDATE()) >= 1) THEN 'Y' ELSE 'N' END) AS YN_SEND
INTO #PU_POH
FROM SA_SOH SH
JOIN SA_SOL SL ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
JOIN PU_POL PL ON PL.CD_COMPANY = SL.CD_COMPANY AND PL.NO_SO = SL.NO_SO AND PL.NO_SOLINE = SL.SEQ_SO
JOIN PU_POH PH ON PH.CD_COMPANY = PL.CD_COMPANY AND PH.NO_PO = PL.NO_PO
LEFT JOIN CZ_SA_DEFERRED_DELIVERY DD ON DD.CD_COMPANY = PH.CD_COMPANY AND DD.TP_TYPE = '2' AND DD.NO_SO = PH.CD_PJT AND DD.NO_KEY = PH.NO_PO AND DD.DT_LIMIT = PL.DT_LIMIT
LEFT JOIN V_CZ_SA_QTN_LOG_EMP VE ON VE.CD_COMPANY = SH.CD_COMPANY AND VE.NO_FILE = SH.NO_SO
LEFT JOIN CZ_SA_AUTO_MAIL_PARTNER AM ON AM.CD_COMPANY = PH.CD_COMPANY AND AM.CD_PARTNER = PH.CD_PARTNER AND AM.TP_PARTNER = '001'
WHERE SH.CD_COMPANY = @P_CD_COMPANY
AND (ISNULL(@P_NO_PO, '') = '' OR PH.NO_PO LIKE @P_NO_PO + '%')
AND SH.DT_SO BETWEEN DATEADD(YEAR, -3, GETDATE()) AND CONVERT(CHAR(8), GETDATE(), 112)
AND ISNULL(SH.YN_CLOSE, 'N') <> 'Y' 
AND ISNULL(SL.QT_SO, 0) > ISNULL(SL.QT_GIR, 0) 
AND LEFT(PH.NO_PO, 2) NOT IN ('SB', 'NS', 'BE', 'TE')
AND PL.QT_PO > ISNULL(PL.QT_RCV, 0)
AND PL.CD_ITEM NOT LIKE 'SD%' 
AND DATEDIFF(DAY, PL.DT_LIMIT, GETDATE()) >= ISNULL(AM.QT_LT_OVERDUE, 0)
AND (ISNULL(@P_NO_EMP, '') = '' OR VE.CD_FLAG1 = @P_NO_EMP)
GROUP BY PH.CD_COMPANY, PH.CD_PARTNER
OPTION(RECOMPILE)

;WITH A AS
(
    SELECT AM.CD_COMPANY,
           AM.CD_PARTNER,
           AM.TP_PARTNER,
    	   AM.QT_LT_OVERDUE,
    	   AM.TP_PERIOD,
    	   AM.YN_MON,
	       AM.YN_TUE,
	       AM.YN_WED,
	       AM.YN_THU,
	       AM.YN_FRI,
           AM.TP_SEND,
           AM.NO_EMP_SEND,
    	   (CASE WHEN TP_PERIOD = 'DAY' AND DATEPART(DW, GETDATE()) IN (2, 3, 4, 5, 6) THEN 'Y'
                 WHEN TP_PERIOD = 'WEK' AND YN_MON = 'Y' AND DATEPART(DW, GETDATE()) = 2 THEN 'Y'
                 WHEN TP_PERIOD = 'WEK' AND YN_TUE = 'Y' AND DATEPART(DW, GETDATE()) = 3 THEN 'Y'
                 WHEN TP_PERIOD = 'WEK' AND YN_WED = 'Y' AND DATEPART(DW, GETDATE()) = 4 THEN 'Y'
                 WHEN TP_PERIOD = 'WEK' AND YN_THU = 'Y' AND DATEPART(DW, GETDATE()) = 5 THEN 'Y'
                 WHEN TP_PERIOD = 'WEK' AND YN_FRI = 'Y' AND DATEPART(DW, GETDATE()) = 6 THEN 'Y'
                 ELSE 'N' END) YN_SEND,
    	   (CASE WHEN PH.CD_PARTNER IS NOT NULL THEN 'Y' ELSE 'N' END) AS YN_NOT_GR,
           (CASE WHEN ISNULL(PH.YN_SEND, 'N') = 'Y' THEN 'Y' ELSE 'N' END) AS YN_SEND_DATA
    FROM CZ_SA_AUTO_MAIL_PARTNER AM
    LEFT JOIN #PU_POH PH ON PH.CD_COMPANY = AM.CD_COMPANY AND PH.CD_PARTNER = AM.CD_PARTNER
    WHERE AM.CD_COMPANY = @P_CD_COMPANY
    AND AM.TP_PARTNER = '001'
    AND (ISNULL(@P_CD_PARTNER, '') = '' OR AM.CD_PARTNER = @P_CD_PARTNER)
)
SELECT A.CD_PARTNER,
       MP.LN_PARTNER,
       A.TP_PARTNER,
       A.QT_LT_OVERDUE,
       A.TP_PERIOD,
       A.YN_MON,
	   A.YN_TUE,
	   A.YN_WED,
	   A.YN_THU,
	   A.YN_FRI,
       A.TP_SEND,
       A.NO_EMP_SEND,
       ME1.NM_KOR AS NM_KOR_SEND,
       A.YN_SEND,
       A.YN_NOT_GR,
       A.YN_SEND_DATA
FROM A
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = A.CD_COMPANY AND MP.CD_PARTNER = A.CD_PARTNER
LEFT JOIN MA_EMP ME1 ON ME1.CD_COMPANY = A.CD_COMPANY AND ME1.NO_EMP = A.NO_EMP_SEND
WHERE 1=1
AND (ISNULL(@P_YN_SEND, 'N') = 'N' OR A.YN_SEND = 'Y')
AND (ISNULL(@P_YN_NOT_GR, 'N') = 'N' OR A.YN_NOT_GR = 'Y')
AND (ISNULL(@P_YN_SEND_DATA, 'N') = 'N' OR A.YN_SEND_DATA = 'Y')
AND (ISNULL(@P_NO_PO, '') = '' OR A.YN_NOT_GR = 'Y')
AND (ISNULL(@P_NO_EMP, '') = '' OR A.YN_NOT_GR = 'Y')
OPTION(RECOMPILE)

DROP TABLE #PU_POH

GO