USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_AUTO_MAIL_MNG_JSON]    Script Date: 2017-01-05 ¿ÀÈÄ 5:48:55 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_SA_AUTO_MAIL_MNG_JSON]          
(
	@P_JSON			NVARCHAR(MAX),
	@P_ID_USER		NVARCHAR(10)	
)  
AS
   
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DELETE AM
FROM CZ_SA_AUTO_MAIL_PARTNER AM
WHERE EXISTS (SELECT 1 
			  FROM OPENJSON(@P_JSON)
			  WITH
			  (
					CD_COMPANY      NVARCHAR(7),
					CD_PARTNER		NVARCHAR(20),
					TP_PARTNER		NVARCHAR(3),
					JSON_FLAG		NVARCHAR(1)
			  ) JS
			  WHERE JS.JSON_FLAG = 'D'
			  AND JS.CD_COMPANY = AM.CD_COMPANY
			  AND JS.CD_PARTNER = AM.CD_PARTNER
			  AND JS.TP_PARTNER = AM.TP_PARTNER)

INSERT INTO CZ_SA_AUTO_MAIL_PARTNER
(
	CD_COMPANY,
	CD_PARTNER,
	TP_PARTNER,
	QT_LT_OVERDUE,
	TP_PERIOD,
	YN_MON,
	YN_TUE,
	YN_WED,
	YN_THU,
	YN_FRI,
	TP_SEND,
	NO_EMP_SEND,
	ID_INSERT,
	DTS_INSERT
)
SELECT CD_COMPANY,
	   CD_PARTNER,
	   TP_PARTNER,
	   QT_LT_OVERDUE,
	   TP_PERIOD,
	   YN_MON,
	   YN_TUE,
	   YN_WED,
	   YN_THU,
	   YN_FRI,
	   TP_SEND,
	   NO_EMP_SEND,
	   @P_ID_USER,
	   NEOE.SF_SYSDATE(GETDATE())
FROM OPENJSON(@P_JSON)
WITH
(
	CD_COMPANY		NVARCHAR(7),
	CD_PARTNER		NVARCHAR(20),
	TP_PARTNER		NVARCHAR(3),
	QT_LT_OVERDUE	INT,
	TP_PERIOD		NVARCHAR(3),
	YN_MON			NVARCHAR(1),
	YN_TUE			NVARCHAR(1),
	YN_WED			NVARCHAR(1),
	YN_THU			NVARCHAR(1),
	YN_FRI			NVARCHAR(1),
	TP_SEND			NVARCHAR(3),
	NO_EMP_SEND		NVARCHAR(10),
	JSON_FLAG		NVARCHAR(1)
) JS
WHERE JS.JSON_FLAG = 'I'

UPDATE AM
SET AM.QT_LT_OVERDUE = JS.QT_LT_OVERDUE,
	AM.TP_PERIOD = JS.TP_PERIOD,
	AM.YN_MON = JS.YN_MON,
	AM.YN_TUE = JS.YN_TUE,
	AM.YN_WED = JS.YN_WED,
	AM.YN_THU = JS.YN_THU,
	AM.YN_FRI = JS.YN_FRI,
	AM.TP_SEND = JS.TP_SEND,
	AM.NO_EMP_SEND = JS.NO_EMP_SEND,
	AM.ID_UPDATE = @P_ID_USER,
	AM.DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
FROM CZ_SA_AUTO_MAIL_PARTNER AM
JOIN OPENJSON(@P_JSON)
WITH
(
	CD_COMPANY		    NVARCHAR(7),
	CD_PARTNER		    NVARCHAR(20),
	TP_PARTNER		    NVARCHAR(3),
	QT_LT_OVERDUE	    INT,
	TP_PERIOD		    NVARCHAR(3),
	YN_MON			    NVARCHAR(1),
	YN_TUE			    NVARCHAR(1),
	YN_WED			    NVARCHAR(1),
	YN_THU			    NVARCHAR(1),
	YN_FRI			    NVARCHAR(1),
	TP_SEND				NVARCHAR(3),
	NO_EMP_SEND			NVARCHAR(10),
	JSON_FLAG			NVARCHAR(1)
) JS
ON JS.CD_COMPANY = AM.CD_COMPANY AND JS.CD_PARTNER = AM.CD_PARTNER AND JS.TP_PARTNER = AM.TP_PARTNER
WHERE JS.JSON_FLAG = 'U'

GO