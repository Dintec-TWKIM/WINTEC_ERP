USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_AUTO_MAIL_ORDER_STATUS]    Script Date: 2016-01-13 오후 9:02:15 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO
 
ALTER PROCEDURE [NEOE].[SP_CZ_SA_AUTO_MAIL_ORDER_STATUS]    
(            
	@P_CD_COMPANY		NVARCHAR(7),
	@P_CD_PARTNER		NVARCHAR(20),
	@P_NO_SO			NVARCHAR(20),
	@P_CD_PARTNER_GRP	NVARCHAR(500),
	@P_YN_EXCLUDE		NVARCHAR(1)
)            
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

;WITH A AS
(
    SELECT SL.CD_COMPANY, SL.NO_SO,
           SL.QT_SO,
           SL.QT_GIR,
           ISNULL(PL.DT_EXPECT, '') AS DT_EXPECT,
           (CASE WHEN ISNULL(SB.QT_STOCK, 0) = ISNULL(SB.QT_BOOK, 0) THEN LEFT(SB.DTS_UPDATE, 8) ELSE NULL END) AS DT_BOOK,
           (CASE WHEN SL.QT_SO > ISNULL(SL.QT_GI, 0) THEN SL.DT_DUEDATE ELSE NULL END) AS DT_DUEDATE,
           (CASE WHEN QL.TP_BOM = 'P' THEN ISNULL(BM.QT_BOM, 0) 
	 		                          ELSE (ISNULL(PL.QT_IO, 0) + ISNULL(SB.QT_BOOK, 0)) END) AS QT_IN 
    FROM SA_SOL SL
    JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = SL.CD_COMPANY AND QL.NO_FILE = SL.NO_SO AND QL.NO_LINE = SL.SEQ_SO
	LEFT JOIN CZ_SA_STOCK_BOOK SB ON SB.CD_COMPANY = SL.CD_COMPANY AND SB.NO_FILE = SL.NO_SO AND SB.NO_LINE = SL.SEQ_SO
	LEFT JOIN (SELECT PL.CD_COMPANY, PL.NO_SO, PL.NO_SOLINE,
	                  SUM(IL.QT_IO) AS QT_IO,
	                  MAX(DD.DT_EXPECT) AS DT_EXPECT
	           FROM PU_POL PL
	           LEFT JOIN PU_RCVL RL ON RL.CD_COMPANY = PL.CD_COMPANY AND RL.NO_PO = PL.NO_PO AND RL.NO_POLINE = PL.NO_LINE
	           LEFT JOIN MM_QTIO IL ON IL.CD_COMPANY = RL.CD_COMPANY AND IL.NO_ISURCV = RL.NO_RCV AND IL.NO_ISURCVLINE = RL.NO_LINE
	           LEFT JOIN CZ_SA_DEFERRED_DELIVERY DD ON DD.CD_COMPANY = PL.CD_COMPANY AND DD.TP_TYPE = '2' AND DD.NO_SO = PL.NO_SO AND DD.NO_KEY = PL.NO_PO AND DD.DT_LIMIT = PL.DT_LIMIT
	           GROUP BY PL.CD_COMPANY, PL.NO_SO, PL.NO_SOLINE) PL
	ON PL.CD_COMPANY = SL.CD_COMPANY AND PL.NO_SO = SL.NO_SO AND PL.NO_SOLINE = SL.SEQ_SO
	LEFT JOIN (SELECT OH.CD_COMPANY, OL.NO_PSO_MGMT, OL.NO_PSOLINE_MGMT,
	                  SUM(OL.QT_IO) AS QT_BOM
	           FROM MM_QTIOH OH
	           LEFT JOIN MM_QTIO OL ON OL.CD_COMPANY = OH.CD_COMPANY AND OL.NO_IO = OH.NO_IO
	           WHERE OH.YN_RETURN <> 'Y' 
	           AND OL.FG_PS = '1' 
	           AND OL.FG_IO = '002'
	           GROUP BY OH.CD_COMPANY, OL.NO_PSO_MGMT, OL.NO_PSOLINE_MGMT) BM
	ON BM.CD_COMPANY = SL.CD_COMPANY AND BM.NO_PSO_MGMT = SL.NO_SO AND BM.NO_PSOLINE_MGMT = SL.SEQ_SO
    WHERE SL.CD_ITEM NOT LIKE 'SD%'
	AND (ISNULL(@P_NO_SO, '') = '' OR SL.NO_SO = @P_NO_SO)
)
SELECT SH.CD_PARTNER,
	   MP.LN_PARTNER,
	   SH.NO_SO,
	   MH.NM_VESSEL,
	   SH.NO_PO_PARTNER,
	   SH.DT_SO,
	   SL.DT_DUEDATE,
	   DATEDIFF(DAY, SL.DT_DUEDATE, GETDATE()) AS QT_DELAY,
	   (CASE WHEN SL.QT_SO = SL.QT_IN THEN 'READY'
	         WHEN SL.QT_IN > 0 THEN 'PARTIAL'
	         WHEN DATEDIFF(DAY, SL.DT_DUEDATE, GETDATE()) > 0 THEN 'DELAYED'
	         ELSE 'PENDING' END) AS ST_SO,
	   (CASE WHEN SL.QT_SO = SL.QT_IN THEN 'ALL'
	         WHEN SL.QT_IN > 0 THEN 'http://tracking.dintec.co.kr/order/status.aspx?noPo=' + SH.NO_PO_PARTNER + '&ref=' + SH.NO_SO
	         ELSE '' END) AS DC_ITEM,
	   (CASE WHEN SL.QT_SO = SL.QT_IN THEN ''
			 WHEN DATEDIFF(DAY, SL.DT_DUEDATE, GETDATE()) > 0 THEN SL.DT_EXPECT 
	                                                          ELSE '' END) AS DT_EXPECT,
	   DD.TP_SEND,
	   DD.DTS_SEND,
	   DD.CD_RMK,
	   DD.DC_RMK1,
	   DD.YN_EXCLUDE,
	   MP.CD_PARTNER_GRP,
	   CD.NM_SYSDEF AS NM_PARTNER_GRP,
	   SH.DC_RMK_TEXT2
FROM SA_SOH SH
JOIN (SELECT A.CD_COMPANY, A.NO_SO,
             SUM(A.QT_SO) AS QT_SO,
             SUM(A.QT_GIR) AS QT_GIR,
             SUM(A.QT_IN) AS QT_IN,
             MAX(CASE WHEN A.QT_SO > ISNULL(A.QT_GIR, 0) THEN A.DT_DUEDATE ELSE NULL END) AS DT_DUEDATE,
	         MAX(CASE WHEN A.QT_SO <= A.QT_IN THEN NULL
					  WHEN ISNULL(A.DT_BOOK, '') >= CONVERT(CHAR(8), DATEADD(DAY, 3, A.DT_EXPECT), 112) THEN A.DT_BOOK
					  WHEN ISNULL(A.DT_EXPECT, '') <> '' THEN CONVERT(CHAR(8), DATEADD(DAY, 3, A.DT_EXPECT), 112)
	                  ELSE ISNULL(A.DT_DUEDATE, '') END) AS DT_EXPECT
      FROM A
      GROUP BY A.CD_COMPANY, A.NO_SO) SL 
ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = SH.CD_COMPANY AND MP.CD_PARTNER = SH.CD_PARTNER
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = SH.NO_IMO
LEFT JOIN CZ_SA_DEFERRED_DELIVERY DD ON DD.CD_COMPANY = SH.CD_COMPANY AND DD.TP_TYPE = '0' AND DD.NO_SO = SH.NO_SO AND DD.NO_KEY = SH.NO_SO AND DD.DT_LIMIT = SL.DT_DUEDATE
LEFT JOIN MA_CODEDTL CD ON CD.CD_COMPANY = MP.CD_COMPANY AND CD.CD_FIELD = 'MA_B000065' AND CD.CD_SYSDEF = MP.CD_PARTNER_GRP
WHERE SH.CD_COMPANY = @P_CD_COMPANY
AND (ISNULL(@P_CD_PARTNER, '') = '' OR SH.CD_PARTNER = @P_CD_PARTNER)
AND (ISNULL(@P_CD_PARTNER_GRP, '') = '' OR MP.CD_PARTNER_GRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_PARTNER_GRP)))
AND LEFT(SH.NO_SO, 2) NOT IN ('SB', 'HB')
AND ISNULL(SH.YN_CLOSE, 'N') = 'N'
AND ISNULL(DD.YN_EXCLUDE, 'N') = @P_YN_EXCLUDE
AND SL.QT_SO > ISNULL(SL.QT_GIR, 0)

GO