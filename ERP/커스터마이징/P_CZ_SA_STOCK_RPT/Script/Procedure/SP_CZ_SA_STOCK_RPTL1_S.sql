USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_STOCK_RPTL1_S]    Script Date: 2016-07-28 오후 7:36:36 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



ALTER PROCEDURE [NEOE].[SP_CZ_SA_STOCK_RPTL1_S] 
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_KEY				NVARCHAR(20),
	@P_TP_DATE			NVARCHAR(3),
	@P_DT_FROM			NVARCHAR(8),
	@P_DT_TO			NVARCHAR(8),
	@P_CD_ITEM			NVARCHAR(20),
	@P_CLS_ITEM			NVARCHAR(1000),
	@P_CLS_L			NVARCHAR(4),
	@P_CLS_M			NVARCHAR(4),
	@P_CLS_S			NVARCHAR(4)
)
AS 

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT SB.CD_COMPANY,
	   SB.NO_FILE AS NO_SO,
	   SB.NO_LINE,
	   SB.CD_ITEM,
	   MI.NM_ITEM,
	   QL.CD_ITEM_PARTNER,
	   QL.NM_ITEM_PARTNER,
	   QL.CD_SPEC,
	   MI.STND_DETAIL_ITEM,
	   MI.STND_ITEM,
	   MI.MAT_ITEM,
	   MI.CLS_L,
	   MI.CLS_M,
	   MI.CLS_S,
	   C1.NM_SYSDEF AS NM_CLS_L,
	   C2.NM_SYSDEF AS NM_CLS_M,
	   C3.NM_SYSDEF AS NM_CLS_S,
	   ISNULL(SB.QT_STOCK, 0) AS QT_STOCK,
	   ISNULL(SB.QT_BOOK, 0) AS QT_BOOK,
	   ISNULL(SB.QT_HOLD, 0) AS QT_HOLD,
	   ISNULL(GL.QT_GIR_STOCK, 0) AS QT_GIR_STOCK,
	   ISNULL(GL.QT_IO, 0) AS QT_IO,
	   ISNULL(GL.QT_IV, 0) AS QT_IV,
	   ISNULL(SB.UM_KR, 0) AS UM_STOCK,
	   ISNULL(SL.UM_SO, 0) AS UM_SO,
	   ISNULL(GL.UM_GIR, 0) AS UM_GIR,
	   ISNULL(GL.UM_IO, 0) AS UM_IO,
	   ISNULL(GL.UM_IV, 0) AS UM_IV,
	   (ISNULL(SB.QT_STOCK, 0) * ISNULL(SB.UM_KR, 0)) AS AM_STOCK,
	   (ISNULL(SB.QT_BOOK, 0) * ISNULL(SB.UM_KR, 0)) AS AM_BOOK,
	   (ISNULL(SB.QT_HOLD, 0) * ISNULL(SB.UM_KR, 0)) AS AM_HOLD,
	   (ISNULL(SB.QT_STOCK, 0) * ISNULL(SL.UM_KR_S, 0)) AS AM_SO,
	   ISNULL(GL.AM_GIR, 0) AS AM_GIR,
	   ISNULL(GL.AM_IO, 0) AS AM_IO,
	   ISNULL(GL.AM_IV, 0) AS AM_IV
FROM SA_SOL SL
JOIN CZ_SA_STOCK_BOOK SB ON SB.CD_COMPANY = SL.CD_COMPANY AND SB.NO_FILE = SL.NO_SO AND SB.NO_LINE = SL.SEQ_SO
LEFT JOIN (SELECT GL.CD_COMPANY, GL.NO_SO, GL.SEQ_SO,
	  	          SUM(ISNULL(GL.QT_GIR_STOCK, 0)) AS QT_GIR_STOCK,
	  	          SUM(CASE WHEN ISNULL(OL.QT_IO, 0) > 0 THEN ISNULL(GL.QT_GIR_STOCK, 0) ELSE 0 END) AS QT_IO,
	  	          SUM(CASE WHEN ISNULL(IL.QT_CLS, 0) > 0 THEN ISNULL(GL.QT_GIR_STOCK, 0) ELSE 0 END) AS QT_IV,
				  MAX(ISNULL(GL.UM, 0) * ISNULL(GL.RT_EXCH, 1)) AS UM_GIR,
				  MAX(OL.UM) AS UM_IO,
				  MAX(IL.UM_ITEM_CLS) AS UM_IV,
				  SUM(ISNULL(GL.QT_GIR_STOCK, 0) * (ISNULL(GL.UM, 0) * ISNULL(GL.RT_EXCH, 1))) AS AM_GIR,
				  SUM((CASE WHEN ISNULL(OL.QT_IO, 0) > 0 THEN ISNULL(GL.QT_GIR_STOCK, 0) ELSE 0 END) * ISNULL(OL.UM, 0)) AS AM_IO,
				  SUM((CASE WHEN ISNULL(IL.QT_CLS, 0) > 0 THEN ISNULL(GL.QT_GIR_STOCK, 0) ELSE 0 END) * ISNULL(IL.UM_ITEM_CLS, 0)) AS AM_IV
	  FROM SA_GIRL GL
	  LEFT JOIN MM_QTIO OL ON OL.CD_COMPANY = GL.CD_COMPANY AND OL.NO_ISURCV = GL.NO_GIR AND OL.NO_ISURCVLINE = GL.SEQ_GIR
	  LEFT JOIN SA_IVL IL ON IL.CD_COMPANY = OL.CD_COMPANY AND IL.NO_IO = OL.NO_IO AND IL.NO_IOLINE = OL.NO_IOLINE AND IL.CD_ITEM NOT LIKE 'SD%'
	  WHERE GL.QT_GIR_STOCK IS NOT NULL
	  AND GL.QT_GIR_STOCK > 0
	  AND (@P_TP_DATE NOT IN ('003', '005') OR
		   (@P_TP_DATE = '003' AND EXISTS (SELECT 1 
										   FROM MM_QTIOH OH
										   WHERE OH.CD_COMPANY = OL.CD_COMPANY
										   AND OH.NO_IO = OL.NO_IO
										   AND OH.DT_IO BETWEEN @P_DT_FROM AND @P_DT_TO)) OR
		   (@P_TP_DATE = '005' AND EXISTS (SELECT 1 
										   FROM SA_IVH IH 
										   WHERE IH.CD_COMPANY = IL.CD_COMPANY
										   AND IH.NO_IV = IL.NO_IV
										   AND IH.DT_PROCESS BETWEEN @P_DT_FROM AND @P_DT_TO)))
	  GROUP BY GL.CD_COMPANY, GL.NO_SO, GL.SEQ_SO) GL
ON GL.CD_COMPANY = SL.CD_COMPANY AND GL.NO_SO = SL.NO_SO AND GL.SEQ_SO = SL.SEQ_SO
LEFT JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = SL.CD_COMPANY AND QL.NO_FILE = SL.NO_SO AND QL.NO_LINE = SL.SEQ_SO
LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = SL.CD_COMPANY AND MI.CD_PLANT = SL.CD_PLANT AND MI.CD_ITEM = SL.CD_ITEM
LEFT JOIN MA_CODEDTL C1 ON C1.CD_COMPANY = MI.CD_COMPANY AND C1.CD_FIELD = 'MA_B000030' AND C1.CD_SYSDEF = MI.CLS_L
LEFT JOIN MA_CODEDTL C2 ON C2.CD_COMPANY = MI.CD_COMPANY AND C2.CD_FIELD = 'MA_B000031' AND C2.CD_SYSDEF = MI.CLS_M
LEFT JOIN MA_CODEDTL C3 ON C3.CD_COMPANY = MI.CD_COMPANY AND C3.CD_FIELD = 'MA_B000032' AND C3.CD_SYSDEF = MI.CLS_S
WHERE SL.CD_COMPANY = @P_CD_COMPANY
AND SL.NO_SO = @P_KEY
AND (ISNULL(@P_CLS_ITEM, '') = '' OR MI.CLS_ITEM IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CLS_ITEM)))
AND (ISNULL(@P_CD_ITEM, '') = '' OR SB.CD_ITEM LIKE @P_CD_ITEM + '%')
AND (ISNULL(@P_CLS_L, '') = '' OR MI.CLS_L = @P_CLS_L)
AND (ISNULL(@P_CLS_M, '') = '' OR MI.CLS_M = @P_CLS_M)
AND (ISNULL(@P_CLS_S, '') = '' OR MI.CLS_S = @P_CLS_S)
AND (@P_TP_DATE NOT IN ('003', '004', '005') OR
	 (@P_TP_DATE = '004' AND LEFT(SB.DTS_INSERT, 8) BETWEEN @P_DT_FROM AND @P_DT_TO) OR
	 (@P_TP_DATE IN ('003', '005') AND GL.CD_COMPANY IS NOT NULL))

GO

