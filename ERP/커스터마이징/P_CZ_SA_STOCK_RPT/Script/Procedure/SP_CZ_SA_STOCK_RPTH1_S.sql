USE [NEOE]
GO
/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_STOCK_RPTH1_S]    Script Date: 2016-07-28 오후 7:36:09 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_SA_STOCK_RPTH1_S] 
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_TP_DATE			NVARCHAR(3),
	@P_DT_FROM			NVARCHAR(8),
	@P_DT_TO			NVARCHAR(8),
	@P_NO_SO			NVARCHAR(20),
	@P_NO_SO_PRE		NVARCHAR(4),
	@P_CD_PARTNER	    NVARCHAR(20),
	@P_NO_IMO			NVARCHAR(10),
	@P_CD_ITEM			NVARCHAR(20),
	@P_CD_ITEM_MULTI	NVARCHAR(1000),
	@P_CLS_ITEM			NVARCHAR(1000),
	@P_CLS_L			NVARCHAR(4),
	@P_CLS_M			NVARCHAR(4),
	@P_CLS_S			NVARCHAR(4)
)
AS 

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

WITH A AS
(
	SELECT SH.CD_COMPANY,
		   SH.NO_SO,
		   SH.NO_EMP,
		   SH.CD_SALEGRP,
		   SH.DT_SO,
		   SH.DT_CONTRACT,
		   SH.CD_PARTNER,
		   SH.NO_IMO
	FROM SA_SOH SH
	WHERE SH.CD_COMPANY = @P_CD_COMPANY
	AND (ISNULL(@P_NO_SO, '') = '' OR SH.NO_SO = @P_NO_SO)
	AND (ISNULL(@P_NO_SO_PRE, '') = '' OR SH.NO_SO LIKE @P_NO_SO_PRE + '%')
	AND (ISNULL(@P_CD_PARTNER, '') = '' OR SH.CD_PARTNER = @P_CD_PARTNER)
	AND (ISNULL(@P_NO_IMO, '') = '' OR SH.NO_IMO = @P_NO_IMO)
	AND ((@P_TP_DATE NOT IN ('001', '002') OR 
		 (@P_TP_DATE = '001' AND SH.DT_SO BETWEEN @P_DT_FROM AND @P_DT_TO) OR
		 (@P_TP_DATE = '002' AND SH.DT_CONTRACT BETWEEN @P_DT_FROM AND @P_DT_TO)))
	AND EXISTS (SELECT 1 
			    FROM MA_CODEDTL MC
			    WHERE MC.CD_COMPANY = SH.CD_COMPANY
			    AND MC.CD_FIELD = 'CZ_SA00023'
			    AND MC.CD_SYSDEF NOT IN ('00', 'ZZ', 'T-')
				AND SH.NO_SO LIKE MC.CD_SYSDEF + '%')
)
SELECT SH.CD_COMPANY,
	   SH.NO_SO,
	   (SELECT NM_KOR FROM MA_EMP WHERE CD_COMPANY = SH.CD_COMPANY AND NO_EMP = SH.NO_EMP) AS NM_EMP,
	   (SELECT NM_SALEGRP FROM MA_SALEGRP WHERE CD_COMPANY = SH.CD_COMPANY AND CD_SALEGRP = SH.CD_SALEGRP) AS NM_SALEGRP,
	   SH.DT_SO,
	   SH.DT_CONTRACT,
	   SL.DT_IO AS DT_OUT,
	   SL.DT_IV,
	   (SELECT LN_PARTNER FROM MA_PARTNER WHERE CD_COMPANY = SH.CD_COMPANY AND CD_PARTNER = SH.CD_PARTNER) AS NM_PARTNER,
	   (SELECT NM_VESSEL FROM CZ_MA_HULL WHERE NO_IMO = SH.NO_IMO) AS NM_VESSEL,
	   ISNULL(SL.QT_STOCK, 0) AS QT_STOCK,
	   ISNULL(SL.QT_BOOK, 0) AS QT_BOOK,
	   ISNULL(SL.QT_HOLD, 0) AS QT_HOLD,
	   ISNULL(SL.QT_GIR_STOCK, 0) AS QT_GIR_STOCK,
	   ISNULL(SL.QT_IO, 0) AS QT_IO,
	   ISNULL(SL.QT_IV, 0) AS QT_IV,
	   ISNULL(SL.AM_STOCK, 0) AS AM_STOCK,
	   ISNULL(SL.AM_BOOK, 0) AS AM_BOOK,
	   ISNULL(SL.AM_HOLD, 0) AS AM_HOLD,
	   ISNULL(SL.AM_SO, 0) AS AM_SO,
	   ISNULL(SL.AM_GIR, 0) AS AM_GIR,
	   ISNULL(SL.AM_IO, 0) AS AM_IO,
	   ISNULL(SL.AM_IV, 0) AS AM_IV
FROM A SH
JOIN (SELECT SL.CD_COMPANY, SL.NO_SO,
			 MAX(GL.DT_IO) AS DT_IO,
			 MAX(GL.DT_IV) AS DT_IV,
			 SUM(SB.QT_STOCK) AS QT_STOCK,
			 SUM(SB.QT_BOOK) AS QT_BOOK,
			 SUM(SB.QT_HOLD) AS QT_HOLD,
			 SUM(GL.QT_GIR_STOCK) AS QT_GIR_STOCK,
			 SUM(GL.QT_IO) AS QT_IO,
			 SUM(GL.QT_IV) AS QT_IV,
			 SUM(ISNULL(SB.QT_STOCK, 0) * ISNULL(SB.UM_KR, 0)) AS AM_STOCK,
			 SUM(ISNULL(SB.QT_BOOK, 0) * ISNULL(SB.UM_KR, 0)) AS AM_BOOK,
			 SUM(ISNULL(SB.QT_HOLD, 0) * ISNULL(SB.UM_KR, 0)) AS AM_HOLD,
			 SUM(ISNULL(SB.QT_STOCK, 0) * ISNULL(SL.UM_KR_S, 0)) AS AM_SO,
			 SUM(GL.AM_GIR) AS AM_GIR,
			 SUM(GL.AM_IO) AS AM_IO,
			 SUM(GL.AM_IV) AS AM_IV
	  FROM SA_SOL SL
	  JOIN CZ_SA_STOCK_BOOK SB ON SB.CD_COMPANY = SL.CD_COMPANY AND SB.NO_FILE = SL.NO_SO AND SB.NO_LINE = SL.SEQ_SO
	  LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = SL.CD_COMPANY AND MI.CD_PLANT = SL.CD_PLANT AND MI.CD_ITEM = SL.CD_ITEM
	  LEFT JOIN (SELECT GL.CD_COMPANY, GL.NO_SO, GL.SEQ_SO,
					    MAX(OH.DT_IO) AS DT_IO,
						MAX(IH.DT_PROCESS) AS DT_IV,
						SUM(GL.QT_GIR_STOCK) AS QT_GIR_STOCK,
						SUM(CASE WHEN ISNULL(OL.QT_IO, 0) > 0 THEN GL.QT_GIR_STOCK ELSE 0 END) AS QT_IO,
						SUM(CASE WHEN ISNULL(IL.QT_CLS, 0) > 0 THEN GL.QT_GIR_STOCK ELSE 0 END) AS QT_IV,
						SUM(ISNULL(GL.QT_GIR_STOCK, 0) * (ISNULL(GL.UM, 0) * ISNULL(GL.RT_EXCH, 1))) AS AM_GIR,
						SUM((CASE WHEN ISNULL(OL.QT_IO, 0) > 0 THEN GL.QT_GIR_STOCK ELSE 0 END) * ISNULL(OL.UM, 0)) AS AM_IO,
						SUM((CASE WHEN ISNULL(IL.QT_CLS, 0) > 0 THEN GL.QT_GIR_STOCK ELSE 0 END) * ISNULL(IL.UM_ITEM_CLS, 0)) AS AM_IV
				 FROM SA_GIRL GL
				 LEFT JOIN MM_QTIO OL ON OL.CD_COMPANY = GL.CD_COMPANY AND OL.NO_ISURCV = GL.NO_GIR AND OL.NO_ISURCVLINE = GL.SEQ_GIR
				 LEFT JOIN MM_QTIOH OH ON OH.CD_COMPANY = OL.CD_COMPANY AND OH.NO_IO = OL.NO_IO
				 LEFT JOIN SA_IVL IL ON IL.CD_COMPANY = OL.CD_COMPANY AND IL.NO_IO = OL.NO_IO AND IL.NO_IOLINE = OL.NO_IOLINE AND IL.CD_ITEM NOT LIKE 'SD%' 
				 LEFT JOIN SA_IVH IH ON IH.CD_COMPANY = IL.CD_COMPANY AND IH.NO_IV = IL.NO_IV
				 WHERE GL.QT_GIR_STOCK IS NOT NULL
				 AND GL.QT_GIR_STOCK > 0
				 AND (@P_TP_DATE NOT IN ('003', '005') OR
				      (@P_TP_DATE = '003' AND OH.DT_IO BETWEEN @P_DT_FROM AND @P_DT_TO) OR
					  (@P_TP_DATE = '005' AND IH.DT_PROCESS BETWEEN @P_DT_FROM AND @P_DT_TO))
				 GROUP BY GL.CD_COMPANY, GL.NO_SO, GL.SEQ_SO) GL 
      ON GL.CD_COMPANY = SL.CD_COMPANY AND GL.NO_SO = SL.NO_SO AND GL.SEQ_SO = SL.SEQ_SO
	  WHERE (ISNULL(@P_CD_ITEM, '') = '' OR SL.CD_ITEM LIKE @P_CD_ITEM + '%')
	  AND (ISNULL(@P_CLS_L, '') = '' OR MI.CLS_L = @P_CLS_L)
	  AND (ISNULL(@P_CLS_M, '') = '' OR MI.CLS_M = @P_CLS_M)
	  AND (ISNULL(@P_CLS_S, '') = '' OR MI.CLS_S = @P_CLS_S)
	  AND (ISNULL(@P_CD_ITEM_MULTI, '') = '' OR SL.CD_ITEM IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_ITEM_MULTI)))
	  AND (ISNULL(@P_CLS_ITEM, '') = '' OR EXISTS (SELECT 1 
												   FROM MA_PITEM MI
												   WHERE MI.CD_COMPANY = SL.CD_COMPANY
												   AND MI.CD_ITEM = SL.CD_ITEM
												   AND MI.CD_PLANT = SL.CD_PLANT
												   AND MI.CLS_ITEM IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CLS_ITEM))))
	  AND (@P_TP_DATE NOT IN ('003', '004', '005') OR
		   (@P_TP_DATE = '004' AND LEFT(SB.DTS_INSERT, 8) BETWEEN @P_DT_FROM AND @P_DT_TO) OR
		   (@P_TP_DATE IN ('003', '005') AND GL.CD_COMPANY IS NOT NULL))
	  GROUP BY SL.CD_COMPANY, SL.NO_SO) SL
ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO

GO