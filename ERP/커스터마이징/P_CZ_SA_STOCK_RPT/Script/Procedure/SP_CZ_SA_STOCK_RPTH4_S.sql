USE [NEOE]
GO
/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_STOCK_RPTH4_S]    Script Date: 2016-07-28 오후 7:36:09 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_SA_STOCK_RPTH4_S] 
(
	@P_CD_COMPANY		NVARCHAR(7)
)
AS 

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

WITH A AS
(
	SELECT (CASE ISNULL(MC.NM_SYSDEF, '') WHEN '' THEN '기타'
							              WHEN 'HYUNDAI' THEN 'HYUNDAI' 
							              ELSE MC.NM_SYSDEF END) AS NM_CLS_M,
		    LEFT(SH.DT_SO, 6) AS DT_MONTH,
		    SUM(ISNULL(SB.QT_STOCK, 0) * ISNULL(SB.UM_KR, 0)) AS AM_STOCK
	FROM SA_SOH SH
	JOIN SA_SOL SL ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
	JOIN CZ_SA_STOCK_BOOK SB ON SB.CD_COMPANY = SL.CD_COMPANY AND SB.NO_FILE = SL.NO_SO AND SB.NO_LINE = SL.SEQ_SO
	JOIN MA_PITEM MI ON MI.CD_COMPANY = SL.CD_COMPANY AND MI.CD_PLANT = SL.CD_PLANT AND MI.CD_ITEM = SL.CD_ITEM
	LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = MI.CD_COMPANY AND MC.CD_FIELD = 'MA_B000031' AND MC.CD_SYSDEF = MI.CLS_M
	WHERE SH.CD_COMPANY = @P_CD_COMPANY
	AND LEFT(SH.DT_SO, 6) BETWEEN CONVERT(NVARCHAR(6), DATEADD(MONTH, -12, GETDATE()), 112) AND CONVERT(NVARCHAR(6), GETDATE(), 112)
	AND EXISTS (SELECT 1 
				FROM MA_CODEDTL MC
				WHERE MC.CD_COMPANY = SH.CD_COMPANY
				AND MC.CD_FIELD = 'CZ_SA00023'
				AND MC.CD_SYSDEF NOT IN ('00', 'ZZ', 'T-')
				AND SH.NO_SO LIKE MC.CD_SYSDEF + '%')
	GROUP BY MC.NM_SYSDEF, LEFT(SH.DT_SO, 6)
),
B AS
(
	SELECT A.NM_CLS_M,
		   A.DT_MONTH,
		   ISNULL(A.AM_STOCK, 0) AS AM_STOCK,
	       DATEDIFF(MONTH, CONVERT(NVARCHAR(8), DATEADD(MONTH, -12, GETDATE()), 112), A.DT_MONTH + '01') AS IDX
	FROM A
),
C AS
(
	SELECT NM_CLS_M,
		   SUM([0]) AS AM_0, 
		   SUM([1]) AS AM_1, 
		   SUM([2]) AS AM_2, 
		   SUM([3]) AS AM_3, 
		   SUM([4]) AS AM_4, 
		   SUM([5]) AS AM_5, 
		   SUM([6]) AS AM_6, 
		   SUM([7]) AS AM_7, 
		   SUM([8]) AS AM_8, 
		   SUM([9]) AS AM_9, 
		   SUM([10]) AS AM_10, 
		   SUM([11]) AS AM_11, 
		   SUM([12]) AS AM_CURRENT
	FROM B
	PIVOT (SUM(B.AM_STOCK) FOR IDX IN ([0], [1], [2], [3], [4], [5], [6], [7], [8], [9], [10], [11], [12])) AS PVT
	GROUP BY NM_CLS_M
)
SELECT C.*,
	   B1.AM_STOCK AS AM_TOTAL,
	   (CASE WHEN ISNULL(B1.QT_MONTH, 0) = 0 THEN 0 ELSE ROUND((B1.AM_STOCK / B1.QT_MONTH), 0) END) AS AM_AVG,
	   (CASE WHEN ISNULL(B1.QT_MONTH, 0) = 0 OR ISNULL(B1.AM_STOCK, 0) = 0 THEN 0 ELSE ROUND((C.AM_CURRENT / (B1.AM_STOCK / B1.QT_MONTH)) * 100, 0) END) AS RT_SALES
FROM C
LEFT JOIN (SELECT B.NM_CLS_M,
				  SUM(B.AM_STOCK) AS AM_STOCK,
				  COUNT(1) AS QT_MONTH
		   FROM B
		   WHERE B.IDX BETWEEN 0 AND 11
		   GROUP BY B.NM_CLS_M) B1
ON B1.NM_CLS_M = C.NM_CLS_M

GO