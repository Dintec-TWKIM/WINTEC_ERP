USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_STOCK_RPTL2_S]    Script Date: 2016-07-28 오후 7:36:36 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



ALTER PROCEDURE [NEOE].[SP_CZ_SA_STOCK_RPTL2_S] 
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_KEY				NVARCHAR(20),
	@P_TP_DATE			NVARCHAR(3),
	@P_DT_FROM			NVARCHAR(8),
	@P_DT_TO			NVARCHAR(8),
	@P_NO_SO			NVARCHAR(20),
	@P_NO_SO_PRE		NVARCHAR(4),
	@P_CD_PARTNER	    NVARCHAR(20),
	@P_NO_IMO			NVARCHAR(10),
	@P_CD_ITEM			NVARCHAR(20),
	@P_CLS_ITEM			NVARCHAR(1000)
)
AS 

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

WITH A AS
(
	SELECT SL.CD_COMPANY,
		   SL.NO_SO,
		   SH.NO_PO_PARTNER,
		   SH.DT_SO,
	       SH.DT_CONTRACT,
		   SH.CD_PARTNER,
		   SH.NO_IMO,
		   SL.SEQ_SO,
		   GL.DT_IO,
		   GL.DT_IV,
		   QL.CD_ITEM_PARTNER,
	       QL.NM_ITEM_PARTNER,
	       QL.CD_SPEC,
	       ISNULL(SB.QT_STOCK, 0) AS QT_STOCK,
	       ISNULL(SB.QT_BOOK, 0) AS QT_BOOK,
	       ISNULL(SB.QT_HOLD, 0) AS QT_HOLD,
	       ISNULL(GL.QT_GIR_STOCK, 0) AS QT_GIR_STOCK,
	       ISNULL(GL.QT_IO, 0) AS QT_IO,
	       ISNULL(GL.QT_IV, 0) AS QT_IV,
	       ISNULL(SB.UM_KR, 0) AS UM_STOCK,
	       ISNULL(SL.UM_KR_S, 0) AS UM_SO,
	       ISNULL(GL.UM_GIR, 0) AS UM_GIR,
	       ISNULL(GL.UM_IO, 0) AS UM_IO,
	       ISNULL(GL.UM_IV, 0) AS UM_IV,
	       (ISNULL(SB.QT_STOCK, 0) * ISNULL(SB.UM_KR, 0)) AS AM_STOCK,
	       (ISNULL(SB.QT_BOOK, 0) * ISNULL(SB.UM_KR, 0)) AS AM_BOOK,
	       (ISNULL(SB.QT_HOLD, 0) * ISNULL(SB.UM_KR, 0)) AS AM_HOLD,
	       (ISNULL(SB.QT_STOCK, 0) * ISNULL(SL.UM_KR_S, 0)) AS AM_SO,
	       ISNULL(GL.AM_GIR, 0) AS AM_GIR,
	       ISNULL(GL.AM_IO, 0) AS AM_IO,
	       ISNULL(GL.AM_IV, 0) AS AM_IV
    FROM SA_SOH SH
	JOIN SA_SOL SL ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
	JOIN CZ_SA_STOCK_BOOK SB ON SB.CD_COMPANY = SL.CD_COMPANY AND SB.NO_FILE = SL.NO_SO AND SB.NO_LINE = SL.SEQ_SO
	LEFT JOIN (SELECT GL.CD_COMPANY, GL.NO_SO, GL.SEQ_SO,
					  MAX(OH.DT_IO) AS DT_IO,
					  MAX(IH.DT_PROCESS) AS DT_IV,
					  SUM(GL.QT_GIR_STOCK) AS QT_GIR_STOCK,
					  SUM(CASE WHEN ISNULL(OL.QT_IO, 0) > 0 THEN ISNULL(GL.QT_GIR_STOCK, 0) ELSE 0 END) AS QT_IO,
					  SUM(CASE WHEN ISNULL(IL.QT_CLS, 0) > 0 THEN ISNULL(GL.QT_GIR_STOCK, 0) ELSE 0 END) AS QT_IV,
					  MAX(ISNULL(GL.UM, 0) * ISNULL(GL.RT_EXCH, 1)) AS UM_GIR,
					  MAX(OL.UM) AS UM_IO,
					  MAX(IL.UM_ITEM_CLS) AS UM_IV,
					  SUM(ISNULL(GL.QT_GIR_STOCK, 0) * (ISNULL(GL.UM, 0) * ISNULL(GL.RT_EXCH, 1))) AS AM_GIR,
					  SUM((CASE WHEN ISNULL(OL.QT_IO, 0) > 0 THEN ISNULL(GL.QT_GIR_STOCK, 0) ELSE 0 END) * ISNULL(OL.UM, 0)) AS AM_IO,
					  SUM((CASE WHEN ISNULL(IL.QT_CLS, 0) > 0 THEN ISNULL(GL.QT_GIR_STOCK, 0) ELSE 0 END) * ISNULL(IL.UM_ITEM_CLS, 0)) AS AM_IV
		       FROM SA_GIRL GL
		       LEFT JOIN MM_QTIO OL ON OL.CD_COMPANY = GL.CD_COMPANY AND OL.NO_ISURCV = GL.NO_GIR AND OL.NO_ISURCVLINE = GL.SEQ_GIR
		       LEFT JOIN MM_QTIOH OH ON OH.CD_COMPANY = OL.CD_COMPANY AND OH.NO_IO = OL.NO_IO
		       LEFT JOIN SA_IVL IL ON IL.CD_COMPANY = OL.CD_COMPANY AND IL.NO_IO = OL.NO_IO AND IL.NO_IOLINE = OL.NO_IOLINE AND IL.CD_ITEM NOT LIKE 'SD%'
			   LEFT JOIN SA_IVH IH ON IH.CD_COMPANY = IL.CD_COMPANY AND IH.NO_IV = IL.NO_IV
			   WHERE GL.QT_GIR_STOCK IS NOT NULL
			   AND GL.QT_GIR_STOCK > 0
			   AND (@P_TP_DATE NOT IN ('003', '005') OR
				    (@P_TP_DATE = '003' AND OH.DT_IO BETWEEN @P_DT_FROM AND @P_DT_TO) OR
					(@P_TP_DATE = '005' AND IH.DT_PROCESS BETWEEN @P_DT_FROM AND @P_DT_TO))
		       GROUP BY GL.CD_COMPANY, GL.NO_SO, GL.SEQ_SO) GL
    ON GL.CD_COMPANY = SL.CD_COMPANY AND GL.NO_SO = SL.NO_SO AND GL.SEQ_SO = SL.SEQ_SO
	LEFT JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = SL.CD_COMPANY AND QL.NO_FILE = SL.NO_SO AND QL.NO_LINE = SL.SEQ_SO
	LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = SL.CD_COMPANY AND MI.CD_PLANT = SL.CD_PLANT AND MI.CD_ITEM = SL.CD_ITEM
	WHERE SL.CD_COMPANY = @P_CD_COMPANY
	AND SL.CD_ITEM = @P_KEY
	AND (ISNULL(@P_NO_SO, '') = '' OR SH.NO_SO = @P_NO_SO)
	AND (ISNULL(@P_NO_SO_PRE, '') = '' OR SH.NO_SO LIKE @P_NO_SO_PRE + '%')
	AND (ISNULL(@P_CD_PARTNER, '') = '' OR SH.CD_PARTNER = @P_CD_PARTNER)
	AND (ISNULL(@P_NO_IMO, '') = '' OR SH.NO_IMO = @P_NO_IMO)
	AND (ISNULL(@P_CD_ITEM, '') = '' OR SL.CD_ITEM LIKE @P_CD_ITEM + '%')
	AND (ISNULL(@P_CLS_ITEM, '') = '' OR EXISTS (SELECT 1 
												 FROM MA_PITEM MI
												 WHERE MI.CD_COMPANY = SL.CD_COMPANY
												 AND MI.CD_ITEM = SL.CD_ITEM
												 AND MI.CD_PLANT = SL.CD_PLANT
												 AND MI.CLS_ITEM IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CLS_ITEM))))
	AND ((@P_TP_DATE = '001' AND SH.DT_SO BETWEEN @P_DT_FROM AND @P_DT_TO) OR 
		 (@P_TP_DATE = '002' AND SH.DT_CONTRACT BETWEEN @P_DT_FROM AND @P_DT_TO) OR
		 (@P_TP_DATE = '004' AND LEFT(SB.DTS_INSERT, 8) BETWEEN @P_DT_FROM AND @P_DT_TO) OR
		 (@P_TP_DATE IN ('003', '005') AND GL.CD_COMPANY IS NOT NULL))
),
B AS
(
	SELECT A.CD_COMPANY, 
		   A.NO_SO,
		   MAX(A.NO_PO_PARTNER) AS NO_PO_PARTNER,
		   MAX(A.DT_SO) AS DT_SO,
		   MAX(A.DT_CONTRACT) AS DT_CONTRACT,
		   MAX(A.CD_PARTNER) AS CD_PARTNER,
		   MAX(A.NO_IMO) AS NO_IMO,
		   MAX(A.SEQ_SO) AS SEQ_SO,
		   MAX(A.DT_IO) AS DT_OUT,
		   MAX(A.DT_IV) AS DT_IV,
		   MAX(A.CD_ITEM_PARTNER) AS CD_ITEM_PARTNER,
	       MAX(A.NM_ITEM_PARTNER) AS NM_ITEM_PARTNER,
	       MAX(A.CD_SPEC) AS CD_SPEC,
	       SUM(A.QT_STOCK) AS QT_STOCK,
	       SUM(A.QT_BOOK) AS QT_BOOK,
	       SUM(A.QT_HOLD) AS QT_HOLD,
	       SUM(A.QT_GIR_STOCK) AS QT_GIR_STOCK,
	       SUM(A.QT_IO) AS QT_IO,
	       SUM(A.QT_IV) AS QT_IV,
	       SUM(A.UM_STOCK) AS UM_STOCK,
	       SUM(A.UM_SO) AS UM_SO,
	       SUM(A.UM_GIR) AS UM_GIR,
	       SUM(A.UM_IO) AS UM_IO,
	       SUM(A.UM_IV) AS UM_IV,
	       SUM(A.AM_STOCK) AS AM_STOCK,
	       SUM(A.AM_BOOK) AS AM_BOOK,
	       SUM(A.AM_HOLD) AS AM_HOLD,
	       SUM(A.AM_SO) AS AM_SO,
	       SUM(A.AM_GIR) AS AM_GIR,
	       SUM(A.AM_IO) AS AM_IO,
	       SUM(A.AM_IV) AS AM_IV
	FROM A
	GROUP BY A.CD_COMPANY, A.NO_SO
)
SELECT @P_KEY AS CD_ITEM,
	   B.NO_SO AS NO_FILE,
	   B.NO_PO_PARTNER,
	   B.DT_SO,
	   B.DT_CONTRACT,
	   B.DT_OUT,
	   B.DT_IV,
	   (SELECT LN_PARTNER FROM MA_PARTNER WHERE CD_COMPANY = B.CD_COMPANY AND CD_PARTNER = B.CD_PARTNER) AS NM_PARTNER,
	   (SELECT NM_VESSEL FROM CZ_MA_HULL WHERE NO_IMO = B.NO_IMO) AS NM_VESSEL,
	   B.SEQ_SO AS NO_LINE,
	   B.CD_ITEM_PARTNER,
	   B.NM_ITEM_PARTNER,
	   B.CD_SPEC,
       B.QT_STOCK,
       B.QT_BOOK,
       B.QT_HOLD,
       B.QT_GIR_STOCK,
       B.QT_IO,
       B.QT_IV,
       B.UM_STOCK,
       B.UM_SO,
       B.UM_GIR,
       B.UM_IO,
       B.UM_IV,
       B.AM_STOCK,
       B.AM_BOOK,
       B.AM_HOLD,
       B.AM_SO,
       B.AM_GIR,
       B.AM_IO,
       B.AM_IV,
	   (SELECT MAX(HE.NM_MODEL) AS NM_MODEL
		FROM CZ_MA_HULL_ENGINE HE
		WHERE HE.NO_IMO = B.NO_IMO
		AND HE.CD_ENGINE = 'ME') AS NM_MODEL_ME,
	   (SELECT MAX(HE.NM_MODEL) AS NM_MODEL
		FROM CZ_MA_HULL_ENGINE HE
		WHERE HE.NO_IMO = B.NO_IMO
		AND HE.CD_ENGINE = 'GE') AS NM_MODEL_GE
FROM B

GO

