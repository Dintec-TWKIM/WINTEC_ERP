USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_TRCREDITSCH_S]    Script Date: 2016-01-29 오전 11:42:16 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [NEOE].[SP_CZ_SA_TRCREDITSCH_S]
( 
	@P_CD_COMPANY			NVARCHAR(7),
	@P_TP_TAB				NVARCHAR(1),
	@P_TP_SEARCH			NVARCHAR(3),
	@P_DT_PROCESS_FROM		NVARCHAR(8),
	@P_DT_PROCESS_TO		NVARCHAR(8),
	@P_CD_PARTNER			NVARCHAR(1000),
	@P_DT_TODAY				NVARCHAR(8),
	@P_NO_EMP				NVARCHAR(20),
	@P_BILL_PARTNER			NVARCHAR(20),
	@P_CHK_AM_MISU			NVARCHAR(1),
	@P_NO_IV				NVARCHAR(20)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

IF @P_TP_TAB = 0
BEGIN
	SELECT 'N' AS S,
		   IH.CD_COMPANY,
		   IH.NO_IV,
		   IH.NO_SO,
		   '' AS DT_SO,
		   IH.CD_PJT,
		   IH.DT_PROCESS,
		   IH.NO_PO_PARTNER,  
		   IH.CD_PARTNER,
		   MP.LN_PARTNER AS NM_PARTNER,
		   IH.NO_IMO,
		   IH.NM_VESSEL,
		   IH.BILL_PARTNER,
		   MP1.LN_PARTNER AS BILL_NM_PARTNER,
		   IL.NM_KOR AS NM_EMP,
		   IL.NM_DEPT,
		   IH.CD_EXCH,
		   IH.RT_EXCH,
		   (ISNULL(IH.AM_EX, 0) + ISNULL(IL.VAT_TAX_EX, 0)) AS AM_EX_IV,
		   (ISNULL(IH.AM_K, 0) + ISNULL(IH.VAT_TAX, 0)) AS AM_IV,
		   ISNULL(IH.AM_BAN_EX, 0) AS AM_BAN_EX,
		   ISNULL(IH.AM_BAN, 0) AS AM_BAN,
		   ISNULL(RH.AM_RCP, 0) AS AM_RCP,
		   ISNULL(RH.AM_RCP_EX, 0) AS AM_RCP_EX,
		   ((ISNULL(IH.AM_EX, 0) + ISNULL(IL.VAT_TAX_EX, 0)) - ISNULL(IH.AM_BAN_EX, 0)) AS AM_MISU_EX,
		   ((ISNULL(IH.AM_K, 0) + ISNULL(IH.VAT_TAX, 0)) - ISNULL(IH.AM_BAN, 0)) AS AM_MISU,
		   ((ISNULL(IH.AM_EX, 0) + ISNULL(IL.VAT_TAX_EX, 0)) - ISNULL(RH.AM_RCP_EX, 0)) AS AM_RCP_MISU_EX,
		   ((ISNULL(IH.AM_K, 0) + ISNULL(IH.VAT_TAX, 0)) - ISNULL(RH.AM_RCP, 0)) AS AM_RCP_MISU,

		   ISNULL(IH.AM_EX, 0) AS AM_EX,
		   ISNULL(IH.AM_K, 0) AS AM,
		   ISNULL(IL.AM_EX_CHARGE, 0) AS AM_EX_CHARGE,
		   ISNULL(IL.AM_CHARGE, 0) AS AM_CHARGE,
		   (ISNULL(IH.AM_EX, 0) - ISNULL(IL.AM_EX_CHARGE, 0)) AS AM_EX_NET,
		   (ISNULL(IH.AM_K, 0) - ISNULL(IL.AM_CHARGE, 0)) AS AM_NET,
		   RH.DT_RCP,
		   IH.DT_RCP_RSV,
		   DATEDIFF(DAY, ISNULL(IH.DT_RCP_RSV, GETDATE()), @P_DT_TODAY) AS DT,
		   IH.DC_REMARK,
		   IH.MEMO_CD
	FROM (SELECT IH.CD_COMPANY,
				 IH.NO_IV,
				 IH.CD_PARTNER,
				 IH.BILL_PARTNER,
				 IH.AM_K,
				 IH.VAT_TAX,
				 IH.DT_PROCESS,
				 IH.CD_EXCH,
				 IH.RT_EXCH,
				 IH.AM_EX,
				 IH.AM_BAN_EX,
				 IH.AM_BAN,
				 IH.DT_RCP_RSV,
				 IH.DC_REMARK,
				 IH.MEMO_CD,
				 STUFF((SELECT ',' + IL.NO_SO
				 	    FROM SA_IVL IL
				 	    WHERE IL.CD_COMPANY = IH.CD_COMPANY
				 	    AND IL.NO_IV = IH.NO_IV
				 		GROUP BY IL.NO_SO
				 	    FOR XML PATH('')),1,1,'') AS NO_SO,
				 STUFF((SELECT ',' + IL.CD_PJT
				 		FROM SA_IVL IL
				 		WHERE IL.CD_COMPANY = IH.CD_COMPANY
				 		AND IL.NO_IV = IH.NO_IV
				 		GROUP BY IL.CD_PJT
				 		FOR XML PATH('')),1,1,'') AS CD_PJT,
				 STUFF((SELECT ',' + SH.NO_PO_PARTNER
		   			    FROM SA_IVL IL
		   			    LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = IL.CD_COMPANY AND SH.NO_SO = IL.NO_SO
		   			    WHERE IL.CD_COMPANY = IH.CD_COMPANY
		   			    AND IL.NO_IV = IH.NO_IV
					    GROUP BY SH.NO_PO_PARTNER
		   			    FOR XML PATH('')),1,1,'') AS NO_PO_PARTNER,
				 STUFF((SELECT ',' + SH.NO_IMO
		   				FROM SA_IVL IL
		   				LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = IL.CD_COMPANY AND SH.NO_SO = IL.NO_SO
		   				WHERE IL.CD_COMPANY = IH.CD_COMPANY
		   				AND IL.NO_IV = IH.NO_IV
						GROUP BY SH.NO_IMO
		   				FOR XML PATH('')),1,1,'') AS NO_IMO,
				 STUFF((SELECT ',' + MH.NM_VESSEL
		   				FROM SA_IVL IL
		   				LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = IL.CD_COMPANY AND SH.NO_SO = IL.NO_SO
		   				LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = SH.NO_IMO
		   				WHERE IL.CD_COMPANY = IH.CD_COMPANY
		   				AND IL.NO_IV = IH.NO_IV
						GROUP BY MH.NM_VESSEL
		   				FOR XML PATH('')),1,1,'') AS NM_VESSEL
		  FROM SA_IVH IH
		  WHERE IH.CD_COMPANY = @P_CD_COMPANY
	      AND IH.DT_PROCESS BETWEEN @P_DT_PROCESS_FROM AND @P_DT_PROCESS_TO
	      AND IH.DT_PROCESS <= @P_DT_TODAY
	      AND (ISNULL(@P_NO_IV, '') = '' OR IH.NO_IV = @P_NO_IV)
	      AND (ISNULL(@P_CD_PARTNER, '') = '' OR IH.CD_PARTNER IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_PARTNER)))
	      AND (ISNULL(@P_NO_EMP, '') = '' OR IH.NO_EMP = @P_NO_EMP)
	      AND (ISNULL(@P_BILL_PARTNER, '') = '' OR IH.BILL_PARTNER = @P_BILL_PARTNER)) IH
	LEFT JOIN (SELECT IL.CD_COMPANY, IL.NO_IV,
					  SUM(CASE WHEN IL.CD_ITEM LIKE 'SD%' THEN IL.AM_EX_CLS ELSE 0 END) AS AM_EX_CHARGE,
					  SUM(CASE WHEN IL.CD_ITEM LIKE 'SD%' THEN IL.AM_CLS ELSE 0 END) AS AM_CHARGE,
					  SUM(ROUND((CASE WHEN ISNULL(IL.YN_RETURN, 'N') = 'Y' THEN -IL.VAT ELSE IL.VAT END) / 
							    (CASE WHEN ISNULL(IH.RT_EXCH, 0) = 0 THEN 1 ELSE IH.RT_EXCH END), 2)) AS VAT_TAX_EX, 
					  MAX(ME.NM_KOR) AS NM_KOR,
					  MAX(MD.NM_DEPT) AS NM_DEPT
			   FROM SA_IVL IL
			   LEFT JOIN SA_IVH IH ON IH.CD_COMPANY = IL.CD_COMPANY AND IH.NO_IV = IL.NO_IV
			   LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = IL.CD_COMPANY AND SH.NO_SO = IL.NO_SO
			   LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = SH.CD_COMPANY AND ME.NO_EMP = SH.NO_EMP
			   LEFT JOIN MA_DEPT MD ON MD.CD_COMPANY = ME.CD_COMPANY AND MD.CD_DEPT = ME.CD_DEPT
			   GROUP BY IL.CD_COMPANY, IL.NO_IV) IL
	ON IL.CD_COMPANY = IH.CD_COMPANY AND IL.NO_IV = IH.NO_IV
	LEFT JOIN (SELECT RD.CD_COMPANY, 
			   		  RD.NO_IV,
					  MAX(RH.DT_RCP) AS DT_RCP,
			   		  SUM(RD.AM_RCP) AS AM_RCP,
			   		  SUM(RD.AM_RCP_EX) AS AM_RCP_EX
			   FROM SA_RCPH RH
			   JOIN (SELECT RD.CD_COMPANY, RD.NO_RCP, RD.NO_TX AS NO_IV,
			   	  	 	    (ISNULL(RD.AM_RCP_TX, 0) + ISNULL(RD.AM_PL, 0)) AS AM_RCP,
			   	  	 	    ISNULL(RD.AM_RCP_TX_EX, 0) AS AM_RCP_EX 
			   		 FROM SA_RCPD RD 
			   		 UNION ALL
			   		 SELECT BD.CD_COMPANY, BD.NO_RCP, BD.NO_IV,
			   	  	 		(ISNULL(BD.AM_RCPS, 0) + ISNULL(BD.AM_PL, 0)) AS AM_RCP,
			   	  	 		ISNULL(BD.AM_RCPS_EX, 0) AS AM_RCP_EX 
			   		 FROM SA_BILLSD BD) RD
			   ON RD.CD_COMPANY = RH.CD_COMPANY AND RD.NO_RCP = RH.NO_RCP
			   WHERE RH.DT_RCP <= @P_DT_TODAY
			   AND RD.NO_IV IS NOT NULL
			   GROUP BY RD.CD_COMPANY, RD.NO_IV) RH
	ON RH.CD_COMPANY = IL.CD_COMPANY AND RH.NO_IV = IL.NO_IV
	LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = IH.CD_COMPANY AND MP.CD_PARTNER = IH.CD_PARTNER
	LEFT JOIN MA_PARTNER MP1 ON MP1.CD_COMPANY = IH.CD_COMPANY AND MP1.CD_PARTNER = IH.BILL_PARTNER
	WHERE (@P_CHK_AM_MISU = 'Y' OR (((ISNULL(IH.AM_K, 0) + ISNULL(IH.VAT_TAX, 0)) - ISNULL(RH.AM_RCP, 0)) <> 0))
END
ELSE
BEGIN
	SELECT 'N' AS S,
		   IH.CD_COMPANY,
		   IH.NO_IV,
		   IL.NO_SO,
		   IL.DT_SO,
		   IL.CD_PJT,
		   IH.DT_PROCESS,
		   IL.NO_PO_PARTNER,
		   IH.CD_PARTNER,
		   MP.LN_PARTNER AS NM_PARTNER,
		   IL.NO_IMO,
		   IL.NM_VESSEL,
		   IH.BILL_PARTNER,
		   MP1.LN_PARTNER AS BILL_NM_PARTNER,
		   IL.NO_EMP,
		   ME.NM_KOR AS NM_EMP,
		   ME.CD_DEPT,
		   MD.NM_DEPT,
		   IH.CD_EXCH,
		   IH.RT_EXCH,
		   (ISNULL(IL.AM_EX_CLS, 0) + ISNULL(IL.VAT_EX, 0)) AS AM_EX_IV,
		   (ISNULL(IL.AM_CLS, 0) + ISNULL(IL.VAT, 0)) AS AM_IV,
		   ISNULL(IH.AM_BAN_EX, 0) AS AM_BAN_EX,
		   ISNULL(IH.AM_BAN, 0) AS AM_BAN,
		   ISNULL(RH.AM_RCP, 0) AS AM_RCP,
		   ISNULL(RH.AM_RCP_EX, 0) AS AM_RCP_EX,
		   ((ISNULL(IH.AM_EX, 0) + ROUND((IH.VAT_TAX / IH.RT_EXCH), 2)) - ISNULL(IH.AM_BAN_EX, 0)) AS AM_MISU_EX,
		   ((ISNULL(IH.AM_K, 0) + ISNULL(IH.VAT_TAX, 0)) - ISNULL(IH.AM_BAN, 0)) AS AM_MISU,
		   ((ISNULL(IL.AM_EX_CLS, 0) + ISNULL(IL.VAT_EX, 0)) - ISNULL(RH.AM_RCP_EX, 0)) AS AM_RCP_MISU_EX,
		   ((ISNULL(IL.AM_CLS, 0) + ISNULL(IL.VAT, 0)) - ISNULL(RH.AM_RCP, 0)) AS AM_RCP_MISU,

		   ISNULL(IL.AM_EX_CLS, 0) AS AM_EX,
		   ISNULL(IL.AM_CLS, 0) AS AM,
		   ISNULL(IL.AM_EX_CHARGE, 0) AS AM_EX_CHARGE,
		   ISNULL(IL.AM_CHARGE, 0) AS AM_CHARGE,
		   (ISNULL(IL.AM_EX_CLS, 0) - ISNULL(IL.AM_EX_CHARGE, 0)) AS AM_NET_EX,
		   (ISNULL(IL.AM_CLS, 0) - ISNULL(IL.AM_CHARGE, 0)) AS AM_NET,

		   RH.DT_RCP,
		   IH.DT_RCP_RSV,
		   DATEDIFF(DAY, ISNULL(IH.DT_RCP_RSV, GETDATE()), @P_DT_TODAY) AS DT,
		   IH.DC_REMARK,
		   IH.MEMO_CD
	FROM SA_IVH IH
	LEFT JOIN (SELECT IL.CD_COMPANY, IL.NO_IV, IL.NO_SO, SH.NO_PO_PARTNER, IL.CD_PJT, SH.NO_IMO, MH.NM_VESSEL, SH.NO_EMP, SH.DT_SO,
					  SUM(ISNULL(CASE WHEN IL.YN_RETURN = 'Y' THEN -IL.AM_EX_CLS ELSE IL.AM_EX_CLS END, 0)) AS AM_EX_CLS,
					  SUM(ISNULL(CASE WHEN IL.YN_RETURN = 'Y' THEN -IL.AM_CLS ELSE IL.AM_CLS END, 0)) AS AM_CLS,
					  SUM(ROUND((CASE WHEN IL.YN_RETURN = 'Y' THEN -IL.VAT ELSE IL.VAT END) / IL.RT_EXCH, 2)) AS VAT_EX,
					  SUM(ISNULL(CASE WHEN IL.YN_RETURN = 'Y' THEN -IL.VAT ELSE IL.VAT END, 0)) AS VAT,
					  SUM(CASE WHEN IL.CD_ITEM LIKE 'SD%' THEN (ISNULL(IL.AM_EX_CLS, 0) + ROUND(IL.VAT / IL.RT_EXCH, 2)) ELSE 0 END) AS AM_EX_CHARGE,
					  SUM(CASE WHEN IL.CD_ITEM LIKE 'SD%' THEN (ISNULL(IL.AM_CLS, 0) + ISNULL(IL.VAT, 0)) ELSE 0 END) AS AM_CHARGE
			   FROM SA_IVL IL
			   LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = IL.CD_COMPANY AND SH.NO_SO = IL.NO_SO
			   LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = SH.NO_IMO
			   GROUP BY IL.CD_COMPANY, IL.NO_IV, IL.NO_SO, SH.NO_PO_PARTNER, IL.CD_PJT, SH.NO_IMO, MH.NM_VESSEL, SH.NO_EMP, SH.DT_SO) IL 
	ON IL.CD_COMPANY = IH.CD_COMPANY AND IL.NO_IV = IH.NO_IV
	LEFT JOIN (SELECT RH.CD_COMPANY, RH.NO_TX, FD.CD_PJT,
			   		  MAX(RH.DT_RCP) AS DT_RCP,
			   		  SUM(RH.AM_RCP_TX + RH.AM_PL) AS AM_RCP,
			   		  SUM(RH.AM_RCP_TX_EX) AS AM_RCP_EX
			   FROM (SELECT RD.CD_COMPANY, RD.NO_TX,
			   				RH.DT_RCP,
			   				RD.AM_RCP_TX,
			   				RD.AM_RCP_TX_EX,
			   				RD.AM_PL,
							RD.NO_DOCU,
							RD.NO_DOLINE
			   		 FROM SA_RCPD RD
			   		 JOIN SA_RCPH RH ON RH.CD_COMPANY = RD.CD_COMPANY AND RH.NO_RCP = RD.NO_RCP
			   		 UNION ALL
			   		 SELECT BD.CD_COMPANY, BD.NO_IV,
			   				RH.DT_RCP,
			   				BD.AM_RCPS,
			   				BD.AM_RCPS_EX,
			   				BD.AM_PL,
							BD.NO_DOCU_IV,
							BD.NO_DOLINE_IV
			   		 FROM SA_BILLSD BD
			   		 JOIN SA_RCPH RH ON RH.CD_COMPANY = BD.CD_COMPANY AND RH.NO_RCP = BD.NO_RCP) RH
			   LEFT JOIN FI_DOCU FD ON FD.CD_COMPANY = RH.CD_COMPANY AND FD.NO_DOCU = RH.NO_DOCU AND FD.NO_DOLINE = RH.NO_DOLINE
			   WHERE RH.DT_RCP <= @P_DT_TODAY
			   GROUP BY RH.CD_COMPANY, RH.NO_TX, FD.CD_PJT) RH
	ON RH.CD_COMPANY = IL.CD_COMPANY AND RH.NO_TX = IL.NO_IV AND RH.CD_PJT = IL.CD_PJT
	LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = IH.CD_COMPANY AND MP.CD_PARTNER = IH.CD_PARTNER
	LEFT JOIN MA_PARTNER MP1 ON MP1.CD_COMPANY = IH.CD_COMPANY AND MP1.CD_PARTNER = IH.BILL_PARTNER
	LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = IL.CD_COMPANY AND ME.NO_EMP = IL.NO_EMP
	LEFT JOIN MA_DEPT MD ON MD.CD_COMPANY = ME.CD_COMPANY AND MD.CD_DEPT = ME.CD_DEPT
	WHERE IH.CD_COMPANY = @P_CD_COMPANY
	AND (CASE @P_TP_SEARCH WHEN '000' THEN IH.DT_PROCESS
						   WHEN '001' THEN IL.DT_SO END) BETWEEN @P_DT_PROCESS_FROM AND @P_DT_PROCESS_TO
	AND (@P_CHK_AM_MISU = 'Y' OR (((ISNULL(IL.AM_CLS, 0) + ISNULL(IL.VAT, 0)) - ISNULL(RH.AM_RCP, 0)) <> 0))
	AND (ISNULL(@P_NO_IV, '') = '' OR IH.NO_IV = @P_NO_IV)
	AND (ISNULL(@P_CD_PARTNER, '') = '' OR IH.CD_PARTNER IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_PARTNER)))
	AND (ISNULL(@P_NO_EMP, '') = '' OR IH.NO_EMP = @P_NO_EMP)
	AND (ISNULL(@P_BILL_PARTNER, '') = '' OR IH.BILL_PARTNER = @P_BILL_PARTNER)
END

GO