USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_FORWARD_CHARGE_H_S]    Script Date: 2016-06-27 오후 5:01:58 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_SA_FORWARD_CHARGE_H_S]          
(  
	@P_CD_COMPANY		NVARCHAR(7),
	@P_CD_FORWARD		NVARCHAR(20),
	@P_TP_DELIVERY		NVARCHAR(4),
	@P_DT_MONTH			NVARCHAR(6),
	@P_NO_IO			NVARCHAR(20)
)  
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

;WITH A AS
(
    SELECT FC.CD_COMPANY,
           PC.CD_PC,
           FC.CD_PARTNER,
    	   MP.LN_PARTNER,
    	   FC.TP_DELIVERY,
    	   FC.DT_MONTH,
    	   FC.NO_MDOCU,
    	   FC.NO_MDOCU_TAX,
    	   FC.DC_RMK
    FROM CZ_SA_FORWARD_CHARGE_H FC
    JOIN MA_PARTNER MP ON MP.CD_COMPANY = FC.CD_COMPANY AND MP.CD_PARTNER = FC.CD_PARTNER
    LEFT JOIN MA_PC PC ON PC.CD_COMPANY = FC.CD_COMPANY
    WHERE FC.CD_COMPANY = @P_CD_COMPANY
    AND (ISNULL(@P_DT_MONTH, '') = '' OR FC.DT_MONTH = @P_DT_MONTH)
    AND (ISNULL(@P_CD_FORWARD, '') = '' OR FC.CD_PARTNER = @P_CD_FORWARD)
    AND (ISNULL(@P_TP_DELIVERY, '') = '' OR FC.TP_DELIVERY = @P_TP_DELIVERY)
    AND (ISNULL(@P_NO_IO, '') = '' OR EXISTS (SELECT 1 
    										  FROM CZ_SA_FORWARD_CHARGE_L FL
    										  WHERE FL.CD_COMPANY = FC.CD_COMPANY
    										  AND FL.CD_PARTNER = FC.CD_PARTNER
    										  AND FL.TP_DELIVERY = FC.TP_DELIVERY
    										  AND FL.DT_MONTH = FC.DT_MONTH
    										  AND FL.NO_IO = @P_NO_IO))
),
B AS
(
    SELECT FD.CD_COMPANY,
           FD.NO_MDOCU,
    	   FD.NO_DOCU, 
    	   FD.CD_PC,
    	   SUM(BH.AM_DOCU) AS AM_DOCU,
    	   SUM(BH.AM_BAN) AS AM_BAN 
    FROM FI_DOCU FD
    LEFT JOIN FI_BANH BH ON BH.CD_COMPANY = FD.CD_COMPANY AND BH.NO_DOCU = FD.NO_DOCU AND BH.NO_DOLINE = FD.NO_DOLINE
    GROUP BY FD.CD_COMPANY, FD.NO_MDOCU, FD.NO_DOCU, FD.CD_PC
    HAVING EXISTS (SELECT 1 
                   FROM A
                   WHERE A.CD_COMPANY = FD.CD_COMPANY
                   AND (A.NO_MDOCU = FD.NO_MDOCU OR A.NO_MDOCU_TAX = FD.NO_MDOCU)) 
)
SELECT A.CD_PC,
       A.CD_PARTNER,
       A.LN_PARTNER,
       A.TP_DELIVERY,
       A.DT_MONTH,
       A.NO_MDOCU,
       B.NO_DOCU,
	   B.AM_DOCU,
	   B.AM_BAN,
       A.NO_MDOCU_TAX,
       B1.NO_DOCU AS NO_DOCU_TAX,
	   B1.AM_DOCU AS AM_DOCU_TAX,
	   B1.AM_BAN AS AM_BAN_TAX,
       A.DC_RMK,
       ISNULL(MC.CD_FLAG1, 'N') AS YN_MULTI
FROM A
LEFT JOIN B ON B.CD_COMPANY = A.CD_COMPANY AND B.CD_PC = A.CD_PC AND B.NO_MDOCU = A.NO_MDOCU
LEFT JOIN B AS B1 ON B1.CD_COMPANY = A.CD_COMPANY AND B1.CD_PC = A.CD_PC AND B1.NO_MDOCU = A.NO_MDOCU_TAX
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = A.CD_COMPANY AND MC.CD_FIELD = 'CZ_SA00047' AND MC.CD_SYSDEF = A.TP_DELIVERY

GO