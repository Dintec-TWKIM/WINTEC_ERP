USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_CRM_CHART_E01_S]    Script Date: 2017-07-13 오후 3:13:12 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



ALTER PROCEDURE [NEOE].[SP_CZ_SA_CRM_CHART_E01_S]
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_DT_FROM			NVARCHAR(8),
	@P_DT_TO			NVARCHAR(8),
	@P_CD_PARTNER		NVARCHAR(1000),
	@P_NO_IMO			NVARCHAR(10),
	@P_NO_EMP			NVARCHAR(10),
	@P_CD_ITEMGRP		NVARCHAR(20),
	@P_CD_SALEORG		NVARCHAR(7),
	@P_CD_SALEGRP		NVARCHAR(1000),
	@P_LENGTH			NUMERIC(1)
) 
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT ME.NM_KOR AS NM_EMP,
	   SH.NO_EMP,
	   SUM(AM_SO) AS AM_SO,
	   SUM(AM_STOCK) AS AM_STOCK,
	   SUM(AM_PO) AS AM_PO,
	   SUM(AM_PROFIT) AS AM_PROFIT,
	   ROUND((CASE WHEN SUM(AM_SO) = 0 THEN 0
								       ELSE (SUM(AM_PROFIT) / SUM(AM_SO)) * 100 END), 2) AS RT_PROFIT
FROM (SELECT SH.CD_COMPANY, SH.NO_EMP,
	  		 SUM(SL.AM_SO) AS AM_SO,
	  		 SUM(SL.AM_STOCK) AS AM_STOCK,
	  		 SUM(SL.AM_PO) AS AM_PO,
			 SUM(ISNULL(SL.AM_SO, 0) - (ISNULL(SL.AM_PO, 0) + ISNULL(SL.AM_STOCK, 0))) AS AM_PROFIT
	  FROM SA_SOH SH
	  LEFT JOIN MA_SALEGRP SG ON SG.CD_COMPANY = SH.CD_COMPANY AND SG.CD_SALEGRP = SH.CD_SALEGRP
	  JOIN (SELECT SL.CD_COMPANY, SL.NO_SO,
	  			   SUM(SL.AM_KR_S) AS AM_SO,
	  			   SUM(ISNULL(SB.QT_STOCK, 0) * ISNULL(SB.UM_KR, 0)) AS AM_STOCK,
	  			   SUM(PL.AM_PO) AS AM_PO 
	  		FROM SA_SOL SL
			LEFT JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = SL.CD_COMPANY AND QL.NO_FILE = SL.NO_SO AND QL.NO_LINE = SL.SEQ_SO
	  		LEFT JOIN CZ_SA_STOCK_BOOK SB ON SB.CD_COMPANY = SL.CD_COMPANY AND SB.NO_FILE = SL.NO_SO AND SB.NO_LINE = SL.SEQ_SO
	  		LEFT JOIN (SELECT CD_COMPANY, NO_SO, NO_SOLINE,
	  		 				  SUM(AM) AS AM_PO 
	  		 	       FROM PU_POL
	  		 		   GROUP BY CD_COMPANY, NO_SO, NO_SOLINE) PL
	  		ON PL.CD_COMPANY = SL.CD_COMPANY AND PL.NO_SO = SL.NO_SO AND PL.NO_SOLINE = SL.SEQ_SO
			WHERE SL.CD_ITEM NOT LIKE 'SD%'
			AND (ISNULL(@P_CD_ITEMGRP, '') = '' OR QL.GRP_ITEM = @P_CD_ITEMGRP)
	  		GROUP BY SL.CD_COMPANY, SL.NO_SO) SL
	  ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
	  WHERE SH.CD_COMPANY = @P_CD_COMPANY
	  AND SH.DT_SO BETWEEN @P_DT_FROM AND @P_DT_TO
	  AND (ISNULL(@P_NO_EMP, '') = '' OR SH.NO_EMP = @P_NO_EMP)
	  AND (ISNULL(@P_CD_SALEORG, '') = '' OR SG.CD_SALEORG = @P_CD_SALEORG)
      AND (ISNULL(@P_CD_SALEGRP, '') = '' OR SH.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP)))
	  AND (ISNULL(@P_CD_PARTNER, '') = '' OR SH.CD_PARTNER IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_PARTNER)))
      AND (ISNULL(@P_NO_IMO, '') = '' OR SH.NO_IMO = @P_NO_IMO)
	  GROUP BY SH.CD_COMPANY, SH.NO_EMP
	  UNION ALL
	  SELECT SH.CD_COMPANY, ISNULL(SH1.NO_EMP, SH.NO_EMP) AS NO_EMP,
	  		 SUM(CASE WHEN TS.RET = 'Y' THEN -SL.AM_SO ELSE SL.AM_SO END) AS AM_SO,
	  		 0 AS AM_STOCK,
	  		 0 AS AM_PO,
			 SUM(CASE WHEN TS.RET = 'Y' THEN -SL.AM_SO ELSE SL.AM_SO END) AS AM_PROFIT
	  FROM SA_SOH SH
	  JOIN (SELECT SL.CD_COMPANY, SL.NO_SO,
	  			   MAX(SL.NO_PROJECT) AS NO_PROJECT,
	  			   SUM(SL.AM_WONAMT) AS AM_SO 
	  		FROM SA_SOL SL
			LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = SL.CD_COMPANY AND MI.CD_ITEM = SL.CD_ITEM
			WHERE SL.CD_ITEM NOT LIKE 'SD%'
			AND (ISNULL(@P_CD_ITEMGRP, '') = '' OR MI.GRP_ITEM = @P_CD_ITEMGRP)
	  		GROUP BY SL.CD_COMPANY, SL.NO_SO) SL
	  ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
	  LEFT JOIN SA_SOH SH1 ON SH1.CD_COMPANY = SH.CD_COMPANY AND SH1.NO_SO = (CASE WHEN ISNULL(SH.NO_PROJECT, '') = '' THEN SL.NO_PROJECT ELSE SH.NO_PROJECT END)
	  LEFT JOIN CZ_SA_QTNH QH ON QH.CD_COMPANY = SH.CD_COMPANY AND QH.NO_FILE = SH.NO_SO
	  LEFT JOIN SA_TPSO TS ON TS.CD_COMPANY = SH.CD_COMPANY AND TS.TP_SO = SH.TP_SO
	  LEFT JOIN MA_SALEGRP SG ON SG.CD_COMPANY = SH.CD_COMPANY AND SG.CD_SALEGRP = ISNULL(SH1.CD_SALEGRP, SH.CD_SALEGRP)
	  WHERE SH.CD_COMPANY = @P_CD_COMPANY
	  AND SH.DT_SO BETWEEN @P_DT_FROM AND @P_DT_TO
	  AND QH.NO_FILE IS NULL
	  AND (ISNULL(@P_NO_EMP, '') = '' OR ISNULL(SH1.NO_EMP, SH.NO_EMP) = @P_NO_EMP)
	  AND (ISNULL(@P_CD_SALEORG, '') = '' OR SG.CD_SALEORG = @P_CD_SALEORG)
      AND (ISNULL(@P_CD_SALEGRP, '') = '' OR ISNULL(SH1.CD_SALEGRP, SH.CD_SALEGRP) IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP)))
	  AND (ISNULL(@P_CD_PARTNER, '') = '' OR ISNULL(SH1.CD_PARTNER, SH.CD_PARTNER) IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_PARTNER)))
      AND (ISNULL(@P_NO_IMO, '') = '' OR ISNULL(SH1.NO_IMO, SH.NO_IMO) = @P_NO_IMO)
	  GROUP BY SH.CD_COMPANY, ISNULL(SH1.NO_EMP, SH.NO_EMP)
	  UNION ALL
	  SELECT PH.CD_COMPANY, PH.NO_EMP,
	  	     0 AS AM_SO,
	  	     0 AS AM_STOCK,
	  	     SUM(PL.AM_PO) AS AM_PO,
			 0 AS AM_PROFIT 
	  FROM PU_POH PH
	  JOIN (SELECT PL.CD_COMPANY, PL.NO_PO,
	  			   SUM(PL.AM) AS AM_PO 
	  		FROM PU_POL PL
			LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = PL.CD_COMPANY AND MI.CD_ITEM = PL.CD_ITEM
			WHERE PL.CD_ITEM NOT LIKE 'SD%'
			AND (ISNULL(@P_CD_ITEMGRP, '') = '' OR MI.GRP_ITEM = @P_CD_ITEMGRP)
	  		GROUP BY PL.CD_COMPANY, PL.NO_PO) PL
	  ON PL.CD_COMPANY = PH.CD_COMPANY AND PL.NO_PO = PH.NO_PO
	  LEFT JOIN CZ_SA_QTNH QH ON QH.CD_COMPANY = PH.CD_COMPANY AND QH.NO_FILE = PH.CD_PJT
	  LEFT JOIN (SELECT CD_COMPANY, NO_EMP, 
						MAX(CD_SALEGRP) AS CD_SALEGRP 
				 FROM MA_USER
				 GROUP BY CD_COMPANY, NO_EMP) MU ON MU.CD_COMPANY = PH.CD_COMPANY AND MU.NO_EMP = PH.NO_EMP
	  LEFT JOIN MA_SALEGRP SG ON SG.CD_COMPANY = MU.CD_COMPANY AND SG.CD_SALEGRP = MU.CD_SALEGRP
	  WHERE PH.CD_COMPANY = @P_CD_COMPANY
	  AND PH.DT_PO BETWEEN @P_DT_FROM AND @P_DT_TO
	  AND QH.NO_FILE IS NULL
	  AND (ISNULL(@P_NO_EMP, '') = '' OR PH.NO_EMP = @P_NO_EMP)
	  AND (ISNULL(@P_CD_SALEORG, '') = '' OR SG.CD_SALEORG = @P_CD_SALEORG)
      AND (ISNULL(@P_CD_SALEGRP, '') = '' OR MU.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP)))
	  AND (ISNULL(@P_CD_PARTNER, '') = '' OR 1 <> 1)
      AND (ISNULL(@P_NO_IMO, '') = '' OR 1 <> 1)
	  GROUP BY PH.CD_COMPANY, PH.NO_EMP) SH
JOIN MA_EMP ME ON ME.CD_COMPANY = SH.CD_COMPANY AND ME.NO_EMP = SH.NO_EMP
WHERE ISNULL(ME.DT_RETIRE, '00000000') = '00000000'
GROUP BY SH.CD_COMPANY, SH.NO_EMP, ME.NM_KOR
ORDER BY SUM(AM_PROFIT) DESC

GO