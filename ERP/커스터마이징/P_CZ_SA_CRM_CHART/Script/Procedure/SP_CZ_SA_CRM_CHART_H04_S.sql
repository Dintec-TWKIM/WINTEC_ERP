USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_CRM_CHART_H04_S]    Script Date: 2017-07-13 오후 3:13:12 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



ALTER PROCEDURE [NEOE].[SP_CZ_SA_CRM_CHART_H04_S]
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_DT_FROM			NVARCHAR(8),
	@P_DT_TO			NVARCHAR(8),
	@P_CD_PARTNER		NVARCHAR(1000),
	@P_NO_IMO			NVARCHAR(10),
	@P_NO_EMP			NVARCHAR(10),
	@P_CD_ITEMGRP		NVARCHAR(20),
	@P_CD_SALEORG		NVARCHAR(7),
	@P_CD_SALEGRP		NVARCHAR(1000),
	@P_LENGTH			NUMERIC(1)
) 
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT QH.NO_FILE,
	   QH.DT_QTN,
	   SH.DT_SO,
	   QH.CD_PARTNER,
	   MP.LN_PARTNER,
	   MH.NO_IMO,
	   MH.NM_VESSEL,
	   MH.NO_HULL,
	   QH.NO_EMP,
	   ME.NM_KOR AS NM_EMP,
	   ISNULL(COUNT(1), 0) AS QT_ITEM_QTN,
	   ISNULL(SUM(CASE WHEN SL.CD_COMPANY IS NOT NULL THEN 1 ELSE 0 END), 0) AS QT_ITEM_SO,
	   ISNULL(SUM(QL.AM_KR_S), 0) AS AM_QTN,
	   ISNULL(SUM(SL.AM_KR_S), 0) AS AM_SO,
	   ISNULL(SUM(PL.AM), 0) AS AM_PO,
	   ISNULL(SUM(ISNULL(SB.QT_STOCK, 0) * ISNULL(SB.UM_KR, 0)), 0) AS AM_STOCK,
	   (ISNULL(SUM(SL.AM_KR_S), 0) - (ISNULL(SUM(PL.AM), 0) + SUM(ISNULL(SB.QT_STOCK, 0) * ISNULL(SB.UM_KR, 0)))) AS AM_PROFIT,
	   (CASE WHEN ISNULL(SUM(SL.AM_KR_S), 0) = 0 THEN 0
	   											 ELSE ROUND((ISNULL(SUM(SL.AM_KR_S), 0) - (ISNULL(SUM(PL.AM), 0) + SUM(ISNULL(SB.QT_STOCK, 0) * ISNULL(SB.UM_KR, 0)))) / ISNULL(SUM(SL.AM_KR_S), 0) * 100, 2) END) AS RT_PROFIT 
FROM CZ_SA_QTNH QH
LEFT JOIN MA_SALEGRP SG ON SG.CD_COMPANY = QH.CD_COMPANY AND SG.CD_SALEGRP = QH.CD_SALEGRP
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = QH.CD_COMPANY AND MP.CD_PARTNER = QH.CD_PARTNER
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = QH.NO_IMO
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = QH.CD_COMPANY AND ME.NO_EMP = QH.NO_EMP
LEFT JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = QH.CD_COMPANY AND QL.NO_FILE = QH.NO_FILE
LEFT JOIN SA_SOL SL ON SL.CD_COMPANY = QL.CD_COMPANY AND SL.NO_SO = QL.NO_FILE AND SL.SEQ_SO = QL.NO_LINE
LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = SL.CD_COMPANY AND SH.NO_SO = SL.NO_SO
LEFT JOIN CZ_SA_STOCK_BOOK SB ON SB.CD_COMPANY = SL.CD_COMPANY AND SB.NO_FILE = SL.NO_SO AND SB.NO_LINE = SL.SEQ_SO
LEFT JOIN (SELECT PL.CD_COMPANY, PL.NO_SO, PL.NO_SOLINE,
				  SUM(PL.AM) AS AM 
		   FROM PU_POL PL
		   GROUP BY PL.CD_COMPANY, PL.NO_SO, PL.NO_SOLINE) PL
ON PL.CD_COMPANY = SL.CD_COMPANY AND PL.NO_SO = SL.NO_SO AND PL.NO_SOLINE = SL.SEQ_SO
WHERE QH.CD_COMPANY = @P_CD_COMPANY
AND QH.DT_QTN BETWEEN @P_DT_FROM AND @P_DT_TO
AND ISNULL(QL.CD_ITEM, '') NOT LIKE 'SD%'
AND (ISNULL(@P_CD_PARTNER, '') = '' OR QH.CD_PARTNER IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_PARTNER)))
AND (ISNULL(@P_NO_IMO, '') = '' OR QH.NO_IMO = @P_NO_IMO)
AND (ISNULL(@P_NO_EMP, '') = '' OR QH.NO_EMP = @P_NO_EMP)
AND (ISNULL(@P_CD_ITEMGRP, '') = '' OR QL.GRP_ITEM = @P_CD_ITEMGRP)
AND (ISNULL(@P_CD_SALEORG, '') = '' OR SG.CD_SALEORG = @P_CD_SALEORG)
AND (ISNULL(@P_CD_SALEGRP, '') = '' OR QH.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP)))
GROUP BY QH.NO_FILE, QH.DT_QTN, SH.DT_SO, QH.CD_PARTNER, MP.LN_PARTNER, MH.NO_IMO, MH.NM_VESSEL, MH.NO_HULL, QH.NO_EMP, ME.NM_KOR
ORDER BY QH.DT_QTN DESC

GO