USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_CRM_CHART_C01_S]    Script Date: 2017-07-13 오후 3:13:12 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



ALTER PROCEDURE [NEOE].[SP_CZ_SA_CRM_CHART_C01_S]
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_DT_FROM			NVARCHAR(8),
	@P_DT_TO			NVARCHAR(8),
	@P_CD_PARTNER		NVARCHAR(1000),
	@P_NO_IMO			NVARCHAR(10),
	@P_NO_EMP			NVARCHAR(10),
	@P_CD_ITEMGRP		NVARCHAR(20),
	@P_CD_SALEORG		NVARCHAR(7),
	@P_CD_SALEGRP		NVARCHAR(1000),
	@P_LENGTH			NUMERIC(1)
) 
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT LEFT(SH.DT_SO, @P_LENGTH) AS DT_SO,
	   SUM(1) AS QT_SO,
	   SUM(CASE WHEN CH.NO_CLAIM IS NOT NULL THEN 1 ELSE 0 END) AS QT_CLAIM,
	   SUM(SL.AM_SO) AS AM_SO,
	   SUM(SL.AM_CLAIM) AS AM_CLAIM,
	   ROUND((SUM(CASE WHEN CH.NO_CLAIM IS NOT NULL THEN 1.0 ELSE 0 END) / SUM(1.0)) * 100, 2) AS RT_CLAIM_QT,
	   ROUND((SUM(SL.AM_CLAIM) / SUM(SL.AM_SO)) * 100, 2) AS RT_CLAIM_AM 
FROM SA_SOH SH
LEFT JOIN CZ_SA_CLAIMH CH ON CH.CD_COMPANY = SH.CD_COMPANY AND CH.NO_SO = SH.NO_SO
LEFT JOIN MA_SALEGRP SG ON SG.CD_COMPANY = SH.CD_COMPANY AND SG.CD_SALEGRP = SH.CD_SALEGRP
JOIN (SELECT SL.CD_COMPANY, SL.NO_SO,
	  		 SUM(SL.AM_KR_S) AS AM_SO,
			 SUM(CL.AM_CLAIM) AS AM_CLAIM
	  FROM SA_SOL SL
	  LEFT JOIN CZ_SA_CLAIML CL ON CL.CD_COMPANY = SL.CD_COMPANY AND CL.NO_SO = SL.NO_SO AND CL.SEQ_SO = SL.SEQ_SO
	  LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = SL.CD_COMPANY AND MI.CD_PLANT = SL.CD_PLANT AND MI.CD_ITEM = SL.CD_ITEM
	  WHERE (ISNULL(@P_CD_ITEMGRP, '') = '' OR MI.GRP_ITEM = @P_CD_ITEMGRP)
	  GROUP BY SL.CD_COMPANY, SL.NO_SO) SL
ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
WHERE SH.CD_COMPANY = @P_CD_COMPANY
AND SH.DT_SO BETWEEN @P_DT_FROM AND @P_DT_TO
AND (ISNULL(@P_CD_PARTNER, '') = '' OR SH.CD_PARTNER IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_PARTNER)))
AND (ISNULL(@P_NO_IMO, '') = '' OR SH.NO_IMO = @P_NO_IMO)
AND (ISNULL(@P_NO_EMP, '') = '' OR SH.NO_EMP = @P_NO_EMP)
AND (ISNULL(@P_CD_SALEORG, '') = '' OR SG.CD_SALEORG = @P_CD_SALEORG)
AND (ISNULL(@P_CD_SALEGRP, '') = '' OR SH.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SALEGRP)))
GROUP BY LEFT(SH.DT_SO, @P_LENGTH)
ORDER BY LEFT(SH.DT_SO, @P_LENGTH)

GO