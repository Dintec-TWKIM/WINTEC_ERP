USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_IV_PJT_RPTH_S]    Script Date: 2016-06-23 오후 4:50:48 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO




ALTER PROCEDURE [NEOE].[SP_CZ_SA_IV_PJT_RPTH_S]  
(
    @P_CD_COMPANY			NVARCHAR(7),
	@P_DT_FROM				NVARCHAR(8),
	@P_DT_TO				NVARCHAR(8),
	@P_CD_PARTNER			NVARCHAR(20),
	@P_NO_EMP				NVARCHAR(20),
	@P_NO_PROJECT			NVARCHAR(20),
	@P_CD_CC				NVARCHAR(1000),
	@P_DT_REQUIRED			NUMERIC(10, 0),
	@P_YN_FREE				NVARCHAR(1),
	@P_YN_CLOSE				NVARCHAR(1)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DECLARE @V_TO_DAY NVARCHAR(8)

SET @V_TO_DAY = CONVERT(NVARCHAR(8), GETDATE(), 112)

SELECT PJ.NO_PROJECT,
	   PJ.NM_PROJECT,
	   SH.TP_SO,
	   ST.NM_SO,
	   SH.CD_PARTNER,
	   MP.LN_PARTNER,
	   SH.NO_IMO,
	   MH.NM_VESSEL,
	   SH.CD_SALEGRP,
	   SG.NM_SALEGRP,
	   SG.CD_CC,
	   MC.NM_CC,
	   SH.NO_EMP,
	   ME.NM_KOR,
	   PJ.DT_CHANGE,
	   FD1.DT_ACCT AS DT_ACCT_SA,
	   FD3.DT_ACCT AS DT_ACCT_PU,
	   DATEDIFF(DAY, ISNULL(FD3.DT_ACCT, @V_TO_DAY), ISNULL(FD1.DT_ACCT, @V_TO_DAY)) AS DT_ACCT_DIFF,
	   ISNULL(SL.AM_SO, 0) AS AM_SO,
	   (ISNULL(PL.AM_PO, 0) + ISNULL(SB.AM_STOCK, 0)) AS AM_PO,
	   ISNULL(IL.AM_CLS, 0) AS AM_SA_IV,
	   (ISNULL(IL1.AM_CLS, 0) + ISNULL(SB.AM_BOOK, 0)) AS AM_PU_IV,
	   ISNULL(FD1.AM_SALES, 0) AS AM_SALES,
	   ISNULL(FD2.AM_PROFIT, 0) AS AM_PROFIT,
	   ISNULL(FD3.AM_PURCHASE, 0) AS AM_PURCHASE,
	   ISNULL(FD4.AM_CHARGE, 0) AS AM_CHARGE,
	   ((ISNULL(FD1.AM_SALES, 0) + ISNULL(FD2.AM_PROFIT, 0)) - (ISNULL(FD3.AM_PURCHASE, 0) + ISNULL(SB.AM_BOOK, 0) + ISNULL(FD4.AM_CHARGE, 0))) AS AM_TOT_PROFIT,
	   (CASE WHEN (ISNULL(FD1.AM_SALES, 0) + ISNULL(FD2.AM_PROFIT, 0)) = 0 THEN 0
																		   ELSE ROUND((1 - ((ISNULL(FD3.AM_PURCHASE, 0) + ISNULL(SB.AM_BOOK, 0) + ISNULL(FD4.AM_CHARGE, 0)) / (ISNULL(FD1.AM_SALES, 0) + ISNULL(FD2.AM_PROFIT, 0)))) * 100, 2) END) AS RT_PROFIT,
	   ISNULL(SL.QT_SO, 0) AS QT_SO,
	   ISNULL(SL.QT_OUT, 0) AS QT_OUT,
	   (CASE WHEN ISNULL(SL.QT_SO, 0) = ISNULL(SL.QT_OUT, 0) THEN 'Y'
			 WHEN ISNULL(SL.QT_OUT, 0) > 0					 THEN 'P' 
															 ELSE 'N' END) AS STA_OUT,
	   SH.DC_RMK_CONTRACT,
	   STUFF((SELECT DISTINCT CHAR(10) + '[' + WD.NO_GIR + '] ' + WD.DC_RMK2 
			  FROM SA_GIRL GL
			  LEFT JOIN CZ_SA_GIRH_WORK_DETAIL WD ON WD.CD_COMPANY = GL.CD_COMPANY AND WD.NO_GIR = GL.NO_GIR
			  WHERE GL.CD_COMPANY = PJ.CD_COMPANY
			  AND GL.NO_SO = PJ.NO_PROJECT
			  AND ISNULL(WD.DC_RMK2, '') <> ''
			  FOR XML PATH('')),1,1,'') AS DC_RMK2,
	   WH.CD_RMK,
	   WH.DC_RMK,
	   SH.DC_RMK1,
	   SH.DC_RMK_TEXT2,
	   ISNULL(SH.DT_SO, PJ.DT_CHANGE) AS DT_SO,
	   PJ.DC_RMK AS DC_RMK_PJT
FROM SA_PROJECTH PJ
LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = PJ.CD_COMPANY AND SH.NO_SO = PJ.NO_PROJECT
LEFT JOIN FI_GWDOCU GW ON GW.CD_COMPANY = 'K100' AND GW.CD_PC = '010000' AND GW.NO_DOCU = SH.NO_DOCU
LEFT JOIN CZ_MA_WORKFLOWH WH ON WH.CD_COMPANY = PJ.CD_COMPANY AND WH.NO_KEY = PJ.NO_PROJECT AND WH.TP_STEP = '19'
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = PJ.CD_COMPANY AND MP.CD_PARTNER = ISNULL(SH.CD_PARTNER, PJ.CD_PARTNER)
LEFT JOIN MA_SALEGRP SG ON SG.CD_COMPANY = PJ.CD_COMPANY AND SG.CD_SALEGRP = ISNULL(SH.CD_SALEGRP, PJ.CD_SALEGRP)
LEFT JOIN MA_CC MC ON MC.CD_COMPANY = SG.CD_COMPANY AND MC.CD_CC = SG.CD_CC
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = PJ.CD_COMPANY AND ME.NO_EMP = ISNULL(SH.NO_EMP, PJ.NO_EMP)
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = SH.NO_IMO
LEFT JOIN SA_TPSO ST ON ST.CD_COMPANY = SH.CD_COMPANY AND ST.TP_SO = SH.TP_SO
LEFT JOIN (SELECT SL.CD_COMPANY, SL.NO_PROJECT,
				  SUM(CASE WHEN ST.RET = 'Y' THEN -SL.AM_WONAMT ELSE SL.AM_WONAMT END) AS AM_SO,
				  SUM(CASE WHEN ST.RET = 'Y' THEN -SL.QT_SO ELSE SL.QT_SO END) AS QT_SO,
				  SUM(OL.QT_IO) AS QT_OUT 
		   FROM SA_SOL SL
		   LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = SL.CD_COMPANY AND SH.NO_SO = SL.NO_SO
		   LEFT JOIN SA_TPSO ST ON ST.CD_COMPANY = SL.CD_COMPANY AND ST.TP_SO = SH.TP_SO
		   LEFT JOIN MM_EJTP ET ON ET.CD_COMPANY = SL.CD_COMPANY AND ET.CD_QTIOTP = SL.TP_GI
		   LEFT JOIN (SELECT OL.CD_COMPANY, OL.NO_PSO_MGMT, OL.NO_PSOLINE_MGMT,
							 SUM(CASE WHEN OH.YN_RETURN = 'Y' THEN -OL.QT_IO ELSE OL.QT_IO END) AS QT_IO 
					  FROM MM_QTIO OL
					  JOIN SA_GIRL GL ON GL.CD_COMPANY = OL.CD_COMPANY AND GL.NO_GIR = OL.NO_ISURCV AND GL.SEQ_GIR = OL.NO_ISURCVLINE
					  LEFT JOIN MM_QTIOH OH ON OH.CD_COMPANY = OL.CD_COMPANY AND OH.NO_IO = OL.NO_IO
					  WHERE OL.FG_PS = '2'
					  GROUP BY OL.CD_COMPANY, OL.NO_PSO_MGMT, OL.NO_PSOLINE_MGMT) OL 
		   ON OL.CD_COMPANY = SL.CD_COMPANY AND OL.NO_PSO_MGMT = SL.NO_SO AND OL.NO_PSOLINE_MGMT = SL.SEQ_SO
		   WHERE (@P_YN_FREE = 'N' OR (@P_YN_FREE = 'Y' AND ET.YN_AM = 'Y'))
		   GROUP BY SL.CD_COMPANY, SL.NO_PROJECT) SL
ON SL.CD_COMPANY = PJ.CD_COMPANY AND SL.NO_PROJECT = PJ.NO_PROJECT
LEFT JOIN (SELECT PH.CD_COMPANY, PH.CD_PJT,
				  SUM(CASE WHEN PT.YN_RETURN = 'Y' THEN -PL.AM ELSE PL.AM END) AS AM_PO 
		   FROM PU_POH PH
		   LEFT JOIN PU_TPPO PT ON PT.CD_COMPANY = PH.CD_COMPANY AND PT.CD_TPPO = PH.CD_TPPO
		   LEFT JOIN MM_EJTP ET ON ET.CD_COMPANY = PT.CD_COMPANY AND ET.CD_QTIOTP = PT.FG_TPRCV
		   LEFT JOIN PU_POL PL ON PL.CD_COMPANY = PH.CD_COMPANY AND PL.NO_PO = PH.NO_PO
		   WHERE (@P_YN_FREE = 'N' OR (@P_YN_FREE = 'Y' AND ET.YN_AM = 'Y'))
		   GROUP BY PH.CD_COMPANY, PH.CD_PJT) PL
ON PL.CD_COMPANY = PJ.CD_COMPANY AND PL.CD_PJT = PJ.NO_PROJECT
LEFT JOIN (SELECT CD_COMPANY, NO_FILE,
		   		  SUM(QT_STOCK * UM_KR) AS AM_STOCK,
		   		  SUM(QT_BOOK * UM_KR) AS AM_BOOK 
		   FROM CZ_SA_STOCK_BOOK
		   GROUP BY CD_COMPANY, NO_FILE) SB
ON SB.CD_COMPANY = PJ.CD_COMPANY AND SB.NO_FILE = PJ.NO_PROJECT
LEFT JOIN (SELECT IL.CD_COMPANY, IL.CD_PJT, 
				  SUM(CASE WHEN IL.YN_RETURN = 'Y' THEN -IL.AM_CLS ELSE IL.AM_CLS END) AS AM_CLS 
		   FROM SA_IVL IL
		   GROUP BY IL.CD_COMPANY, IL.CD_PJT) IL
ON IL.CD_COMPANY = PJ.CD_COMPANY AND IL.CD_PJT = PJ.NO_PROJECT
LEFT JOIN (SELECT IL.CD_COMPANY, IL.CD_PJT, 
				  SUM(CASE WHEN IL.YN_RETURN = 'Y' THEN -IL.AM_CLS ELSE IL.AM_CLS END) AS AM_CLS 
		   FROM PU_IVL IL
		   GROUP BY IL.CD_COMPANY, IL.CD_PJT) IL1
ON IL1.CD_COMPANY = PJ.CD_COMPANY AND IL1.CD_PJT = PJ.NO_PROJECT
LEFT JOIN (SELECT FD.CD_COMPANY, FD.CD_PJT,
				  MAX(FD.DT_ACCT) AS DT_ACCT, 
				  SUM(FD.AM_CR + FD.AM_DR) AS AM_SALES 
		   FROM FI_DOCU FD
		   LEFT JOIN FI_ACCTCODE AC ON AC.CD_COMPANY = FD.CD_COMPANY AND AC.CD_ACCT = FD.CD_ACCT
		   WHERE AC.CD_ACGRP = '510'
		   GROUP BY FD.CD_COMPANY, FD.CD_PJT) FD1
ON FD1.CD_COMPANY = PJ.CD_COMPANY AND FD1.CD_PJT = PJ.NO_PROJECT
LEFT JOIN (SELECT FD.CD_COMPANY, FD.CD_PJT, 
				  SUM(FD.AM_CR + FD.AM_DR) AS AM_PROFIT 
		   FROM FI_DOCU FD
		   LEFT JOIN FI_ACCTCODE AC ON AC.CD_COMPANY = FD.CD_COMPANY AND AC.CD_ACCT = FD.CD_ACCT
		   WHERE AC.CD_ACGRP = '560'
		   GROUP BY FD.CD_COMPANY, FD.CD_PJT) FD2
ON FD2.CD_COMPANY = PJ.CD_COMPANY AND FD2.CD_PJT = PJ.NO_PROJECT
LEFT JOIN (SELECT FD.CD_COMPANY, FD.CD_PJT,
				  MAX(FD.DT_ACCT) AS DT_ACCT, 
				  SUM(FD.AM_CR + FD.AM_DR) AS AM_PURCHASE 
		   FROM FI_DOCU FD
		   LEFT JOIN FI_ACCTCODE AC ON AC.CD_COMPANY = FD.CD_COMPANY AND AC.CD_ACCT = FD.CD_ACCT
		   WHERE AC.CD_ACGRP = '112'
		   GROUP BY FD.CD_COMPANY, FD.CD_PJT) FD3
ON FD3.CD_COMPANY = PJ.CD_COMPANY AND FD3.CD_PJT = PJ.NO_PROJECT
LEFT JOIN (SELECT FD.CD_COMPANY, FD.CD_PJT, 
				  SUM(FD.AM_CR + FD.AM_DR) AS AM_CHARGE 
		   FROM FI_DOCU FD
		   LEFT JOIN FI_ACCTCODE AC ON AC.CD_COMPANY = FD.CD_COMPANY AND AC.CD_ACCT = FD.CD_ACCT
		   WHERE (AC.CD_ACGRP = '520' OR AC.CD_ACGRP = '540' OR AC.CD_ACGRP = '570')
		   GROUP BY FD.CD_COMPANY, FD.CD_PJT) FD4
ON FD4.CD_COMPANY = PJ.CD_COMPANY AND FD4.CD_PJT = PJ.NO_PROJECT
WHERE PJ.CD_COMPANY = @P_CD_COMPANY
AND PJ.DT_CHANGE BETWEEN @P_DT_FROM AND @P_DT_TO
AND (@P_YN_CLOSE = 'N' OR (@P_YN_CLOSE = 'Y' AND (ISNULL(SH.YN_CLOSE, 'N') <> 'Y' OR GW.ST_STAT <> '1'))) 
AND (@P_YN_FREE = 'N' OR (@P_YN_FREE = 'Y' AND EXISTS (SELECT 1 
													   FROM SA_SOL SL
													   LEFT JOIN MM_EJTP ET ON ET.CD_COMPANY = SL.CD_COMPANY AND ET.CD_QTIOTP = SL.TP_GI
													   WHERE SL.CD_COMPANY = PJ.CD_COMPANY
													   AND SL.NO_PROJECT = PJ.NO_PROJECT
													   AND ET.YN_AM = 'Y')))
AND (ISNULL(@P_CD_PARTNER, '') = '' OR SH.CD_PARTNER = @P_CD_PARTNER)
AND (ISNULL(@P_NO_EMP, '') = '' OR SH.NO_EMP = @P_NO_EMP)
AND (ISNULL(@P_NO_PROJECT, '') = '' OR PJ.NO_PROJECT LIKE @P_NO_PROJECT + '%')
AND (ISNULL(@P_CD_CC, '') = '' OR SG.CD_CC IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_CC)))
AND (@P_DT_REQUIRED = 0 OR (DATEDIFF(DAY, ISNULL(FD3.DT_ACCT, @V_TO_DAY), ISNULL(FD1.DT_ACCT, @V_TO_DAY)) >= @P_DT_REQUIRED))

GO