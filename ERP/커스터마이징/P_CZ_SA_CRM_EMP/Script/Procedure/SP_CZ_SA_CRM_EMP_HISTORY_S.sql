USE [NEOE]
GO
/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_CUST_REL_MNG_TIME_LINE_S]    Script Date: 2017-05-25 오후 1:06:53 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_SA_CRM_EMP_HISTORY_S]
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_NO_EMP			NVARCHAR(10),
	@P_TP_GUBUN			NVARCHAR(3),
	@P_DT_START			NVARCHAR(8),
	@P_DT_END			NVARCHAR(8)
) 
AS
 
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

IF @P_TP_GUBUN = '001'
BEGIN
	SELECT QH.NO_FILE,
		   QH.CD_PARTNER,
		   MP.LN_PARTNER AS NM_PARTNER,
		   MH.NM_VESSEL,
		   IG.NM_ITEMGRP,
		   QH.DT_INQ,
		   QH.DT_QTN,
		   SH.DT_SO,
		   1.0 AS QT_QTN,
		   (CASE WHEN SH.CD_COMPANY IS NULL THEN 0 ELSE 1.0 END) AS QT_SO,
		   QL.AM_SO_QTN,
		   QL.AM_PO_QTN,
		   QL.AM_SO,
		   QL.AM_PO,
		   QL.AM_STOCK,
		   (ISNULL(QL.AM_SO_QTN, 0) - ISNULL(QL.AM_PO_QTN, 0)) AS AM_PROFIT_QTN,
		   (ISNULL(QL.AM_SO, 0) - (ISNULL(QL.AM_PO, 0) + ISNULL(QL.AM_STOCK, 0))) AS AM_PROFIT,
		   ROUND(CASE WHEN ISNULL(QL.AM_SO_QTN, 0) = 0 THEN 0 
		   									           ELSE ((ISNULL(QL.AM_SO_QTN, 0) - ISNULL(QL.AM_PO_QTN, 0)) / ISNULL(QL.AM_SO_QTN, 0)) * 100 END, 2) AS RT_PROFIT_QTN,
		   ROUND(CASE WHEN ISNULL(QL.AM_SO, 0) = 0 THEN 0 
		   									       ELSE ((ISNULL(QL.AM_SO, 0) - (ISNULL(QL.AM_PO, 0) + ISNULL(QL.AM_STOCK, 0))) / ISNULL(QL.AM_SO, 0)) * 100 END, 2) AS RT_PROFIT
	FROM CZ_SA_QTNH QH
	LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = QH.CD_COMPANY AND SH.NO_SO = QH.NO_FILE
	JOIN (SELECT QL.CD_COMPANY, QL.NO_FILE,
				 MAX(QL.GRP_ITEM) AS GRP_ITEM,
				 SUM(QL.AM_KR_S) AS AM_SO_QTN,
				 SUM(QL.AM_KR_P) AS AM_PO_QTN,
				 SUM(SL.AM_KR_S) AS AM_SO,
				 SUM(ISNULL(SB.UM_KR, 0) * ISNULL(SB.QT_STOCK, 0)) AS AM_STOCK,
				 SUM(PL.AM_PO) AS AM_PO
		  FROM CZ_SA_QTNL QL
		  LEFT JOIN SA_SOL SL ON SL.CD_COMPANY = QL.CD_COMPANY AND SL.NO_SO = QL.NO_FILE AND SL.SEQ_SO = QL.NO_LINE
		  LEFT JOIN CZ_SA_STOCK_BOOK SB ON SB.CD_COMPANY = SL.CD_COMPANY AND SB.NO_FILE = SL.NO_SO AND SB.NO_LINE = SL.SEQ_SO
		  LEFT JOIN (SELECT CD_COMPANY, NO_SO, NO_SOLINE,
						    SUM(AM) AS AM_PO 
					 FROM PU_POL
					 GROUP BY CD_COMPANY, NO_SO, NO_SOLINE) PL
		  ON PL.CD_COMPANY = SL.CD_COMPANY AND PL.NO_SO = SL.NO_SO AND PL.NO_SOLINE = SL.SEQ_SO
		  WHERE ISNULL(QL.CD_ITEM, '') NOT LIKE 'SD%'
		  GROUP BY QL.CD_COMPANY, QL.NO_FILE) QL
	ON QL.CD_COMPANY = QH.CD_COMPANY AND QL.NO_FILE = QH.NO_FILE
	LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = QH.CD_COMPANY AND MP.CD_PARTNER = QH.CD_PARTNER
	LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = QH.NO_IMO
	LEFT JOIN MA_ITEMGRP IG ON IG.CD_COMPANY = QL.CD_COMPANY AND IG.USE_YN = 'Y' AND IG.CD_ITEMGRP = QL.GRP_ITEM
	WHERE QH.CD_COMPANY = @P_CD_COMPANY
	AND QH.NO_EMP = @P_NO_EMP
	AND QH.DT_QTN BETWEEN @P_DT_START AND @P_DT_END
END
ELSE IF @P_TP_GUBUN = '002'
BEGIN
	SELECT QH.NO_FILE,
		   QH.CD_PARTNER,
		   MP.LN_PARTNER AS NM_SUPPLIER,
		   MI.NM_ITEMGRP,
		   QH.DT_INQ,
		   QH.DT_QTN,
		   QL.DT_PO,
		   QL.DT_IN,
		   QL.LT,
		   DATEDIFF(DAY, QL.DT_PO, QL.DT_IN) AS LT_IN,
		   1 AS QT_INQ,
		   (CASE WHEN QL.CD_COMPANY IS NULL THEN 0 ELSE 1 END) AS QT_QTN,
		   (CASE WHEN QL.DT_PO IS NULL THEN 0 ELSE 1 END) AS QT_PO,
		   QH.AM_INQ,
		   QL.AM_PO_QTN,
		   QL.AM_SO_QTN,
		   QL.AM_SO,
		   QL.AM_SO_PO,
		   QL.AM_PO,
		   QL.AM_STOCK,
		   (ISNULL(QL.AM_SO_QTN, 0) - ISNULL(QL.AM_PO_QTN, 0)) AS AM_PROFIT_QTN,
		   (ISNULL(QL.AM_SO_PO, 0) - ISNULL(QL.AM_PO, 0)) AS AM_PROFIT_PO,
		   (ISNULL(QL.AM_SO, 0) - (ISNULL(QL.AM_PO, 0) + ISNULL(QL.AM_STOCK, 0))) AS AM_PROFIT,
		   ROUND(CASE WHEN ISNULL(QL.AM_SO_QTN, 0) = 0 THEN 0
												       ELSE ((ISNULL(QL.AM_SO_QTN, 0) - ISNULL(QL.AM_PO_QTN, 0)) / ISNULL(QL.AM_SO_QTN, 0)) * 100 END, 2) AS RT_PROFIT_QTN,
		   ROUND(CASE WHEN ISNULL(QL.AM_SO_PO, 0) = 0 THEN 0
												      ELSE ((ISNULL(QL.AM_SO_PO, 0) - ISNULL(QL.AM_PO_QTN, 0)) / ISNULL(QL.AM_SO_PO, 0)) * 100 END, 2) AS RT_PROFIT_PO,
		   ROUND(CASE WHEN ISNULL(QL.AM_SO, 0) = 0 THEN 0
												   ELSE ((ISNULL(QL.AM_SO, 0) - (ISNULL(QL.AM_PO, 0) + ISNULL(QL.AM_STOCK, 0))) / ISNULL(QL.AM_SO, 0)) * 100 END, 2) AS RT_PROFIT
	FROM (SELECT QH.CD_COMPANY, QH.NO_FILE, QH.CD_PARTNER, 
				 QH1.NO_EMP, QH.DT_INQ, QH.DT_QTN,
				 MAX(QL1.GRP_ITEM) AS GRP_ITEM,
				 SUM(QL.AM_KR) AS AM_INQ
		  FROM CZ_PU_QTNH QH
		  JOIN CZ_SA_QTNH QH1 ON QH1.CD_COMPANY = QH.CD_COMPANY AND QH1.NO_FILE = QH.NO_FILE
		  JOIN CZ_PU_QTNL QL ON QL.CD_COMPANY = QH.CD_COMPANY AND QL.NO_FILE = QH.NO_FILE AND QL.CD_PARTNER = QH.CD_PARTNER
		  JOIN CZ_SA_QTNL QL1 ON QL1.CD_COMPANY = QL.CD_COMPANY AND QL1.NO_FILE = QL.NO_FILE AND QL1.NO_LINE = QL.NO_LINE
		  WHERE ISNULL(QL1.TP_BOM, 'S') IN ('S', 'P')
		  GROUP BY QH.CD_COMPANY, QH.NO_FILE, QH.CD_PARTNER,
				   QH1.NO_EMP, QH.DT_INQ, QH.DT_QTN) QH
	LEFT JOIN (SELECT QL.CD_COMPANY, QL.NO_FILE, QL.CD_PARTNER,
			   	      MAX(PH.DT_PO) AS DT_PO,
			   	      MAX(IL.DT_IO) AS DT_IN,
					  MAX(PL.LT) AS LT,
			   	      SUM(QL1.AM_KR_P) AS AM_PO_QTN,
			   	      SUM(QL1.AM_KR_S) AS AM_SO_QTN,
			   	      SUM(SL.AM_KR_S) AS AM_SO,
					  SUM(ISNULL(SL.UM_KR_S, 0) * (ISNULL(SL.QT_SO, 0) - ISNULL(SB.QT_STOCK, 0))) AS AM_SO_PO,
			   	      SUM(PL.AM) AS AM_PO,
			   	      SUM(SB.UM_KR * SB.QT_STOCK) AS AM_STOCK  
			   FROM CZ_PU_QTNL QL
			   JOIN CZ_SA_QTNL QL1 ON QL1.CD_COMPANY = QL.CD_COMPANY AND QL1.NO_FILE = QL.NO_FILE AND QL1.NO_LINE = QL.NO_LINE
			   LEFT JOIN SA_SOL SL ON SL.CD_COMPANY = QL.CD_COMPANY AND SL.NO_SO = QL.NO_FILE AND SL.SEQ_SO = QL.NO_LINE
			   LEFT JOIN CZ_SA_STOCK_BOOK SB ON SB.CD_COMPANY = QL.CD_COMPANY AND SB.NO_FILE = QL.NO_FILE AND SB.NO_LINE = QL.NO_LINE
			   LEFT JOIN PU_POL PL ON PL.CD_COMPANY = QL.CD_COMPANY AND PL.NO_SO = QL.NO_FILE AND PL.NO_LINE = QL.NO_LINE
			   LEFT JOIN PU_POH PH ON PH.CD_COMPANY = PL.CD_COMPANY AND PH.NO_PO = PL.NO_PO
			   LEFT JOIN (SELECT IH.CD_COMPANY, IL.NO_PSO_MGMT, IL.NO_PSOLINE_MGMT,
			   				     MAX(IH.DT_IO) AS DT_IO 
			   		      FROM MM_QTIOH IH
			   		      LEFT JOIN MM_QTIO IL ON IL.CD_COMPANY = IH.CD_COMPANY AND IL.NO_IO = IH.NO_IO
			   		      GROUP BY IH.CD_COMPANY, IL.NO_PSO_MGMT, IL.NO_PSOLINE_MGMT) IL
			   ON IL.CD_COMPANY = PL.CD_COMPANY AND IL.NO_PSO_MGMT = PL.NO_PO AND IL.NO_PSOLINE_MGMT = PL.NO_LINE
			   WHERE QL.YN_CHOICE = 'Y'
			   GROUP BY QL.CD_COMPANY, QL.NO_FILE, QL.CD_PARTNER) QL
	ON QL.CD_COMPANY = QH.CD_COMPANY AND QL.NO_FILE = QH.NO_FILE AND QL.CD_PARTNER = QH.CD_PARTNER
	LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = QH.CD_COMPANY AND MP.CD_PARTNER = QH.CD_PARTNER
	LEFT JOIN MA_ITEMGRP MI ON MI.CD_COMPANY = QH.CD_COMPANY AND MI.USE_YN = 'Y' AND MI.CD_ITEMGRP = QH.GRP_ITEM
	WHERE QH.CD_COMPANY = @P_CD_COMPANY
	AND QH.NO_EMP = @P_NO_EMP
	AND QH.DT_QTN BETWEEN @P_DT_START AND @P_DT_END
END

GO