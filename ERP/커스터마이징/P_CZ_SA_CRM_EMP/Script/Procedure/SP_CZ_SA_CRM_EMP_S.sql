USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_CUST_REL_MNGH_S]    Script Date: 2017-05-23 오전 9:44:28 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



ALTER PROCEDURE [NEOE].[SP_CZ_SA_CRM_EMP_S]
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_NO_EMP			NVARCHAR(20),
	@P_DT_START			NVARCHAR(8),
	@P_DT_END			NVARCHAR(8)
) 
AS
 
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT ME.NO_EMP, 
	   (CASE WHEN ISNULL(ME.NM_CHIN, '') = '' AND ISNULL(ME.NM_ENG, '') = '' THEN ISNULL(ME.NM_KOR, '')
			 WHEN ISNULL(ME.NM_CHIN, '') = '' AND ISNULL(ME.NM_ENG, '') <> '' THEN ISNULL(ME.NM_KOR, '') + ' (' + ISNULL(ME.NM_ENG, '') + ')'
			 WHEN ISNULL(ME.NM_CHIN, '') <> '' AND ISNULL(ME.NM_ENG, '') = '' THEN ISNULL(ME.NM_KOR, '') + ' (' + ISNULL(ME.NM_CHIN, '') + ')'
			 ELSE ISNULL(ME.NM_KOR, '') + ' (' + ISNULL(ME.NM_CHIN, '') + ', ' + ISNULL(ME.NM_ENG, '') + ')' END) AS NM_EMP,
	   ME.CD_DEPT,
	   MD.NM_DEPT,
	   ME.CD_DUTY_RANK,
	   CD.NM_SYSDEF AS NM_DUTY_RANK,
	   ME.DT_GENTER,
	   (CASE WHEN ME.DT_ENTER = 00000000 THEN ''
			 ELSE (CONVERT(VARCHAR, DATEDIFF(DAY, ME.DT_GENTER, GETDATE()) / 365) + ' 년 ' + 
				   CONVERT(VARCHAR, (DATEDIFF(DAY, ME.DT_GENTER, GETDATE()) % 365) / 30) + ' 개월 ' +
				   CONVERT(VARCHAR, (DATEDIFF(DAY, ME.DT_GENTER, GETDATE()) % 365) % 30) + ' 일') END) AS DT_WORKING_PERIOD,
	   ME.NO_POST_CUR,
	   ME.DC_ADDRESS_CUR1,
	   ME.DC_ADDRESS_CUR2,
	   ME.NO_TEL,
	   ME.NO_TEL_EMER AS NO_HP,
	   ME.NO_EMAIL,
	   ME.DC_RMK1 AS NO_FAX,
	   HP.DC_PHOTO,
	   ISNULL(QH.AM_SO, 0) AS AM_SO,
	   (ISNULL(QH.AM_SO, 0) - (ISNULL(QH.AM_PO, 0) + ISNULL(QH.AM_STOCK, 0))) AS AM_PROFIT,
	   (CASE WHEN ISNULL(QH.AM_SO, 0) = 0 THEN 0
										  ELSE ROUND(((ISNULL(QH.AM_SO, 0) - (ISNULL(QH.AM_PO, 0) + ISNULL(QH.AM_STOCK, 0))) / ISNULL(QH.AM_SO, 0)) * 100, 2) END) AS RT_PROFIT,
	   (CASE WHEN ISNULL(QH.QT_QTN, 0) = 0 THEN 0
									        ELSE ROUND((ISNULL(QH.QT_SO, 0) / ISNULL(QH.QT_QTN, 0)) * 100, 2) END) AS RT_SO
FROM MA_EMP ME
LEFT JOIN HR_PHOTO HP ON HP.CD_COMPANY = ME.CD_COMPANY AND HP.NO_EMP = ME.NO_EMP
LEFT JOIN MA_DEPT MD ON MD.CD_COMPANY = ME.CD_COMPANY AND MD.CD_DEPT = ME.CD_DEPT
LEFT JOIN MA_CODEDTL CD ON CD.CD_COMPANY = ME.CD_COMPANY AND CD.CD_FIELD = 'HR_H000002' AND CD.CD_SYSDEF = ME.CD_DUTY_RANK
LEFT JOIN (SELECT QH.CD_COMPANY, 
		   		  QH.NO_EMP,
		   		  SUM(1.0) AS QT_QTN,
		   		  SUM(CASE WHEN SH.CD_COMPANY IS NULL THEN 0 ELSE 1.0 END) AS QT_SO,
		   		  SUM(QL.AM_SO) AS AM_SO,
		   		  SUM(QL.AM_STOCK) AS AM_STOCK,
		   		  SUM(QL.AM_PO) AS AM_PO
		   FROM CZ_SA_QTNH QH
		   LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = QH.CD_COMPANY AND SH.NO_SO = QH.NO_FILE
		   JOIN (SELECT QL.CD_COMPANY, QL.NO_FILE,
		   		 	    SUM(SL.AM_KR_S) AS AM_SO,
		   		 	    SUM(ISNULL(SB.QT_STOCK, 0) * ISNULL(SB.UM_KR, 0)) AS AM_STOCK,
		   		 	    SUM(PL.AM_PO) AS AM_PO 
		   		 FROM CZ_SA_QTNL QL
		   		 LEFT JOIN SA_SOL SL ON SL.CD_COMPANY = QL.CD_COMPANY AND SL.NO_SO = QL.NO_FILE AND SL.SEQ_SO = QL.NO_LINE
		   		 LEFT JOIN CZ_SA_STOCK_BOOK SB ON SB.CD_COMPANY = QL.CD_COMPANY AND SB.NO_FILE = QL.NO_FILE AND SB.NO_LINE = QL.NO_LINE
		   		 LEFT JOIN (SELECT PL.CD_COMPANY, PL.NO_SO, PL.NO_SOLINE,
		   		 		 		   SUM(PL.AM) AS AM_PO 
		   		 			FROM PU_POL PL
		   		 		    GROUP BY PL.CD_COMPANY, PL.NO_SO, PL.NO_SOLINE) PL
		   		 ON PL.CD_COMPANY = SL.CD_COMPANY AND PL.NO_SO = SL.NO_SO AND PL.NO_SOLINE = SL.SEQ_SO
		   		 WHERE ISNULL(QL.CD_ITEM, '') NOT LIKE 'SD%'
		   		 GROUP BY QL.CD_COMPANY, QL.NO_FILE) QL
		   ON QL.CD_COMPANY = QH.CD_COMPANY AND QL.NO_FILE = QH.NO_FILE
		   WHERE QH.DT_QTN BETWEEN @P_DT_START AND @P_DT_END
		   GROUP BY QH.CD_COMPANY, QH.NO_EMP) QH
ON QH.CD_COMPANY = ME.CD_COMPANY AND QH.NO_EMP = ME.NO_EMP
WHERE ME.CD_COMPANY = @P_CD_COMPANY
AND ME.NO_EMP = @P_NO_EMP

GO

