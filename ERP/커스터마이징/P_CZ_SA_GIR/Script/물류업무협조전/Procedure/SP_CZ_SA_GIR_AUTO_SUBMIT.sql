USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_GIR_AUTO_SUBMIT]    Script Date: 2016-04-08 오후 3:19:19 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

--EXEC SP_CZ_SA_GIR_AUTO_SUBMIT 'TEST', 'BE16000064'	 
ALTER PROCEDURE [NEOE].[SP_CZ_SA_GIR_AUTO_SUBMIT]
(
	@P_CD_COMPANY    NVARCHAR(7),
	@P_NO_SO		 NVARCHAR(20)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

UPDATE SA_SOH
SET CD_USERDEF1 = 'RUN'
WHERE CD_COMPANY = @P_CD_COMPANY
AND NO_SO = @P_NO_SO

BEGIN TRAN SP_CZ_SA_GIR_AUTO_SUBMIT
BEGIN TRY

DECLARE @V_ERRMSG   VARCHAR(255),   -- ERROR 메시지
		@V_DT_CAL	NVARCHAR(8)

SELECT TOP 1 @V_DT_CAL = ISNULL(DT_CAL, CONVERT(CHAR(8), GETDATE(), 112))
FROM MA_CALENDAR
WHERE CD_COMPANY = @P_CD_COMPANY
AND FG1_HOLIDAY = 'W'
AND DT_CAL >= CONVERT(CHAR(8), GETDATE(), 112)
ORDER BY DT_CAL

CREATE TABLE #SA_GIRH
(
	CD_COMPANY	NVARCHAR(7),
	NO_GIR		NVARCHAR(20)
)

INSERT INTO #SA_GIRH
SELECT SL.CD_COMPANY, GH.NO_GIR
	   --SL.NO_SO, 
	   --ISNULL(SUM(SL.QT_SO), 0) AS QT_SO,
	   --ISNULL(SUM(SL.QT_PO), 0) AS QT_PSO,
	   --ISNULL(SUM(PL.QT_PO), 0) AS QT_PO,
	   --ISNULL(SUM(SB.QT_STOCK), 0) AS QT_STOCK,
	   --ISNULL(SUM(IL.QT_IO), 0) AS QT_IN 
FROM SA_SOL SL
LEFT JOIN PU_POL PL ON PL.CD_COMPANY = SL.CD_COMPANY AND PL.NO_SO = SL.NO_SO AND PL.NO_SOLINE = SL.SEQ_SO
LEFT JOIN MM_QTIO IL ON IL.CD_COMPANY = PL.CD_COMPANY AND IL.NO_PSO_MGMT = PL.NO_PO AND IL.NO_PSOLINE_MGMT = PL.NO_LINE AND IL.FG_PS = '1'
LEFT JOIN CZ_SA_STOCK_BOOK SB ON SB.CD_COMPANY = SL.CD_COMPANY AND SB.NO_FILE = SL.NO_SO AND SB.NO_LINE = SL.SEQ_SO
LEFT JOIN SA_GIRL GL ON GL.CD_COMPANY = SL.CD_COMPANY AND GL.NO_SO = SL.NO_SO AND GL.SEQ_SO = SL.SEQ_SO
LEFT JOIN SA_GIRH GH ON GH.CD_COMPANY = GL.CD_COMPANY AND GH.NO_GIR = GL.NO_GIR
LEFT JOIN CZ_SA_GIRH_WORK_DETAIL WD ON WD.CD_COMPANY = GH.CD_COMPANY AND WD.NO_GIR = GH.NO_GIR
WHERE SL.CD_COMPANY = @P_CD_COMPANY
AND SL.NO_SO = @P_NO_SO 
AND ISNULL(GH.STA_GIR, '') = ''
AND WD.YN_AUTO_SUBMIT = 'Y'
AND SL.CD_ITEM NOT LIKE 'SD%'
GROUP BY SL.CD_COMPANY, SL.NO_SO, GH.NO_GIR
HAVING ISNULL(SUM(SL.QT_SO), 0) = (ISNULL(SUM(SL.QT_PO), 0) + ISNULL(SUM(SB.QT_STOCK), 0))
AND ISNULL(SUM(PL.QT_PO), 0) = ISNULL(SUM(IL.QT_IO), 0)

IF EXISTS (SELECT 1 FROM #SA_GIRH)
BEGIN
	UPDATE GH
	SET GH.STA_GIR = 'O',
		GH.DT_GIR = CONVERT(CHAR(8), GETDATE(), 112),
		GH.ID_UPDATE = 'AUTO',
		GH.DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
	FROM SA_GIRH GH
	JOIN #SA_GIRH TG ON TG.CD_COMPANY = GH.CD_COMPANY AND TG.NO_GIR = GH.NO_GIR
	
	UPDATE GH
	SET GH.DT_START = @V_DT_CAL,
		GH.DT_COMPLETE = CONVERT(CHAR(8), DATEADD(DD, GH.DT_AUTO_COMPLETE, @V_DT_CAL), 112),
		GH.DTS_SUBMIT = NEOE.SF_SYSDATE(GETDATE()),
		GH.ID_UPDATE = 'AUTO',
		GH.DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
	FROM CZ_SA_GIRH_WORK_DETAIL GH
	JOIN #SA_GIRH TG ON TG.CD_COMPANY = GH.CD_COMPANY AND TG.NO_GIR = GH.NO_GIR	

	SELECT ('협조전번호 : ' + GH.NO_GIR + CHAR(13) +
		    '메인카테고리 : ' + ISNULL(MC.NM_SYSDEF, '') + CHAR(13) +
		    '서브카테고리 : ' + ISNULL(MC1.NM_SYSDEF, '') + CHAR(13) +
		    '협조내용 : ' + ISNULL(MC2.NM_SYSDEF, '')) AS MSG	    
	FROM SA_GIRH GH
	JOIN #SA_GIRH TG ON TG.CD_COMPANY = GH.CD_COMPANY AND TG.NO_GIR = GH.NO_GIR
	LEFT JOIN CZ_SA_GIRH_WORK_DETAIL WD ON WD.CD_COMPANY = GH.CD_COMPANY AND WD.NO_GIR = GH.NO_GIR
	LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = WD.CD_COMPANY AND MC.CD_FIELD = 'CZ_SA00006' AND MC.CD_SYSDEF = WD.CD_MAIN_CATEGORY
	LEFT JOIN MA_CODEDTL MC1 ON MC1.CD_COMPANY = WD.CD_COMPANY AND MC1.CD_FIELD = MC.CD_FLAG1 AND MC1.CD_SYSDEF = WD.CD_SUB_CATEGORY
	LEFT JOIN MA_CODEDTL MC2 ON MC2.CD_COMPANY = WD.CD_COMPANY AND MC2.CD_FIELD = 'CZ_SA00032' AND MC2.CD_SYSDEF = WD.CD_RMK
	
	UPDATE SA_SOH
	SET CD_USERDEF1 = 'DONE'
	WHERE CD_COMPANY = @P_CD_COMPANY
	AND NO_SO = @P_NO_SO 
END

DROP TABLE #SA_GIRH

COMMIT TRAN SP_CZ_SA_GIR_AUTO_SUBMIT

END TRY
BEGIN CATCH
	ROLLBACK TRAN SP_CZ_SA_GIR_AUTO_SUBMIT
	SELECT @V_ERRMSG = ERROR_MESSAGE()
	GOTO ERROR
END CATCH

RETURN
ERROR: RAISERROR(@V_ERRMSG, 16, 1)

GO