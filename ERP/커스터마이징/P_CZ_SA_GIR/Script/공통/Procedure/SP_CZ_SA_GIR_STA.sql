USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_GIR_STA]    Script Date: 12/2/2015 10:10:47 AM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



ALTER PROCEDURE [NEOE].[SP_CZ_SA_GIR_STA]  
(  
    @P_CD_COMPANY   NVARCHAR(7),
    @P_NO_GIR       NVARCHAR(20),
	@P_YN_PACK		NVARCHAR(1),
	@P_STA_GIR		NVARCHAR(1),
	@P_YN_SUBMIT	NVARCHAR(1),
	@P_ID_UPDATE	NVARCHAR(15)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DECLARE @V_STA_GIR_OLD		NVARCHAR(1),
		@V_MAIN_CATEGORY	NVARCHAR(3),
		@V_SUB_CATEGORY		NVARCHAR(3),
		@V_ERRMSG			VARCHAR(255)   -- ERROR 메시지

IF @P_YN_PACK = 'Y'
BEGIN
	IF EXISTS (SELECT GH.NO_GIR, GH.CD_PARTNER, GL.CD_PARTNER, GL.NO_SO
			   FROM CZ_SA_GIRH_PACK GH
			   JOIN (SELECT GL.CD_COMPANY, GL.NO_GIR, SH.CD_PARTNER,
			                MAX(SH.NO_SO) AS NO_SO
			         FROM CZ_SA_GIRL_PACK GL
			         JOIN SA_SOH SH ON SH.CD_COMPANY = GL.CD_COMPANY AND SH.NO_SO = GL.NO_SO
			         GROUP BY GL.CD_COMPANY, GL.NO_GIR, SH.CD_PARTNER) GL
			   ON GL.CD_COMPANY = GH.CD_COMPANY AND GL.NO_GIR = GH.NO_GIR AND GL.CD_PARTNER <> GH.CD_PARTNER
			   WHERE GH.CD_COMPANY = @P_CD_COMPANY
			   AND GH.NO_GIR = @P_NO_GIR)
	BEGIN
		SELECT @V_ERRMSG = '매출처가 다른 수주건이 포함되어 있습니다.'
		GOTO ERROR
	END

	SELECT @V_STA_GIR_OLD = GH.STA_GIR
	FROM CZ_SA_GIRH_PACK GH
	WHERE GH.CD_COMPANY = @P_CD_COMPANY
	AND GH.NO_GIR = @P_NO_GIR

	IF (@P_STA_GIR = 'D' AND @V_STA_GIR_OLD <> 'R') OR 
	   (@P_STA_GIR = 'R' AND @V_STA_GIR_OLD <> 'D') OR
	   (@P_STA_GIR = 'O' AND @V_STA_GIR_OLD <> '') OR
	   (@P_STA_GIR = '' AND @V_STA_GIR_OLD <> 'O')
	BEGIN
		SELECT @V_ERRMSG = '상태를 변경 할 수 없습니다. (이전상태 : ' + @V_STA_GIR_OLD + ', 변경상태 : ' + @P_STA_GIR + ')'                     
		GOTO ERROR
	END

	IF (@V_STA_GIR_OLD = 'O' AND @P_STA_GIR = '' AND EXISTS (SELECT 1 
															 FROM CZ_SA_GIRH_PACK_DETAIL
															 WHERE CD_COMPANY = @P_CD_COMPANY
															 AND NO_GIR = @P_NO_GIR
															 AND ISNULL(DTS_PRINT, '') <> ''))
	BEGIN
		SELECT @V_ERRMSG = '협조전 출력 대상이기 때문에 상태를 변경 할 수 없습니다.\n(납품의뢰현황 -> 출력대상해제 후 상태 변경 가능 합니다.)'                     
		GOTO ERROR
	END

	UPDATE CZ_SA_GIRH_PACK
	SET STA_GIR = @P_STA_GIR,
	    ID_UPDATE = @P_ID_UPDATE,
		DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
	WHERE CD_COMPANY = @P_CD_COMPANY
	AND NO_GIR = @P_NO_GIR

	IF @P_YN_SUBMIT = 'Y'
	BEGIN
		UPDATE PD
		SET PD.DTS_SUBMIT = NEOE.SF_SYSDATE(GETDATE()),
		    PD.ID_UPDATE = @P_ID_UPDATE,
			PD.DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
		FROM CZ_SA_GIRH_PACK_DETAIL PD
		WHERE PD.CD_COMPANY = @P_CD_COMPANY
		AND PD.NO_GIR = @P_NO_GIR
	END
END
ELSE
BEGIN
	IF EXISTS (SELECT GH.NO_GIR, GH.CD_PARTNER, GL.CD_PARTNER, GL.NO_SO
			   FROM SA_GIRH GH
			   JOIN (SELECT GL.CD_COMPANY, GL.NO_GIR, SH.CD_PARTNER,
			                MAX(SH.NO_SO) AS NO_SO
			         FROM SA_GIRL GL
			         JOIN SA_SOH SH ON SH.CD_COMPANY = GL.CD_COMPANY AND SH.NO_SO = GL.NO_SO
			         GROUP BY GL.CD_COMPANY, GL.NO_GIR, SH.CD_PARTNER) GL
			   ON GL.CD_COMPANY = GH.CD_COMPANY AND GL.NO_GIR = GH.NO_GIR AND GL.CD_PARTNER <> GH.CD_PARTNER
			   WHERE GH.CD_COMPANY = @P_CD_COMPANY
			   AND GH.NO_GIR = @P_NO_GIR)
	BEGIN
		SELECT @V_ERRMSG = '매출처가 다른 수주건이 포함되어 있습니다.'
		GOTO ERROR
	END

	SELECT @V_STA_GIR_OLD = GH.STA_GIR,
		   @V_MAIN_CATEGORY = WD.CD_MAIN_CATEGORY,
		   @V_SUB_CATEGORY = WD.CD_SUB_CATEGORY
	FROM SA_GIRH GH
	LEFT JOIN CZ_SA_GIRH_WORK_DETAIL WD ON WD.CD_COMPANY = GH.CD_COMPANY AND WD.NO_GIR = GH.NO_GIR 
	WHERE GH.CD_COMPANY = @P_CD_COMPANY
	AND GH.NO_GIR = @P_NO_GIR

	IF (@P_STA_GIR = 'D' AND @V_STA_GIR_OLD <> 'R') OR 
	   (@P_STA_GIR = 'R' AND (@V_MAIN_CATEGORY = '003' AND @V_SUB_CATEGORY = '006' AND @V_STA_GIR_OLD <> '') AND @V_STA_GIR_OLD <> 'D') OR
	   (@P_STA_GIR = 'O' AND @V_STA_GIR_OLD <> '') OR
	   (@P_STA_GIR = '' AND @V_STA_GIR_OLD <> 'O')
	BEGIN
		SELECT @V_ERRMSG = '상태를 변경 할 수 없습니다. (이전상태 : ' + @V_STA_GIR_OLD + ', 변경상태 : ' + @P_STA_GIR + ')'                     
		GOTO ERROR
	END

	IF (@V_STA_GIR_OLD = 'O' AND @P_STA_GIR = '' AND EXISTS (SELECT 1 
															 FROM CZ_SA_GIRH_WORK_DETAIL
															 WHERE CD_COMPANY = @P_CD_COMPANY
															 AND NO_GIR = @P_NO_GIR
															 AND ISNULL(DTS_PRINT, '') <> ''))
	BEGIN
		SELECT @V_ERRMSG = '협조전 출력 대상이기 때문에 상태를 변경 할 수 없습니다.\n(납품의뢰현황 -> 출력대상해제 후 상태 변경 가능 합니다.)'                     
		GOTO ERROR
	END

	IF (@V_MAIN_CATEGORY = '003' AND @V_SUB_CATEGORY = '006' AND @V_STA_GIR_OLD = ''  AND @P_STA_GIR = 'R')
	BEGIN
		EXEC SP_CZ_SA_STOCK_GI_REQ_I @P_CD_COMPANY, @P_NO_GIR, '', '001', 'SL02', 'SL03', @P_ID_UPDATE, 'N'
	END

	UPDATE SA_GIRH
	SET STA_GIR = @P_STA_GIR,
	    ID_UPDATE = @P_ID_UPDATE,
		DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
	WHERE CD_COMPANY = @P_CD_COMPANY
	AND NO_GIR = @P_NO_GIR

	IF @P_YN_SUBMIT = 'Y'
	BEGIN
		UPDATE WD
		SET WD.CD_TAEGBAE = (CASE WHEN MD.CD_PARTNER IN ('DLV230200077', 
														 'DLV230200076', 
														 'DLV230200075', 
														 'DLV230200054') AND WD.CD_MAIN_CATEGORY = '003' AND WD.CD_SUB_CATEGORY = '005' THEN NULL
								  WHEN WD.CD_MAIN_CATEGORY = '003' AND WD.CD_SUB_CATEGORY IN ('004', '005') THEN MD.CD_TAEGBAE 
																											ELSE NULL END),
			WD.DC_RMK_PL = (CASE WHEN (WD.CD_MAIN_CATEGORY = '003' AND WD.CD_SUB_CATEGORY = '004') THEN ISNULL(GR.NM_DELIVERY, '') + ' ' + 
																									    ISNULL(GR.DC_DELIVERY_ADDR, '') + ' ' + 
																									    ISNULL(GR.DC_DELIVERY_TEL, '') + ' ' +
																										ISNULL(MC.NM_SYSDEF, '') ELSE ISNULL(GR.NM_DELIVERY, '') END),
		    WD.DTS_SUBMIT = NEOE.SF_SYSDATE(GETDATE()),
		    WD.ID_UPDATE = @P_ID_UPDATE,
			WD.DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
		FROM CZ_SA_GIRH_WORK_DETAIL WD
		LEFT JOIN CZ_MA_DELIVERY_MAP DM ON DM.CD_COMPANY = WD.CD_COMPANY AND DM.CD_PARTNER_OLD = WD.CD_DELIVERY_TO
		LEFT JOIN CZ_MA_DELIVERY MD ON MD.CD_COMPANY = WD.CD_COMPANY AND MD.CD_PARTNER = ISNULL(DM.CD_PARTNER_NEW, WD.CD_DELIVERY_TO)
		LEFT JOIN CZ_SA_GIRH_REMARK GR ON GR.CD_COMPANY = WD.CD_COMPANY AND GR.NO_GIR = WD.NO_GIR
		LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = WD.CD_COMPANY AND MC.CD_FIELD = 'CZ_SA00032' AND MC.CD_SYSDEF = WD.CD_RMK
		WHERE WD.CD_COMPANY = @P_CD_COMPANY
		AND WD.NO_GIR = @P_NO_GIR
	END
END

RETURN

ERROR: 
RAISERROR(@V_ERRMSG, 16, 1)

GO

