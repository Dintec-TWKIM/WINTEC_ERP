USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_IV_SUBL_S]    Script Date: 2017-02-21 오전 11:13:53 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_SA_IV_SUBL_S]
(
    @P_CD_COMPANY           NVARCHAR(7),		-- 회사코드
	@P_NO_IO				NVARCHAR(20),		-- 출고번호 (Key)
    @P_CD_PARTNER           NVARCHAR(20) = '',  -- 매출처
    @P_CD_PROJECT           NVARCHAR(20) = '',  -- 프로젝트
    @P_NO_EMP               NVARCHAR(10) = '',  -- 수주담당자
    @P_NO_SO				NVARCHAR(20) = '',	-- 수주번호 : 장은경 (2010.09.24)
    @P_GI_PARTNER           NVARCHAR(20) = '',
	@P_NO_IMO				NVARCHAR(10) = '',  -- IMO 번호
	@P_FG_DT_LOADING		NVARCHAR(1)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

WITH A AS
(
	SELECT OL.CD_COMPANY,
		   OL.NO_IO,
	       OL.NO_IOLINE,
		   OL.CD_PLANT,
	       OL.CD_ITEM,
		   ISNULL(OL.QT_UNIT_MM, 0) AS QT_CLS_MM,
	       (ISNULL(OL.QT_UNIT_MM, 0) - ISNULL(OL.QT_CLS_MM, 0)) AS QT_GI_CLS,
	       (ISNULL(OL.QT_IO, 0) - ISNULL(OL.QT_CLS, 0)) AS QT_CLS,
	       (ISNULL(OL.QT_UNIT_MM, 0) - ISNULL(OL.QT_CLS_MM, 0)) AS QT_CLS_REMAIN,
		   ISNULL(OL.UM_EX, 0) AS UM_EX_CLS,
		   OL.NO_ISURCV,
		   OL.NO_ISURCVLINE,
		   OL.CD_PJT,
		   OL.FG_TRANS,
		   OL.FG_TPIO,
           OL.NO_PSO_MGMT,
           OL.NO_PSOLINE_MGMT,
		   OL.CD_EXCH,
		   OL.FG_TAX,
           OL.CD_PARTNER,
           OL.NO_LC,
           OL.CD_BIZAREA,
           OL.GI_PARTNER,
           OL.DC_RMK,
		   OL.DT_IO,
	       OL.SEQ_PROJECT,
	       OL.TP_UM_TAX, 
	       OL.UMVAT_GI,
	       OL.CD_SL,
		   OL.CD_QTIOTP,
		   GD.NO_IMO,
		   ISNULL(GD.DT_LOADING, '') AS DT_LOADING,
		   CD.CD_FLAG1,
		   (CONVERT(NUMERIC(17, 4), CASE WHEN ISNULL(CD.CD_FLAG1,'') = '' THEN '0' ELSE CD.CD_FLAG1 END) / 100) AS RT_VAT,
		   EX.RT_EXCH,
	       (CASE WHEN OL.CD_COMPANY = 'S100' THEN 2 ELSE 0 END) AS AM_FSIZE,
		   GD.TP_CHARGE,
		   ROUND(ISNULL(OL1.AM_EX, 0) * ISNULL(EX.RT_EXCH, 1), 0) AS AM_SO
	FROM MM_QTIO OL
	LEFT JOIN CZ_SA_GIRH_WORK_DETAIL GD ON GD.CD_COMPANY = OL.CD_COMPANY AND GD.NO_GIR = OL.NO_ISURCV
	LEFT JOIN MA_CODEDTL CD ON CD.CD_COMPANY = OL.CD_COMPANY AND CD.CD_FIELD = 'MA_B000040' AND CD.CD_SYSDEF = OL.FG_TAX
	LEFT JOIN (SELECT EX.CD_COMPANY, EX.YYMMDD, EX.CURR_SOUR,
		   		   	  (CASE WHEN CURR_SOUR = '002' THEN RATE_BASE ELSE ROUND(RATE_BASE, 2) END) AS RT_EXCH
		   	   FROM MA_EXCHANGE EX
		   	   WHERE EX.NO_SEQ = '1'
		   	   AND EX.CURR_DEST = '000') EX
    ON EX.CD_COMPANY = GD.CD_COMPANY AND EX.YYMMDD = GD.DT_LOADING AND EX.CURR_SOUR = OL.CD_EXCH
	LEFT JOIN (SELECT OL.CD_COMPANY, OL.NO_IO, OL.NO_PSO_MGMT,
					  (CASE WHEN OH.YN_RETURN = 'Y' THEN -OL.AM_EX ELSE OL.AM_EX END) AS AM_EX
			   FROM MM_QTIOH OH
			   JOIN (SELECT OL.CD_COMPANY, OL.NO_IO, OL.NO_PSO_MGMT,
					        SUM(ROUND(ISNULL(OL.UM_EX, 0) * (ISNULL(OL.QT_IO, 0) - ISNULL(OL.QT_CLS, 0)), 2)) AS AM_EX 
					 FROM MM_QTIO OL
					 GROUP BY OL.CD_COMPANY, OL.NO_IO, OL.NO_PSO_MGMT) OL 
			   ON OL.CD_COMPANY = OH.CD_COMPANY AND OL.NO_IO = OH.NO_IO) OL1
	ON OL1.CD_COMPANY = OL.CD_COMPANY AND OL1.NO_IO = OL.NO_IO AND OL1.NO_PSO_MGMT = OL.NO_PSO_MGMT
	WHERE OL.CD_COMPANY = @P_CD_COMPANY
	AND OL.NO_IO = @P_NO_IO
	AND OL.FG_PS = '2'
	AND OL.FG_IO IN ('010', '041')
	AND OL.YN_PURSALE = 'Y'
	AND OL.QT_UNIT_MM - OL.QT_CLS_MM > 0
	AND OL.YN_AM = 'Y'
	AND (ISNULL(@P_CD_PARTNER, '') = '' OR OL.CD_PARTNER = @P_CD_PARTNER)
	AND (ISNULL(@P_CD_PROJECT, '') = '' OR OL.CD_PJT  = @P_CD_PROJECT)
	AND	(ISNULL(@P_NO_SO, '') = '' OR OL.NO_PSO_MGMT LIKE @P_NO_SO + '%')
	AND (ISNULL(@P_GI_PARTNER, '') = '' OR OL.GI_PARTNER = @P_GI_PARTNER)
	AND (@P_FG_DT_LOADING = 'A' OR 
		(@P_FG_DT_LOADING = 'Y' AND ISNULL(GD.DT_LOADING, '') <> '') OR
	    (@P_FG_DT_LOADING = 'N' AND ISNULL(GD.DT_LOADING, '') = ''))
),
B AS
(	SELECT *
	FROM (SELECT *,
		         ROW_NUMBER() OVER (PARTITION BY OL.CD_COMPANY, OL.NO_IO ORDER BY OL.AM_SO DESC, OL.NO_IOLINE DESC) AS IDX
		  FROM A OL) OL
	WHERE OL.IDX = 1
),
C AS
(
	SELECT A.CD_COMPANY,
		   A.NO_IO,
	       A.NO_IOLINE,
		   A.CD_PLANT,
	       A.CD_ITEM,
		   A.QT_CLS_MM,
	       A.QT_GI_CLS,
	       A.QT_CLS,
	       A.QT_CLS_REMAIN,
		   A.UM_EX_CLS,
		   A.NO_ISURCV,
		   A.NO_ISURCVLINE,
		   A.CD_PJT,
		   A.FG_TRANS,
		   A.FG_TPIO,
           A.NO_PSO_MGMT,
           A.NO_PSOLINE_MGMT,
		   A.CD_EXCH,
		   A.FG_TAX,
           A.CD_PARTNER,
           A.NO_LC,
           A.CD_BIZAREA,
           A.GI_PARTNER,
           A.DC_RMK,
		   A.DT_IO,
	       A.SEQ_PROJECT,
	       A.TP_UM_TAX, 
	       A.UMVAT_GI,
	       A.CD_SL,
		   A.CD_QTIOTP,
		   A.NO_IMO,
		   A.DT_LOADING,
		   A.CD_FLAG1,
		   A.RT_VAT,
		   A.RT_EXCH,
	       A.AM_FSIZE,
		   'N' AS YN_AUTO_CHARGE
	FROM A
	WHERE A.CD_ITEM NOT LIKE 'SD%'
	UNION ALL
	SELECT A.CD_COMPANY,
		   A.NO_IO,
	       A.NO_IOLINE,
		   A.CD_PLANT,
	       A.CD_ITEM,
		   A.QT_CLS_MM,
	       A.QT_GI_CLS,
	       A.QT_CLS,
	       A.QT_CLS_REMAIN,
		   A.UM_EX_CLS,
		   A.NO_ISURCV,
		   A.NO_ISURCVLINE,
		   A.CD_PJT,
		   A.FG_TRANS,
		   A.FG_TPIO,
           A.NO_PSO_MGMT,
           A.NO_PSOLINE_MGMT,
		   A.CD_EXCH,
		   A.FG_TAX,
           A.CD_PARTNER,
           A.NO_LC,
           A.CD_BIZAREA,
           A.GI_PARTNER,
           A.DC_RMK,
		   A.DT_IO,
	       A.SEQ_PROJECT,
	       A.TP_UM_TAX, 
	       A.UMVAT_GI,
	       A.CD_SL,
		   A.CD_QTIOTP,
		   A.NO_IMO,
		   A.DT_LOADING,
		   A.CD_FLAG1,
		   A.RT_VAT,
		   A.RT_EXCH,
	       A.AM_FSIZE,
		   'N' AS YN_AUTO_CHARGE
	FROM A
	WHERE A.CD_ITEM LIKE 'SD%'
	AND (ISNULL(A.TP_CHARGE, '001') = '001' OR NOT EXISTS (SELECT 1 
														   FROM CZ_SA_GIRH_CHARGE GC
														   WHERE GC.CD_COMPANY = A.CD_COMPANY
														   AND GC.NO_GIR = A.NO_ISURCV
														   AND GC.CD_ITEM = A.CD_ITEM))
	UNION ALL
	SELECT B.CD_COMPANY,
		   B.NO_IO,
		   B.NO_IOLINE,
		   B.CD_PLANT,
		   'SD0003' AS CD_ITEM, -- PACKING CHARGE
		   1 AS QT_CLS_MM,
		   1 AS QT_GI_CLS,
		   1 AS QT_CLS,
		   1 AS QT_CLS_REMAIN,
		   ROUND((PH.AM_CBM / ISNULL(B.RT_EXCH, 1)), 2) AS UM_EX_CLS,
		   B.NO_ISURCV,
		   B.NO_ISURCVLINE,
		   B.CD_PJT,
		   B.FG_TRANS,
		   B.FG_TPIO,
	       B.NO_PSO_MGMT,
	       B.NO_PSOLINE_MGMT,
		   B.CD_EXCH,
		   B.FG_TAX,
	       B.CD_PARTNER,
	       B.NO_LC,
	       B.CD_BIZAREA,
	       B.GI_PARTNER,
	       B.DC_RMK,
		   B.DT_IO,
		   B.SEQ_PROJECT,
		   B.TP_UM_TAX, 
		   B.UMVAT_GI,
		   B.CD_SL,
		   B.CD_QTIOTP,
		   B.NO_IMO,
		   B.DT_LOADING,
		   B.CD_FLAG1,
		   B.RT_VAT,
		   B.RT_EXCH,
		   B.AM_FSIZE,
		   'Y' AS YN_AUTO_CHARGE 
	FROM B
	JOIN (SELECT PH.CD_COMPANY,
		  	     PH.NO_GIR,
		  	     SUM(CASE WHEN PH.QT_CBM >= 1 THEN PH.QT_CBM * TC.UM ELSE TC.UM END) AS AM_CBM
		  FROM (SELECT PH.CD_COMPANY,
		  		       PH.NO_GIR,
		  		       CONVERT(NUMERIC(17, 2), ((PH1.QT_WIDTH * 0.001) * (PH1.QT_HEIGHT * 0.001) * (PH1.QT_LENGTH * 0.001))) AS QT_CBM
		  	    FROM CZ_SA_PACKH PH
		  	    JOIN CZ_SA_PACKH PH1 ON PH1.CD_COMPANY = PH.CD_COMPANY AND PH1.NO_GIR = PH.NO_GIR_ORG AND PH1.NO_PACK = PH.NO_PACK_ORG
		  	    WHERE PH.CD_COMPANY = @P_CD_COMPANY 
				AND PH1.CD_TYPE = '003'
		  	    AND PH1.YN_OUTSOURCING = 'Y'
		  	    AND EXISTS (SELECT 1 
		  	  			    FROM SA_GIRH GH
		  	  			    WHERE GH.CD_COMPANY = PH.CD_COMPANY
		  	  			    AND GH.NO_GIR = PH.NO_GIR)) PH
		  LEFT JOIN (SELECT TH.CD_COMPANY,
					 	    TL.SIZE,
					 	    TL.UM 
					 FROM CZ_MA_TARIFF_CBM_H TH
					 JOIN CZ_MA_TARIFF_CBM_L TL ON TL.CD_COMPANY = TH.CD_COMPANY AND TL.CD_PARTNER = TH.CD_PARTNER
					 WHERE ISNULL(TH.YN_USE, 'N') = 'Y'
					 AND CONVERT(NVARCHAR(8), GETDATE(), 112) BETWEEN TL.DT_START AND TL.DT_END) TC
	      ON TC.CD_COMPANY = PH.CD_COMPANY AND TC.SIZE = (CASE WHEN PH.QT_CBM >= 1 THEN 1 ELSE PH.QT_CBM END)
	      GROUP BY PH.CD_COMPANY, PH.NO_GIR) PH 
    ON PH.CD_COMPANY = B.CD_COMPANY AND PH.NO_GIR = B.NO_ISURCV
	WHERE (ISNULL(B.TP_CHARGE, '001') = '001' OR NOT EXISTS (SELECT 1 
															 FROM CZ_SA_GIRH_CHARGE GC
															 WHERE GC.CD_COMPANY = B.CD_COMPANY
															 AND GC.NO_GIR = B.NO_ISURCV
															 AND GC.CD_ITEM = 'SD0003'))
    AND NOT EXISTS (SELECT 1 
					FROM SA_IVL IL
					WHERE IL.CD_COMPANY = B.CD_COMPANY
					AND IL.NO_IO = B.NO_IO
					AND IL.CD_ITEM = 'SD0003')
	AND NOT EXISTS (SELECT 1 
					FROM MM_QTIO OL
					WHERE OL.CD_COMPANY = B.CD_COMPANY
					AND OL.NO_IO = B.NO_IO
					AND OL.CD_ITEM = 'SD0003')
    AND NOT EXISTS (SELECT 1 
				    FROM MM_QTIO OL
				    JOIN SA_SOH SH ON SH.CD_COMPANY = OL.CD_COMPANY AND SH.NO_SO = OL.NO_PSO_MGMT
				    JOIN MA_CODEDTL MC ON MC.CD_COMPANY = SH.CD_COMPANY AND MC.CD_FIELD = 'TR_IM00011' AND MC.CD_SYSDEF = SH.TP_PACKING
				    WHERE OL.CD_COMPANY = B.CD_COMPANY
				    AND OL.NO_IO = B.NO_IO
				    AND MC.CD_FLAG1 = 'N')
	UNION ALL
	SELECT B.CD_COMPANY,
		   B.NO_IO,
		   B.NO_IOLINE,
		   B.CD_PLANT,
		   'SD0001' AS CD_ITEM, -- FREIGHT CHARGE
		   1 AS QT_CLS_MM,
		   1 AS QT_GI_CLS,
		   1 AS QT_CLS,
		   1 AS QT_CLS_REMAIN,
		   ROUND((FC.AM_IV / ISNULL(B.RT_EXCH, 1)), 2) AS UM_EX_CLS,
		   B.NO_ISURCV,
		   B.NO_ISURCVLINE,
		   B.CD_PJT,
		   B.FG_TRANS,
		   B.FG_TPIO,
	       B.NO_PSO_MGMT,
	       B.NO_PSOLINE_MGMT,
		   B.CD_EXCH,
		   B.FG_TAX,
	       B.CD_PARTNER,
	       B.NO_LC,
	       B.CD_BIZAREA,
	       B.GI_PARTNER,
	       B.DC_RMK,
		   B.DT_IO,
		   B.SEQ_PROJECT,
		   B.TP_UM_TAX, 
		   B.UMVAT_GI,
		   B.CD_SL,
		   B.CD_QTIOTP,
		   B.NO_IMO,
		   B.DT_LOADING,
		   B.CD_FLAG1,
		   B.RT_VAT,
		   B.RT_EXCH,
		   B.AM_FSIZE,
		   'Y' AS YN_AUTO_CHARGE 
	FROM B
	JOIN (SELECT CD_COMPANY, 
			     NO_IO,
			     SUM(ISNULL(AM_IV, 0) + ISNULL(AM_TAX, 0)) AS AM_IV
		  FROM CZ_SA_FORWARD_CHARGE_L
		  WHERE (ISNULL(AM_IV, 0) + ISNULL(AM_TAX, 0)) > 0
		  GROUP BY CD_COMPANY, NO_IO) FC
	ON FC.CD_COMPANY = B.CD_COMPANY AND FC.NO_IO = B.NO_IO
	WHERE (ISNULL(B.TP_CHARGE, '001') = '001' OR NOT EXISTS (SELECT 1 
															 FROM CZ_SA_GIRH_CHARGE GC
															 WHERE GC.CD_COMPANY = B.CD_COMPANY
															 AND GC.NO_GIR = B.NO_ISURCV
															 AND GC.CD_ITEM = 'SD0001'))
	AND NOT EXISTS (SELECT 1 
					FROM SA_IVL IL
					WHERE IL.CD_COMPANY = B.CD_COMPANY
					AND IL.NO_IO = B.NO_IO
					AND IL.CD_ITEM = 'SD0001')
	AND NOT EXISTS (SELECT 1 
					FROM MM_QTIO OL
					WHERE OL.CD_COMPANY = B.CD_COMPANY
					AND OL.NO_IO = B.NO_IO
					AND OL.CD_ITEM = 'SD0001')
	UNION ALL
	SELECT B.CD_COMPANY,
		   B.NO_IO,
		   B.NO_IOLINE,
		   B.CD_PLANT,
		   GC.CD_ITEM AS CD_ITEM,
		   1 AS QT_CLS_MM,
		   1 AS QT_GI_CLS,
		   1 AS QT_CLS,
		   1 AS QT_CLS_REMAIN,
		   ROUND(GC.AM_EX_CHARGE, 2) AS UM_EX_CLS,
		   B.NO_ISURCV,
		   B.NO_ISURCVLINE,
		   B.CD_PJT,
		   B.FG_TRANS,
		   B.FG_TPIO,
	       B.NO_PSO_MGMT,
	       B.NO_PSOLINE_MGMT,
		   B.CD_EXCH,
		   B.FG_TAX,
	       B.CD_PARTNER,
	       B.NO_LC,
	       B.CD_BIZAREA,
	       B.GI_PARTNER,
	       B.DC_RMK,
		   B.DT_IO,
		   B.SEQ_PROJECT,
		   B.TP_UM_TAX, 
		   B.UMVAT_GI,
		   B.CD_SL,
		   B.CD_QTIOTP,
		   B.NO_IMO,
		   B.DT_LOADING,
		   B.CD_FLAG1,
		   B.RT_VAT,
		   B.RT_EXCH,
		   B.AM_FSIZE,
		   'Y' AS YN_AUTO_CHARGE 
	FROM B
	JOIN CZ_SA_GIRH_CHARGE GC ON GC.CD_COMPANY = B.CD_COMPANY AND GC.NO_GIR = B.NO_ISURCV
	WHERE ISNULL(B.TP_CHARGE, '001') = '002'
	AND GC.AM_EX_CHARGE > 0
	AND NOT EXISTS (SELECT 1 
					FROM SA_IVL IL
					WHERE IL.CD_COMPANY = B.CD_COMPANY
					AND IL.NO_IO = B.NO_IO
					AND IL.CD_ITEM = GC.CD_ITEM)
),
D AS
(
	SELECT *,
	       ROUND(C.UM_EX_CLS * ISNULL(C.RT_EXCH, 1), C.AM_FSIZE) AS UM_ITEM_CLS,
	       ROUND(C.UM_EX_CLS * C.QT_CLS, 2) AS AM_EX_CLS,
		   ROUND(ROUND(C.UM_EX_CLS * C.QT_CLS, 2) * ISNULL(C.RT_EXCH, 1), C.AM_FSIZE) AS AM_CLS
    FROM C
),
E AS
(
	SELECT *,
		   (CASE WHEN D.CD_COMPANY = 'S100' THEN ROUND(D.AM_CLS * D.RT_VAT, D.AM_FSIZE) 
											ELSE CONVERT(NUMERIC, FLOOR(D.AM_CLS * D.RT_VAT)) END) AS VAT 
	FROM D
)
SELECT 'N' S,
       '' AS NO_IV,
       0 AS NO_LINE,
       OL.NO_IO,
       OL.NO_IOLINE,
       OL.CD_ITEM,
       MI.NM_ITEM,
       MI.STND_ITEM,
       MI.UNIT_IM,
	   MI.CLS_ITEM,
       (CASE WHEN ISNULL(OH.YN_RETURN, 'N') = 'Y' THEN -OL.QT_CLS_MM ELSE OL.QT_CLS_MM END) AS QT_CLS_MM,
       (CASE WHEN ISNULL(OH.YN_RETURN, 'N') = 'Y' THEN -OL.QT_GI_CLS ELSE OL.QT_GI_CLS END) AS QT_GI_CLS,
       (CASE WHEN ISNULL(OH.YN_RETURN, 'N') = 'Y' THEN -OL.QT_CLS ELSE OL.QT_CLS END) AS QT_CLS,
       (CASE WHEN ISNULL(OH.YN_RETURN, 'N') = 'Y' THEN -OL.QT_CLS_REMAIN ELSE OL.QT_CLS_REMAIN END) AS QT_CLS_REMAIN,
	   (CASE WHEN ISNULL(OH.YN_RETURN, 'N') = 'Y' THEN -OL.UM_EX_CLS ELSE OL.UM_EX_CLS END) AS UM_EX_CLS,
	   (CASE WHEN ISNULL(OH.YN_RETURN, 'N') = 'Y' THEN -OL.UM_ITEM_CLS ELSE OL.UM_ITEM_CLS END) AS UM_ITEM_CLS,
	   (CASE WHEN ISNULL(OH.YN_RETURN, 'N') = 'Y' THEN -OL.AM_EX_CLS ELSE OL.AM_EX_CLS END) AS AM_EX_CLS,
	   (CASE WHEN ISNULL(OH.YN_RETURN, 'N') = 'Y' THEN -OL.AM_CLS ELSE OL.AM_CLS END) AS AM_CLS,
	   (CASE WHEN ISNULL(OH.YN_RETURN, 'N') = 'Y' THEN -OL.VAT ELSE OL.VAT END) AS VAT,
	   (CASE WHEN ISNULL(OH.YN_RETURN, 'N') = 'Y' THEN -(ISNULL(OL.AM_CLS, 0) + ISNULL(OL.VAT, 0))
												  ELSE (ISNULL(OL.AM_CLS, 0) + ISNULL(OL.VAT, 0)) END) AS AM_TOT,
       0.0000 AS AM_DISCOUNT,
       SH.CD_SALEGRP,
       SG.NM_SALEGRP,
       SG.CD_CC,
	   (SELECT NM_CC FROM MA_CC MC WHERE MC.CD_COMPANY = SG.CD_COMPANY AND MC.CD_CC = SG.CD_CC) AS NM_CC,
       OL.CD_PJT,
	   (SELECT NM_PROJECT FROM SA_PROJECTH PH WHERE PH.CD_COMPANY = OL.CD_COMPANY AND PH.NO_PROJECT = OL.CD_PJT) AS NM_PROJECT,
       OL.FG_TRANS,
	   (SELECT NM_QTIOTP FROM MM_EJTP EJ WHERE EJ.CD_COMPANY = OL.CD_COMPANY AND EJ.CD_QTIOTP = OL.CD_QTIOTP) AS NM_QTIOTP,
       ISNULL(TP.TP_IV, OL.FG_TPIO) AS TP_IV,
       OL.CD_PLANT,
       SH.NO_EMP,
       OL.NO_PSO_MGMT AS NO_SO,
       OL.NO_PSOLINE_MGMT AS NO_SOLINE,
       OL.CD_EXCH,
       ISNULL(OL.RT_EXCH, 1) AS RT_EXCH,
       OL.FG_TAX,
	   OL.CD_FLAG1 AS FG_TAX_VAT,
	   OL.CD_FLAG1 AS RT_VAT,
       OH.YN_RETURN,
       MI.UNIT_SO_FACT,
       OL.CD_PARTNER,
       OL.NO_LC,
       OL.CD_BIZAREA,
       MP1.LN_PARTNER AS GI_PARTNER,
       OL.GI_PARTNER AS CD_GI_PARTNER,
       'N' AS YN_PJTCREDIT,
       ISNULL(MP.TP_TAX, '001') AS TP_SUMTAX,
	   (SELECT ME.NM_KOR FROM MA_EMP ME WHERE ME.CD_COMPANY = SH.CD_COMPANY AND ME.NO_EMP = SH.NO_EMP) AS NM_KOR,
       ISNULL((SELECT MA.CD_DOCU 
			   FROM MA_AISPOSTH MA 
			   WHERE MA.CD_COMPANY = OL.CD_COMPANY 
			   AND MA.CD_TP = OL.FG_TPIO 
			   AND MA.FG_TP = '100'), (CASE WHEN OL.FG_TRANS = '001' THEN '23' ELSE '24' END)) AS CD_DOCU,
       OL.DC_RMK,
	   MP.LN_PARTNER AS LN_PARTNER,
	   MP.NO_COMPANY AS NO_BIZAREA,
	   ISNULL(MP.DT_RCP_PREARRANGED, 0) AS DT_RCP_PREARRANGED,
       MP.TP_RCP_DD,
       MP.DT_RCP_DD,
	   MP.FG_BILL,
	   MP.CD_CON,
	   GL.DC_RMK  AS G_DC_RMK,
	   GL.DC_RMK2 AS G_DC_RMK2,
	   ISNULL(SH.TP_PRICE, (SELECT GH.FG_UM 
							FROM SA_GIRH GH 
							WHERE GH.CD_COMPANY = OL.CD_COMPANY 
							AND GH.NO_GIR = OL.NO_ISURCV)) AS TP_PRICE,
	   PS.CD_BIZAREA AS SALEGRP_CD_BIZAREA,
	   PS.NM_BIZAREA AS SALEGRP_NM_BIZAREA,
	   SL.CD_MNGD1, 
	   (SELECT NM_MNGD FROM FI_MNGD FM WHERE FM.CD_COMPANY = SL.CD_COMPANY AND FM.CD_MNG = 'A21' AND FM.CD_MNGD = SL.CD_MNGD1) AS NM_MNGD1,
	   SL.CD_MNGD2,
	   (SELECT NM_MNGD FROM FI_MNGD FM WHERE FM.CD_COMPANY = SL.CD_COMPANY AND FM.CD_MNG = 'A22' AND FM.CD_MNGD = SL.CD_MNGD2) AS NM_MNGD2,
	   SL.CD_MNGD3, 
	   (SELECT NM_MNGD FROM FI_MNGD FM WHERE FM.CD_COMPANY = SL.CD_COMPANY AND FM.CD_MNG = 'A25' AND FM.CD_MNGD = SL.CD_MNGD3) AS NM_MNGD3,
	   SL.CD_MNGD4,
	   (SELECT NM_SYSDEF 
		FROM MA_PARTNER_SUB PS 
		LEFT JOIN MA_CODEDTL CD ON CD.CD_COMPANY = PS.CD_COMPANY AND CD.CD_FIELD = 'SA_B000021' AND CD.CD_SYSDEF = PS.FG_UM
		WHERE PS.CD_COMPANY = MP1.CD_COMPANY 
		AND PS.CD_PARTNER = MP1.CD_PARTNER) AS NM_FG_UM_GI,
	   TP.NM_SO,
	   OL.DT_IO,
	   MP.DT_RCP_PRETOLERANCE AS DT_RCP_PRETOLERANCE,
	   OL.SEQ_PROJECT AS CD_PJTLINE,
	   PL.CD_ITEM AS CD_UNIT,
	   MI2.NM_ITEM AS NM_UNIT,
	   MI2.STND_ITEM AS STND_UNIT,
	   OH.DC_RMK AS DC_REMARK,
	   SL.TXT_USERDEF1,
	   SL.TXT_USERDEF2,
	   SL.CD_ITEM_REF,
	   MI1.NM_ITEM AS NM_ITEM_REF,
       MI1.STND_ITEM AS STND_ITEM_REF,
       SL.YN_PICKING,
	   (SELECT SO_PRICE FROM MA_SALEORG SO WHERE SO.CD_COMPANY = SG.CD_COMPANY AND SO.CD_SALEORG = SG.CD_SALEORG) AS SO_PRICE, 
	   SH.FG_VAT, 
	   SL.FG_USE,
	   OL.TP_UM_TAX, 
	   OL.UMVAT_GI AS UMVAT_IV,
	   OL.CD_SL,
	   (SELECT NM_SL FROM MA_SL MS WHERE MS.CD_COMPANY = OL.CD_COMPANY AND MS.CD_PLANT = OL.CD_PLANT AND MS.CD_BIZAREA = OL.CD_BIZAREA AND MS.CD_SL = OL.CD_SL) AS NM_SL,
	   OL.NO_IMO,
	   (SELECT NM_VESSEL FROM CZ_MA_HULL MH WHERE MH.NO_IMO = OL.NO_IMO) AS NM_VESSEL,
	   OL.DT_LOADING,
	   (CASE WHEN EXISTS(SELECT 1 
						 FROM CZ_SA_QTNH QH 
						 WHERE QH.CD_COMPANY = SH.CD_COMPANY 
						 AND QH.NO_FILE = SH.NO_SO) THEN 'N' ELSE 'Y' END) AS YN_AUTO,
	   QL.NO_DSP,
	   QL.CD_ITEM_PARTNER,
	   QL.NM_ITEM_PARTNER,
	   OL.YN_AUTO_CHARGE
FROM MM_QTIOH OH
JOIN E OL ON OL.CD_COMPANY = OH.CD_COMPANY AND OL.NO_IO = OH.NO_IO
LEFT JOIN SA_GIRL GL ON GL.CD_COMPANY = OL.CD_COMPANY AND GL.NO_GIR = OL.NO_ISURCV AND GL.SEQ_GIR = OL.NO_ISURCVLINE
LEFT JOIN SA_SOL SL ON SL.CD_COMPANY = OL.CD_COMPANY AND SL.NO_SO = OL.NO_PSO_MGMT AND SL.SEQ_SO = OL.NO_PSOLINE_MGMT
LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = SL.CD_COMPANY AND SH.NO_SO = SL.NO_SO
LEFT JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = SL.CD_COMPANY AND QL.NO_FILE = SL.NO_SO AND QL.NO_LINE = SL.SEQ_SO
LEFT JOIN SA_PROJECTL PL ON PL.CD_COMPANY = OL.CD_COMPANY AND PL.NO_PROJECT = OL.CD_PJT AND PL.SEQ_PROJECT = OL.SEQ_PROJECT
LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = OL.CD_COMPANY AND MI.CD_PLANT = OL.CD_PLANT AND MI.CD_ITEM = OL.CD_ITEM
LEFT JOIN MA_PITEM MI1 ON MI1.CD_COMPANY = SL.CD_COMPANY AND MI1.CD_PLANT = SL.CD_PLANT AND MI1.CD_ITEM = SL.CD_ITEM_REF
LEFT JOIN MA_PITEM MI2 ON MI2.CD_COMPANY = PL.CD_COMPANY AND MI2.CD_PLANT = PL.CD_PLANT AND MI2.CD_ITEM = PL.CD_ITEM
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = OH.CD_COMPANY AND MP.CD_PARTNER = OH.CD_PARTNER
LEFT JOIN MA_PARTNER MP1 ON MP1.CD_COMPANY = OL.CD_COMPANY AND MP1.CD_PARTNER = OL.GI_PARTNER
LEFT JOIN MA_SALEGRP SG ON SG.CD_COMPANY = SH.CD_COMPANY AND SG.CD_SALEGRP = SH.CD_SALEGRP
LEFT JOIN SA_TPSO TP ON TP.CD_COMPANY = SH.CD_COMPANY AND TP.TP_SO = SH.TP_SO
LEFT JOIN (SELECT PS.CD_COMPANY,
				  PS.CD_PARTNER,
				  BA.CD_BIZAREA,
				  BA.NM_BIZAREA
		   FROM MA_PARTNER_SUB PS
		   JOIN MA_SALEGRP SG ON SG.CD_COMPANY = PS.CD_COMPANY AND SG.CD_SALEGRP = PS.CD_SALEGRP
		   JOIN MA_CC MC ON MC.CD_COMPANY = SG.CD_COMPANY AND MC.CD_CC = SG.CD_CC 
		   JOIN MA_BIZAREA BA ON BA.CD_COMPANY = MC.CD_COMPANY AND BA.CD_BIZAREA = MC.CD_BIZAREA) PS
ON PS.CD_COMPANY = MP.CD_COMPANY AND PS.CD_PARTNER = MP.CD_PARTNER
WHERE OH.CD_COMPANY = @P_CD_COMPANY
AND (ISNULL(@P_NO_IMO,'') = '' OR SH.NO_IMO =  @P_NO_IMO)
AND (ISNULL(@P_NO_EMP, '') = '' OR SH.NO_EMP = @P_NO_EMP)

GO