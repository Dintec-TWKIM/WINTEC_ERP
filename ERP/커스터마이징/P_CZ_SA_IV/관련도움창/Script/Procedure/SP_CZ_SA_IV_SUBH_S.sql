USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_IV_SUBH_S]    Script Date: 2017-02-21 오전 11:13:31 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_SA_IV_SUBH_S]
(
    @P_CD_COMPANY           NVARCHAR(7),		-- 회사코드
	@P_NO_IO				NVARCHAR(20) = '',  -- 출고번호
    @P_DT_FROM              NCHAR(8),			-- 출고일자
    @P_DT_TO                NCHAR(8),			-- 출고일자
    @P_FG_TRANS             NVARCHAR(3),		-- 거래구분
    @P_CD_PARTNER           NVARCHAR(20) = '',  -- 매출처
    @P_CD_PROJECT           NVARCHAR(20) = '',  -- 프로젝트
    @P_NO_EMP               NVARCHAR(10) = '',  -- 수주담당자
    @P_NO_SO				NVARCHAR(20) = '',	-- 수주번호 : 장은경 (2010.09.24)
    @P_GI_PARTNER           NVARCHAR(20) = '',
	@P_NO_IMO				NVARCHAR(10) = '',  -- IMO 번호
	@P_FG_DT_LOADING		NVARCHAR(1)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

WITH A AS
(
	SELECT OL.CD_COMPANY, 
		   OL.NO_IO,
		   OL.NO_PSO_MGMT,
		   MAX(OL.NO_ISURCV) AS NO_ISURCV,
		   MAX(OL.FG_TAX) AS FG_TAX,
		   MAX(OL.CD_BIZAREA) AS CD_BIZAREA,
		   MAX(OL.CD_EXCH) AS CD_EXCH,
		   SUM(ROUND(ISNULL(OL.UM_EX, 0) * (ISNULL(OL.QT_IO, 0) - ISNULL(OL.QT_CLS, 0)), 2)) AS AM_EX
	FROM MM_QTIO OL
	WHERE OL.CD_COMPANY = @P_CD_COMPANY 
	AND OL.FG_PS = '2'
	AND OL.FG_IO IN ('010', '041')
	AND OL.YN_PURSALE = 'Y'
	AND OL.YN_AM = 'Y'
	AND OL.QT_UNIT_MM > OL.QT_CLS_MM
	AND (@P_CD_PARTNER = '' OR OL.CD_PARTNER = @P_CD_PARTNER)
	AND (@P_CD_PROJECT = '' OR OL.CD_PJT = @P_CD_PROJECT)
	AND (@P_NO_SO = '' OR OL.NO_PSO_MGMT LIKE @P_NO_SO + '%')
	AND (@P_GI_PARTNER = '' OR OL.GI_PARTNER = @P_GI_PARTNER)
	AND (OL.CD_ITEM NOT LIKE 'SD%' OR EXISTS (SELECT 1 
											  FROM CZ_SA_GIRH_WORK_DETAIL GD 
											  WHERE GD.CD_COMPANY = OL.CD_COMPANY 
											  AND GD.NO_GIR = OL.NO_ISURCV 
											  AND ISNULL(GD.TP_CHARGE, '001') = '001') OR NOT EXISTS (SELECT 1 
														                                              FROM CZ_SA_GIRH_CHARGE GC
														                                              WHERE GC.CD_COMPANY = OL.CD_COMPANY
														                                              AND GC.NO_GIR = OL.NO_ISURCV
														                                              AND GC.CD_ITEM = OL.CD_ITEM))
	GROUP BY OL.CD_COMPANY, OL.NO_IO, OL.NO_PSO_MGMT
),
B AS
(
	SELECT OL.*,
		   WD.NO_IMO,
		   WD.DT_LOADING,
		   WD.CD_RMK,
		   WD.CD_MAIN_CATEGORY,
		   WD.CD_SUB_CATEGORY,
		   (CASE WHEN WD.DC_RMK2 IS NULL OR WD.DC_RMK2 = '' THEN '' ELSE '[' + WD.NO_GIR + '] ' + WD.DC_RMK2 END) AS DC_RMK_GIR,
	       (CASE WHEN OL.CD_COMPANY = 'S100' THEN 2 ELSE 0 END) AS AM_FSIZE,
		   (CONVERT(NUMERIC(17, 4), CASE WHEN ISNULL(CD.CD_FLAG1,'') = '' THEN '0' ELSE CD.CD_FLAG1 END) / 100) AS RT_VAT,
		   (SELECT (CASE WHEN EX.CURR_SOUR = '002' THEN EX.RATE_BASE ELSE ROUND(EX.RATE_BASE, 2) END) 
		    FROM MA_EXCHANGE EX
			WHERE EX.CD_COMPANY = OL.CD_COMPANY
			AND EX.YYMMDD = WD.DT_LOADING
			AND EX.NO_SEQ = '1'
			AND EX.CURR_DEST = '000'
			AND EX.CURR_SOUR = OL.CD_EXCH) AS RT_EXCH,
		   STUFF((SELECT DISTINCT ',' + FL.DC_RMK
				  FROM CZ_SA_FORWARD_CHARGE_L FL
				  WHERE FL.CD_COMPANY = OL.CD_COMPANY
				  AND FL.NO_IO = OL.NO_IO
				  FOR XML PATH('')),1,1,'') AS DC_RMK_FORWARD
	FROM A OL
	LEFT JOIN CZ_SA_GIRH_WORK_DETAIL WD ON WD.CD_COMPANY = OL.CD_COMPANY AND WD.NO_GIR = OL.NO_ISURCV
	LEFT JOIN MA_CODEDTL CD ON CD.CD_COMPANY = OL.CD_COMPANY AND CD.CD_FIELD = 'MA_B000040' AND CD.CD_SYSDEF = OL.FG_TAX
	WHERE 1=1
	AND (@P_NO_IMO = '' OR WD.NO_IMO =  @P_NO_IMO)
	AND (@P_FG_DT_LOADING = 'A' OR
		 (@P_FG_DT_LOADING = 'Y' AND ISNULL(WD.DT_LOADING, '') <> '') OR
		 (@P_FG_DT_LOADING = 'N' AND ISNULL(WD.DT_LOADING, '') = ''))
	AND (@P_NO_EMP = '' OR EXISTS (SELECT 1 
		  						   FROM SA_SOH SH
		  						   WHERE SH.CD_COMPANY = OL.CD_COMPANY
		  						   AND SH.NO_SO = OL.NO_PSO_MGMT 
		  						   AND SH.NO_EMP = @P_NO_EMP))
),
C AS
(
	SELECT OL.*,
		   SL.QT_SO,
           SL.QT_GI,
           OL1.QT_IO,
		   ROUND(OL.AM_EX * ISNULL(OL.RT_EXCH, 1), OL.AM_FSIZE) AS AM,
		   (CASE WHEN OL.CD_COMPANY = 'S100' THEN ROUND(ROUND(OL.AM_EX * ISNULL(OL.RT_EXCH, 1), OL.AM_FSIZE) * OL.RT_VAT, OL.AM_FSIZE)
											 ELSE CONVERT(NUMERIC, FLOOR(ROUND(OL.AM_EX * ISNULL(OL.RT_EXCH, 1), OL.AM_FSIZE) * OL.RT_VAT)) END) AS VAT
	FROM B OL
	LEFT JOIN (SELECT SL.CD_COMPANY, SL.NO_SO,
	                  SUM(SL.QT_SO) AS QT_SO,
	                  SUM(SL.QT_GI) AS QT_GI
	           FROM SA_SOL SL
	           WHERE SL.CD_ITEM NOT LIKE 'SD%'
	           GROUP BY SL.CD_COMPANY, SL.NO_SO) SL
	ON SL.CD_COMPANY = OL.CD_COMPANY AND SL.NO_SO = OL.NO_PSO_MGMT
	LEFT JOIN (SELECT OL.CD_COMPANY, OL.NO_PSO_MGMT,
	                  COUNT(DISTINCT OL.NO_IO) AS QT_IO
		       FROM MM_QTIO OL
		       WHERE OL.FG_PS = '2'
		       AND OL.YN_PURSALE = 'Y'
		       AND OL.CD_QTIOTP IN ('200', '240', '250', '210', '241', '251')
	           GROUP BY OL.CD_COMPANY, OL.NO_PSO_MGMT) OL1
	ON OL1.CD_COMPANY = SL.CD_COMPANY AND OL1.NO_PSO_MGMT = SL.NO_SO
),
D AS
(
	SELECT OH.CD_COMPANY,
		   OH.NO_IO,
		   OH.DT_IO,
		   OH.DC_RMK,
		   OH.CD_PLANT,
		   MP.LN_PARTNER,
		   OL.NO_IMO,
		   OL.DT_LOADING,
		   OL.DC_RMK_GIR,
		   OL.DC_RMK_FORWARD,
		   OL.CD_RMK,
		   (SELECT NM_VESSEL FROM CZ_MA_HULL MH WHERE MH.NO_IMO = OL.NO_IMO) AS NM_VESSEL,
		   (SELECT NM_BIZAREA FROM MA_BIZAREA BA WHERE BA.CD_COMPANY = OL.CD_COMPANY AND BA.CD_BIZAREA = OL.CD_BIZAREA) AS NM_BIZAREA,
		   OL.FG_TAX,
		   (CASE WHEN OH.YN_RETURN = 'Y' THEN -OL.AM_EX ELSE OL.AM_EX END) AS AM_EX,
		   (CASE WHEN OH.YN_RETURN = 'Y' THEN -OL.AM ELSE OL.AM END) AS AM,
		   (CASE WHEN OH.YN_RETURN = 'Y' THEN -OL.VAT ELSE OL.VAT END) AS VAT,
		   (CASE WHEN OH.YN_RETURN = 'Y' THEN -(ISNULL(OL.AM, 0) + ISNULL(OL.VAT, 0)) 
										 ELSE (ISNULL(OL.AM, 0) + ISNULL(OL.VAT, 0)) END) AS AM_TOT,
		   (SELECT SUM(AM_RCP_A)
			FROM SA_RCPH RH
			WHERE RH.CD_COMPANY = OH.CD_COMPANY
			AND RH.AM_RCP_A > 0
			AND RH.NO_PROJECT = OL.NO_PSO_MGMT) AS AM_RCP_A,
		   (CASE WHEN OH.FG_TRANS = '005' AND 
					  OH.YN_RETURN <> 'Y' AND
					  MP.YN_JEONJA <> '4' AND
					  NOT EXISTS (SELECT 1 
								  FROM MM_QTIO OL
								  JOIN SA_GIRL GL ON GL.CD_COMPANY = OL.CD_COMPANY AND GL.NO_GIR = OL.NO_ISURCV AND GL.SEQ_GIR = OL.NO_ISURCVLINE
								  JOIN SA_SOH SH ON SH.CD_COMPANY = OL.CD_COMPANY AND SH.NO_SO = OL.NO_PSO_MGMT
								  LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = SH.CD_COMPANY AND MC.CD_FIELD = 'CZ_SA00013' AND MC.CD_SYSDEF = SH.COND_PAY
								  WHERE OL.CD_COMPANY = OH.CD_COMPANY
								  AND OL.NO_IO = OH.NO_IO
								  AND ((SH.NO_SO NOT LIKE 'DS%' AND ISNULL(GL.TXT_USERDEF1, '') = '') OR 
									   LEFT(SH.NO_SO, 2) IN ('TE', 'BS', 'BE') OR
									   ISNULL(MC.CD_FLAG1, '') <> '' OR
									   SH.COND_PAY = '102' OR
									   (SH.CD_COMPANY = 'K100' AND (SH.CD_PARTNER_GRP IN ('53', '54') OR SH.NO_SO LIKE 'DB%')) OR 
									   (SH.CD_COMPANY = 'K200' AND (SH.CD_PARTNER_GRP = '82' OR SH.NO_SO LIKE 'D-%')) OR
									   (SH.TP_SO IN ('2000', '2100') AND ISNULL(SH.TP_VAT, '') <> '15') OR
									   (SH.CD_PARTNER = '09737' AND LEFT(SH.NO_SO, 2) = 'SB'))) THEN 'Y' ELSE 'N' END) AS YN_AUTO
	FROM MM_QTIOH OH
	LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = OH.CD_COMPANY AND MP.CD_PARTNER = OH.CD_PARTNER
	JOIN C OL ON OL.CD_COMPANY = OH.CD_COMPANY AND OL.NO_IO = OH.NO_IO
	WHERE OH.CD_COMPANY = @P_CD_COMPANY
	AND OH.DT_IO BETWEEN @P_DT_FROM AND @P_DT_TO
	AND OH.FG_TRANS  = @P_FG_TRANS
	AND (ISNULL(@P_NO_IO, '') = '' OR OH.NO_IO =  @P_NO_IO)
	AND (OH.YN_RETURN = 'Y' OR EXISTS (SELECT 1 
									   FROM MA_FILEINFO
									   WHERE CD_COMPANY = OL.CD_COMPANY 
									   AND CD_MODULE = 'SA'
									   AND ID_MENU = 'P_CZ_SA_GIM_REG'
									   AND CD_FILE = OL.NO_ISURCV + '_' + OL.CD_COMPANY))
)
SELECT 'N' AS S,
	   NO_IO, 
	   DT_IO,
	   DC_RMK,
	   CD_PLANT,
	   LN_PARTNER,
	   NO_IMO,
	   DT_LOADING,
	   DC_RMK_GIR,
	   DC_RMK_FORWARD,
	   CD_RMK,
	   NM_VESSEL,
	   NM_BIZAREA,
	   FG_TAX,
	   MAX(YN_AUTO) AS YN_AUTO,
	   SUM(AM_EX) AS AM_EX,
	   SUM(AM) AS AM,
	   SUM(VAT) AS VAT,
	   SUM(AM_TOT) AS AM_TOT,
	   SUM(AM_RCP_A) AS AM_RCP_A,
	   REPLACE(STUFF((SELECT DISTINCT CHAR(10) + '[' + SH.NO_SO + '] ' + SH.DC_RMK1 
					  FROM SA_SOH SH
					  WHERE SH.CD_COMPANY = D.CD_COMPANY
					  AND SH.DC_RMK1 IS NOT NULL
					  AND SH.DC_RMK1 <> ''
					  AND SH.NO_SO IN (SELECT OL.NO_PSO_MGMT 
									   FROM A OL
									   WHERE OL.CD_COMPANY = D.CD_COMPANY
									   AND OL.NO_IO = D.NO_IO)
					  FOR XML PATH('')),1,1,''), CHAR(10), CHAR(13) + CHAR(10)) AS DC_RMK_SO
FROM D
GROUP BY CD_COMPANY,
		 NO_IO, 
	     DT_IO,
	     DC_RMK,
	     CD_PLANT,
	     LN_PARTNER,
	     NO_IMO,
	     DT_LOADING,
	     DC_RMK_GIR,
		 DC_RMK_FORWARD,
	     CD_RMK,
	     NM_VESSEL,
	     NM_BIZAREA,
	     FG_TAX
OPTION(RECOMPILE)

GO