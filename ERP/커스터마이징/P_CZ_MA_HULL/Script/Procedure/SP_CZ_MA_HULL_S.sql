USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_HULL_S]    Script Date: 2015-06-22 오후 1:29:29 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [NEOE].[SP_CZ_MA_HULL_S]
(
	@P_CD_COMPANY      NVARCHAR(7),
	@P_NO_IMO		   NVARCHAR(10),
	@P_NO_IMO_MULTI    NVARCHAR(1000), 
	@P_NO_HULL         NVARCHAR(20),
	@P_NM_VESSEL       NVARCHAR(50), 
	@P_NM_SHIP_OWNER   NVARCHAR(80), 
	@P_CD_PARTNER      NVARCHAR(20),
	@P_ENGINE_MAKER    NVARCHAR(2),
	@P_NM_MODEL		   NVARCHAR(100),
	@P_SERIAL		   NVARCHAR(20),
	@P_CD_CLS_L		   NVARCHAR(4),
	@P_CD_CLS_M		   NVARCHAR(4),
	@P_CD_CLS_S		   NVARCHAR(4),
	@P_YN_NEWSHIP	   NVARCHAR(1),
	@P_CD_VENDOR	   NVARCHAR(20),
	@P_YN_SISTER	   NVARCHAR(1),
	@P_DC_SHIPBUILDER  NVARCHAR(100),
	@P_YN_LEADER	   NVARCHAR(1),
	@P_YN_ENG		   NVARCHAR(1),
	@P_YN_ATTACH	   NVARCHAR(1),
	@P_YN_UPLOAD	   NVARCHAR(1),
	@P_TP_SHIP		   NVARCHAR(3)
) 
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT MH.NO_IMO,
	   MH.NO_HULL,
	   MH.CALL_SIGN,				
	   MH.NM_VESSEL,			
	   MH.NM_CERT,				
	   MH.NM_SHIP_OWNER,		
	   MH.DT_SHIP_DLV,
	   MH.TP_SHIP_YARD,
	   MC1.NM_SYSDEF AS NM_SHIP_YARD,
	   MH.TP_SHIP,		
	   MC.NM_SYSDEF AS NM_TP_SHIP,		
	   MH.YN_NEWSHIP,			
	   MH.CD_PARTNER,			
	   MP.LN_PARTNER,
	   MH.PURIFIER,				
	   MH.BOILER,				
	   MH.INCINERATOR,			
	   MH.PUMP,					
	   MH.REF_SYS,				
	   MH.ER_MACH,				
	   MH.DECK_MACH,			
	   MH.PRE_MACH,				
	   MH.DC_RMK,			
	   MH.PROPELLER_SERIAL,		
	   MH.STERNPOST_SERIAL,		
	   MH.INVOICE_COMPANY,
	   MH.INVOICE_TEL,		
	   MH.INVOICE_ADDRESS,		
	   MH.INVOICE_EMAIL,		
	   MH.INVOICE_RMK,
	   MH.TP_VAT,
	   MH.CD_NATION,
	   MH.NO_ORIGIN_BUYER,
	   MH.QT_DEAD_WEIGHT,
	   MH.YN_DOM,
	   MH.YN_SCRUBBER,
	   MH.DC_BWTS,
	   P1.ME1 AS ME1_MODEL,
	   P1.GE1 AS GE1_MODEL,
	   P1.GE2 AS GE2_MODEL,
	   P1.TC1 AS TC1_MODEL,
	   P1.TC2 AS TC2_MODEL,
	   P1.TC3 AS TC3_MODEL,
	   P2.ME1 AS ME1_MAKER,
	   P2.GE1 AS GE1_MAKER,
	   P2.GE2 AS GE2_MAKER,
	   P2.TC1 AS TC1_MAKER,
	   P2.TC2 AS TC2_MAKER,
	   P2.TC3 AS TC3_MAKER,
	   P3.ME1 AS ME1_CLS_M,
	   P3.GE1 AS GE1_CLS_M,
	   P3.GE2 AS GE2_CLS_M,
	   P3.TC1 AS TC1_CLS_M,
	   P3.TC2 AS TC2_CLS_M,
	   P3.TC3 AS TC3_CLS_M,
	   P4.ME1 AS ME1_SERIAL,
	   P4.GE1 AS GE1_SERIAL,
	   P4.GE2 AS GE2_SERIAL,
	   P4.TC1 AS TC1_SERIAL,
	   P4.TC2 AS TC2_SERIAL,
	   P4.TC3 AS TC3_SERIAL,
	   NULL AS DTS_ETRYPT,
	   SS.NO_SISTER,
       SS.CD_SISTER,
	   MH.DC_SHIPBUILDER,
	   (CASE WHEN EXISTS (SELECT 1 
						  FROM MA_FILEINFO MF 
						  WHERE MF.CD_MODULE = 'MA'
						  AND MF.ID_MENU = 'P_CZ_MA_HULL'
						  AND MF.CD_FILE = MH.NO_IMO) THEN 'Y' ELSE 'N' END) AS YN_FILE
FROM CZ_MA_HULL MH
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = @P_CD_COMPANY AND MP.CD_PARTNER = MH.CD_PARTNER
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = @P_CD_COMPANY AND MC.CD_FIELD = 'CZ_MA00002' AND MC.CD_SYSDEF = MH.TP_SHIP
LEFT JOIN MA_CODEDTL MC1 ON MC1.CD_COMPANY = @P_CD_COMPANY AND MC1.CD_FIELD = 'CZ_MA00001' AND MC1.CD_SYSDEF = MH.TP_SHIP_YARD
LEFT JOIN (SELECT *
		   FROM (SELECT ME.NO_IMO,
		   				ME.CD_ENGINE + CONVERT(NVARCHAR, ROW_NUMBER() OVER (PARTITION BY ME.NO_IMO, ME.CD_ENGINE ORDER BY ME.CD_ENGINE)) AS CD_ENGINE,
		   				ME.NM_MODEL
				 FROM CZ_MA_HULL_ENGINE ME) AS ME
		   PIVOT (MAX(ME.NM_MODEL) FOR ME.CD_ENGINE IN ([ME1],[GE1],[GE2],[TC1],[TC2],[TC3])) PV) P1
ON P1.NO_IMO = MH.NO_IMO
LEFT JOIN (SELECT *
		   FROM (SELECT ME.NO_IMO,
		   				ME.CD_ENGINE + CONVERT(NVARCHAR, ROW_NUMBER() OVER (PARTITION BY ME.NO_IMO, ME.CD_ENGINE ORDER BY ME.CD_ENGINE)) AS CD_ENGINE,
		   				MC.NM_SYSDEF AS NM_MAKER
		   		 FROM CZ_MA_HULL_ENGINE ME
		   		 LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = 'K100' AND MC.CD_FIELD = 'CZ_MA00003' AND MC.CD_SYSDEF = ME.CD_MAKER) AS ME
		   PIVOT (MAX(ME.NM_MAKER) FOR ME.CD_ENGINE IN ([ME1],[GE1],[GE2],[TC1],[TC2],[TC3])) PV) P2
ON P2.NO_IMO = MH.NO_IMO
LEFT JOIN (SELECT *
		   FROM (SELECT ME.NO_IMO,
		   				ME.CD_ENGINE + CONVERT(NVARCHAR, ROW_NUMBER() OVER (PARTITION BY ME.NO_IMO, ME.CD_ENGINE ORDER BY ME.CD_ENGINE)) AS CD_ENGINE,
		   				MC.NM_SYSDEF AS NM_CLS_M
		   		 FROM CZ_MA_HULL_ENGINE ME
		   		 LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = 'K100' AND MC.CD_FIELD = 'MA_B000031' AND MC.CD_SYSDEF = ME.CLS_M) AS ME
		   PIVOT (MAX(ME.NM_CLS_M) FOR ME.CD_ENGINE IN ([ME1],[GE1],[GE2],[TC1],[TC2],[TC3])) PV) P3
ON P3.NO_IMO = MH.NO_IMO
LEFT JOIN (SELECT *
		   FROM (SELECT ME.NO_IMO,
		   				ME.CD_ENGINE + CONVERT(NVARCHAR, ROW_NUMBER() OVER (PARTITION BY ME.NO_IMO, ME.CD_ENGINE ORDER BY ME.CD_ENGINE)) AS CD_ENGINE,
		   				ME.SERIAL
		   		 FROM CZ_MA_HULL_ENGINE ME) AS ME
		   PIVOT (MAX(ME.SERIAL) FOR ME.CD_ENGINE IN ([ME1],[GE1],[GE2],[TC1],[TC2],[TC3])) PV) P4
ON P4.NO_IMO = MH.NO_IMO
LEFT JOIN (SELECT LRNO AS NO_IMO,
				  NO_SISTER,
				  (CASE WHEN ROW_NUMBER() OVER (PARTITION BY NO_SISTER ORDER BY BUILT) = 1 THEN 'P' ELSE 'C' END) AS CD_SISTER
		   FROM CZ_MA_SEAWEB_SISTER
		   WHERE NO_SISTER IN (SELECT NO_SISTER 
							   FROM (SELECT NO_SISTER,	
											LRNO AS NO_IMO,	
											ROW_NUMBER() OVER (PARTITION BY NO_SISTER ORDER BY BUILT) AS RN1,	
											ROW_NUMBER() OVER (PARTITION BY LRNO ORDER BY NO_SISTER) AS RN2
									 FROM CZ_MA_SEAWEB_SISTER) AS A
							   WHERE RN1 = 1 AND RN2 = 1)) SS
ON SS.NO_IMO = MH.NO_IMO
WHERE (ISNULL(@P_NO_IMO, '') = '' OR MH.NO_IMO LIKE @P_NO_IMO + '%')
AND (ISNULL(@P_NO_IMO_MULTI, '') = '' OR MH.NO_IMO IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_NO_IMO_MULTI))) 
AND (ISNULL(@P_NO_HULL, '') = '' OR MH.NO_HULL LIKE '%' + @P_NO_HULL + '%') 
AND (ISNULL(@P_NM_VESSEL, '') = '' OR MH.NM_VESSEL LIKE '%' + @P_NM_VESSEL + '%') 
AND (ISNULL(@P_NM_SHIP_OWNER, '') = '' OR MH.NM_SHIP_OWNER LIKE @P_NM_SHIP_OWNER + '%') 
AND (ISNULL(@P_CD_PARTNER, '') = '' OR MH.CD_PARTNER = @P_CD_PARTNER)
AND (ISNULL(@P_DC_SHIPBUILDER, '') = '' OR MH.DC_SHIPBUILDER LIKE '%' + @P_DC_SHIPBUILDER + '%')
AND (ISNULL(@P_TP_SHIP, '') = '' OR MH.TP_SHIP = @P_TP_SHIP)
AND (@P_YN_NEWSHIP  = 'N' OR (@P_YN_NEWSHIP = 'Y' AND MH.YN_NEWSHIP = 'Y'))
AND (@P_YN_SISTER = 'N' OR (@P_YN_SISTER = 'Y' AND SS.CD_SISTER IS NOT NULL))
AND (@P_YN_ATTACH = 'N' OR (@P_YN_ATTACH = 'Y' AND EXISTS (SELECT 1 
														   FROM CZ_MA_HULL_VENDOR_TYPE VT
														   JOIN MA_FILEINFO MF ON MF.CD_COMPANY = 'K100' AND MF.CD_MODULE = 'MA' AND MF.ID_MENU = 'P_CZ_MA_HULL' AND MF.CD_FILE = VT.NO_IMO + '_' + VT.CD_VENDOR + '_' + CONVERT(CHAR, VT.NO_TYPE)
														   WHERE VT.NO_IMO = MH.NO_IMO
														   AND (ISNULL(@P_CD_VENDOR, '') = '' OR VT.CD_VENDOR = @P_CD_VENDOR))))
AND ((ISNULL(@P_ENGINE_MAKER, '') = '' AND 
	  ISNULL(@P_NM_MODEL, '') = '' AND 
	  ISNULL(@P_SERIAL, '') = '') OR EXISTS (SELECT 1 
											 FROM CZ_MA_HULL_ENGINE HE
											 WHERE HE.NO_IMO = MH.NO_IMO
											 AND (ISNULL(@P_ENGINE_MAKER, '') = '' OR HE.CD_MAKER = @P_ENGINE_MAKER)
											 AND (ISNULL(@P_NM_MODEL, '') = '' OR HE.NM_MODEL LIKE @P_NM_MODEL + '%')
											 AND (ISNULL(@P_SERIAL, '') = '' OR HE.SERIAL LIKE @P_SERIAL + '%')))
AND ((ISNULL(@P_CD_CLS_L, '') = '' AND
	  ISNULL(@P_CD_CLS_M, '') = '' AND
	  ISNULL(@P_CD_CLS_S, '') = '') OR EXISTS (SELECT 1 
											   FROM (SELECT NO_IMO, CLS_L, CLS_M, CLS_S
													 FROM CZ_MA_HULL_ENGINE
													 UNION ALL
													 SELECT NO_IMO, CLS_L, CLS_M, CLS_S 
													 FROM CZ_MA_HULL_VENDOR_TYPE) A
											   WHERE A.NO_IMO = MH.NO_IMO
											   AND (ISNULL(@P_CD_CLS_L, '') = '' OR A.CLS_L = @P_CD_CLS_L)
											   AND (ISNULL(@P_CD_CLS_M, '') = '' OR A.CLS_M = @P_CD_CLS_M)
											   AND (ISNULL(@P_CD_CLS_S, '') = '' OR A.CLS_S = @P_CD_CLS_S)))
AND (ISNULL(@P_CD_VENDOR, '') = '' OR EXISTS (SELECT 1 
											  FROM CZ_MA_HULL_VENDOR
											  WHERE NO_IMO = MH.NO_IMO
											  AND CD_VENDOR = @P_CD_VENDOR
											  AND (ISNULL(@P_YN_LEADER, '') = 'N' OR CD_VENDOR NOT IN (SELECT CD_SYSDEF 
																									   FROM CZ_MA_CODEDTL
																									   WHERE CD_COMPANY = 'K100'
																									   AND CD_FIELD = 'CZ_MA00051'
																									   AND YN_USE = 'Y'))
											  AND (ISNULL(@P_YN_ENG, '') = 'N' OR CD_VENDOR = '02428')))
AND (@P_YN_UPLOAD = 'N' OR EXISTS (SELECT 1 
								   FROM CZ_MA_HULL_ENGINE_ITEM HI
								   WHERE HI.NO_IMO = MH.NO_IMO
								   AND HI.YN_UPLOAD = 'Y'))

GO

