USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_HULL_ENGINE_ITEM_EXCEL]    Script Date: 2019-01-16 오전 10:57:55 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_MA_HULL_ENGINE_ITEM_EXCEL] 
(
	@P_XML			XML, 
	@P_ID_USER		NVARCHAR(10)
) 
AS 

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DECLARE @V_DOC INT

EXEC SP_XML_PREPAREDOCUMENT @V_DOC OUTPUT, @P_XML 

SELECT * INTO #XML
FROM OPENXML (@V_DOC, '/XML/ROW', 2) WITH 
(
    NO_IMO			NVARCHAR(10), 
	NO_ENGINE		INT,
	NO_PLATE		NVARCHAR(100),
	NM_PLATE		NVARCHAR(200),
	WEIGHT			NUMERIC(11, 3),
	UNIT			NVARCHAR(4),
	CD_ITEM			NVARCHAR(20),
	UCODE			NVARCHAR(20),
	NO_POSITION		NUMERIC(11, 0),
	QT_PLATE		NUMERIC(11, 0),
	DC_RMK			NVARCHAR(1000),
	KCODE			NVARCHAR(20),
	KCODE2			NVARCHAR(20),
	YN_DELETE		NVARCHAR(1)
)

EXEC SP_XML_REMOVEDOCUMENT @V_DOC 

-- ================================================== DELETE
DELETE A
FROM CZ_MA_HULL_ENGINE_ITEM A
JOIN #XML B ON B.NO_IMO = A.NO_IMO AND B.NO_ENGINE = A.NO_ENGINE AND B.NO_PLATE = A.NO_PLATE
WHERE B.YN_DELETE = 'Y';

-- ================================================== INSERT, UPDATE
MERGE INTO CZ_MA_HULL_ENGINE_ITEM AS T
	  USING (SELECT NO_IMO,
					NO_ENGINE,
					NO_PLATE,
					NM_PLATE,
					WEIGHT,
					UNIT,
					CD_ITEM,
					UCODE,
					NO_POSITION,
					QT_PLATE,
					DC_RMK,
					KCODE,
					KCODE2
	         FROM #XML
			 WHERE ISNULL(YN_DELETE, 'N') = 'N') AS S
	  ON T.NO_IMO = S.NO_IMO
	  AND T.NO_ENGINE = S.NO_ENGINE
	  AND T.NO_PLATE = S.NO_PLATE
	  WHEN MATCHED THEN
		UPDATE SET T.NM_PLATE = S.NM_PLATE,
				   T.WEIGHT = S.WEIGHT,
				   T.UNIT = S.UNIT,
				   T.CD_ITEM = S.CD_ITEM,
				   T.UCODE = S.UCODE,
				   T.NO_POSITION = S.NO_POSITION,
				   T.QT_PLATE = S.QT_PLATE,
				   T.DC_RMK = REPLACE(S.DC_RMK, CHAR(10), CHAR(13) + CHAR(10)),
				   T.KCODE = S.KCODE,
				   T.KCODE2 = S.KCODE2,
				   T.ID_UPDATE = @P_ID_USER, 
				   T.DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
	  WHEN NOT MATCHED THEN 
		INSERT (NO_IMO,
				NO_ENGINE,
				NO_PLATE,
				NM_PLATE,
				WEIGHT,
				UNIT,
				CD_ITEM,
				UCODE,
				NO_POSITION,
				QT_PLATE,
				DC_RMK,
				KCODE,
				KCODE2,
				ID_INSERT,
				DTS_INSERT) 
	   	VALUES (S.NO_IMO,
				S.NO_ENGINE,
				S.NO_PLATE,
				S.NM_PLATE,
				S.WEIGHT,
				S.UNIT,
				S.CD_ITEM,
				S.UCODE,
				S.NO_POSITION,
				S.QT_PLATE,
				REPLACE(S.DC_RMK, CHAR(10), CHAR(13) + CHAR(10)),
				S.KCODE,
				S.KCODE2,
				@P_ID_USER, 
				NEOE.SF_SYSDATE(GETDATE()));

DROP TABLE #XML

GO