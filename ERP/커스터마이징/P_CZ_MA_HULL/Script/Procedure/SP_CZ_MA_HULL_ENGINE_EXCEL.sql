USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_HULL_SUPPLIER_EXCEL]    Script Date: 2019-01-16 오전 10:57:55 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO






ALTER PROCEDURE [NEOE].[SP_CZ_MA_HULL_ENGINE_EXCEL] 
(
	@P_XML			XML, 
	@P_ID_USER		NVARCHAR(10)
) 
AS 

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DECLARE @V_DOC INT

EXEC SP_XML_PREPAREDOCUMENT @V_DOC OUTPUT, @P_XML 

SELECT * INTO #XML
FROM OPENXML (@V_DOC, '/XML/ROW', 2) WITH 
(
    NO_IMO			NVARCHAR(10),
	NO_ENGINE		INT,
	CD_ENGINE		NVARCHAR(3),
	NM_MODEL		NVARCHAR(100),
	CD_MAKER		NVARCHAR(4),
	CAPACITY		NUMERIC(10, 0),
	SERIAL			NVARCHAR(20),
	CLS_L			NVARCHAR(4),
	CLS_M			NVARCHAR(4),
	CLS_S			NVARCHAR(4),
	DC_VERSION		NVARCHAR(50),
	DC_RMK			NVARCHAR(100),
	YN_DELETE		NVARCHAR(1)
)

EXEC SP_XML_REMOVEDOCUMENT @V_DOC 

-- DELETE
IF EXISTS (SELECT 1 
		   FROM CZ_SA_QTNH QH
		   LEFT JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = QH.CD_COMPANY AND QL.NO_FILE = QH.NO_FILE
		   WHERE EXISTS (SELECT 1 
			   			 FROM #XML A 
			   			 WHERE A.NO_IMO = QH.NO_IMO
			   			 AND A.NO_ENGINE = QL.NO_ENGINE
						 AND A.YN_DELETE = 'Y'))
BEGIN
	DECLARE @V_DC_MSG NVARCHAR(500)
	SET @V_DC_MSG = '견적등록에 사용중인 엔진이 포함되어 있습니다.'

	RAISERROR(@V_DC_MSG, 16, 1)
	RETURN
END
ELSE
BEGIN
	DELETE A
	FROM CZ_MA_HULL_ENGINE A
	JOIN #XML B ON B.NO_IMO = A.NO_IMO AND B.NO_ENGINE = A.NO_ENGINE
	WHERE B.YN_DELETE = 'Y'
	
	DELETE A
	FROM CZ_MA_HULL_ENGINE_ITEM A
	JOIN #XML B ON B.NO_IMO = A.NO_IMO AND B.NO_ENGINE = A.NO_ENGINE
	WHERE B.YN_DELETE = 'Y'
END;

-- INSERT
WITH A AS
(
	SELECT A.NO_IMO,
		   ISNULL(B.NO_ENGINE, 0) + ROW_NUMBER() OVER (PARTITION BY A.NO_IMO ORDER BY A.NO_IMO) AS NO_ENGINE,
	       A.CD_ENGINE,
		   A.NM_MODEL,
		   A.CD_MAKER,
		   A.CAPACITY,
		   A.SERIAL,
	       A.CLS_L,
	       A.CLS_M,
	       A.CLS_S,
		   A.DC_VERSION,
	       A.DC_RMK,
	       @P_ID_USER AS ID_INSERT,
	       NEOE.SF_SYSDATE(GETDATE()) AS DTS_INSERT
	FROM #XML A
	LEFT JOIN (SELECT NO_IMO,
					  MAX(NO_ENGINE) AS NO_ENGINE
			   FROM CZ_MA_HULL_ENGINE 
			   GROUP BY NO_IMO) B
	ON B.NO_IMO = A.NO_IMO
	WHERE A.NO_ENGINE IS NULL
)
INSERT INTO CZ_MA_HULL_ENGINE
(
	NO_IMO,
	NO_ENGINE,
	CD_ENGINE,
	NM_MODEL,
	CD_MAKER,
	CAPACITY,
	SERIAL,
	CLS_L,
	CLS_M,
	CLS_S,
	DC_VERSION,
	DC_RMK,
	ID_INSERT,
	DTS_INSERT
)
SELECT NO_IMO,
	   NO_ENGINE,
	   CD_ENGINE,
	   NM_MODEL,
	   CD_MAKER,
	   CAPACITY,
	   SERIAL,
	   CLS_L,
	   CLS_M,
	   CLS_S,
	   DC_VERSION,
	   DC_RMK,
	   ID_INSERT,
	   DTS_INSERT 
FROM A;

-- UPDATE
MERGE INTO CZ_MA_HULL_ENGINE AS T
	  USING (SELECT NO_IMO,
					NO_ENGINE,
					CD_ENGINE,
					NM_MODEL,
					CD_MAKER,
					CAPACITY,
					SERIAL,
					CLS_L,
					CLS_M,
					CLS_S,
					DC_VERSION,
					DC_RMK
	         FROM #XML
			 WHERE NO_ENGINE IS NOT NULL) AS S
	  ON T.NO_IMO = S.NO_IMO
	  AND T.NO_ENGINE = S.NO_ENGINE
	  WHEN MATCHED THEN
		UPDATE SET T.CD_ENGINE = S.CD_ENGINE,
				   T.NM_MODEL = S.NM_MODEL,
				   T.CD_MAKER = S.CD_MAKER,
				   T.CAPACITY = S.CAPACITY,
				   T.SERIAL = S.SERIAL,
				   T.CLS_L = S.CLS_L,
				   T.CLS_M = S.CLS_M,
				   T.CLS_S = S.CLS_S,
				   T.DC_VERSION = S.DC_VERSION,
				   T.DC_RMK = S.DC_RMK,
				   T.ID_UPDATE = @P_ID_USER,
				   T.DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE());

DROP TABLE #XML

GO