USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_MA_TARIFF_CBM_L_XML]    Script Date: 2016-06-28 오후 1:28:30 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO




ALTER PROCEDURE [NEOE].[SP_CZ_MA_TARIFF_CBM_L_XML] 
(
	@P_XML			XML, 
	@P_ID_USER		NVARCHAR(10), 
    @DOC			INT = NULL
) 
AS 

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

EXEC SP_XML_PREPAREDOCUMENT @DOC OUTPUT, @P_XML 

-- ================================================== DELETE
DELETE A 
FROM CZ_MA_TARIFF_CBM_L A 
JOIN OPENXML (@DOC, '/XML/D', 2) 
        WITH (CD_COMPANY	NVARCHAR(7),
			  CD_PARTNER	NVARCHAR(20),
			  SIZE			NUMERIC(18, 2),
			  SEQ 			INT) B 
ON A.CD_COMPANY = B.CD_COMPANY 
AND A.CD_PARTNER = B.CD_PARTNER
AND A.SIZE = B.SIZE
AND A.SEQ = B.SEQ;
-- ================================================== INSERT
WITH A AS
(
	SELECT CD_COMPANY,
		   CD_PARTNER,
		   SIZE,
		   0 AS SEQ,
		   UM,
		   DT_START,
		   DT_END,
		   @P_ID_USER AS ID_INSERT, 
		   NEOE.SF_SYSDATE(GETDATE()) AS DTS_INSERT 
    FROM OPENXML (@DOC, '/XML/I', 2) 
	        WITH (CD_COMPANY		NVARCHAR(7), 
				  CD_PARTNER		NVARCHAR(20),
				  SIZE				NUMERIC(18, 2),
				  UM				NUMERIC(18, 0),
				  DT_START			NVARCHAR(8),
				  DT_END			NVARCHAR(8)) 
),
B AS
(
	SELECT CD_COMPANY,
		   CD_PARTNER,
		   SIZE,
		   SEQ,
		   UM,
		   DT_START,
		   DT_END,
		   ID_INSERT,
		   DTS_INSERT
	FROM CZ_MA_TARIFF_CBM_L TL
	WHERE EXISTS (SELECT 1 
				  FROM A
				  WHERE A.CD_COMPANY = TL.CD_COMPANY
				  AND A.CD_PARTNER = TL.CD_PARTNER
				  AND A.SIZE = TL.SIZE)
	UNION ALL
	SELECT *
	FROM A	
),
C AS
(
	SELECT *,
		   ROW_NUMBER() OVER (PARTITION BY B.CD_COMPANY, B.CD_PARTNER, B.SIZE ORDER BY B.DTS_INSERT) AS SEQ1
	FROM B
)
INSERT INTO CZ_MA_TARIFF_CBM_L 
(
	CD_COMPANY,
	CD_PARTNER,
	SIZE,
	SEQ,
	UM,
	DT_START,
	DT_END,
	ID_INSERT,
	DTS_INSERT
)
SELECT CD_COMPANY,
	   CD_PARTNER,
	   SIZE,
	   SEQ1 AS SEQ,
	   UM,
	   DT_START,
	   DT_END,
	   ID_INSERT,
	   DTS_INSERT 
FROM C
WHERE C.SEQ = 0

-- ================================================== UPDATE    
UPDATE A 
   SET A.UM = B.UM,
	   A.DT_START = B.DT_START,
	   A.DT_END = B.DT_END,
	   A.ID_UPDATE = @P_ID_USER, 
	   A.DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
  FROM CZ_MA_TARIFF_CBM_L A 
  JOIN OPENXML (@DOC, '/XML/U', 2) 
          WITH (CD_COMPANY		NVARCHAR(7),
				CD_PARTNER		NVARCHAR(20),
				SIZE			NUMERIC(18, 2),
				SEQ				INT,
				UM				NUMERIC(18, 0),
				DT_START		NVARCHAR(8),
				DT_END			NVARCHAR(8)) B 
  ON A.CD_COMPANY = B.CD_COMPANY
  AND A.CD_PARTNER = B.CD_PARTNER
  AND A.SIZE = B.SIZE
  AND A.SEQ = B.SEQ

EXEC SP_XML_REMOVEDOCUMENT @DOC 

GO