USE [NEOE]
GO
/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_CONTRACT_RPTH_S]    Script Date: 2016-01-05 오후 6:05:09 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [NEOE].[SP_CZ_SA_CONTRACT_RPTH_S] 
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_TP_SO			NVARCHAR(2),
	@P_NO_SO			NVARCHAR(20),
	@P_TP_DATE			NVARCHAR(3),
	@P_DT_DATE_FROM		NCHAR(8),
	@P_DT_DATE_TO	    NCHAR(8),
	@P_NO_EMP			NVARCHAR(10),
	@P_NO_CONTRACT		NVARCHAR(40),
	@P_CD_PARTNER_GRP	NVARCHAR(10),
	@P_CD_PARTNER		NVARCHAR(20),
	@P_CD_PO_PARTNER	NVARCHAR(20),
	@P_YN_STA			NVARCHAR(1),
	@P_YN_CHARGE		NVARCHAR(1),
	@P_YN_CLAIM			NVARCHAR(1),
	@P_GRP_ITEM			NVARCHAR(20),
	@P_CD_SALEGRP		NVARCHAR(7),
	@P_NO_IMO			NVARCHAR(10),
	@P_YN_CLOSE			NVARCHAR(1),
	@P_YN_RE_CONFIRM	NVARCHAR(1),
	@P_YN_LOW_PROFIT	NVARCHAR(1)
) 
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

;WITH A AS
(
	SELECT SH.NO_CONTRACT, 
		   SH.NO_SO, 
		   SH.STA_SO,
		   MC.NM_SYSDEF AS NM_STA_SO,
		   SH.CD_SALEGRP,
		   SG.NM_SALEGRP,
		   SH.CD_PARTNER,
		   MP.LN_PARTNER,
		   SH.CD_PARTNER_GRP,
		   MC1.NM_SYSDEF AS NM_PARTNER_GRP,
		   MH.NO_HULL,
		   MH.NM_VESSEL, 
		   (CASE WHEN TP.RET = 'Y' THEN -ISNULL(SL.AM_SO, 0) ELSE ISNULL(SL.AM_SO, 0) END) AS AM_EX_SO,
		   (CASE WHEN TP.RET = 'Y' THEN -ISNULL(SL.AM_WONAMT, 0) ELSE ISNULL(SL.AM_WONAMT, 0) END) AS AM_WONAMT,
		   (ISNULL(PL.AM_PO, 0) + ISNULL(SB.AM_STOCK, 0)) AS AM_PO,
		   (CASE WHEN TP.RET = 'Y' THEN -ISNULL(SL.AM_SO_CHARGE, 0) ELSE ISNULL(SL.AM_SO_CHARGE, 0) END) AS AM_SO_CHARGE,
		   ISNULL(PL.AM_PO_CHARGE, 0) AS AM_PO_CHARGE,
		   ((CASE WHEN TP.RET = 'Y' THEN -ISNULL(SL.AM_WONAMT, 0) ELSE ISNULL(SL.AM_WONAMT, 0) END) - (ISNULL(PL.AM_PO, 0) + ISNULL(SB.AM_STOCK, 0))) AS PROFIT,
		   ((CASE WHEN TP.RET = 'Y' THEN -ISNULL(SL.AM_WONAMT_ALL, 0) ELSE ISNULL(SL.AM_WONAMT_ALL, 0) END) - (ISNULL(PL.AM_PO_ALL, 0) + ISNULL(SB.AM_STOCK, 0))) AS PROFIT_ALL,
		   (CASE WHEN (CASE WHEN TP.RET = 'Y' THEN -ISNULL(SL.AM_WONAMT, 0) ELSE ISNULL(SL.AM_WONAMT, 0) END) = 0 THEN 0 
																												  ELSE ROUND(((1 - ((ISNULL(PL.AM_PO, 0) + ISNULL(SB.AM_STOCK, 0)) / (CASE WHEN TP.RET = 'Y' THEN -ISNULL(SL.AM_WONAMT, 0) ELSE ISNULL(SL.AM_WONAMT, 0) END))) * 100), 2) END) AS RT_PROFIT,
		   (CASE WHEN (CASE WHEN TP.RET = 'Y' THEN -ISNULL(SL.AM_WONAMT_ALL, 0) ELSE ISNULL(SL.AM_WONAMT_ALL, 0) END) = 0 THEN 0 
																														  ELSE ROUND(((1 - ((ISNULL(PL.AM_PO_ALL, 0) + ISNULL(SB.AM_STOCK, 0)) / (CASE WHEN TP.RET = 'Y' THEN -ISNULL(SL.AM_WONAMT_ALL, 0) ELSE ISNULL(SL.AM_WONAMT_ALL, 0) END))) * 100), 2) END) AS RT_PROFIT_ALL,
		   MC2.NM_SYSDEF AS NM_EXCH,
		   ISNULL(SH.RT_EXCH, 0) RT_EXCH,
		   QH.DT_QTN, 
		   SH.DT_SO, 
		   DT_CONTRACT, 
		   ME.NM_KOR NM_SO_EMP,
		   ME1.NM_KOR NM_QT_EMP,
		   PL.DT_PO, 
		   SH.TP_TRANS,
		   SH.PORT_LOADING AS TP_TRANSPORT,
		   MC3.NM_SYSDEF + ' ' + SH.PORT_LOADING AS NM_TP_TRANSPORT,
		   SH.COND_PAY,
		   MC4.NM_SYSDEF AS NM_COND_PAY,
		   (CASE WHEN ISNULL(MC4.CD_FLAG1, '') <> '' THEN 'Y' ELSE 'N' END) AS YN_CIA, 
		   SH.DC_RMK1,
		   SH.DC_RMK_CONTRACT AS DC_RMK, 
		   ISNULL(WH.YN_DONE, 'N') AS YN_ACK,
		   SL.QT_SO,
		   SL.QT_SO_ORG,
		   (ISNULL(SL.QT_PO, 0) + ISNULL(SB.QT_STOCK, 0)) AS QT_PO_ORG,
		   SH.NO_PO_PARTNER,
		   QH.NO_REF,
		   SL.QT_GIR,
		   SL.QT_GIR_PACK, 
		   (CASE WHEN ISNULL(QH.CD_COMPANY, '') = '' THEN 'Y' ELSE 'N' END) AS YN_CHARGE,
		   CR.ID_FIRST_APPROVAL,
		   MU.NM_USER AS NM_FIRST_APPROVAL,
		   CR.DTS_FIRST_APPROVAL,
		   CR.ID_SECOND_APPROVAL,
		   MU1.NM_USER AS NM_SECOND_APPROVAL,
		   CR.DTS_SECOND_APPROVAL,
		   CR.DC_COMMENT_FIRST,
		   CR.DC_COMMENT_SECOND,
		   TP.NM_SO,
		   EJ.YN_AM,
		   (CASE WHEN EXISTS (SELECT 1 
							  FROM CZ_MA_WORKFLOWL WL 
							  WHERE WL.CD_COMPANY = SH.CD_COMPANY
							  AND WL.NO_KEY = SH.NO_SO
							  AND WL.TP_STEP = '56') THEN 'Y' ELSE 'N' END) AS YN_PROFORMA,
		   SH.RT_PROFIT AS RT_PROFIT_SO,
		   SH.RT_PROFIT_CHARGE
	FROM SA_SOH SH
	JOIN (SELECT SL.CD_COMPANY, SL.NO_SO,
	 			 ISNULL(SUM(SL.QT_SO), 0) AS QT_SO,
	 			 ISNULL(SUM(CASE WHEN MI.CD_ITEM NOT LIKE 'SD%' OR SL.QT_PO > 0 THEN SL.QT_SO ELSE 0 END), 0) AS QT_SO_ORG,
	 			 ISNULL(SUM(CASE WHEN MI.CD_ITEM NOT LIKE 'SD%' THEN SL.AM_SO ELSE 0 END), 0) AS AM_SO,
	 			 ISNULL(SUM(CASE WHEN MI.CD_ITEM NOT LIKE 'SD%' THEN SL.AM_WONAMT ELSE 0 END), 0) AS AM_WONAMT,
				 ISNULL(SUM(CASE WHEN MI.CD_ITEM LIKE 'SD%' THEN SL.AM_WONAMT ELSE 0 END), 0) AS AM_SO_CHARGE,
				 ISNULL(SUM(SL.AM_WONAMT), 0) AS AM_WONAMT_ALL,
				 ISNULL(SUM(SL.QT_PO), 0) AS QT_PO,
	 			 ISNULL(SUM(SL.QT_GIR), 0) AS QT_GIR,
				 ISNULL(SUM(GL.QT_GIR_PACK), 0) AS QT_GIR_PACK
		  FROM SA_SOL SL
		  LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = SL.CD_COMPANY AND MI.CD_PLANT = SL.CD_PLANT AND MI.CD_ITEM = SL.CD_ITEM
		  LEFT JOIN CZ_SA_QTNL QL ON QL.CD_COMPANY = SL.CD_COMPANY AND QL.NO_FILE = SL.NO_SO AND QL.NO_LINE = SL.SEQ_SO
		  LEFT JOIN (SELECT CD_COMPANY, NO_SO, SEQ_SO,
					 	    SUM(QT_GIR) AS QT_GIR_PACK 
					 FROM CZ_SA_GIRL_PACK
					 GROUP BY CD_COMPANY, NO_SO, SEQ_SO) GL
		  ON GL.CD_COMPANY = SL.CD_COMPANY AND GL.NO_SO = SL.NO_SO AND GL.SEQ_SO = SL.SEQ_SO
		  WHERE (ISNULL(@P_GRP_ITEM, '') = '' OR QL.GRP_ITEM = @P_GRP_ITEM)
		  AND (ISNULL(@P_CD_PO_PARTNER, '') = '' OR EXISTS (SELECT 1 
					                                        FROM PU_POH PH
															LEFT JOIN PU_POL PL ON PL.CD_COMPANY = PH.CD_COMPANY AND PL.NO_PO = PH.NO_PO
					                                        WHERE PH.CD_COMPANY = SL.CD_COMPANY 
															AND PH.CD_PARTNER = @P_CD_PO_PARTNER
															AND PH.CD_TPPO NOT IN ('1300', '1400', '2300', '2400')
															AND PL.NO_SO = SL.NO_SO
															AND PL.NO_SOLINE = SL.SEQ_SO
					                                        UNION
					                                        SELECT 1 
					                                        FROM CZ_SA_STOCK_BOOK SB
					                                        WHERE SB.CD_COMPANY = SL.CD_COMPANY 
															AND @P_CD_PO_PARTNER = 'STOCK'
															AND SB.NO_FILE = SL.NO_SO 
															AND SB.NO_LINE = SL.SEQ_SO))
		  GROUP BY SL.CD_COMPANY, SL.NO_SO) SL
	ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
	LEFT JOIN (SELECT SB.CD_COMPANY, SB.NO_FILE,
			   	      ISNULL(SUM(SB.QT_STOCK), 0) AS QT_STOCK,
			   	      ISNULL(SUM(SB.UM_KR * SB.QT_STOCK), 0) AS AM_STOCK
			   FROM CZ_SA_STOCK_BOOK SB
			   WHERE (ISNULL(@P_CD_PO_PARTNER, '') = '' OR @P_CD_PO_PARTNER = 'STOCK')
			   GROUP BY SB.CD_COMPANY, SB.NO_FILE) SB
	ON SB.CD_COMPANY = SH.CD_COMPANY AND SB.NO_FILE = SH.NO_SO
	LEFT JOIN (SELECT PH.CD_COMPANY, PH.CD_PJT,
			   	      ISNULL(SUM(CASE WHEN PL.CD_ITEM NOT LIKE 'SD%' THEN PL.AM ELSE 0 END), 0) AS AM_PO,
			   	      ISNULL(SUM(CASE WHEN PL.CD_ITEM LIKE 'SD%' THEN PL.AM ELSE 0 END), 0) AS AM_PO_CHARGE,
			   	      ISNULL(SUM(PL.AM), 0) AS AM_PO_ALL,
			   	      MAX(PH.DT_PO) AS DT_PO   
			   FROM PU_POH PH
			   JOIN PU_POL PL ON PL.CD_COMPANY = PH.CD_COMPANY AND PL.NO_PO = PH.NO_PO
			   WHERE PH.CD_TPPO NOT IN ('1300', '1400', '2300', '2400')
			   AND (ISNULL(@P_CD_PO_PARTNER, '') = '' OR PH.CD_PARTNER = @P_CD_PO_PARTNER)
			   GROUP BY PH.CD_COMPANY, PH.CD_PJT) PL
	ON PL.CD_COMPANY = SH.CD_COMPANY AND PL.CD_PJT = SH.NO_SO
	LEFT JOIN SA_TPSO TP ON TP.CD_COMPANY = SH.CD_COMPANY AND TP.TP_SO = SH.TP_SO
	LEFT JOIN MM_EJTP EJ ON EJ.CD_COMPANY = TP.CD_COMPANY AND EJ.CD_QTIOTP = TP.TP_GI
	LEFT JOIN FI_GWDOCU GW ON GW.CD_COMPANY = 'K100' AND GW.CD_PC = '010000' AND GW.NO_DOCU = SH.NO_DOCU
	LEFT JOIN CZ_SA_CONTRACT_RPTH CR ON CR.CD_COMPANY = SH.CD_COMPANY AND CR.NO_SO = SH.NO_SO
	LEFT JOIN MA_SALEGRP SG ON SG.CD_COMPANY = SH.CD_COMPANY AND SG.CD_SALEGRP = SH.CD_SALEGRP
	LEFT JOIN CZ_SA_QTNH QH ON QH.CD_COMPANY = SH.CD_COMPANY AND QH.NO_FILE = SH.NO_SO 
	LEFT JOIN CZ_SA_CLAIMH CH ON CH.CD_COMPANY = SH.CD_COMPANY AND CH.NO_CLAIM = SH.NO_SO 
	LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = SH.CD_COMPANY AND MP.CD_PARTNER = SH.CD_PARTNER 
	LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = SH.NO_IMO 
	LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = SH.CD_COMPANY AND ME.NO_EMP = SH.NO_EMP
	LEFT JOIN MA_EMP ME1 ON ME1.CD_COMPANY = QH.CD_COMPANY AND ME1.NO_EMP = QH.NO_EMP
	LEFT JOIN MA_USER MU ON MU.CD_COMPANY = CR.CD_COMPANY AND MU.ID_USER = CR.ID_FIRST_APPROVAL
	LEFT JOIN MA_USER MU1 ON MU1.CD_COMPANY = CR.CD_COMPANY AND MU1.ID_USER = CR.ID_SECOND_APPROVAL
	LEFT JOIN CZ_MA_WORKFLOWH WH ON WH.CD_COMPANY = SH.CD_COMPANY AND WH.NO_KEY = SH.NO_SO AND WH.TP_STEP = '09'
	LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = SH.CD_COMPANY AND MC.CD_FIELD = 'SA_B000016' AND MC.CD_SYSDEF = SH.STA_SO
	LEFT JOIN MA_CODEDTL MC1 ON MC1.CD_COMPANY = SH.CD_COMPANY AND MC1.CD_FIELD = 'MA_B000065' AND MC1.CD_SYSDEF = SH.CD_PARTNER_GRP
	LEFT JOIN MA_CODEDTL MC2 ON MC2.CD_COMPANY = SH.CD_COMPANY AND MC2.CD_FIELD = 'MA_B000005' AND MC2.CD_SYSDEF = SH.CD_EXCH
	LEFT JOIN MA_CODEDTL MC3 ON MC3.CD_COMPANY = SH.CD_COMPANY AND MC3.CD_FIELD = 'TR_IM00003' AND MC3.CD_SYSDEF = SH.TP_TRANS
	LEFT JOIN MA_CODEDTL MC4 ON MC4.CD_COMPANY = SH.CD_COMPANY AND MC4.CD_FIELD = 'CZ_SA00013' AND MC4.CD_SYSDEF = SH.COND_PAY
	WHERE SH.CD_COMPANY = @P_CD_COMPANY
	AND (@P_YN_STA = 'A' OR
		(@P_YN_STA = 'Y' AND ISNULL(SH.NO_CONTRACT, '') <> '') OR 
		(@P_YN_STA = 'N' AND ISNULL(SH.NO_CONTRACT, '') = ''))
	AND (@P_YN_CLOSE = 'A' OR
		(@P_YN_CLOSE = 'Y' AND ISNULL(SH.YN_CLOSE, 'N') = 'Y' AND GW.ST_STAT = '1') OR 
		(@P_YN_CLOSE = 'N' AND ISNULL(SH.YN_CLOSE, 'N') <> 'Y' OR GW.ST_STAT <> '1'))
	AND (@P_YN_CHARGE = 'Y' OR 
		(@P_YN_CHARGE = 'N' AND QH.CD_COMPANY IS NOT NULL))
	AND (@P_YN_CLAIM = 'Y' OR 
		(@P_YN_CLAIM = 'N' AND SH.NO_SO NOT LIKE 'CL%' AND CH.NO_CLAIM IS NULL))
	AND ((@P_TP_DATE = '001' AND SH.DT_SO BETWEEN @P_DT_DATE_FROM AND @P_DT_DATE_TO) OR 
		 (@P_TP_DATE = '002' AND SH.DT_CONTRACT BETWEEN @P_DT_DATE_FROM AND @P_DT_DATE_TO))
	AND (ISNULL(@P_NO_SO, '') = '' OR SH.NO_SO = @P_NO_SO) 
	AND (ISNULL(@P_NO_IMO, '') = '' OR SH.NO_IMO = @P_NO_IMO) 
	AND (ISNULL(@P_TP_SO, '') = '' OR SH.NO_SO LIKE @P_TP_SO + '%') 
	AND (ISNULL(@P_NO_CONTRACT, '') = '' OR SH.NO_CONTRACT = @P_NO_CONTRACT)
	AND (ISNULL(@P_NO_EMP, '') = '' OR SH.NO_EMP = @P_NO_EMP) 
	AND (ISNULL(@P_CD_PARTNER, '') = '' OR SH.CD_PARTNER = @P_CD_PARTNER)
	AND (ISNULL(@P_CD_PARTNER_GRP, '') = '' OR MP.CD_PARTNER_GRP = @P_CD_PARTNER_GRP)
	AND (ISNULL(@P_CD_SALEGRP, '') = '' OR SH.CD_SALEGRP = @P_CD_SALEGRP)
)
SELECT 'N' AS S, * 
FROM A
WHERE (@P_YN_RE_CONFIRM = 'N' OR 
	   (@P_YN_RE_CONFIRM = 'Y' AND ISNULL(A.NO_CONTRACT, '') <> '' AND (A.RT_PROFIT_SO <> A.RT_PROFIT OR A.RT_PROFIT_CHARGE <> A.RT_PROFIT_ALL)))
AND (@P_YN_LOW_PROFIT = 'N' OR
	 (@P_YN_LOW_PROFIT = 'Y' AND A.RT_PROFIT_ALL < 5))
OPTION(RECOMPILE)

GO