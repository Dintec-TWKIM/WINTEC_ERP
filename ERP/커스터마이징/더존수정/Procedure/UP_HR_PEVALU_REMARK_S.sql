USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[UP_HR_PEVALU_REMARK_S]    Script Date: 2015-11-12 오전 9:09:15 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

/*******************************************
**  System		: 인사관리
**  Sub System	: 인사평가
**  Page		: 인사평가의견현황
**  Desc		: 조회(NO목표등록)
**  
**  Return Values
**
**  작    성    자 	: 강연철
**  작    성    일	: 2015.06.04
**  수    정    자  : 
**  수  정  내  용  : 
*********************************************
** Change History
*********************************************
*********************************************/

ALTER PROCEDURE [NEOE].[UP_HR_PEVALU_REMARK_S]
(
	@P_CD_COMPANY	NVARCHAR(7),
	@P_CD_EVALU		NVARCHAR(8),
	@P_CD_EVTYPE	NVARCHAR(3),
	@P_CD_EVNUMBER	NVARCHAR(3),
	@P_CD_GROUP		NVARCHAR(3),
	@P_NO_EMPM		NVARCHAR(10),
	@P_NO_EMPAN		NVARCHAR(10),
	@P_YN_OBJECT	NVARCHAR(1)
)
AS
BEGIN	
	IF @P_YN_OBJECT = 'N'
	BEGIN
		SELECT	CE.CD_COMPANY,
				CE.CD_EVALU,
				CE.CD_FIELD,
				CE.CD_HCODE,
				CE.NM_HCODE AS NM_EVTYPE,
				CE.YN_OBJECT,
				CE.YN_BEFO,
				CE2.NM_HCODE AS NM_EVNUMBER,
				IM.CD_EVTYPE,
				CE2.CD_HCODE AS CD_EVNUMBER,
				HN.NO_EMPM AS NO_EMPM,
				HN.NO_EMPAN AS NO_EMPAN,
				EP.NM_KOR,
				IM.LV1_EVITEM,
				IM.LV1_NMITEM,
				IM.LV1_LBITEM,
				IM.LV2_EVITEM,
				IM.LV2_NMITEM,
				IM.LV2_LBITEM,
				IM.LV3_EVITEM,
				IM.LV3_NMITEM,
				IM.LV3_LBITEM,
				SC.DC_COMMENT1
		FROM	HR_PEVALU_CODE CE
		LEFT OUTER JOIN (	SELECT	LV1.CD_COMPANY,
									LV1.CD_EVALU,
									LV1.CD_EVTYPE,
									LV1.CD_GROUP,
									LV1.CD_EVITEM AS LV1_EVITEM,
									LV1.NM_EVITEM AS LV1_NMITEM,
									LV1.LB_EVITEM AS LV1_LBITEM,
									LV1.CD_SCALE AS LV1_CDSCALE,
									LV1.DC_INDEX AS LV1_DCINDEX,
									LV1.NUM_EWEIGHT AS LV1_EWEIGHT,
									LV2.CD_EVITEM AS LV2_EVITEM,
									LV2.NM_EVITEM AS LV2_NMITEM,
									LV2.LB_EVITEM AS LV2_LBITEM,
									LV2.CD_SCALE AS LV2_CDSCALE,
									LV2.DC_INDEX AS LV2_DCINDEX,
									LV2.NUM_EWEIGHT AS LV2_EWEIGHT,
									LV3.CD_EVITEM AS LV3_EVITEM,
									LV3.NM_EVITEM AS LV3_NMITEM,
									LV3.LB_EVITEM AS LV3_LBITEM,
									LV3.CD_SCALE AS LV3_CDSCALE,
									LV3.DC_INDEX  AS LV3_DCINDEX,
									LV3.NUM_EWEIGHT AS LV3_EWEIGHT
							FROM	HR_PEVALU_ITEM LV1
							LEFT OUTER JOIN HR_PEVALU_ITEM LV2
							ON		LV1.CD_COMPANY	= LV2.CD_COMPANY
							AND		LV1.CD_EVALU	= LV2.CD_EVALU
							AND		LV1.CD_EVTYPE	= LV2.CD_EVTYPE
							AND		LV1.CD_GROUP	= LV2.CD_GROUP
							AND		LV2.H_EVITEM	= LV1.CD_EVITEM
							AND		LV2.LB_EVITEM	= 2
							LEFT OUTER JOIN HR_PEVALU_ITEM LV3
							ON		LV2.CD_COMPANY	= LV3.CD_COMPANY
							AND		LV2.CD_EVALU	= LV3.CD_EVALU
							AND		LV2.CD_EVTYPE	= LV3.CD_EVTYPE
							AND		LV2.CD_GROUP	= LV3.CD_GROUP
							AND		LV3.H_EVITEM	= LV2.CD_EVITEM
							AND		LV3.LB_EVITEM	= 3
							WHERE	LV1.CD_COMPANY	= @P_CD_COMPANY
							AND		LV1.CD_EVALU	= @P_CD_EVALU
							AND		LV1.CD_EVTYPE	= @P_CD_EVTYPE
							AND		LV1.CD_GROUP	= @P_CD_GROUP
							AND		LV1.LB_EVITEM	= 1 ) IM
		ON		CE.CD_COMPANY	= IM.CD_COMPANY
		AND		CE.CD_EVALU		= IM.CD_EVALU
		AND		CE.CD_FIELD		= IM.CD_EVTYPE
		LEFT OUTER JOIN HR_PEVALU_SCORE SC
		ON		SC.CD_COMPANY = CE.CD_COMPANY
		AND		SC.CD_EVALU = CE.CD_EVALU
		AND		SC.CD_EVTYPE  = CE.CD_FIELD
		AND		SC.CD_EVNUMBER	= @P_CD_EVNUMBER
		AND     SC.CD_GROUP = @P_CD_GROUP
		AND		SC.CD_EVOTYPE	IN (IM.LV1_EVITEM, IM.LV2_EVITEM, IM.LV3_EVITEM)
		AND		SC.LB_EVITEM	IN (IM.LV1_LBITEM, IM.LV2_LBITEM, IM.LV3_LBITEM)
		AND		SC.NO_EMPM		= @P_NO_EMPM
		AND		(@P_NO_EMPAN IS NULL OR @P_NO_EMPAN = '' OR SC.NO_EMPAN IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_NO_EMPAN)))
		LEFT OUTER JOIN HR_PEVALU_HUMEMPAN HN
		ON		HN.CD_COMPANY	= SC.CD_COMPANY
		AND		HN.CD_EVALU		= SC.CD_EVALU
		AND		HN.CD_EVTYPE	= SC.CD_EVTYPE
		AND		HN.CD_EVNUMBER	= SC.CD_EVNUMBER
		AND		HN.CD_GROUP		= SC.CD_GROUP
		AND		HN.NO_EMPM		= SC.NO_EMPM
		AND     HN.NO_EMPAN     = SC.NO_EMPAN
		LEFT OUTER JOIN MA_EMP EP
		ON		EP.CD_COMPANY	= HN.CD_BIZAREA
		AND		EP.NO_EMP		= HN.NO_EMPAN
		LEFT OUTER JOIN HR_PEVALU_CODE CE2
		ON		CE2.CD_COMPANY	= CE.CD_COMPANY
		AND		CE2.CD_EVALU	= CE.CD_EVALU
		AND		CE2.CD_FIELD	= '300'
		AND		CE2.CD_HCODE	= @P_CD_EVNUMBER
		WHERE	CE.CD_COMPANY	= @P_CD_COMPANY
		AND		CE.CD_EVALU		= @P_CD_EVALU
		AND		CE.CD_FIELD		= '100'
		AND		CE.CD_HCODE		= @P_CD_EVTYPE
	END
	ELSE
	BEGIN
		SELECT	OT.CD_COMPANY,
				OT.CD_EVALU,
				OT.CD_EVTYPE,
				CE1.NM_HCODE AS NM_EVTYPE,
				OT.CD_EVOTYPE,
				CE2.CD_HCODE AS CD_EVNUMBER,
				CE2.NM_HCODE AS NM_EVNUMBER,
				HN.NO_EMPM AS NO_EMPM,
				HN.NO_EMPAN AS NO_EMPAN,
				EP.NM_KOR,
				OT.NO_ODERBY,
				OT.CD_OTASK,
				OT.NM_OTASK,
				OT.DC_DOBJECT,
				OT.CD_SCALE,
				OT.NUM_CBLEVEL,
				OT.RT_WEIGHT AS EWEIGHT,
				'0' AS LB_EVITEM,
				OT.CD_SCALE AS CDSCALE,
				SC.DC_COMMENT1
		FROM	HR_PEVALU_OBJECT OT
		LEFT OUTER JOIN HR_PEVALU_SCORE SC
		ON		OT.CD_COMPANY	= SC.CD_COMPANY
		AND		OT.CD_EVALU		= SC.CD_EVALU
		AND		OT.CD_EVTYPE	= SC.CD_EVTYPE
		AND		OT.CD_EVOTYPE	= SC.CD_EVOTYPE
		AND		SC.CD_EVNUMBER	= @P_CD_EVNUMBER
		AND		SC.NO_EMPM		= @P_NO_EMPM
		AND		(@P_NO_EMPAN IS NULL OR @P_NO_EMPAN = '' OR SC.NO_EMPAN IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_NO_EMPAN)))
		LEFT OUTER JOIN HR_PEVALU_HUMEMPAN HN
		ON		HN.CD_COMPANY	= SC.CD_COMPANY
		AND		HN.CD_EVALU		= SC.CD_EVALU
		AND		HN.CD_EVTYPE	= SC.CD_EVTYPE
		AND		HN.CD_EVNUMBER	= SC.CD_EVNUMBER
		AND		HN.CD_GROUP		= SC.CD_GROUP
		AND		HN.NO_EMPM		= SC.NO_EMPM
		AND     HN.NO_EMPAN     = SC.NO_EMPAN
		LEFT OUTER JOIN MA_EMP EP
		ON		EP.CD_COMPANY	= HN.CD_BIZAREA
		AND		EP.NO_EMP		= HN.NO_EMPAN
		LEFT OUTER JOIN HR_PEVALU_CODE CE1
		ON		CE1.CD_COMPANY	= OT.CD_COMPANY
		AND		CE1.CD_EVALU	= OT.CD_EVALU
		AND		CE1.CD_FIELD	= '100'
		AND		CE1.CD_HCODE	= OT.CD_EVTYPE
		LEFT OUTER JOIN HR_PEVALU_CODE CE2
		ON		CE2.CD_COMPANY	= OT.CD_COMPANY
		AND		CE2.CD_EVALU	= OT.CD_EVALU
		AND		CE2.CD_FIELD	= '300'
		AND		CE2.CD_HCODE	= @P_CD_EVNUMBER
		WHERE	OT.CD_COMPANY	= @P_CD_COMPANY
		AND		OT.CD_EVALU		= @P_CD_EVALU
		AND		OT.CD_EVTYPE	= @P_CD_EVTYPE
		AND		OT.NO_EMP		= @P_NO_EMPAN
	END
	
END
GO

