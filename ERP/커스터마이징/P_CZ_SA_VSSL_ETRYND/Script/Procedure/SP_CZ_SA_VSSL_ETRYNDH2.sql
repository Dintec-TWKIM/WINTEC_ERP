USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_VSSL_ETRYNDH2]    Script Date: 2018-11-26 오후 7:22:57 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO




ALTER PROCEDURE [NEOE].[SP_CZ_SA_VSSL_ETRYNDH2]
(
    @P_CD_COMPANY	NVARCHAR(7),
    @P_CD_PRTAG		NVARCHAR(500),
	@P_DT_START		NVARCHAR(8),
	@P_DT_END		NVARCHAR(8),
	@P_NO_IMO		NVARCHAR(1000),
	@P_NM_VSSL		NVARCHAR(20),
	@P_CALL_SIGN	NVARCHAR(20)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

WITH A AS
(
	SELECT VH.CLSGN,
		   VH.NM_VSSL,
		   MAX(VH.NM_VSSL_NLTY) AS NM_VSSL_NLTY, 
		   MAX(VH.NM_VSSL_KND) AS NM_VSSL_KND,
		   COUNT(1) AS QT_ETRYPT,
		   MAX(VH.DTS_ETRYPT) AS DTS_ETRYPT,
		   MAX(VH.CALL_SIGN) AS CALL_SIGN
	FROM CZ_SA_VSSL_ETRYNDH VH
	WHERE (VH.DTS_ETRYPT BETWEEN @P_DT_START + '000000' AND @P_DT_END + '999999')
	AND ISNULL(VH.CALL_SIGN, '') <> ''
	AND (ISNULL(@P_NM_VSSL, '') = '' OR VH.NM_VSSL LIKE @P_NM_VSSL + '%')
	AND (ISNULL(@P_CALL_SIGN, '') = '' OR VH.CALL_SIGN = @P_CALL_SIGN)
	AND (ISNULL(@P_CD_PRTAG, '') = '' OR VH.CD_PRTAG IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_PRTAG)))
	AND (ISNULL(@P_NO_IMO, '') = '' OR EXISTS (SELECT 1 
											   FROM CZ_MA_HULL MH
											   WHERE MH.NO_IMO IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_NO_IMO))
											   AND MH.CALL_SIGN = VH.CALL_SIGN))
	GROUP BY VH.CLSGN, VH.NM_VSSL
)
SELECT A.CLSGN, 
	   A.NM_VSSL,
	   A.NM_VSSL_NLTY, 
	   A.NM_VSSL_KND,
	   A.QT_ETRYPT,
	   A.DTS_ETRYPT, 
	   VL.GRTG,
	   VL.CNT_CREW,
	   MH.NO_IMO,
	   MH.QT_INQ,
	   MH.DT_INQ
FROM A
LEFT JOIN (SELECT CLSGN, NM_VSSL,
				  MAX(GRTG) AS GRTG,
				  MAX(CNT_CREW) AS CNT_CREW
		   FROM CZ_SA_VSSL_ETRYNDL
		   GROUP BY CLSGN, NM_VSSL) VL
ON VL.CLSGN = A.CLSGN AND VL.NM_VSSL = A.NM_VSSL
LEFT JOIN (SELECT MH.CALL_SIGN,
				  MAX(MH.NO_IMO) AS NO_IMO,
				  COUNT(QH.NO_FILE) AS QT_INQ,
				  MAX(QH.DT_INQ) AS DT_INQ 
		   FROM CZ_MA_HULL MH
		   LEFT JOIN CZ_SA_QTNH QH ON QH.CD_COMPANY = @P_CD_COMPANY AND QH.NO_IMO = MH.NO_IMO
		   GROUP BY MH.CALL_SIGN) MH
ON MH.CALL_SIGN = A.CALL_SIGN
ORDER BY A.QT_ETRYPT DESC

GO

