USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_LOG_CHARGE_RPTL_S1]    Script Date: 2016-02-12 오후 1:40:06 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO




ALTER PROCEDURE [NEOE].[SP_CZ_SA_LOG_CHARGE_RPTL_S1]  
(
    @P_CD_COMPANY		NVARCHAR(7),
	@P_NO_GIR			NVARCHAR(20)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

WITH A AS
(
	SELECT PH.CD_COMPANY,
		   PH.NO_GIR,
		   PH.NO_PACK,
		   PH.DT_PACK,
		   PH.QT_WIDTH,
		   PH.QT_HEIGHT,
		   PH.QT_LENGTH,
		   (PH.QT_WIDTH * 0.001) AS QT_WIDTH_M,
		   (PH.QT_HEIGHT * 0.001) AS QT_HEIGHT_M,
		   (PH.QT_LENGTH * 0.001) AS QT_LENGTH_M,
		   PH.QT_NET_WEIGHT,
		   PH.QT_GROSS_WEIGHT,
		   PF.NM_FILE,
		   PF.DC_FILE,
		   STUFF((SELECT DISTINCT ',' + PL.NO_FILE
				  FROM CZ_SA_PACKL PL
				  WHERE PL.CD_COMPANY = PH.CD_COMPANY
				  AND PL.NO_GIR = PH.NO_GIR
				  AND PL.NO_PACK = PH.NO_PACK
				  FOR XML PATH('')),1,1,'') AS NO_FILE
	FROM CZ_SA_PACKH PH
	LEFT JOIN (SELECT PF.CD_COMPANY, 
			   	      PF.NO_GIR, 
			   	      PF.NO_PACK,
					  PF.NM_FILE,
			   	      PF.DC_FILE 
			   FROM (SELECT *,
			   	  	        ROW_NUMBER() OVER (PARTITION BY CD_COMPANY, NO_GIR, NO_PACK ORDER BY SEQ DESC) AS IDX
			   	     FROM CZ_SA_PACKH_FILE) PF
			   WHERE PF.IDX = 1) PF
	ON PF.CD_COMPANY = PH.CD_COMPANY AND PF.NO_GIR = PH.NO_GIR AND PF.NO_PACK = PH.NO_PACK
	WHERE PH.CD_COMPANY = @P_CD_COMPANY
	AND PH.NO_GIR = @P_NO_GIR

),
B AS
(
	SELECT A.CD_COMPANY,
		   A.NO_GIR,
		   A.NO_FILE,
		   A.NO_PACK,
		   A.DT_PACK,
		   A.QT_WIDTH,
		   A.QT_HEIGHT,
		   A.QT_LENGTH,
		   A.QT_NET_WEIGHT,
		   A.QT_GROSS_WEIGHT,
		   A.NM_FILE,
		   A.DC_FILE,
	       CONVERT(NUMERIC(17, 2), (A.QT_WIDTH_M * A.QT_HEIGHT_M * A.QT_LENGTH_M)) AS QT_CBM
	FROM A	
)
SELECT B.CD_COMPANY,
	   B.NO_GIR,
	   B.NO_FILE,
	   B.NO_PACK,
	   B.DT_PACK,
	   B.QT_WIDTH,
	   B.QT_HEIGHT,
	   B.QT_LENGTH,
	   B.QT_NET_WEIGHT,
	   B.QT_GROSS_WEIGHT,
	   B.NM_FILE,
	   B.DC_FILE,
	   B.QT_CBM,
	   (CASE WHEN B.QT_CBM >= 1 THEN B.QT_CBM * (SELECT TL.UM 
												 FROM CZ_MA_TARIFF_CBM_H TH
												 JOIN CZ_MA_TARIFF_CBM_L TL ON TL.CD_COMPANY = TH.CD_COMPANY AND TL.CD_PARTNER = TH.CD_PARTNER
												 WHERE TH.CD_COMPANY = B.CD_COMPANY
											     AND ISNULL(TH.YN_USE, 'N') = 'Y'
												 AND TL.SIZE = 1
											     AND CONVERT(NVARCHAR(8), GETDATE(), 112) BETWEEN TL.DT_START AND TL.DT_END) 
								ELSE (SELECT TL.UM 
									  FROM CZ_MA_TARIFF_CBM_H TH
									  JOIN CZ_MA_TARIFF_CBM_L TL ON TL.CD_COMPANY = TH.CD_COMPANY AND TL.CD_PARTNER = TH.CD_PARTNER
									  WHERE TH.CD_COMPANY = B.CD_COMPANY
									  AND ISNULL(TH.YN_USE, 'N') = 'Y'
								      AND TL.SIZE = B.QT_CBM
									  AND CONVERT(NVARCHAR(8), GETDATE(), 112) BETWEEN TL.DT_START AND TL.DT_END) END) AS AM
FROM B


GO