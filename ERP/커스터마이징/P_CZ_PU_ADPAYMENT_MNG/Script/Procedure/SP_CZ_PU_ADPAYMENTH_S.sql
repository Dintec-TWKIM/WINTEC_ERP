USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_PU_ADPAYMENTH_S]    Script Date: 2015-06-16 오후 3:36:33 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_PU_ADPAYMENTH_S] 
(
	@P_CD_COMPANY    NVARCHAR(7),
	@P_LANGUAGE		 NVARCHAR(5) = 'KR',
	@P_NO_PO		 NVARCHAR(20),
	@P_DT_PO_FROM    NVARCHAR(8),
	@P_DT_PO_TO      NVARCHAR(8),
	@P_CD_PURGRP     NVARCHAR(7),
	@P_FG_TRANS      NVARCHAR(4),
	@P_CD_PARTNER    NVARCHAR(20),
	@P_NO_EMP        NVARCHAR(10),
	@P_FG_GUBON      NVARCHAR(1),
	@P_CD_TPPO       NVARCHAR(4),
	@P_TP_BAN		 NVARCHAR(3)
) 
AS 

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

IF(@P_FG_GUBON = 'N') 
SELECT 'N' AS S,
	   '' AS CD_GW_STATUS,
	   '' AS NM_GW_STATUS,
	   '' AS NO_ADPAY,
	   PH.NO_PO,
	   PH.DT_PO,
	   '' AS DT_ADPAY,
	   '' AS DT_ACCT,
	   PH.CD_PARTNER,
	   MP.LN_PARTNER AS NM_SUPPLIER,
	   PH.FG_TRANS,
	   MC.NM_SYSDEF AS NM_FG_TRANS,
	   PH.CD_TPPO,
	   TP.NM_TPPO,
	   PH.CD_EXCH,
	   MC1.NM_SYSDEF AS NM_EXCH,
	   ROUND(PH.RT_EXCH, 2) AS RT_EXCH,
	   ISNULL(PL.AM_EX_PO, 0) AS AM_EX_PO,
	   ISNULL(PL.AM_PO, 0) AS AM_PO,
	   ISNULL(PL.RT_VAT, 0) AS RT_VAT,
	   ISNULL(PL.VAT_EX, 0) AS VAT_EX,
	   ISNULL(PL.VAT, 0) AS VAT,
	   (ISNULL(PL.AM_EX_PO, 0) + ISNULL(PL.VAT_EX, 0)) AS AM_EX_PO_TOTAL,
	   (ISNULL(PL.AM_PO, 0) + ISNULL(PL.VAT, 0)) AS AM_PO_TOTAL,
	   ISNULL(PL.AM_EX_ADPAY, 0) AS AM_EX_ADPAY,
	   ISNULL(PL.AM_ADPAY, 0) AS AM_ADPAY,
	   (ISNULL(PL.QT_PO, 0) - ISNULL(PL.QT_ADPAY, 0)) AS QT_REMAIN,
	   (ISNULL(PL.AM_EX_PO, 0) + ISNULL(PL.VAT_EX, 0) - ISNULL(PL.AM_EX_ADPAY, 0)) AS AM_EX_REMAIN,
	   (ISNULL(PL.AM_PO, 0) + ISNULL(PL.VAT, 0) - ISNULL(PL.AM_ADPAY, 0)) AS AM_REMAIN,
	   0.00 AS AM_EX_PRE,
	   0.00 AS AM_PRE,
	   '' AS NO_DOCU,
	   0.00 AS AM_EX_DOCU,
	   0.00 AS AM_DOCU,
	   0.00 AS AM_EX_BAN,
	   0.00 AS AM_BAN,
	   0.00 AS AM_EX_IV,
	   0.00 AS AM_IV,
	   0.00 AS AM_DOCU_EX_IV,
	   0.00 AS AM_DOCU_IV,
	   0.00 AS AM_BAN_EX_IV,
	   0.00 AS AM_BAN_IV,
	   '' AS YN_JEONJA,
	   '' AS DT_PAY_SCHEDULE,
	   '' AS PO_CONDITION,
	   '' AS CD_PC,
	   '' AS NO_EMP,
	   '250' AS NO_MODULE,
	   PL.CD_PJT,
	   ME.NM_KOR,
	   PH.TXT_USERDEF1,
	   (CASE WHEN EXISTS (SELECT 1 
						  FROM MA_FILEINFO MF
						  WHERE MF.CD_COMPANY = PH.CD_COMPANY
						  AND MF.CD_MODULE = 'MA'
						  AND MF.ID_MENU = 'P_CZ_PU_ADPAYMENT_MNG'
						  AND MF.CD_FILE = PH.NO_PO) THEN 'Y' ELSE 'N' END) AS YN_FILE,
	   '' AS DC_RESULT
FROM PU_POH PH
JOIN (SELECT PL.CD_COMPANY, PL.NO_PO, PL.CD_PJT,
			 SUM(PL.AM_EX) AS AM_EX_PO,
			 SUM(PL.AM) AS AM_PO,
			 SUM(ROUND(PL.AM_EX * MC.RT_VAT, (CASE WHEN PL.CD_COMPANY = 'S100' OR PH.CD_EXCH <> '000' THEN 2 ELSE 0 END))) AS VAT_EX,
			 SUM(PL.VAT) AS VAT,
			 SUM(PL.AM_EX_ADPAY) AS AM_EX_ADPAY,
			 SUM(PL.AM_ADPAY) AS AM_ADPAY,
			 SUM(PL.QT_ADPAY_MM) AS QT_ADPAY,
			 SUM(PL.QT_PO_MM) AS QT_PO,
			 MAX(MC.RT_VAT) AS RT_VAT
	  FROM PU_POL PL
	  LEFT JOIN PU_POH PH ON PH.CD_COMPANY = PL.CD_COMPANY AND PH.NO_PO = PL.NO_PO
	  LEFT JOIN (SELECT CD_COMPANY, CD_SYSDEF, 
	  				    ROUND(CONVERT(NUMERIC(17, 4), CASE WHEN ISNULL(CD_FLAG1, '') = '' THEN '0' ELSE CD_FLAG1 END) / 100, 2) AS RT_VAT 
	  		     FROM MA_CODEDTL
	  		     WHERE CD_FIELD = 'MA_B000046') MC
	  ON MC.CD_COMPANY = PH.CD_COMPANY AND MC.CD_SYSDEF = PH.FG_TAX
	  WHERE FG_POST = 'R' -- 입고여부
	  GROUP BY PL.CD_COMPANY, PL.NO_PO, PL.CD_PJT) PL 
ON PL.CD_COMPANY = PH.CD_COMPANY AND PL.NO_PO = PH.NO_PO
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = PH.CD_COMPANY AND MP.CD_PARTNER = PH.CD_PARTNER
LEFT JOIN PU_TPPO TP ON TP.CD_COMPANY = PH.CD_COMPANY AND TP.CD_TPPO = PH.CD_TPPO
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = PH.CD_COMPANY AND MC.CD_FIELD = 'PU_C000016' AND MC.CD_SYSDEF = PH.FG_TRANS
LEFT JOIN MA_CODEDTL MC1 ON MC1.CD_COMPANY = PH.CD_COMPANY AND MC1.CD_FIELD = 'MA_B000005' AND MC1.CD_SYSDEF = PH.CD_EXCH
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = PH.CD_COMPANY AND ME.NO_EMP = PH.NO_EMP
WHERE PH.CD_COMPANY = @P_CD_COMPANY
AND PH.FG_PAYMENT = '001'
AND ISNULL(PL.QT_PO, 0) <> ISNULL(PL.QT_ADPAY, 0)
AND PH.DT_PO BETWEEN @P_DT_PO_FROM AND @P_DT_PO_TO
AND (ISNULL(@P_CD_PARTNER, '') = '' OR PH.CD_PARTNER = @P_CD_PARTNER)
AND (ISNULL(@P_NO_PO, '') = '' OR PH.NO_PO LIKE '%' + @P_NO_PO + '%')
AND (ISNULL(@P_CD_PURGRP, '') = '' OR PH.CD_PURGRP = @P_CD_PURGRP)
AND (ISNULL(@P_FG_TRANS, '') = '' OR PH.FG_TRANS = @P_FG_TRANS)
AND (ISNULL(@P_NO_EMP, '') = '' OR PH.NO_EMP = @P_NO_EMP)
AND (ISNULL(@P_CD_TPPO, '') = '' OR PH.CD_TPPO = @P_CD_TPPO)
ELSE 
SELECT 'N' AS S,
	   GW.ST_STAT AS CD_GW_STATUS,
	   MC.NM_SYSDEF AS NM_GW_STATUS,
	   PA.NO_ADPAY,
	   PA.NO_PO,
	   PH.DT_PO,
	   PA.DT_ADPAY,
	   PA.DT_ACCT,
	   PH.CD_PARTNER,
	   MP.LN_PARTNER AS NM_SUPPLIER,
	   PH.FG_TRANS,
	   MC1.NM_SYSDEF AS NM_FG_TRANS,
	   PH.CD_TPPO,
	   TP.NM_TPPO,
	   PH.CD_EXCH,
	   MC2.NM_SYSDEF AS NM_EXCH,
	   ROUND(PH.RT_EXCH, 2) AS RT_EXCH,
	   ISNULL(PL.AM_EX_PO, 0) AS AM_EX_PO,
	   ISNULL(PL.AM_PO, 0) AS AM_PO,
	   ISNULL(PL.RT_VAT, 0) AS RT_VAT,
	   ISNULL(PL.VAT_EX, 0) AS VAT_EX,
	   ISNULL(PL.VAT, 0) AS VAT,
	   (ISNULL(PL.AM_EX_PO, 0) + ISNULL(PL.VAT_EX, 0)) AS AM_EX_PO_TOTAL,
	   (ISNULL(PL.AM_PO, 0) + ISNULL(PL.VAT, 0)) AS AM_PO_TOTAL,
	   ISNULL(PL.AM_EX_ADPAY, 0) AS AM_EX_ADPAY,
	   ISNULL(PL.AM_ADPAY, 0) AS AM_ADPAY,
	   (ISNULL(PL.QT_PO, 0) - ISNULL(PL.QT_ADPAY, 0)) AS QT_REMAIN,
	   (ISNULL(PL.AM_EX_PO, 0) + ISNULL(PL.VAT_EX, 0) - ISNULL(PL.AM_EX_ADPAY, 0)) AS AM_EX_REMAIN,
	   (ISNULL(PL.AM_PO, 0) + ISNULL(PL.VAT, 0) - ISNULL(PL.AM_ADPAY, 0)) AS AM_REMAIN,
	   ISNULL(PA.AM_EX_PRE, 0) AS AM_EX_PRE,
	   ISNULL(PA.AM_PRE, 0) AS AM_PRE,
	   FT.NO_DOCU,
	   ISNULL(FT.AM_EX_DOCU, 0) AS AM_EX_DOCU,
	   ISNULL(FT.AM_DOCU, 0) AS AM_DOCU,
	   ISNULL(FT.AM_EX_BAN, 0) AS AM_EX_BAN,
	   ISNULL(FT.AM_BAN, 0) AS AM_BAN,
	   ISNULL(PL.AM_EX_IV, 0) AS AM_EX_IV,
	   ISNULL(PL.AM_IV, 0) AS AM_IV,
	   ISNULL(IL.AM_EX_DOCU, 0) AS AM_DOCU_EX_IV,
	   ISNULL(IL.AM_DOCU, 0) AS AM_DOCU_IV,
	   ISNULL(IL.AM_EX_BAN, 0) AS AM_BAN_EX_IV,
	   ISNULL(IL.AM_BAN, 0) AS AM_BAN_IV,
	   PA.YN_JEONJA,
	   PA.DT_PAY_SCHEDULE,
	   PA.PO_CONDITION,
	   PL.QT_IN,
	   GW.APP_DOC_ID,
	    FT.CD_PC,
	   '' AS NO_EMP,
	   '250' AS NO_MODULE,
	   PL.CD_PJT,
	   ME.NM_KOR,
	   PH.TXT_USERDEF1,
	   (CASE WHEN EXISTS (SELECT 1 
						  FROM MA_FILEINFO MF
						  WHERE MF.CD_COMPANY = PA.CD_COMPANY
						  AND MF.CD_MODULE = 'MA'
						  AND MF.ID_MENU = 'P_CZ_PU_ADPAYMENT_MNG'
						  AND MF.CD_FILE = PA.NO_PO) THEN 'Y' ELSE 'N' END) AS YN_FILE,
	   '' AS DC_RESULT
FROM (SELECT AP.CD_COMPANY, 
			 AP.NO_ADPAY,
			 AP.DT_ADPAY,
			 AP.DT_ACCT,
			 AP.NO_PO, 
			 AP.YN_JEONJA,
			 AP.DT_PAY_SCHEDULE, 
			 AP.PO_CONDITION,
			 SUM(AP.AM_EX) AS AM_EX_PRE,
			 SUM(AP.AM) AS AM_PRE
	  FROM PU_ADPAYMENT AP
	  WHERE ISNULL(TP_AIS, 'Y') = 'Y'
	  GROUP BY AP.CD_COMPANY, AP.NO_ADPAY, AP.DT_ADPAY, AP.DT_ACCT, AP.NO_PO, AP.YN_JEONJA, AP.DT_PAY_SCHEDULE, AP.PO_CONDITION) PA
LEFT JOIN (SELECT FD.CD_COMPANY, FD.NO_MDOCU, FD.NO_DOCU, FD.CD_PC,
				  SUM(BH.AM_EX) AS AM_EX_DOCU,
				  SUM(BH.AM_DOCU) AS AM_DOCU,
				  SUM(BH.AM_EXBAN) AS AM_EX_BAN,
				  SUM(BH.AM_BAN) AS AM_BAN 
		   FROM FI_DOCU FD
		   LEFT JOIN FI_BANH BH ON BH.CD_COMPANY = FD.CD_COMPANY AND BH.NO_DOCU = FD.NO_DOCU AND BH.NO_DOLINE = FD.NO_DOLINE
		   GROUP BY FD.CD_COMPANY, FD.NO_MDOCU, FD.NO_DOCU, FD.CD_PC) FT 
ON FT.CD_COMPANY = PA.CD_COMPANY AND FT.NO_MDOCU = PA.NO_ADPAY
JOIN PU_POH PH ON PH.CD_COMPANY = PA.CD_COMPANY AND PH.NO_PO = PA.NO_PO
JOIN (SELECT PL.CD_COMPANY, PL.NO_PO, PL.CD_PJT,
			 SUM(PL.AM_EX) AS AM_EX_PO,
			 SUM(PL.AM) AS AM_PO,
			 SUM(ROUND(PL.AM_EX * MC.RT_VAT, (CASE WHEN PL.CD_COMPANY = 'S100' OR PH.CD_EXCH <> '000' THEN 2 ELSE 0 END))) AS VAT_EX,
			 SUM(PL.VAT) AS VAT,
			 SUM(PL.AM_EX_ADPAY) AS AM_EX_ADPAY,
			 SUM(PL.AM_ADPAY) AS AM_ADPAY,
			 SUM(PL.QT_ADPAY_MM) AS QT_ADPAY,
			 SUM(PL.QT_PO_MM) AS QT_PO,
			 SUM(IL.QT_IO) AS QT_IN,
			 SUM(IL.AM_EX_CLS) AS AM_EX_IV,
			 SUM(IL.AM_IV) AS AM_IV,
			 MAX(MC.RT_VAT) AS RT_VAT
	  FROM PU_POL PL
	  LEFT JOIN PU_POH PH ON PH.CD_COMPANY = PL.CD_COMPANY AND PH.NO_PO = PL.NO_PO
	  LEFT JOIN (SELECT CD_SYSDEF, 
	  				    ROUND(CONVERT(NUMERIC(17, 4), CASE WHEN ISNULL(CD_FLAG1, '') = '' THEN '0' ELSE CD_FLAG1 END) / 100, 2) AS RT_VAT 
	  		     FROM MA_CODEDTL
	  		     WHERE CD_COMPANY = @P_CD_COMPANY
	  		     AND CD_FIELD = 'MA_B000046') MC 
	  ON MC.CD_SYSDEF = PH.FG_TAX
	  LEFT JOIN (SELECT ML.CD_COMPANY, ML.NO_PSO_MGMT, ML.NO_PSOLINE_MGMT,
						SUM(ML.QT_IO) AS QT_IO,
						SUM(IL.AM_EX_CLS) AS AM_EX_CLS,
						SUM(IL.AM_CLS + IL.VAT) AS AM_IV 
				 FROM MM_QTIO ML
				 LEFT JOIN PU_IVL IL ON IL.CD_COMPANY = ML.CD_COMPANY AND IL.NO_IO = ML.NO_IO AND IL.NO_IOLINE = ML.NO_IOLINE
				 GROUP BY ML.CD_COMPANY, ML.NO_PSO_MGMT, ML.NO_PSOLINE_MGMT) IL
	  ON IL.CD_COMPANY = PL.CD_COMPANY AND IL.NO_PSO_MGMT = PL.NO_PO AND IL.NO_PSOLINE_MGMT = PL.NO_LINE
	  --WHERE PL.FG_POST = 'R'
	  GROUP BY PL.CD_COMPANY, PL.NO_PO, PL.CD_PJT) PL 
ON PL.CD_COMPANY = PH.CD_COMPANY AND PL.NO_PO = PH.NO_PO
LEFT JOIN (SELECT IL.CD_COMPANY, IL.NO_PO,
		   		  SUM(FD.AM_EX_DOCU) AS AM_EX_DOCU,
		   		  SUM(FD.AM_DOCU) AS AM_DOCU,
		   		  SUM(FD.AM_EX_BAN) AS AM_EX_BAN,
		   		  SUM(FD.AM_BAN) AS AM_BAN
		   FROM (SELECT CD_COMPANY, NO_IV, NO_PO 
		   		 FROM PU_IVL
		   		 GROUP BY CD_COMPANY, NO_IV, NO_PO) IL
		   LEFT JOIN (SELECT FD.CD_COMPANY, FD.NO_MDOCU,	
		   					 SUM(BH.AM_EX) AS AM_EX_DOCU,
		   					 SUM(BH.AM_DOCU) AS AM_DOCU,
		   					 SUM(BH.AM_EXBAN) AS AM_EX_BAN,
		   					 SUM(BH.AM_BAN) AS AM_BAN
		   			  FROM FI_DOCU FD
		   			  LEFT JOIN FI_BANH BH ON BH.CD_COMPANY = FD.CD_COMPANY AND BH.NO_DOCU = FD.NO_DOCU AND BH.NO_DOLINE = FD.NO_DOLINE
		   			  GROUP BY FD.CD_COMPANY, FD.NO_MDOCU) FD
		   ON FD.CD_COMPANY = IL.CD_COMPANY AND FD.NO_MDOCU = IL.NO_IV
		   GROUP BY IL.CD_COMPANY, IL.NO_PO) IL
ON IL.CD_COMPANY = PH.CD_COMPANY AND IL.NO_PO = PH.NO_PO
LEFT JOIN FI_GWDOCU GW ON GW.CD_COMPANY = 'K100' AND GW.CD_PC = '010000' AND GW.NO_DOCU = PA.CD_COMPANY + '-' + PA.NO_ADPAY
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = PH.CD_COMPANY AND MP.CD_PARTNER = PH.CD_PARTNER
LEFT JOIN PU_TPPO TP ON TP.CD_COMPANY = PH.CD_COMPANY AND TP.CD_TPPO = PH.CD_TPPO
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = PA.CD_COMPANY AND MC.CD_FIELD = 'PU_C000065' AND MC.CD_SYSDEF = GW.ST_STAT
LEFT JOIN MA_CODEDTL MC1 ON MC1.CD_COMPANY = PH.CD_COMPANY AND MC1.CD_FIELD = 'PU_C000016' AND MC1.CD_SYSDEF = PH.FG_TRANS
LEFT JOIN MA_CODEDTL MC2 ON MC2.CD_COMPANY = PH.CD_COMPANY AND MC2.CD_FIELD = 'MA_B000005' AND MC2.CD_SYSDEF = PH.CD_EXCH
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = PH.CD_COMPANY AND ME.NO_EMP = PH.NO_EMP
WHERE PH.CD_COMPANY = @P_CD_COMPANY
AND PH.DT_PO BETWEEN @P_DT_PO_FROM AND @P_DT_PO_TO
AND (ISNULL(@P_CD_PARTNER, '') = '' OR PH.CD_PARTNER = @P_CD_PARTNER)
AND (ISNULL(@P_NO_PO, '') = '' OR PH.NO_PO LIKE '%' + @P_NO_PO + '%')
AND (ISNULL(@P_CD_PURGRP, '') = '' OR PH.CD_PURGRP = @P_CD_PURGRP)
AND (ISNULL(@P_FG_TRANS, '') = '' OR PH.FG_TRANS = @P_FG_TRANS)
AND (ISNULL(@P_NO_EMP, '') = '' OR PH.NO_EMP = @P_NO_EMP)
AND (ISNULL(@P_CD_TPPO, '') = '' OR PH.CD_TPPO = @P_CD_TPPO)
AND (ISNULL(@P_TP_BAN, '') = '' OR (ISNULL(PL.AM_IV, 0) > 0 AND 
	((@P_TP_BAN = '000' AND ABS(ISNULL(FT.AM_EX_DOCU, 0) - ISNULL(FT.AM_EX_BAN, 0)) = 0) OR 
	(@P_TP_BAN = '001' AND ABS(ISNULL(FT.AM_EX_DOCU, 0) - ISNULL(FT.AM_EX_BAN, 0)) > 0))))

GO