SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_SA_GIM_REG_H_S] 
(
    @P_CD_COMPANY       NVARCHAR(100),  --회사  
    @P_DT_IO_FROM       NCHAR(8),   --수불일자FROM  
    @P_DT_IO_TO         NCHAR(8),   --수불일자TO  
    @P_DT_LIMIT_FROM    NCHAR(8),   --납기일자FROM  
    @P_DT_LIMIT_TO      NCHAR(8),   --납기일자TO  
    @P_CD_PARTNER       NVARCHAR(20),  --매출처  
    @P_NO_EMP           NVARCHAR(10),  --사번  
    @P_FG_TRANS         NVARCHAR(3),  --거래구분  
    @P_YN_RETURN        NVARCHAR(1),  --출고구분(반품유무)  
    @P_CD_QTIOTP        NVARCHAR(3),  --출고형태  
    @P_MULTI_SALEORG    NVARCHAR(4000), --영업조직(멀티)
    @P_MULTI_SALEGRP    NVARCHAR(4000),
    @P_CD_PJT			NVARCHAR(20),   --프로젝트 2011.04.05 추가
	@P_NO_GIR            NVARCHAR(20) = NULL,
    @P_NO_IO            NVARCHAR(20) = NULL,
    @P_NO_EMP_LOGIN     NVARCHAR(10) = NULL,
    @P_NO_SO            NVARCHAR(20) = NULL,
	@P_YN_AUTO			NVARCHAR(1)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT 'N' AS S, 
	   H.CD_COMPANY,
	   MC.NM_COMPANY,
	   H.NO_IO,
	   B.NO_ISURCV, 
	   B.DT_LOADING,
	   H.DT_IO, 
	   MP.LN_PARTNER,
	   MH.NM_VESSEL, 
	   ME.NM_KOR, 
	   (CASE WHEN (B.FG_IO = '042') THEN 'C' 
								    ELSE H.YN_RETURN END) AS YN_RETURN,
	   H.DC_RMK, 
	   B.DTS_INSERT,
	   B.FILE_PATH_MNG,
	   B.DTS_INSERT1,
	   B.FILE_PATH_MNG1,
	   MP.CD_PARTNER_GRP, 
	   MP.CD_PARTNER_GRP_2,
	   B.DT_GIR, 
	   H.MEMO_CD, 
	   H.CHECK_PEN,
	   GW.ST_STAT AS CD_GW_STATUS,
	   MD.NM_SYSDEF AS NM_GW_STATUS,
	   B.CD_MAIN_CATEGORY,
	   B.CD_SUB_CATEGORY,
	   B.QT_ITEM,
	   MP1.LN_PARTNER AS NM_DELIVERY_TO,
	   (SELECT STRING_AGG(OL.NO_PSO_MGMT, ',') 
	    FROM (SELECT OL.NO_PSO_MGMT 
	          FROM MM_QTIO OL
		      WHERE OL.CD_COMPANY = H.CD_COMPANY
		      AND OL.NO_IO = H.NO_IO
		      GROUP BY OL.NO_PSO_MGMT) OL) AS NO_SO
FROM MM_QTIOH H
JOIN (SELECT L.CD_COMPANY, L.NO_IO, 
			 MAX(L.FG_IO) AS FG_IO,
			 MAX(L.NO_ISURCV) AS NO_ISURCV,
			 MAX(WD.DT_LOADING) AS DT_LOADING,
			 MAX(MF.DTS_INSERT) AS DTS_INSERT,
			 MAX(MF.FILE_PATH_MNG) AS FILE_PATH_MNG,
			 MAX(MF1.DTS_INSERT) AS DTS_INSERT1,
			 MAX(MF1.FILE_PATH_MNG) AS FILE_PATH_MNG1,
			 MAX(WD.NO_IMO) AS NO_IMO,
			 MAX(GH.DT_GIR) AS DT_GIR,
			 MAX(WD.CD_MAIN_CATEGORY) AS CD_MAIN_CATEGORY,
			 MAX(WD.CD_SUB_CATEGORY) AS CD_SUB_CATEGORY,
			 MAX(WD.CD_DELIVERY_TO) AS CD_DELIVERY_TO,
             COUNT(1) AS QT_ITEM
      FROM MM_QTIO L
	  LEFT JOIN SA_GIRH GH ON GH.CD_COMPANY = L.CD_COMPANY AND GH.NO_GIR = L.NO_ISURCV
	  LEFT JOIN CZ_SA_GIRH_WORK_DETAIL WD ON WD.CD_COMPANY = GH.CD_COMPANY AND WD.NO_GIR = GH.NO_GIR
	  LEFT JOIN CZ_SA_QTNH QH ON QH.CD_COMPANY = L.CD_COMPANY AND QH.NO_FILE = L.NO_PSO_MGMT
      LEFT JOIN MA_SALEGRP MS ON L.CD_COMPANY = MS.CD_COMPANY AND L.CD_GROUP = MS.CD_SALEGRP
	  LEFT JOIN (SELECT CD_COMPANY, CD_MODULE, ID_MENU, CD_FILE,
						MAX(DTS_INSERT) AS DTS_INSERT,
						MAX(FILE_NAME) + '(' + CONVERT(VARCHAR, COUNT(1)) + ')' AS FILE_PATH_MNG
				 FROM MA_FILEINFO
				 GROUP BY CD_COMPANY, CD_MODULE, ID_MENU, CD_FILE) MF 
	  ON MF.CD_COMPANY = L.CD_COMPANY AND MF.CD_MODULE = 'SA' AND MF.ID_MENU = 'P_CZ_SA_GIM_REG' AND MF.CD_FILE = L.NO_ISURCV + '_' + L.CD_COMPANY
	  LEFT JOIN (SELECT CD_COMPANY, CD_MODULE, ID_MENU, CD_FILE,
						MAX(DTS_INSERT) AS DTS_INSERT,
						MAX(FILE_NAME) + '(' + CONVERT(VARCHAR, COUNT(1)) + ')' AS FILE_PATH_MNG
				 FROM MA_FILEINFO
				 GROUP BY CD_COMPANY, CD_MODULE, ID_MENU, CD_FILE) MF1 
	  ON MF1.CD_COMPANY = L.CD_COMPANY AND MF1.CD_MODULE = 'SA' AND MF1.ID_MENU = 'P_CZ_SA_GIM_REG' AND MF1.CD_FILE = L.NO_ISURCV + '_' + L.CD_COMPANY + '_ETC' 
      WHERE L.FG_PS = '2'
	  AND (ISNULL(@P_CD_COMPANY, '') = '' OR L.CD_COMPANY IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_COMPANY)))
	  AND (ISNULL(@P_NO_SO, '') = '' OR L.NO_PSO_MGMT = @P_NO_SO) 
	  AND (ISNULL(@P_NO_GIR, '') = '' OR GH.NO_GIR = @P_NO_GIR)
      AND (ISNULL(@P_FG_TRANS, '') = '' OR L.FG_TRANS = @P_FG_TRANS)  
      AND (ISNULL(@P_CD_QTIOTP, '') = '' OR L.CD_QTIOTP = @P_CD_QTIOTP)
      --AND L.FG_IO IN ('010','041','042')        --판매출고, 판매반품, 판매반품정리 
      AND (ISNULL(@P_MULTI_SALEGRP, '') = '' OR CD_GROUP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_MULTI_SALEGRP)))
      AND (ISNULL(@P_MULTI_SALEORG, '') = '' OR MS.CD_SALEORG IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_MULTI_SALEORG)))
      AND (ISNULL(@P_CD_PJT, '') = '' OR L.CD_PJT = @P_CD_PJT)	-- 프로젝트 2011.04.05 추가
	  AND (((@P_YN_RETURN = 'N' AND L.FG_IO = '010') OR 
			(@P_YN_RETURN = 'Y' AND L.FG_IO = '041') OR 
			(@P_YN_RETURN = 'C' AND L.FG_IO = '042')) OR 
			@P_YN_RETURN = '' OR @P_YN_RETURN IS NULL)
	  AND (@P_YN_AUTO = 'N' OR
		  (@P_YN_AUTO = 'Y' AND QH.CD_COMPANY IS NOT NULL)) -- 자동프로세스확인
      GROUP BY L.CD_COMPANY, L.NO_IO) B 
ON H.CD_COMPANY = B.CD_COMPANY AND H.NO_IO = B.NO_IO  
LEFT JOIN MA_PARTNER MP ON H.CD_COMPANY = MP.CD_COMPANY AND H.CD_PARTNER = MP.CD_PARTNER   
LEFT JOIN CZ_MA_DELIVERY MP1 ON MP1.CD_COMPANY = B.CD_COMPANY AND MP1.CD_PARTNER = B.CD_DELIVERY_TO
LEFT JOIN MA_EMP ME ON H.CD_COMPANY = ME.CD_COMPANY AND H.NO_EMP = ME.NO_EMP
LEFT JOIN MA_PARTNER_SUB MB ON MB.CD_COMPANY = H.CD_COMPANY AND MB.CD_PARTNER = H.CD_PARTNER
LEFT JOIN MA_COMPANY MC ON MC.CD_COMPANY = H.CD_COMPANY
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = B.NO_IMO
LEFT JOIN FI_GWDOCU GW ON GW.CD_COMPANY = 'K100' AND GW.CD_PC = '010000' AND GW.NO_DOCU = H.CD_COMPANY + '-' + H.NO_IO + '-D'
LEFT JOIN MA_CODEDTL MD ON MD.CD_COMPANY = GW.CD_COMPANY AND MD.CD_FIELD = 'PU_C000065' AND MD.CD_SYSDEF = GW.ST_STAT
WHERE H.DT_IO BETWEEN @P_DT_IO_FROM AND @P_DT_IO_TO
AND (ISNULL(@P_CD_COMPANY, '') = '' OR H.CD_COMPANY IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_COMPANY)))  
AND (ISNULL(@P_CD_PARTNER, '') = '' OR H.CD_PARTNER = @P_CD_PARTNER)  
AND (ISNULL(@P_NO_EMP, '') = '' OR H.NO_EMP = @P_NO_EMP)  
AND (ISNULL(@P_NO_IO, '') = '' OR H.NO_IO = @P_NO_IO)

GO
