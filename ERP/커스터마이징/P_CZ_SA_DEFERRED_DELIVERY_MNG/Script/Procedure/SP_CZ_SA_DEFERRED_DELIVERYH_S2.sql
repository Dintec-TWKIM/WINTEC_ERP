USE [NEOE]
GO
/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_DEFERRED_DELIVERYH_S2]    Script Date: 2023-05-25 오후 1:55:36 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_SA_DEFERRED_DELIVERYH_S2] 
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_DT_START			NVARCHAR(8),
	@P_DT_END			NVARCHAR(8),
	@P_NO_SO			NVARCHAR(20) = '',
	@P_IN_STATE			NVARCHAR(3) = '001', -- 001 -> 전체, 002 -> 미입고, 003 -> 입고
	@P_GIR_STATE		NVARCHAR(3) = '001', -- 001 -> 전체, 002 -> 미의뢰, 003 -> 의뢰
	@P_OUT_STATE		NVARCHAR(3) = '001', -- 001 -> 전체, 002 -> 미출고, 003 -> 출고
	@P_CLOSE_STATE		NVARCHAR(3) = '001', -- 001 -> 전체, 002 -> 미마감, 003 -> 마감
	@P_NO_IMO			NVARCHAR(10) = '',
	@P_CD_PARTNER		NVARCHAR(20) = '',
	@P_CD_SUPPLIER		NVARCHAR(4000) = '',
	@P_NO_ORDER			NVARCHAR(200) = '',
	@P_MULTI_SALEGRP    NVARCHAR(4000) = '',
	@P_CD_PARTNER_GRP   NVARCHAR(4000) = '',
	@P_DT_DELAYED	    NUMERIC(10, 0),
	@P_NO_EMP			NVARCHAR(10) = ''	
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT 'N' AS S,
	   SL.CD_COMPANY, 
	   SL.NO_SO,
	   SH.CD_PARTNER,
	   MP.LN_PARTNER AS NM_PARTNER,
	   SH.NO_IMO,
	   MH.NM_VESSEL,
	   SH.CD_SALEGRP,
	   SG.NM_SALEGRP,
	   SH.CD_PARTNER_GRP,
	   CD.NM_SYSDEF AS NM_PARTNER_GRP,
	   MP.CD_NATION,
	   CD1.NM_SYSDEF AS NM_NATION,
	   SH.NO_EMP AS NO_SO_EMP,
	   ME.NM_KOR AS NM_SO_EMP,
	   WH.ID_LOG,
	   MU.NM_USER AS NM_LOG_EMP,
	   SL.NO_PO AS NO_KEY,
	   SL.NO_EMP AS NO_PO_EMP,
	   ME1.NM_KOR AS NM_PO_EMP,
	   SL.CD_SUPPLIER,
	   SL.NM_SUPPLIER, 
	   SL.NO_ORDER,
	   SL.DT_PO,
	   SL.DT_LIMIT,
	   SL.DT_IN,
	   SL.AM_PO,
	   SL.QT_SO,
	   SL.QT_PO,
	   SL.QT_IN,
	   SL.QT_GIR,
	   SL.QT_OUT,
	   (CASE WHEN SL.QT_PO > SL.QT_IN AND SL.DT_LIMIT <> '' AND SL.DT_LIMIT <> 'STOCK' THEN DATEDIFF(DAY, SL.DT_LIMIT, GETDATE()) ELSE '' END) AS DT_DELAY,
	   (CASE WHEN SL.DT_PO <> 'STOCK' AND ISNULL(SL.DT_IN, '') <> '' THEN DATEDIFF(DAY, SL.DT_PO, SL.DT_IN) ELSE '' END) AS DT_IN_DAYS,
	   ISNULL(SH.YN_CLOSE, 'N') AS YN_CLOSE,
	   (CASE WHEN SL.QT_PO > 0 AND SL.QT_PO = SL.QT_IN THEN 'Y' ELSE 'N' END) AS YN_ALL_IN,
	   SH.DC_RMK_TEXT,
	   SH.DC_RMK_TEXT2,
	   DD.DT_EXPECT,
	   DD.DT_REPLY,
	   DD.CD_DELAY,
	   DD.DC_RMK,
	   DD.TP_SEND,
	   DD.DTS_SEND
FROM SA_SOH SH 
JOIN (SELECT SL.CD_COMPANY, SL.NO_SO,
			 PL.NO_PO,
			 MAX(PL.NO_EMP) AS NO_EMP,
			 MAX(PL.CD_SUPPLIER) AS CD_SUPPLIER,
			 MAX(PL.NM_SUPPLIER) AS NM_SUPPLIER,
			 MAX(PL.NO_ORDER) AS NO_ORDER,
			 MAX(PL.DT_PO) AS DT_PO,
			 MAX(PL.DT_IN) AS DT_IN,
			 PL.DT_LIMIT,
			 ISNULL(SUM(PL.AM_PO), 0) AS AM_PO,
			 COUNT(SL.CD_ITEM) AS QT_SO,
			 ISNULL(SUM(PL.QT_PO), 0) AS QT_PO,
			 ISNULL(SUM(PL.QT_IN), 0) AS QT_IN,
			 ISNULL(SUM(CASE WHEN SL.QT_SO = GL.QT_GIR THEN 1 ELSE 0 END), 0) AS QT_GIR,
			 ISNULL(SUM(CASE WHEN SL.QT_SO = GL.QT_OUT THEN 1 ELSE 0 END), 0) AS QT_OUT
	  FROM SA_SOL SL
	  JOIN (SELECT PL.CD_COMPANY, PL.NO_SO, PL.NO_SOLINE,
				   PL.NO_PO,
				   MAX(PH.NO_EMP) AS NO_EMP,
				   MAX(PH.CD_PARTNER) AS CD_SUPPLIER,
				   MAX(MP.LN_PARTNER) AS NM_SUPPLIER,
				   MAX(PH.NO_ORDER) AS NO_ORDER,
				   MAX(PH.DT_PO) AS DT_PO,
				   MAX(IL.DT_IN) AS DT_IN,
				   PL.DT_LIMIT,
				   SUM(PL.AM) AS AM_PO,
				   COUNT(PL.CD_ITEM) AS QT_PO,
				   SUM(CASE WHEN ISNULL(PL.QT_PO, 0) = ISNULL(IL.QT_IN, 0) THEN 1 ELSE 0 END) AS QT_IN 
			FROM PU_POL PL
			LEFT JOIN PU_POH PH ON PH.CD_COMPANY = PL.CD_COMPANY AND PH.NO_PO = PL.NO_PO
			LEFT JOIN (SELECT IL.CD_COMPANY, IL.NO_PSO_MGMT, IL.NO_PSOLINE_MGMT, 
							  MAX(IH.DT_IO) AS DT_IN,
							  SUM(IL.QT_IO) AS QT_IN 
					   FROM MM_QTIO IL
					   JOIN MM_QTIOH IH ON IH.CD_COMPANY = IL.CD_COMPANY AND IH.NO_IO = IL.NO_IO
				       WHERE IH.YN_RETURN = 'N'
					   GROUP BY IL.CD_COMPANY, IL.NO_PSO_MGMT, IL.NO_PSOLINE_MGMT) IL
			ON IL.CD_COMPANY = PL.CD_COMPANY AND IL.NO_PSO_MGMT = PL.NO_PO AND IL.NO_PSOLINE_MGMT = PL.NO_LINE
			LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = PH.CD_COMPANY AND MP.CD_PARTNER = PH.CD_PARTNER
			GROUP BY PL.CD_COMPANY, PL.NO_SO, PL.NO_SOLINE, PL.NO_PO, PL.DT_LIMIT
			UNION ALL
			SELECT SB.CD_COMPANY, SB.NO_FILE, SB.NO_LINE,
				   'STOCK' AS NO_PO,
				   'STOCK' AS NO_EMP,
				   'STOCK' AS CD_SUPPLIER,
				   'STOCK' AS NM_SUPPLIER,
				   'STOCK' AS NO_ORDER,
				   'STOCK' AS DT_PO,
				   'STOCK' AS DT_IN,
				   'STOCK' AS DT_LIMIT,
				   (SB.UM_KR * SB.QT_STOCK) AS AM_PO,
				   1 AS QT_PO,
				   (CASE WHEN SB.QT_STOCK = SB.QT_BOOK THEN 1 ELSE 0 END) AS QT_IN
			FROM CZ_SA_STOCK_BOOK SB) PL
	  ON PL.CD_COMPANY = SL.CD_COMPANY AND PL.NO_SO = SL.NO_SO AND PL.NO_SOLINE = SL.SEQ_SO
	  LEFT JOIN (SELECT GL.CD_COMPANY, GL.NO_SO, GL.SEQ_SO, 
						ISNULL(SUM(GL.QT_GIR), 0) AS QT_GIR,
						ISNULL(SUM(OL.QT_OUT), 0) AS QT_OUT
			  FROM SA_GIRL GL
			  LEFT JOIN (SELECT OL.CD_COMPANY, OL.NO_ISURCV, OL.NO_ISURCVLINE, SUM(OL.QT_IO) AS QT_OUT
						 FROM MM_QTIO OL
						 JOIN MM_QTIOH IH ON IH.CD_COMPANY = OL.CD_COMPANY AND IH.NO_IO = OL.NO_IO
						 WHERE IH.YN_RETURN = 'N'
						 GROUP BY OL.CD_COMPANY, OL.NO_ISURCV, OL.NO_ISURCVLINE) OL
			  ON OL.CD_COMPANY = GL.CD_COMPANY AND OL.NO_ISURCV = GL.NO_GIR AND OL.NO_ISURCVLINE = GL.SEQ_GIR
			  GROUP BY GL.CD_COMPANY, GL.NO_SO, GL.SEQ_SO) GL
	  ON GL.CD_COMPANY = SL.CD_COMPANY AND GL.NO_SO = SL.NO_SO AND GL.SEQ_SO = SL.SEQ_SO
	  GROUP BY SL.CD_COMPANY, SL.NO_SO, PL.NO_PO, PL.DT_LIMIT) SL
ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
LEFT JOIN FI_GWDOCU GW ON GW.CD_COMPANY = 'K100' AND GW.CD_PC = '010000' AND GW.NO_DOCU = SH.NO_DOCU
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = SL.CD_COMPANY AND MP.CD_PARTNER = SH.CD_PARTNER
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = SH.NO_IMO
LEFT JOIN MA_SALEGRP SG ON SG.CD_COMPANY = SL.CD_COMPANY AND SG.CD_SALEGRP = SH.CD_SALEGRP
LEFT JOIN MA_CODEDTL CD ON CD.CD_COMPANY = SL.CD_COMPANY AND CD.CD_FIELD = 'MA_B000065' AND CD.CD_SYSDEF = SH.CD_PARTNER_GRP
LEFT JOIN MA_CODEDTL CD1 ON CD1.CD_COMPANY = MP.CD_COMPANY AND CD1.CD_FIELD = 'MA_B000020' AND CD1.CD_SYSDEF = MP.CD_NATION
LEFT JOIN CZ_SA_DEFERRED_DELIVERY DD ON DD.CD_COMPANY = SL.CD_COMPANY AND DD.TP_TYPE = '2' AND DD.NO_SO = SL.NO_SO AND DD.NO_KEY = SL.NO_PO AND DD.DT_LIMIT = SL.DT_LIMIT
LEFT JOIN CZ_MA_WORKFLOWH WH ON WH.CD_COMPANY = SH.CD_COMPANY AND WH.NO_KEY = SH.NO_SO AND WH.TP_STEP = '08'
LEFT JOIN MA_USER MU ON MU.CD_COMPANY = WH.CD_COMPANY AND MU.ID_USER = WH.ID_LOG
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = SH.CD_COMPANY AND ME.NO_EMP = SH.NO_EMP
LEFT JOIN MA_EMP ME1 ON ME1.CD_COMPANY = SL.CD_COMPANY AND ME1.NO_EMP = SL.NO_EMP
WHERE SH.CD_COMPANY = @P_CD_COMPANY
AND SH.DT_SO BETWEEN @P_DT_START AND @P_DT_END
AND (ISNULL(SH.YN_CLOSE, 'N') <> 'Y' OR GW.ST_STAT <> '1' OR SL.QT_GIR > 0)
AND (@P_NO_SO = '' OR SH.NO_SO LIKE @P_NO_SO + '%')
AND (@P_NO_IMO = '' OR SH.NO_IMO = @P_NO_IMO)
AND (@P_CD_PARTNER = '' OR SH.CD_PARTNER = @P_CD_PARTNER)
AND (@P_CD_SUPPLIER = '' OR SL.CD_SUPPLIER IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_SUPPLIER)))
AND (@P_NO_ORDER = '' OR SL.NO_ORDER LIKE '%' + @P_NO_ORDER + '%')
AND (@P_MULTI_SALEGRP = '' OR SH.CD_SALEGRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_MULTI_SALEGRP)))
AND (@P_CD_PARTNER_GRP = '' OR SH.CD_PARTNER_GRP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_PARTNER_GRP)))
AND (@P_NO_EMP = '' OR MU.NO_EMP = @P_NO_EMP)
AND (@P_CLOSE_STATE = '001' OR 
	(@P_CLOSE_STATE = '002' AND (ISNULL(SH.YN_CLOSE, 'N') <> 'Y' OR GW.ST_STAT <> '1')) OR
	(@P_CLOSE_STATE = '003' AND ISNULL(SH.YN_CLOSE, 'N') = 'Y' AND GW.ST_STAT = '1'))
AND (@P_IN_STATE = '001' OR 
	(@P_IN_STATE = '002' AND SL.QT_PO > 0 AND SL.QT_PO > SL.QT_IN) OR
	(@P_IN_STATE = '003' AND SL.QT_PO > 0 AND SL.QT_PO = SL.QT_IN))
AND (@P_GIR_STATE = '001' OR 
	(@P_GIR_STATE = '002' AND SL.QT_SO > SL.QT_GIR) OR
	(@P_GIR_STATE = '003' AND SL.QT_SO = SL.QT_GIR))
AND (@P_OUT_STATE = '001' OR 
	(@P_OUT_STATE = '002' AND SL.QT_SO > SL.QT_OUT) OR
	(@P_OUT_STATE = '003' AND SL.QT_SO = SL.QT_OUT))
AND ((CASE WHEN SL.QT_PO > SL.QT_IN AND SL.DT_LIMIT <> '' AND SL.DT_LIMIT <> 'STOCK' THEN DATEDIFF(DAY, SL.DT_LIMIT, GETDATE()) ELSE '' END) >= @P_DT_DELAYED)
ORDER BY SL.NO_SO, SL.NO_PO, SL.DT_LIMIT

GO