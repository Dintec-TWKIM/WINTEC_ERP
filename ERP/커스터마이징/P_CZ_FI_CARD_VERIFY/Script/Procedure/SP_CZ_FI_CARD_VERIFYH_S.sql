USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_FI_CARD_VERIFYH_S]    Script Date: 2016-12-28 오후 3:03:19 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_FI_CARD_VERIFYH_S]
(
	@P_CD_COMPANY			NVARCHAR(7),
	@P_DT_FROM				NVARCHAR(8),
	@P_DT_TO				NVARCHAR(8),
	@P_ADMIN_NO				NVARCHAR(20),
	@P_CARD_NO				NVARCHAR(20),
	@P_YN_DOCU				NVARCHAR(1),
	@P_YN_CONFIRM			NVARCHAR(1),
	@P_YN_ADMIN				NVARCHAR(1),
	@P_YN_APPR_IBK			NVARCHAR(1),
	@P_YN_BILL_IBK			NVARCHAR(1),
	@P_YN_APPR_BC			NVARCHAR(1),
	@P_YN_ACQ_BC			NVARCHAR(1),
	@P_YN_BILL_BC			NVARCHAR(1),
	@P_YN_NO_DATA			NVARCHAR(1),
	@P_YN_VERIFY			NVARCHAR(1)
)
AS  

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

CREATE TABLE #CARD_TEMP
(
	C_CODE		NVARCHAR(10),
	ACCT_NO		NVARCHAR(20), 
	ADMIN_NO	NVARCHAR(20), 
	TRADE_DATE	NVARCHAR(8), 
	ADMIN_AMT   NUMERIC(19, 4),
	AM_DOCU		NUMERIC(19, 4)
)

CREATE TABLE #TCARD_APPR
(
	ACCT_NO		NVARCHAR(20), 
	ADMIN_NO	NVARCHAR(20),
	ADMIN_AMT   NUMERIC(19, 4)
)

CREATE TABLE #TCARD_BILL
(
	ACCT_NO		NVARCHAR(20), 
	ADMIN_NO	NVARCHAR(20),
	ADMIN_AMT   NUMERIC(19, 4)
)

CREATE TABLE #CATS_TMP_APPROVAL
(
	ACCT_NO		NVARCHAR(20), 
	ADMIN_NO	NVARCHAR(20),
	ADMIN_AMT   NUMERIC(19, 4)
)

CREATE TABLE #CATS_TMP_ACQUIRE
(
	ACCT_NO		NVARCHAR(20), 
	ADMIN_NO	NVARCHAR(20),
	ADMIN_AMT   NUMERIC(19, 4)
)

CREATE TABLE #CATS_TMP_BILL
(
	ACCT_NO		NVARCHAR(20), 
	ADMIN_NO	NVARCHAR(20),
	ADMIN_AMT   NUMERIC(19, 4)
)

CREATE NONCLUSTERED INDEX CARD_TEMP ON #CARD_TEMP (ACCT_NO, ADMIN_NO)
CREATE NONCLUSTERED INDEX TCARD_APPR ON #TCARD_APPR (ACCT_NO, ADMIN_NO)
CREATE NONCLUSTERED INDEX TCARD_BILL ON #TCARD_BILL (ACCT_NO, ADMIN_NO)
CREATE NONCLUSTERED INDEX CATS_TMP_APPROVAL ON #CATS_TMP_APPROVAL (ACCT_NO, ADMIN_NO)
CREATE NONCLUSTERED INDEX CATS_TMP_ACQUIRE ON #CATS_TMP_ACQUIRE (ACCT_NO, ADMIN_NO)
CREATE NONCLUSTERED INDEX CATS_TMP_BILL ON #CATS_TMP_BILL (ACCT_NO, ADMIN_NO)

INSERT INTO #CARD_TEMP
SELECT CT.C_CODE,
	   CT.ACCT_NO,
	   CT.ADMIN_NO,
	   MAX(CT.TRADE_DATE) AS TRADE_DATE,
	   SUM(CT.ADMIN_AMT) AS ADMIN_AMT,
	   SUM(CASE WHEN ISNULL(@P_YN_CONFIRM, 'N') = 'N' THEN ISNULL(FD.AM_CR, 0) + ISNULL(FD.AM_DR, 0)
				WHEN FD.ST_DOCU = '2' THEN ISNULL(FD.AM_CR, 0) + ISNULL(FD.AM_DR, 0)
									  ELSE 0 END) AS AM_DOCU
FROM CARD_TEMP CT
LEFT JOIN FI_DOCU FD ON FD.CD_COMPANY = CT.C_CODE AND FD.NO_DOCU = CT.NO_DOCU AND FD.NO_DOLINE = CT.LINE_NO
WHERE CT.C_CODE = @P_CD_COMPANY
AND (ISNULL(@P_CARD_NO, '') = '' OR CT.ACCT_NO = REPLACE(@P_CARD_NO, '-', ''))
AND (ISNULL(@P_ADMIN_NO, '') = '' OR CT.ADMIN_NO = @P_ADMIN_NO)
GROUP BY CT.C_CODE, CT.ACCT_NO, CT.ADMIN_NO
HAVING MAX(CT.TRADE_DATE) BETWEEN @P_DT_FROM AND @P_DT_TO

INSERT INTO #TCARD_APPR
SELECT CARD_NO, APPR_NO,
	   SUM(APPR_AMT) AS APPR_AMT
FROM [EBRANCH].[ebranch].[dbo].TCARD_APPR
WHERE EXISTS (SELECT 1 
			  FROM #CARD_TEMP CT
			  WHERE CT.ACCT_NO = CARD_NO
			  AND CT.ADMIN_NO = APPR_NO)
GROUP BY CARD_NO, APPR_NO

INSERT INTO #TCARD_BILL
SELECT CARD_NO, APPR_NO,
	   SUM(BILL_AMT) AS BILL_AMT
FROM [EBRANCH].[ebranch].[dbo].TCARD_BILL
WHERE EXISTS (SELECT 1 
			  FROM #CARD_TEMP CT
			  WHERE CT.ACCT_NO = CARD_NO
			  AND CT.ADMIN_NO = APPR_NO)
GROUP BY CARD_NO, APPR_NO

INSERT INTO #CATS_TMP_APPROVAL
SELECT CARDNO, APPRNO,
	   SUM(CASE WHEN CLASS = 'B' THEN -APPRTOT ELSE APPRTOT END) AS APPRTOT
FROM [EBRANCH].[ebranch].[dbo].CATS_TMP_APPROVAL
WHERE EXISTS (SELECT 1 
			  FROM #CARD_TEMP CT
			  WHERE CT.ACCT_NO = CARDNO
			  AND CT.ADMIN_NO = APPRNO)
GROUP BY CARDNO, APPRNO

INSERT INTO #CATS_TMP_ACQUIRE
SELECT CARDNO, APPRNO,
	   SUM(CASE WHEN CLASS = 'B' THEN -PURCHTOT ELSE PURCHTOT END) AS PURCHTOT
FROM [EBRANCH].[ebranch].[dbo].CATS_TMP_ACQUIRE
WHERE EXISTS (SELECT 1 
			  FROM #CARD_TEMP CT
			  WHERE CT.ACCT_NO = CARDNO
			  AND CT.ADMIN_NO = APPRNO)
GROUP BY CARDNO, APPRNO

INSERT INTO #CATS_TMP_BILL
SELECT CARDNO, APPRNO,
	   SUM(BILTOT) AS BILTOT
FROM [EBRANCH].[ebranch].[dbo].CATS_TMP_BILL
WHERE EXISTS (SELECT 1 
			  FROM #CARD_TEMP CT
			  WHERE CT.ACCT_NO = CARDNO
			  AND CT.ADMIN_NO = APPRNO)
GROUP BY CARDNO, APPRNO

;WITH A AS
(
	SELECT A.C_CODE,
		   A.ACCT_NO,
		   A.ADMIN_NO,
		   A.TRADE_DATE,
		   A.ADMIN_AMT,
		   A.AM_DOCU,
		   B.ADMIN_AMT AS AM_APPR_IBK,
		   C.ADMIN_AMT AS AM_BILL_IBK,
		   D.ADMIN_AMT AS AM_APPR_BC,
		   E.ADMIN_AMT AS AM_ACQ_BC,
		   F.ADMIN_AMT AS AM_BILL_BC
	FROM #CARD_TEMP A
	LEFT JOIN #TCARD_APPR B ON B.ACCT_NO = A.ACCT_NO AND B.ADMIN_NO = A.ADMIN_NO
	LEFT JOIN #TCARD_BILL C ON C.ACCT_NO = A.ACCT_NO AND C.ADMIN_NO = A.ADMIN_NO
	LEFT JOIN #CATS_TMP_APPROVAL D ON D.ACCT_NO = A.ACCT_NO AND D.ADMIN_NO = A.ADMIN_NO
	LEFT JOIN #CATS_TMP_ACQUIRE E ON E.ACCT_NO = A.ACCT_NO AND E.ADMIN_NO = A.ADMIN_NO
	LEFT JOIN #CATS_TMP_BILL F ON F.ACCT_NO = A.ACCT_NO AND F.ADMIN_NO = A.ADMIN_NO
)
SELECT 'N' AS S,
	   FC.NO_CARD,
	   FC.NM_CARD,
	   FC.NM_OWNER,
	   A.ACCT_NO,
	   A.ADMIN_NO,
	   A.TRADE_DATE, 
	   A.ADMIN_AMT,
	   A.AM_DOCU,
	   ISNULL(A.AM_APPR_IBK, 0) AS AM_APPR_IBK,
	   ISNULL(A.AM_BILL_IBK, 0) AS AM_BILL_IBK,
	   ISNULL(A.AM_APPR_BC, 0) AS AM_APPR_BC, 
	   ISNULL(A.AM_ACQ_BC, 0) AS AM_ACQ_BC,
	   ISNULL(A.AM_BILL_BC, 0) AS AM_BILL_BC,
	   CV.YN_VERIFY,
	   CV.DC_RMK
FROM A
LEFT JOIN (SELECT CD_COMPANY,
		   	      REPLACE(NO_CARD, '-', '') AS ACCT_NO,
		   	      NO_CARD,
		   	      NM_CARD,
		   	      NM_OWNER
		   FROM FI_CARD) FC
ON FC.CD_COMPANY = A.C_CODE AND FC.ACCT_NO = A.ACCT_NO
LEFT JOIN CZ_FI_CARD_VERIFY CV ON CV.CD_COMPANY = A.C_CODE AND CV.ACCT_NO = A.ACCT_NO AND CV.ADMIN_NO = A.ADMIN_NO
WHERE (ISNULL(@P_YN_VERIFY, 'N') = 'N' OR ISNULL(CV.YN_VERIFY, 'N') = 'N')
AND (ISNULL(@P_YN_DOCU, 'N') = 'N' OR A.AM_DOCU > 0)
AND ((ISNULL(@P_YN_ADMIN, 'N') = 'N' AND ISNULL(@P_YN_APPR_IBK, 'N') = 'N' AND ISNULL(@P_YN_BILL_IBK, 'N') = 'N' AND ISNULL(@P_YN_APPR_BC, 'N') = 'N' AND ISNULL(@P_YN_ACQ_BC, 'N') = 'N' AND ISNULL(@P_YN_BILL_BC, 'N') = 'N') OR 
	 (ISNULL(@P_YN_ADMIN, 'N') = 'Y' AND ISNULL(A.ADMIN_AMT, 0) < ISNULL(A.AM_DOCU, 0)) OR 
	 (ISNULL(@P_YN_APPR_IBK, 'N') = 'Y' AND A.AM_APPR_IBK IS NOT NULL AND ISNULL(A.AM_APPR_IBK, 0) < ISNULL(A.AM_DOCU, 0)) OR
	 (ISNULL(@P_YN_BILL_IBK, 'N') = 'Y' AND A.AM_BILL_IBK IS NOT NULL AND ISNULL(A.AM_BILL_IBK, 0) < ISNULL(A.AM_DOCU, 0)) OR
	 (ISNULL(@P_YN_APPR_BC, 'N') = 'Y' AND A.AM_APPR_BC IS NOT NULL AND ISNULL(A.AM_APPR_BC, 0) < ISNULL(A.AM_DOCU, 0)) OR
	 (ISNULL(@P_YN_ACQ_BC, 'N') = 'Y' AND A.AM_ACQ_BC IS NOT NULL AND ISNULL(A.AM_ACQ_BC, 0) < ISNULL(A.AM_DOCU, 0)) OR
	 (ISNULL(@P_YN_BILL_BC, 'N') = 'Y' AND A.AM_BILL_BC IS NOT NULL AND ISNULL(A.AM_BILL_BC, 0) < ISNULL(A.AM_DOCU, 0)) OR
	 (ISNULL(@P_YN_NO_DATA, 'N') = 'Y' AND A.AM_APPR_IBK IS NULL AND A.AM_BILL_IBK IS NULL AND A.AM_APPR_BC IS NULL AND A.AM_ACQ_BC IS NULL AND A.AM_BILL_BC IS NULL))

GO