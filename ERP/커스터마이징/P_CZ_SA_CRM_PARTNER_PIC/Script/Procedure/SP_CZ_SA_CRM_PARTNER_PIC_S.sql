USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_CRM_PARTNER_PIC_S]    Script Date: 2018-04-06 오후 6:07:57 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO




ALTER PROCEDURE [NEOE].[SP_CZ_SA_CRM_PARTNER_PIC_S]
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_CLS_PARTNER		NVARCHAR(4),
	@P_CD_PARTNER		NVARCHAR(20),
	@P_NM_PTR			NVARCHAR(30)
) 
AS
 
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT MP.CD_COMPANY,
	   FP1.CD_RANK,
	   MP.CLS_PARTNER,
	   CD.NM_SYSDEF AS NM_CLS_PARTNER,
	   MP.FG_PARTNER,
	   CD2.NM_SYSDEF AS NM_FG_PARTNER,
	   MP.CD_PARTNER,
	   MP.LN_PARTNER,
	   FP.SEQ,
	   (CASE WHEN ISNULL(FP.EN_PTR, '') = '' THEN FP.NM_PTR ELSE FP.NM_PTR + ' (' + ISNULL(FP.EN_PTR, '') + ')' END) AS NM_PTR,
	   (CASE WHEN ISNULL(FP.NM_DUTY_RESP, '') = '' AND ISNULL(FP.EN_DUTY_RESP, '') = '' THEN FP.NM_DEPT
			 WHEN ISNULL(FP.EN_DUTY_RESP, '') = '' THEN FP.NM_DEPT + '/' + ISNULL(FP.NM_DUTY_RESP, '')
			 WHEN ISNULL(FP.NM_DUTY_RESP, '') = '' THEN FP.NM_DEPT + '/' + ISNULL(FP.EN_DUTY_RESP, '')
			 ELSE FP.NM_DEPT + '/' + ISNULL(FP.NM_DUTY_RESP, '') + ' (' + ISNULL(FP.EN_DUTY_RESP, '') + ')' END) AS NM_DEPT_DUTY,
	   (CASE WHEN ISNULL(FP.NO_FAX, '') = '' AND ISNULL(FP.NO_TEL, '') = '' THEN NULL
		     WHEN ISNULL(FP.NO_FAX, '') = '' THEN 'TEL : ' + FP.NO_TEL 
		     WHEN ISNULL(FP.NO_TEL, '') = '' THEN 'FAX : ' + ISNULL(FP.NO_FAX, '')
			 ELSE 'TEL : ' + FP.NO_TEL + ' / FAX : ' + ISNULL(FP.NO_FAX, '') END) AS NO_TEL,
	   (CASE WHEN ISNULL(FP.NO_HP, '') = '' THEN NULL ELSE 'HP : ' + FP.NO_HP END) AS NO_HP,
	   (CASE WHEN ISNULL(FP.NM_EMAIL, '') = '' THEN NULL ELSE 'E-MAIL : ' + FP.NM_EMAIL END) AS NM_EMAIL,
	   FP.DC_PHOTO,
	   FP1.CD_GENDER,
	   FP1.CD_NATION,
	   CD1.NM_SYSDEF AS NM_NATION,
	   FP1.CD_RELIGION,
	   FP1.DT_BERTH,
	   FP1.DC_MEMO,
	   MF.FILE_PATH_MNG,
	   LEFT(FP.DTS_INSERT, 8) AS DT_INSERT,
	   LEFT(PC.DTS_UPDATE, 8) AS DT_UPDATE
FROM MA_PARTNER MP
JOIN FI_PARTNERPTR FP ON FP.CD_COMPANY = MP.CD_COMPANY AND FP.CD_PARTNER = MP.CD_PARTNER
LEFT JOIN CZ_FI_PARTNERPTR FP1 ON FP1.CD_COMPANY = FP.CD_COMPANY AND FP1.CD_PARTNER = FP.CD_PARTNER AND FP1.SEQ = FP.SEQ
LEFT JOIN MA_CODEDTL CD ON CD.CD_COMPANY = MP.CD_COMPANY AND CD.CD_FIELD = 'MA_B000003' AND CD.CD_SYSDEF = MP.CLS_PARTNER
LEFT JOIN MA_CODEDTL CD1 ON CD1.CD_COMPANY = FP1.CD_COMPANY AND CD1.CD_FIELD = 'MA_B000020' AND CD1.CD_SYSDEF = FP1.CD_NATION
LEFT JOIN MA_CODEDTL CD2 ON CD2.CD_COMPANY = MP.CD_COMPANY AND CD2.CD_FIELD = 'MA_B000001' AND CD2.CD_SYSDEF = MP.FG_PARTNER
LEFT JOIN (SELECT CD_FILE,
				  CONVERT(VARCHAR, COUNT(1)) AS FILE_PATH_MNG
		   FROM MA_FILEINFO
		   WHERE CD_COMPANY = 'K100' 
		   AND CD_MODULE = 'SA' 
		   AND ID_MENU = 'P_CZ_SA_CRM_PARTNER_PIC'
		   GROUP BY CD_FILE) MF 
ON  MF.CD_FILE = FP.CD_COMPANY + '_' + FP.CD_PARTNER + '_' + CONVERT(NVARCHAR, FP.SEQ)
LEFT JOIN (SELECT CD_COMPANY, CD_PARTNER, SEQ,
				  MAX(DTS_UPDATE) AS DTS_UPDATE 
		   FROM (SELECT CD_COMPANY, CD_PARTNER, SEQ,
				        ISNULL(DTS_UPDATE, '') AS DTS_UPDATE
				 FROM FI_PARTNERPTR
				 UNION ALL
				 SELECT CD_COMPANY, CD_PARTNER, SEQ,
				        ISNULL(DTS_UPDATE, ISNULL(DTS_INSERT, '')) AS DTS_UPDATE 
				 FROM CZ_FI_PARTNERPTR
				 UNION ALL
				 SELECT CD_COMPANY, CD_PARTNER, SEQ,
				        MAX(ISNULL(DTS_UPDATE, ISNULL(DTS_INSERT, ''))) AS DTS_UPDATE 
				 FROM CZ_CRM_PARTNER_PIC_ITEM
				 GROUP BY CD_COMPANY, CD_PARTNER, SEQ
				 UNION ALL
				 SELECT CD_COMPANY, CD_PARTNER, SEQ,
				        MAX(ISNULL(DTS_UPDATE, ISNULL(DTS_INSERT, ''))) AS DTS_UPDATE 
				 FROM CZ_CRM_PARTNER_PIC_PEOPLE
				 GROUP BY CD_COMPANY, CD_PARTNER, SEQ
				 UNION ALL
				 SELECT CD_COMPANY, CD_PARTNER, SEQ,
				        MAX(ISNULL(DTS_UPDATE, ISNULL(DTS_INSERT, ''))) AS DTS_UPDATE 
				 FROM CZ_CRM_PARTNER_PIC_COMPANY
				 GROUP BY CD_COMPANY, CD_PARTNER, SEQ
				 UNION ALL
				 SELECT CD_COMPANY, CD_PARTNER, SEQ,
				        MAX(ISNULL(DTS_UPDATE, ISNULL(DTS_INSERT, ''))) AS DTS_UPDATE 
				 FROM CZ_CRM_PARTNER_PIC_HULL
				 GROUP BY CD_COMPANY, CD_PARTNER, SEQ) A
		   GROUP BY CD_COMPANY, CD_PARTNER, SEQ) PC
ON PC.CD_COMPANY = FP.CD_COMPANY AND PC.CD_PARTNER = FP.CD_PARTNER AND PC.SEQ = FP.SEQ
WHERE MP.CD_COMPANY = @P_CD_COMPANY
AND MP.USE_YN = 'Y'
AND (ISNULL(@P_CLS_PARTNER, '') = '' OR MP.CLS_PARTNER = @P_CLS_PARTNER)
AND (ISNULL(@P_CD_PARTNER, '') = '' OR MP.CD_PARTNER = @P_CD_PARTNER)
AND (ISNULL(@P_NM_PTR, '') = '' OR FP.NM_PTR LIKE '%' + @P_NM_PTR + '%')
ORDER BY FP1.CD_RANK DESC, CD.NM_SYSDEF, MP.LN_PARTNER, FP.SEQ

GO

