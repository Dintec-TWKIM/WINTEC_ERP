USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_PU_STS_BATCH_REGL_S]    Script Date: 2017-01-24 오전 9:23:56 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



ALTER PROCEDURE [NEOE].[SP_CZ_PU_STS_BATCH_REGL_S]
(
	@P_CD_COMPANY			NVARCHAR(7),
	@P_NO_GIREQ				NVARCHAR(20),
	@P_NO_SO				NVARCHAR(20),
	@P_CD_ITEM				NVARCHAR(20),
	@P_CLS_ITEM				NVARCHAR(3),
	@P_GRP_ITEM				NVARCHAR(MAX),
	@P_UM_STOCK				NUMERIC(15, 4),
	@P_TP_DATE				NVARCHAR(3),
	@P_DT_FROM				NVARCHAR(8),
	@P_DT_TO				NVARCHAR(8)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT 'N' AS S,
	   QL.CD_COMPANY,
	   QL.CD_PLANT,
	   QL.NO_GIREQ,
	   QL.NO_SO,
	   SL.NO_DSP,
	   QL.NO_LINE,
	   QL.CD_ITEM,
	   MI.NM_ITEM,
	   MI.CLS_ITEM,
	   MC.NM_SYSDEF AS NM_CLS_ITEM,
	   MI.GRP_ITEM,
	   MG.NM_ITEMGRP,
	   MI.STND_DETAIL_ITEM,
	   MI.STND_ITEM,
	   SL.NM_SUBJECT,
	   SL.CD_ITEM_PARTNER,
	   SL.NM_ITEM_PARTNER,
	   MI.CD_ZONE,
	   QL.CD_SL,
	   MS.NM_SL,
	   QL.CD_GRSL,
	   MS1.NM_SL AS NM_GRSL,
	   QL.QT_GIREQ,
	   QL.QT_GI,
	   ISNULL(ML.QT_INV, 0) AS QT_INV,
	   (ISNULL(ML.QT_INV, 0) - ISNULL(QL.QT_GIREQ, 0)) AS QT_REMAIN,
	   ('V01/D01' + QL.CD_COMPANY + '/D08' + QL.NO_GIREQ + '/D03' + QL.NO_SO) AS DC_QR,
	   IL.NO_IO,
	   IL.DT_IO,
	   IL.NO_EMP,
	   (SELECT NM_KOR 
		FROM MA_EMP
		WHERE CD_COMPANY = IL.CD_COMPANY
		AND NO_EMP = IL.NO_EMP) AS NM_EMP_IO,
	   MP.LN_PARTNER,
	   QL.DC_RMK,
	   MI.STAND_PRC,
	   GH.DC_RMK5
FROM MM_GIREQL QL 
JOIN MM_GIREQH QH ON QH.CD_COMPANY = QL.CD_COMPANY AND QH.NO_GIREQ = QL.NO_GIREQ
LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = QL.CD_COMPANY AND MI.CD_ITEM = QL.CD_ITEM
LEFT JOIN MA_ITEMGRP MG ON MG.CD_COMPANY = MI.CD_COMPANY AND MG.CD_ITEMGRP = MI.GRP_ITEM
LEFT JOIN CZ_SA_QTNL SL ON SL.CD_COMPANY = QL.CD_COMPANY AND SL.NO_FILE = QL.NO_SO AND SL.NO_LINE = QL.SEQ_SO
LEFT JOIN MA_SL MS ON MS.CD_COMPANY = QL.CD_COMPANY AND MS.CD_PLANT = QL.CD_PLANT AND MS.CD_SL = QL.CD_SL
LEFT JOIN MA_SL MS1 ON MS1.CD_COMPANY = QL.CD_COMPANY AND MS1.CD_PLANT = QL.CD_PLANT AND MS1.CD_SL = QL.CD_GRSL
LEFT JOIN MM_EJTP EJ ON EJ.CD_COMPANY = QL.CD_COMPANY AND EJ.CD_QTIOTP = QH.FG_GIREQ
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = QL.CD_COMPANY AND MC.CD_FIELD = 'MA_B000010' AND MC.CD_SYSDEF = MI.CLS_ITEM
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = MI.CD_COMPANY AND MP.CD_PARTNER = MI.PARTNER
LEFT JOIN (SELECT MY.CD_COMPANY, MY.CD_SL, MY.CD_ITEM, 
				  (SUM(MY.QT_GOOD_OPEN + MY.QT_REJECT_OPEN + MY.QT_INSP_OPEN + MY.QT_TRANS_OPEN) 
			       + SUM(MY.QT_GOOD_GR + MY.QT_REJECT_GR + MY.QT_INSP_GR + MY.QT_TRANS_GR) 
			       - SUM(MY.QT_GOOD_GI + MY.QT_REJECT_GI + MY.QT_INSP_GI + MY.QT_TRANS_GI)) AS QT_INV
		   FROM MM_PINVN MY
		   JOIN MA_PITEM MI ON MI.CD_COMPANY = MY.CD_COMPANY AND MI.CD_PLANT = MY.CD_PLANT AND MI.CD_ITEM = MY.CD_ITEM
		   WHERE MY.P_YR = SUBSTRING(CONVERT(NVARCHAR(8), GETDATE(), 112), 1, 4)
		   GROUP BY MY.CD_COMPANY, MY.CD_SL, MY.CD_ITEM) ML
ON ML.CD_COMPANY = QL.CD_COMPANY AND ML.CD_SL = QL.CD_SL AND ML.CD_ITEM = QL.CD_ITEM
LEFT JOIN (SELECT IH.CD_COMPANY,
		   		  IL.NO_ISURCV,
				  IL.NO_ISURCVLINE,
		   		  MAX(IH.NO_IO) AS NO_IO,
				  MAX(IH.DT_IO) AS DT_IO,
		   		  MAX(IH.NO_EMP) AS NO_EMP
		   FROM MM_QTIOH IH
		   LEFT JOIN MM_QTIO IL ON IL.CD_COMPANY = IH.CD_COMPANY AND IL.NO_IO = IH.NO_IO
		   GROUP BY IH.CD_COMPANY, IL.NO_ISURCV, IL.NO_ISURCVLINE) IL
ON IL.CD_COMPANY = QL.CD_COMPANY AND IL.NO_ISURCV = QL.NO_GIREQ AND IL.NO_ISURCVLINE = QL.NO_LINE
LEFT JOIN (SELECT GH.CD_COMPANY,
		   	      GH.NO_GIR,
				  GD.DC_RMK5
		   FROM SA_GIRH GH
		   JOIN CZ_SA_GIRH_WORK_DETAIL GD ON GD.CD_COMPANY = GH.CD_COMPANY AND GD.NO_GIR = GH.NO_GIR
		   UNION ALL
		   SELECT GH.CD_COMPANY,
		   	      GH.NO_GIR,
		   	      NULL AS DC_RMK5
		   FROM CZ_SA_GIRH_PACK GH
		   JOIN CZ_SA_GIRH_PACK_DETAIL GD ON GD.CD_COMPANY = GH.CD_COMPANY AND GD.NO_GIR = GH.NO_GIR
		   UNION ALL
		   SELECT GH.CD_COMPANY,
		          GH.NO_GIR,
		          GD.DC_RMK5
		   FROM (SELECT *,
						ROW_NUMBER() OVER (PARTITION BY CD_COMPANY, NO_GIR, TP_GIR ORDER BY NO_HST DESC) IDX 
				 FROM CZ_SA_GIRH_LOG) GH
		   JOIN CZ_SA_GIRH_DETAIL_LOG GD ON GD.CD_COMPANY = GH.CD_COMPANY AND GD.NO_GIR = GH.NO_GIR AND GD.TP_GIR = GH.TP_GIR AND GD.NO_HST = GH.NO_HST
		   WHERE GH.IDX = 1
		   AND NOT EXISTS (SELECT 1 
						   FROM CZ_SA_GIRH_WORK_DETAIL WD
						   WHERE WD.CD_COMPANY = GH.CD_COMPANY
						   AND WD.NO_GIR = GH.NO_GIR)
		   AND NOT EXISTS (SELECT 1 
						   FROM CZ_SA_GIRH_PACK_DETAIL PD
						   WHERE PD.CD_COMPANY = GH.CD_COMPANY
						   AND PD.NO_GIR = GH.NO_GIR)) GH
ON GH.CD_COMPANY = QH.CD_COMPANY AND GH.NO_GIR = QH.NO_PR_PACK 
WHERE QL.CD_COMPANY = @P_CD_COMPANY
AND QL.NO_GIREQ = @P_NO_GIREQ
AND (@P_UM_STOCK = 0 OR MI.STAND_PRC >= @P_UM_STOCK)
AND (ISNULL(@P_NO_SO, '') = '' OR QL.NO_SO = @P_NO_SO)
AND (ISNULL(@P_CD_ITEM, '') = '' OR QL.CD_ITEM = @P_CD_ITEM)
AND (ISNULL(@P_CLS_ITEM, '') = '' OR MI.CLS_ITEM = @P_CLS_ITEM)
AND (ISNULL(@P_GRP_ITEM, '') = '' OR MI.GRP_ITEM IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_GRP_ITEM)))
AND (@P_TP_DATE IN ('001', '002') OR IL.DT_IO BETWEEN @P_DT_FROM AND @P_DT_TO)
GO

