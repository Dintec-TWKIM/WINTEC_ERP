USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_PU_STS_BATCH_REGH_S]    Script Date: 2017-01-23 오후 7:41:52 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [NEOE].[SP_CZ_PU_STS_BATCH_REGH_S]
(
	@P_CD_COMPANY			NVARCHAR(MAX),
	@P_NO_GIREQ				NVARCHAR(20),
	@P_TP_DATE				NVARCHAR(3),
	@P_DT_FROM				NVARCHAR(8),
	@P_DT_TO				NVARCHAR(8),
	@P_NO_GIR				NVARCHAR(20),
	@P_NO_SO				NVARCHAR(20),
	@P_CD_ITEM				NVARCHAR(20),
    @P_TP_DONE				NVARCHAR(1),
	@P_CLS_ITEM				NVARCHAR(3),
	@P_GRP_ITEM				NVARCHAR(MAX),
	@P_YN_GIR				NVARCHAR(1),
	@P_UM_STOCK				NUMERIC(15, 4)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT 'N' AS S,
	   (CASE WHEN QL.QT_GIREQ = QL.QT_GI THEN '완료' ELSE '미완료' END) AS TP_STATE,
	   QH.CD_COMPANY,
	   MC.NM_COMPANY,
	   QH.CD_PLANT,
	   QH.NO_GIREQ,
	   QH.NO_PR_PACK AS NO_GIR,
	   GH.DT_GIR,
	   GH.DT_START,
	   GH.DT_COMPLETE,
	   QH.DT_GIREQ,
	   QL.CD_SL,
	   MS.NM_SL,
	   QL.CD_GRSL,
	   MS1.NM_SL AS NM_GRSL,
	   QH.CD_PARTNER,
	   MP.LN_PARTNER AS NM_PARTNER,
	   GH.CD_DELIVERY_TO,
	   MP1.LN_PARTNER AS NM_DELIVERY_TO,
	   GH.NO_IMO,
	   MH.NM_VESSEL,
	   QH.NO_EMP AS NO_EMP_GIREQ,
	   ME.NM_KOR AS NM_EMP_GIREQ,
	   GH.STA_GIR,
	   CD.NM_SYSDEF AS NM_STA_GIR,
	   GH.TP_GIR,
	   CD1.NM_SYSDEF AS NM_TP_GIR,
	   GH.CD_MAIN_CATEGORY,
	   CD2.NM_SYSDEF AS NM_MAIN_CATEGORY,
	   GH.CD_SUB_CATEGORY,
	   CD3.NM_SYSDEF AS NM_SUB_CATEGORY,
	   ISNULL(QH.CNT_PRINT, 0) AS CNT_PRINT,
	   QH.LAST_PRINT_ID,
	   (SELECT TOP 1 NM_USER 
	    FROM MA_USER
		WHERE ID_USER = QH.LAST_PRINT_ID) AS NM_PIRNT,
	   GH.YN_DELETE,
	   ISNULL(QL.CNT_GIR, 0) AS CNT_GIR,
	   QL.QT_GIREQ,
	   ISNULL(QL.QT_GI, 0) AS QT_GI,
	   QH.DC50_PO,
	   (SELECT STRING_AGG(A.NM_ITEMGRP, ',') 
	    FROM (SELECT MG.NM_ITEMGRP
	          FROM MM_GIREQL QL
	          LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = QL.CD_COMPANY AND MI.CD_ITEM = QL.CD_ITEM
	          LEFT JOIN MA_ITEMGRP MG ON MG.CD_COMPANY = MI.CD_COMPANY AND MG.CD_ITEMGRP = MI.GRP_ITEM
	          WHERE QL.CD_COMPANY = QH.CD_COMPANY
	          AND QL.NO_GIREQ = QH.NO_GIREQ
	          GROUP BY MG.NM_ITEMGRP) A) AS NM_ITEMGRP,
	   (CASE WHEN QL.PARTNER IN ('11823', '00799', '17252') THEN 1
			 WHEN QL.GRP_ITEM NOT IN ('GS', 'PV') AND QL.PARTNER <> '01340' THEN 2
			 WHEN QL.PARTNER = '01340' THEN 3
			 WHEN QL.GRP_ITEM IN ('GS', 'PV') THEN 4 ELSE 5 END) AS NO_SORT
FROM MM_GIREQH QH
JOIN (SELECT QL.CD_COMPANY, QL.NO_GIREQ,
			 MAX(QL.CD_SL) AS CD_SL,
			 MAX(QL.CD_GRSL) AS CD_GRSL, 
			 COUNT(1) AS CNT_GIR,
			 SUM(QL.QT_GIREQ) AS QT_GIREQ,
			 SUM(QL.QT_GI) AS QT_GI,
			 MAX(MI.GRP_ITEM) AS GRP_ITEM,
			 MAX(MI.PARTNER) AS PARTNER
	  FROM MM_GIREQL QL
	  LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = QL.CD_COMPANY AND MI.CD_PLANT = QL.CD_PLANT AND MI.CD_ITEM = QL.CD_ITEM
	  WHERE (@P_TP_DATE IN ('001', '002') OR 
			 EXISTS (SELECT 1 
				     FROM MM_QTIO OL
				     WHERE OL.CD_COMPANY = QL.CD_COMPANY
				     AND OL.NO_ISURCV = QL.NO_GIREQ 
				     AND OL.NO_ISURCVLINE = QL.NO_LINE
				     AND OL.DT_IO BETWEEN @P_DT_FROM AND @P_DT_TO)) 
	  AND (ISNULL(@P_CD_ITEM, '') = '' OR QL.CD_ITEM = @P_CD_ITEM)
	  AND (ISNULL(@P_NO_SO, '') = '' OR QL.NO_SO = @P_NO_SO)
	  AND (@P_YN_GIR = 'N' OR (@P_YN_GIR = 'Y' AND QL.CD_SL = 'SL02' AND QL.CD_GRSL = 'SL03'))
	  AND EXISTS (SELECT 1 
				  FROM MA_PITEM MI
				  WHERE MI.CD_COMPANY = QL.CD_COMPANY
				  AND MI.CD_PLANT = QL.CD_PLANT
				  AND MI.CD_ITEM = QL.CD_ITEM
				  AND (@P_UM_STOCK = 0 OR MI.STAND_PRC >= @P_UM_STOCK)
				  AND (ISNULL(@P_CLS_ITEM, '') = '' OR MI.CLS_ITEM = @P_CLS_ITEM)
				  AND (ISNULL(@P_GRP_ITEM, '') = '' OR MI.GRP_ITEM IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_GRP_ITEM))))
	  GROUP BY QL.CD_COMPANY, QL.NO_GIREQ) QL
ON QL.CD_COMPANY = QH.CD_COMPANY AND QL.NO_GIREQ = QH.NO_GIREQ
LEFT JOIN (SELECT GH.CD_COMPANY,
		   	      GH.NO_GIR,
		   	      '001' AS TP_GIR,
		   	      'N' AS YN_DELETE,
		   	      GH.STA_GIR,
		   	      GH.DT_GIR,
		   	      GD.DT_START,
				  GD.DT_COMPLETE,
		   	      GD.NO_IMO,
		   	      GD.CD_MAIN_CATEGORY,
		   	      GD.CD_SUB_CATEGORY,
		   	      GD.CD_DELIVERY_TO 
		   FROM SA_GIRH GH
		   JOIN CZ_SA_GIRH_WORK_DETAIL GD ON GD.CD_COMPANY = GH.CD_COMPANY AND GD.NO_GIR = GH.NO_GIR
		   UNION ALL
		   SELECT GH.CD_COMPANY,
		   	      GH.NO_GIR,
		   	      '002' AS TP_GIR,
		   	      'N' AS YN_DELETE,
		   	      GH.STA_GIR,
		   	      GH.DT_GIR,
		   	      GD.DT_START,
				  GD.DT_COMPLETE,
		   	      GD.NO_IMO,
		   	      GD.CD_PACK_CATEGORY,
		   	      GD.CD_SUB_CATEGORY,
		   	      GD.CD_COLLECT_FROM 
		   FROM CZ_SA_GIRH_PACK GH
		   JOIN CZ_SA_GIRH_PACK_DETAIL GD ON GD.CD_COMPANY = GH.CD_COMPANY AND GD.NO_GIR = GH.NO_GIR
		   UNION ALL
		   SELECT GH.CD_COMPANY,
		          GH.NO_GIR,
		          GH.TP_GIR,
				  'Y' AS YN_DELETE,
		          GH.STA_GIR,
				  GH.DT_GIR,
		          GD.DT_START,
				  GD.DT_COMPLETE,
		          GD.NO_IMO,
		          GD.CD_MAIN_CATEGORY,
		          GD.CD_SUB_CATEGORY,
		          GD.CD_DELIVERY_TO
		   FROM (SELECT *,
						ROW_NUMBER() OVER (PARTITION BY CD_COMPANY, NO_GIR, TP_GIR ORDER BY NO_HST DESC) IDX 
				 FROM CZ_SA_GIRH_LOG) GH
		   JOIN CZ_SA_GIRH_DETAIL_LOG GD ON GD.CD_COMPANY = GH.CD_COMPANY AND GD.NO_GIR = GH.NO_GIR AND GD.TP_GIR = GH.TP_GIR AND GD.NO_HST = GH.NO_HST
		   WHERE GH.IDX = 1
		   AND NOT EXISTS (SELECT 1 
						   FROM CZ_SA_GIRH_WORK_DETAIL WD
						   WHERE WD.CD_COMPANY = GH.CD_COMPANY
						   AND WD.NO_GIR = GH.NO_GIR)
		   AND NOT EXISTS (SELECT 1 
						   FROM CZ_SA_GIRH_PACK_DETAIL PD
						   WHERE PD.CD_COMPANY = GH.CD_COMPANY
						   AND PD.NO_GIR = GH.NO_GIR)) GH
ON GH.CD_COMPANY = QH.CD_COMPANY AND GH.NO_GIR = QH.NO_PR_PACK 
LEFT JOIN MA_COMPANY MC ON MC.CD_COMPANY = QH.CD_COMPANY
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = QH.CD_COMPANY AND MP.CD_PARTNER = QH.CD_PARTNER
LEFT JOIN CZ_MA_DELIVERY MP1 ON MP1.CD_COMPANY = GH.CD_COMPANY AND MP1.CD_PARTNER = GH.CD_DELIVERY_TO
LEFT JOIN MA_SL MS ON MS.CD_COMPANY = QL.CD_COMPANY AND MS.CD_SL = QL.CD_SL
LEFT JOIN MA_SL MS1 ON MS1.CD_COMPANY = QL.CD_COMPANY AND MS1.CD_SL = QL.CD_GRSL
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = GH.NO_IMO
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = QH.CD_COMPANY AND ME.NO_EMP = QH.NO_EMP
LEFT JOIN MA_CODEDTL CD ON CD.CD_COMPANY = GH.CD_COMPANY AND CD.CD_FIELD = 'CZ_SA00030' AND CD.CD_SYSDEF = GH.STA_GIR
LEFT JOIN MA_CODEDTL CD1 ON CD1.CD_COMPANY = GH.CD_COMPANY AND CD1.CD_FIELD = 'CZ_SA00021' AND CD1.CD_SYSDEF = GH.TP_GIR 
LEFT JOIN MA_CODEDTL CD2 ON CD2.CD_COMPANY = GH.CD_COMPANY AND CD2.CD_FIELD = (CASE WHEN GH.TP_GIR = '001' THEN 'CZ_SA00006' ELSE 'CZ_SA00020' END) AND CD2.CD_SYSDEF = GH.CD_MAIN_CATEGORY
LEFT JOIN MA_CODEDTL CD3 ON CD3.CD_COMPANY = GH.CD_COMPANY AND CD3.CD_FIELD = CD2.CD_FLAG1 AND CD3.CD_SYSDEF = GH.CD_SUB_CATEGORY
WHERE ((@P_TP_DATE = '001' AND QH.DT_GIREQ BETWEEN @P_DT_FROM AND @P_DT_TO) OR
       (@P_TP_DATE = '002' AND GH.DT_START BETWEEN @P_DT_FROM AND @P_DT_TO) OR
	   @P_TP_DATE = '003')
AND (ISNULL(@P_CD_COMPANY, '') = '' OR QH.CD_COMPANY IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_COMPANY)))
AND (ISNULL(@P_NO_GIREQ, '') = '' OR QH.NO_GIREQ = @P_NO_GIREQ)
AND (ISNULL(@P_NO_GIR, '') = '' OR GH.NO_GIR = @P_NO_GIR)
AND (ISNULL(@P_TP_DONE, '') = '' OR 
	 (@P_TP_DONE = '1' AND QL.QT_GIREQ > QL.QT_GI) OR
	 (@P_TP_DONE = '2' AND QL.QT_GIREQ = QL.QT_GI))

GO