USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_COMMISSION_MNGD_S]    Script Date: 2018-11-01 오후 4:03:14 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_SA_COMMISSION_MNGD_S]
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_TP_DATE			NVARCHAR(4),
	@P_DT_START			NVARCHAR(8),
	@P_DT_END			NVARCHAR(8),
	@P_CD_PARTNER		NVARCHAR(20),
	@P_NO_COMMISSION	NVARCHAR(20),
	@P_YN_ADDED			NVARCHAR(1),
	@P_YN_COD			NVARCHAR(1)
) 
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT 'N' AS S,
	   @P_NO_COMMISSION AS NO_COMMISSION,
	   @P_YN_ADDED AS YN_ADDED,
	   IH.NO_IV,
	   IH.NO_IV + '-' + RIGHT('0' + CONVERT(NVARCHAR(3), IL2.IDX), 2) AS NO_IV2,
	   SH.DT_SO,
	   (SELECT QH.DT_QTN 
		FROM CZ_SA_QTNH QH
		WHERE QH.CD_COMPANY = SH.CD_COMPANY
		AND QH.NO_FILE = SH.NO_SO) AS DT_QTN,
	   IH.DT_PROCESS,
	   RH.DT_RCP,
	   IL.NO_SO,
	   SH.NO_PO_PARTNER,
	   MP.LN_PARTNER,
	   MH.NM_VESSEL,
	   SH.CD_EXCH,
	   MC.NM_SYSDEF AS NM_EXCH,
	   IL.AM_IV,
	   IL.AM_CHARGE,
	   (ISNULL(IL.AM_IV, 0) - ISNULL(IL.AM_CHARGE, 0)) AS AM_NET,
	   ((ISNULL(IL.AM_IV, 0) + ISNULL(IL.VAT_EX, 0)) - ISNULL(RH.AM_RCP_EX, 0)) AS AM_OUTSTANDING,
	   (SELECT ISNULL(SUM(CD.AM_COMMISSION), 0) 
		FROM CZ_SA_COMMISSIOND CD
		WHERE CD.CD_COMPANY = IH.CD_COMPANY
		AND CD.NO_IV = IH.NO_IV
		AND CD.NO_SO = IL.NO_SO) AS AM_COMMISSION_BEFORE,
	   SH.DC_RMK1,
	   IL1.NM_ITEMGRP
FROM SA_IVH IH
JOIN (SELECT IL.CD_COMPANY, IL.NO_IV,
			 (CASE WHEN IL.CD_PJT LIKE 'PJT%' THEN IL.NO_SO ELSE IL.CD_PJT END) AS NO_SO,
			 MAX(IL.CD_PJT) AS CD_PJT,
			 SUM(CASE WHEN IL.YN_RETURN = 'Y' THEN -IL.AM_EX_CLS ELSE IL.AM_EX_CLS END) AS AM_IV,
	  	     SUM(CASE WHEN MI.CLS_ITEM = '010' THEN (CASE WHEN IL.YN_RETURN = 'Y' THEN -IL.AM_EX_CLS ELSE IL.AM_EX_CLS END) 
											   ELSE 0 END) AS AM_CHARGE,
			 SUM(CASE WHEN IL.YN_RETURN = 'Y' THEN -ROUND(IL.VAT / IL.RT_EXCH, 2) 
											  ELSE ROUND(IL.VAT / IL.RT_EXCH, 2) END) AS VAT_EX
	  FROM SA_IVL IL
	  LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = IL.CD_COMPANY AND MI.CD_PLANT = IL.CD_PLANT AND MI.CD_ITEM = IL.CD_ITEM
	  WHERE NOT EXISTS (SELECT 1 
					    FROM CZ_SA_COMMISSIOND
						WHERE CD_COMPANY = IL.CD_COMPANY
						AND NO_IV = IL.NO_IV
						AND NO_SO = (CASE WHEN IL.CD_PJT LIKE 'PJT%' THEN IL.NO_SO ELSE IL.CD_PJT END)
						AND YN_ADDED = @P_YN_ADDED)
	  GROUP BY IL.CD_COMPANY, IL.NO_IV, (CASE WHEN IL.CD_PJT LIKE 'PJT%' THEN IL.NO_SO ELSE IL.CD_PJT END)) IL 
ON IL.CD_COMPANY = IH.CD_COMPANY AND IL.NO_IV = IH.NO_IV
LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = IL.CD_COMPANY AND SH.NO_SO = IL.NO_SO
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = SH.CD_COMPANY AND MP.CD_PARTNER = SH.CD_PARTNER
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = SH.NO_IMO
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = SH.CD_COMPANY AND MC.CD_FIELD = 'MA_B000005' AND MC.CD_SYSDEF = SH.CD_EXCH
LEFT JOIN (SELECT RH.CD_COMPANY, RD.NO_TX, RD.CD_PJT,
				  MAX(RH.DT_RCP) AS DT_RCP,
		          SUM(RD.AM_RCP_TX_EX) AS AM_RCP_EX
		   FROM SA_RCPH RH
		   JOIN (SELECT RD.CD_COMPANY, RD.NO_RCP, RD.NO_TX, FD.CD_PJT,
		   		        RD.AM_RCP_TX_EX
		   		 FROM SA_RCPD RD
		   		 LEFT JOIN FI_DOCU FD ON FD.CD_COMPANY = RD.CD_COMPANY AND FD.NO_DOCU = RD.NO_DOCU AND FD.NO_DOLINE = RD.NO_DOLINE
		   		 UNION ALL
		   		 SELECT BD.CD_COMPANY, BD.NO_RCP, BD.NO_IV, FD.CD_PJT,
		   		        BD.AM_RCPS_EX
		   		 FROM SA_BILLSD BD
		   		 LEFT JOIN FI_DOCU FD ON FD.CD_COMPANY = BD.CD_COMPANY AND FD.NO_DOCU = BD.NO_DOCU_IV AND FD.NO_DOLINE = BD.NO_DOLINE_IV) RD
		   ON RD.CD_COMPANY = RH.CD_COMPANY AND RD.NO_RCP = RH.NO_RCP
		   WHERE RD.NO_TX IS NOT NULL
		   GROUP BY RH.CD_COMPANY, RD.NO_TX, RD.CD_PJT) RH
ON RH.CD_COMPANY = IH.CD_COMPANY AND RH.NO_TX = IH.NO_IV AND RH.CD_PJT = IL.CD_PJT
LEFT JOIN (SELECT IL.CD_COMPANY, IL.NO_IV, IL.NO_SO,
		          STRING_AGG(TRIM(IL.NM_ITEMGRP), ',') AS NM_ITEMGRP 
		   FROM (SELECT IL.CD_COMPANY, IL.NO_IV,
					    (CASE WHEN IL.CD_PJT LIKE 'PJT%' THEN IL.NO_SO ELSE IL.CD_PJT END) AS NO_SO,
						IG.NM_ITEMGRP
		         FROM SA_IVL IL
		         LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = IL.CD_COMPANY AND MI.CD_PLANT = IL.CD_PLANT AND MI.CD_ITEM = IL.CD_ITEM
		         LEFT JOIN MA_ITEMGRP IG ON IG.CD_COMPANY = MI.CD_COMPANY AND IG.CD_ITEMGRP = MI.GRP_ITEM
		         GROUP BY IL.CD_COMPANY, IL.NO_IV, (CASE WHEN IL.CD_PJT LIKE 'PJT%' THEN IL.NO_SO ELSE IL.CD_PJT END), IG.NM_ITEMGRP) IL
		   GROUP BY IL.CD_COMPANY, IL.NO_IV, IL.NO_SO) IL1
ON IL1.CD_COMPANY = IH.CD_COMPANY AND IL1.NO_IV = IH.NO_IV AND IL1.NO_SO = IL.NO_SO
LEFT JOIN (SELECT IL.CD_COMPANY, IL.NO_IV, 
				  (CASE WHEN IL.CD_PJT LIKE 'PJT%' THEN IL.NO_SO ELSE IL.CD_PJT END) AS NO_SO,
				  MAX(IL.IDX) AS IDX
		   FROM (SELECT IL.CD_COMPANY, IL.NO_IV, IL.CD_PJT, IL.NO_SO,
				        DENSE_RANK() OVER (PARTITION BY IL.NO_IV ORDER BY IL.NO_SO) AS IDX
				 FROM SA_IVL IL
				 GROUP BY IL.CD_COMPANY, IL.NO_IV, IL.CD_PJT, IL.NO_SO) IL
		   GROUP BY IL.CD_COMPANY, IL.NO_IV, (CASE WHEN IL.CD_PJT LIKE 'PJT%' THEN IL.NO_SO ELSE IL.CD_PJT END)) IL2
ON IL2.CD_COMPANY = IH.CD_COMPANY AND IL2.NO_IV = IH.NO_IV AND IL2.NO_SO = IL.NO_SO
WHERE IH.CD_COMPANY = @P_CD_COMPANY
AND IH.CD_PARTNER = @P_CD_PARTNER
AND (ISNULL(@P_YN_COD, 'N') = 'N' OR ISNULL(SH.COND_PAY, '') <> '102')
AND (ISNULL(@P_TP_DATE, '') = '' OR
	 (@P_TP_DATE = '001' AND SH.DT_SO BETWEEN @P_DT_START AND @P_DT_END) OR
	 (@P_TP_DATE = '002' AND IH.DT_PROCESS BETWEEN @P_DT_START AND @P_DT_END) OR
	 (@P_TP_DATE = '003' AND RH.DT_RCP BETWEEN @P_DT_START AND @P_DT_END))

GO

