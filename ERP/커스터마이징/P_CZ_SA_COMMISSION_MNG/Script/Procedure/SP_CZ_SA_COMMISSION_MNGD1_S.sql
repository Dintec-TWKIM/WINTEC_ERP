USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_COMMISSION_MNGD1_S]    Script Date: 2018-11-02 오전 10:01:23 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO







ALTER PROCEDURE [NEOE].[SP_CZ_SA_COMMISSION_MNGD1_S]
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_NO_COMMISSION	NVARCHAR(20)
) 
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT 'N' AS S,
	   CH.CD_COMPANY,
	   CH.NO_COMMISSION,
	   CD.YN_ADDED,
	   (SELECT QH.DT_QTN 
		FROM CZ_SA_QTNH QH
		WHERE QH.CD_COMPANY = SH.CD_COMPANY
		AND QH.NO_FILE = SH.NO_SO) AS DT_QTN,
	   CD.DT_SO,
	   CD.DT_IV,
	   CD.DT_RCP,
	   CD.NO_IV,
	   CD.NO_IV + '-' + RIGHT('0' + CONVERT(NVARCHAR(3), IL1.IDX), 2) AS NO_IV2,
	   CD.NO_SO,
	   SH.NO_PO_PARTNER,
	   MP.LN_PARTNER,
	   MH.NO_IMO,
	   MH.NM_VESSEL,
	   ME.NM_KOR AS NM_EMP,
	   CD.CD_EXCH,
	   MC.NM_SYSDEF AS NM_EXCH,
	   CD.AM_IV,
	   CD.AM_CHARGE,
	   CD.AM_NET,
	   CD.AM_OUTSTANDING,
	   (CASE WHEN CD.YN_CHARGE = 'Y' THEN CD.AM_IV ELSE CD.AM_NET END) AS AM_TARGET,
	   CD.YN_CHARGE,
	   CD.RT_COMMISSION,
	   CD.AM_COMMISSION,
	   CD.AM_COMMISSION_BEFORE,
	   SH.DC_RMK1,
	   IL.NM_ITEMGRP
FROM CZ_SA_COMMISSIONH CH
JOIN CZ_SA_COMMISSIOND CD ON CD.CD_COMPANY = CH.CD_COMPANY AND CD.NO_COMMISSION = CH.NO_COMMISSION
LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = CD.CD_COMPANY AND SH.NO_SO = CD.NO_SO
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = SH.CD_COMPANY AND MP.CD_PARTNER = SH.CD_PARTNER
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = SH.NO_IMO
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = SH.CD_COMPANY AND ME.NO_EMP = SH.NO_EMP
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = CD.CD_COMPANY AND MC.CD_FIELD = 'MA_B000005' AND MC.CD_SYSDEF = CD.CD_EXCH
LEFT JOIN (SELECT IL.CD_COMPANY, IL.NO_IV, IL.NO_SO,
		          STRING_AGG(TRIM(IL.NM_ITEMGRP), ',') AS NM_ITEMGRP 
		   FROM (SELECT IL.CD_COMPANY, IL.NO_IV,
					    (CASE WHEN IL.CD_PJT LIKE 'PJT%' THEN IL.NO_SO ELSE IL.CD_PJT END) AS NO_SO,
						IG.NM_ITEMGRP
		         FROM SA_IVL IL
		         LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = IL.CD_COMPANY AND MI.CD_PLANT = IL.CD_PLANT AND MI.CD_ITEM = IL.CD_ITEM
		         LEFT JOIN MA_ITEMGRP IG ON IG.CD_COMPANY = MI.CD_COMPANY AND IG.CD_ITEMGRP = MI.GRP_ITEM
		         GROUP BY IL.CD_COMPANY, IL.NO_IV, (CASE WHEN IL.CD_PJT LIKE 'PJT%' THEN IL.NO_SO ELSE IL.CD_PJT END), IG.NM_ITEMGRP) IL
		   GROUP BY IL.CD_COMPANY, IL.NO_IV, IL.NO_SO) IL
ON IL.CD_COMPANY = CD.CD_COMPANY AND IL.NO_IV = CD.NO_IV AND IL.NO_SO = CD.NO_SO
LEFT JOIN (SELECT IL.CD_COMPANY, IL.NO_IV, 
				  (CASE WHEN IL.CD_PJT LIKE 'PJT%' THEN IL.NO_SO ELSE IL.CD_PJT END) AS NO_SO,
				  MAX(IL.IDX) AS IDX
		   FROM (SELECT IL.CD_COMPANY, IL.NO_IV, IL.CD_PJT, IL.NO_SO,
				        DENSE_RANK() OVER (PARTITION BY IL.NO_IV ORDER BY IL.NO_SO) AS IDX
				 FROM SA_IVL IL
				 GROUP BY IL.CD_COMPANY, IL.NO_IV, IL.CD_PJT, IL.NO_SO) IL
		   GROUP BY IL.CD_COMPANY, IL.NO_IV, (CASE WHEN IL.CD_PJT LIKE 'PJT%' THEN IL.NO_SO ELSE IL.CD_PJT END)) IL1
ON IL1.CD_COMPANY = CD.CD_COMPANY AND IL1.NO_IV = CD.NO_IV AND IL1.NO_SO = CD.NO_SO
WHERE CH.CD_COMPANY = @P_CD_COMPANY
AND CH.NO_COMMISSION = @P_NO_COMMISSION

GO

