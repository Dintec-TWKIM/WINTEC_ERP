USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_AC_RECEIVABLE_RPT_S5]    Script Date: 2017-02-22 오후 4:09:40 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


CREATE PROCEDURE [NEOE].[SP_CZ_SA_AC_RECEIVABLE_RPT_S5]        
(        
	 @P_CD_COMPANY		NVARCHAR(7),
	 @P_DT_FROM			NVARCHAR(8),
	 @P_DT_TO			NVARCHAR(8),
	 @P_DT_TODAY		NVARCHAR(8),
	 @P_CD_PARTNER		NVARCHAR(MAX),
	 @P_CD_DEPT			NVARCHAR(12),
	 @P_CD_SALEGRP		NVARCHAR(12),
	 @P_NO_EMP			NVARCHAR(10),
	 @P_NO_PROJECT		NVARCHAR(20),
	 @P_NO_SO			NVARCHAR(20),
	 @P_NO_IV			NVARCHAR(20),
	 @P_YN_DSP_ZERO		NVARCHAR(1),
	 @P_NO_MONTH		NUMERIC(5, 0) = 0
)
AS        
 
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT IH.CD_COMPANY,
	   SH.NO_SO,
	   SH.NO_PO_PARTNER,
	   IH.DT_IV,
	   IH.DT_RCP,
	   DATEDIFF(DAY, IH.DT_IV, IH.DT_RCP) AS DT_RCP_DAY,
	   IH.DT_IV_MONTH,
	   MP.LN_PARTNER AS NM_PARTNER,
	   SH.NO_IMO,
	   MH.NM_VESSEL,
	   ME.NO_EMP,
	   ME.NM_KOR AS NM_EMP,
	   MD.NM_DEPT,
	   SG.NM_SALEGRP,
	   MC.NM_SYSDEF AS NM_EXCH,
 	   ISNULL(IH.AM_EX_CLS, 0) AS AM_EX_CLS,
	   ISNULL(IH.AM_CLS, 0) AS AM_CLS,
	   ISNULL(IH.AM_RCP_EX, 0) AS AM_RCP_EX,
	   ISNULL(IH.AM_RCP, 0) AS AM_RCP,
	   (ISNULL(IH.AM_EX_CLS, 0) - ISNULL(IH.AM_RCP_EX, 0)) AS AM_EX_REMAIN,
	   (ISNULL(IH.AM_CLS, 0) - ISNULL(IH.AM_RCP, 0)) AS AM_REMAIN,
	   (CASE WHEN IH.DT_IV_MONTH > 2 AND IH.DT_IV_MONTH <= 3 THEN (ISNULL(IH.AM_EX_CLS, 0) - ISNULL(IH.AM_RCP_EX, 0)) ELSE 0 END) AS AM_EX_2MONTH,
	   (CASE WHEN IH.DT_IV_MONTH > 3 AND IH.DT_IV_MONTH <= 6 THEN (ISNULL(IH.AM_EX_CLS, 0) - ISNULL(IH.AM_RCP_EX, 0)) ELSE 0 END) AS AM_EX_3MONTH,
	   (CASE WHEN IH.DT_IV_MONTH > 6 AND IH.DT_IV_MONTH <= 12 THEN (ISNULL(IH.AM_EX_CLS, 0) - ISNULL(IH.AM_RCP_EX, 0)) ELSE 0 END) AS AM_EX_6MONTH,
	   (CASE WHEN IH.DT_IV_MONTH > 12 THEN (ISNULL(IH.AM_EX_CLS, 0) - ISNULL(IH.AM_RCP_EX, 0)) ELSE 0 END) AS AM_EX_12MONTH,
	   (CASE WHEN IH.DT_IV_MONTH > 2 AND IH.DT_IV_MONTH <= 3 THEN (ISNULL(IH.AM_CLS, 0) - ISNULL(IH.AM_RCP, 0)) ELSE 0 END) AS AM_2MONTH,
	   (CASE WHEN IH.DT_IV_MONTH > 3 AND IH.DT_IV_MONTH <= 6 THEN (ISNULL(IH.AM_CLS, 0) - ISNULL(IH.AM_RCP, 0)) ELSE 0 END) AS AM_3MONTH,
	   (CASE WHEN IH.DT_IV_MONTH > 6 AND IH.DT_IV_MONTH <= 12 THEN (ISNULL(IH.AM_CLS, 0) - ISNULL(IH.AM_RCP, 0)) ELSE 0 END) AS AM_6MONTH,
	   (CASE WHEN IH.DT_IV_MONTH > 12 THEN (ISNULL(IH.AM_CLS, 0) - ISNULL(IH.AM_RCP, 0)) ELSE 0 END) AS AM_12MONTH,
	   SH.DC_RMK1,
	   AR.DC_RMK
FROM (SELECT IH.CD_COMPANY,
	         MAX(IL.CD_PJT) AS CD_PJT,
	  	     MAX(IL.NO_SO) AS NO_SO,
			 MAX(IH.CD_EXCH) AS CD_EXCH,
	  	     MIN(IH.DT_PROCESS) AS DT_IV,
	  	     MAX(RH.DT_RCP) AS DT_RCP,
	  	     DATEDIFF(MONTH, MIN(IH.DT_PROCESS), @P_DT_TODAY) AS DT_IV_MONTH,
	  	     SUM(ISNULL(IL.AM_EX_CLS, 0) + ISNULL(IL.VAT_EX, 0)) AS AM_EX_CLS,
	  	     SUM(ISNULL(IL.AM_CLS, 0) + ISNULL(IL.VAT, 0)) AS AM_CLS,
	  	     SUM(CASE WHEN ISNULL(IH.AM_IV, 0) = ISNULL(IH.AM_BAN, 0) THEN (ISNULL(IL.AM_EX_CLS, 0) + ISNULL(IL.VAT_EX, 0)) ELSE RH.AM_RCP_EX END) AS AM_RCP_EX,
	  	     SUM(CASE WHEN ISNULL(IH.AM_IV, 0) = ISNULL(IH.AM_BAN, 0) THEN (ISNULL(IL.AM_CLS, 0) + ISNULL(IL.VAT, 0)) ELSE RH.AM_RCP END) AS AM_RCP
	  FROM (SELECT IH.CD_COMPANY, IH.NO_IV,
	  		       IH.DT_PROCESS,
				   IH.CD_EXCH,
	  			   (ISNULL(IH.AM_K, 0) + ISNULL(IH.VAT_TAX, 0)) AS AM_IV,
	  		       ISNULL(IH.AM_BAN, 0) AS AM_BAN 
	  	    FROM SA_IVH IH
			WHERE IH.CD_COMPANY = @P_CD_COMPANY
			AND IH.NO_IV = @P_NO_IV
			AND IH.DT_PROCESS BETWEEN @P_DT_FROM AND @P_DT_TO
			AND IH.DT_PROCESS <= @P_DT_TODAY) IH
	  JOIN (SELECT IL.CD_COMPANY, IL.NO_IV, IL.CD_PJT,
	  			   MAX(IL.NO_SO) AS NO_SO,
	  		       SUM(CASE WHEN IL.YN_RETURN = 'Y' THEN -IL.AM_EX_CLS ELSE IL.AM_EX_CLS END) AS AM_EX_CLS,
	  	           SUM(CASE WHEN IL.YN_RETURN = 'Y' THEN -IL.AM_CLS ELSE IL.AM_CLS END) AS AM_CLS,
	  	           SUM(ROUND((CASE WHEN IL.YN_RETURN = 'Y' THEN -IL.VAT ELSE IL.VAT END) / IL.RT_EXCH, 2)) AS VAT_EX,
	  	           SUM(CASE WHEN IL.YN_RETURN = 'Y' THEN -IL.VAT ELSE IL.VAT END) AS VAT
	  	    FROM SA_IVL IL
			WHERE (ISNULL(@P_NO_PROJECT, '') = '' OR IL.CD_PJT LIKE @P_NO_PROJECT + '%')
			AND (ISNULL(@P_NO_SO, '') = '' OR IL.NO_SO LIKE @P_NO_SO + '%')
	  	    GROUP BY IL.CD_COMPANY, IL.NO_IV, IL.CD_PJT) IL
	  ON IL.CD_COMPANY = IH.CD_COMPANY AND IL.NO_IV = IH.NO_IV
	  LEFT JOIN (SELECT RH.CD_COMPANY, RD.NO_TX, RD.CD_PJT,
	  	     	        MAX(RH.DT_RCP) AS DT_RCP,
	  	     	        SUM(RD.AM_RCP_TX_EX) AS AM_RCP_EX, 
	  	     	        SUM(RD.AM_RCP) AS AM_RCP
	  	         FROM SA_RCPH RH
	  	         JOIN (SELECT RD.CD_COMPANY, RD.NO_RCP, RD.NO_TX, FD.CD_PJT,
	  	         		      RD.AM_RCP_TX_EX,
	  	         			  (ISNULL(RD.AM_RCP_TX, 0) + ISNULL(RD.AM_PL, 0)) AS AM_RCP 
	  	         	   FROM SA_RCPD RD
	  	         	   LEFT JOIN FI_DOCU FD ON FD.CD_COMPANY = RD.CD_COMPANY AND FD.NO_DOCU = RD.NO_DOCU AND FD.NO_DOLINE = RD.NO_DOLINE
	  	         	   UNION ALL
	  	         	   SELECT BD.CD_COMPANY, BD.NO_RCP, BD.NO_IV, FD.CD_PJT,
	  	         	    BD.AM_RCPS_EX,
	  	         	    (ISNULL(BD.AM_RCPS, 0) + ISNULL(BD.AM_PL, 0)) AS AM_RCP
	  	         	   FROM SA_BILLSD BD
	  	         	   LEFT JOIN FI_DOCU FD ON FD.CD_COMPANY = BD.CD_COMPANY AND FD.NO_DOCU = BD.NO_DOCU_IV AND FD.NO_DOLINE = BD.NO_DOLINE_IV) RD
	  	         ON RD.CD_COMPANY = RH.CD_COMPANY AND RD.NO_RCP = RH.NO_RCP
				 WHERE RD.NO_TX IS NOT NULL
				 AND RH.DT_RCP <= @P_DT_TODAY
	  	         GROUP BY RH.CD_COMPANY, RD.NO_TX, RD.CD_PJT) RH
	  ON RH.CD_COMPANY = IL.CD_COMPANY AND RH.NO_TX = IL.NO_IV AND RH.CD_PJT = IL.CD_PJT
	  GROUP BY IH.CD_COMPANY, (CASE WHEN ISNULL(IL.CD_PJT, '') = '' THEN IL.NO_SO ELSE IL.CD_PJT END)) IH
LEFT JOIN SA_SOH SH1 ON SH1.CD_COMPANY = IH.CD_COMPANY AND SH1.NO_SO = IH.CD_PJT
LEFT JOIN SA_SOH SH ON SH.CD_COMPANY = IH.CD_COMPANY AND SH.NO_SO = ISNULL(SH1.NO_SO, IH.NO_SO)
LEFT JOIN CZ_SA_AC_RECEIVABLE AR ON AR.CD_COMPANY = SH.CD_COMPANY AND AR.NO_KEY = SH.NO_SO
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = SH.CD_COMPANY AND MP.CD_PARTNER = SH.CD_PARTNER
LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = SH.NO_IMO
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = SH.CD_COMPANY AND ME.NO_EMP = SH.NO_EMP
LEFT JOIN (SELECT CD_COMPANY, NO_EMP,
				  MAX(CD_SALEGRP) AS CD_SALEGRP 
		   FROM MA_USER
		   GROUP BY CD_COMPANY, NO_EMP) MU
ON MU.CD_COMPANY = ME.CD_COMPANY AND MU.NO_EMP = ME.NO_EMP
LEFT JOIN MA_DEPT MD ON MD.CD_COMPANY = ME.CD_COMPANY AND MD.CD_DEPT = ME.CD_DEPT
LEFT JOIN MA_SALEGRP SG ON SG.CD_COMPANY = MU.CD_COMPANY AND SG.CD_SALEGRP = MU.CD_SALEGRP
LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = SH.CD_COMPANY AND MC.CD_FIELD = 'MA_B000005' AND MC.CD_SYSDEF = SH.CD_EXCH
WHERE (@P_NO_MONTH = 0 OR IH.DT_IV_MONTH > @P_NO_MONTH) 
AND (ISNULL(@P_YN_DSP_ZERO, 'N') = 'Y' OR (ISNULL(IH.AM_CLS, 0) - ISNULL(IH.AM_RCP, 0)) <> 0)
AND (ISNULL(@P_CD_PARTNER, '') = '' OR SH.CD_PARTNER IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_PARTNER)))
AND (ISNULL(@P_CD_DEPT, '') = '' OR ME.CD_DEPT = @P_CD_DEPT)
AND (ISNULL(@P_CD_SALEGRP, '') = '' OR MU.CD_SALEGRP = @P_CD_SALEGRP)
AND (ISNULL(@P_NO_EMP, '') = '' OR SH.NO_EMP = @P_NO_EMP)
ORDER BY IH.DT_IV ASC

GO

