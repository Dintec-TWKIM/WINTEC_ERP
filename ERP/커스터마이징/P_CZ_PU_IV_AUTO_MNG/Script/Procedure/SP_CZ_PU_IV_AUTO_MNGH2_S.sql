USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_PU_IV_AUTO_MNGH2_S]    Script Date: 2016-11-17 오후 5:00:32 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_PU_IV_AUTO_MNGH2_S]        
(             
	@P_CD_COMPANY           NVARCHAR(7),
	@P_CD_PARTNER			NVARCHAR(20),
	@P_DT_MONTH				NVARCHAR(6),
	@P_NO_FILE				NVARCHAR(20),
	@P_YN_CONFIRM			NVARCHAR(1)
)        
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT 'N' AS S,
	   EH.CD_PARTNER,
	   MP.LN_PARTNER,
	   EH.DT_MONTH,
	   EH.IDX,
	   EH.AM,
	   EL.AM AS AM_EL,
	   EL.AM_PO,
	   EL.AM_IO,
	   EL.AM_IV,
	   (CASE WHEN EH.AM = EL.AM AND EL.AM = EL.AM_IV THEN '매입완료'
			 WHEN EH.AM = EL.AM AND EL.AM = EL.AM_IO THEN '입고완료'
			 WHEN EH.AM = EL.AM THEN '파싱완료'
			 ELSE '' END) AS TP_COMPLETE,
	   EH.DC_LOG,
	   EH.NM_FILE,
	   EH.YN_CONFIRM,
	   EH.DC_RMK,
	   EH.MISS_ROW,
	   MU.NM_USER AS NM_INSERT
FROM CZ_PU_ETAX_DETAILH EH
JOIN (SELECT EL.CD_COMPANY, EL.CD_PARTNER, EL.DT_MONTH,
	  	     SUM(EL.AM) AS AM,
			 SUM(PH.AM_PO) AS AM_PO,
			 SUM(PH.AM_IO) AS AM_IO,
			 SUM(PH.AM_IV) AS AM_IV
	  FROM (SELECT EL.CD_COMPANY, EL.CD_PARTNER, EL.DT_MONTH, EL.NO_FILE,
				   SUM(EL.AM) AS AM
		    FROM CZ_PU_ETAX_DETAILL EL
			GROUP BY EL.CD_COMPANY, EL.CD_PARTNER, EL.DT_MONTH, EL.NO_FILE) EL
	  LEFT JOIN (SELECT PH.CD_COMPANY, PH.CD_PARTNER, PH.CD_PJT,
	  			 		SUM(PL.AM_PO) AS AM_PO,
	  			 		SUM(PL.AM_IO) AS AM_IO,
	  			 		SUM(PL.AM_IV) AS AM_IV,
	  			 	    SUM(ISNULL(PL.AM_IO, 0) - ISNULL(PL.AM_IV, 0)) AS AM_REMAIN
	  			 FROM PU_POH PH
	  			 LEFT JOIN (SELECT PL.CD_COMPANY, PL.NO_PO,
	  			 				   SUM(CASE WHEN ISNULL(IH.YN_RETURN, 'N') = 'Y' THEN -PL.AM ELSE PL.AM END) AS AM_PO,
	  			 				   SUM(CASE WHEN IH.YN_RETURN = 'Y' THEN -IL.AM ELSE IL.AM END) AS AM_IO,
	  			 				   SUM(CASE WHEN IH.YN_RETURN = 'Y' THEN -IL.AM_CLS ELSE IL.AM_CLS END) AS AM_IV
	  			 			FROM PU_POL PL
	  			 			LEFT JOIN MM_QTIO IL ON IL.CD_COMPANY = PL.CD_COMPANY AND IL.NO_PSO_MGMT = PL.NO_PO AND IL.NO_PSOLINE_MGMT = PL.NO_LINE
	  			 		    LEFT JOIN MM_QTIOH IH ON IH.CD_COMPANY = IL.CD_COMPANY AND IH.NO_IO = IL.NO_IO
							WHERE ISNULL(IL.YN_AM, 'Y') = 'Y'
	  			 			GROUP BY PL.CD_COMPANY, PL.NO_PO) PL
	  			 ON PL.CD_COMPANY = PH.CD_COMPANY AND PL.NO_PO = PH.NO_PO
	  			 GROUP BY PH.CD_COMPANY, PH.CD_PARTNER, PH.CD_PJT) PH
	  ON PH.CD_COMPANY = EL.CD_COMPANY AND PH.CD_PARTNER = EL.CD_PARTNER AND PH.CD_PJT = EL.NO_FILE
	  WHERE EL.CD_COMPANY = @P_CD_COMPANY
	  AND (ISNULL(@P_NO_FILE, '') = '' OR EL.NO_FILE = @P_NO_FILE)
	  GROUP BY EL.CD_COMPANY, EL.CD_PARTNER, EL.DT_MONTH) EL
ON EL.CD_COMPANY = EH.CD_COMPANY AND EL.CD_PARTNER = EH.CD_PARTNER AND EL.DT_MONTH = EH.DT_MONTH
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = EH.CD_COMPANY AND MP.CD_PARTNER = EH.CD_PARTNER
LEFT JOIN MA_USER MU ON MU.CD_COMPANY = EH.CD_COMPANY AND MU.ID_USER = EH.ID_INSERT
WHERE EH.CD_COMPANY = @P_CD_COMPANY
AND (ISNULL(@P_CD_PARTNER, '') = '' OR EH.CD_PARTNER = @P_CD_PARTNER)
AND (ISNULL(@P_DT_MONTH, '') = '' OR EH.DT_MONTH = @P_DT_MONTH)
AND (ISNULL(@P_YN_CONFIRM, 'N') = 'N' OR ISNULL(EH.YN_CONFIRM, 'N') = 'N')

GO