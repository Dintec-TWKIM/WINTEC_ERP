USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_GIRSCH_RECEIPT_REMIND]    Script Date: 2021-08-30 오후 7:32:06 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [NEOE].[SP_CZ_SA_GIRSCH_RECEIPT_REMIND]
(
	@P_CD_COMPANY	NVARCHAR(7)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

IF NOT EXISTS (SELECT 1
			   FROM MA_CALENDAR
			   WHERE CD_COMPANY = @P_CD_COMPANY
			   AND FG1_HOLIDAY = 'H'
			   AND DT_CAL = CONVERT(CHAR(8), GETDATE(), 112))
BEGIN
	DECLARE @V_FROM_EMAIL	NVARCHAR(100)
	DECLARE @V_TO_EMAIL		NVARCHAR(MAX)
	DECLARE @V_BCC_EMAIL	NVARCHAR(100)
	DECLARE @V_DC_SUBJECT	NVARCHAR(MAX)
	DECLARE @V_DC_BODY		NVARCHAR(MAX)
	
	DECLARE CUR CURSOR FOR
	
	WITH A AS
	(
		SELECT MC.NM_COMPANY,
			   (CASE WHEN GH.CD_COMPANY = 'K100' THEN 'receipt@dintec.co.kr' ELSE 'receipt@dubheco.com' END) AS FROM_EMAIL,
			   WD.NO_GIR,
			   MD.LN_PARTNER,
			   MH.NM_VESSEL,
			   ISNULL(NULLIF(WD.NO_DELIVERY_EMAIL, ''), MD.NO_EMAIL) AS TO_EMAIL,
			   OL.NO_IO,
			   OL.DT_IO,
			   OL.NO_PO_PARTNER,
			   OL.NO_PO_PARTNER1,
			   DATEDIFF(DAY, OL.DT_IO, CONVERT(CHAR(8), GETDATE(), 112)) AS QT_DIFF,
			   ME.NM_KOR,
			   ME.NO_TEL,
			   ME.NO_EMAIL,
			   (SELECT COUNT(1) 
				FROM MA_CALENDAR
				WHERE CD_COMPANY = GH.CD_COMPANY
				AND FG1_HOLIDAY = 'H'
				AND DT_CAL BETWEEN OL.DT_IO AND CONVERT(CHAR(8), GETDATE(), 112)) AS QT_HOLIDAY
		FROM SA_GIRH GH
		JOIN CZ_SA_GIRH_WORK_DETAIL WD ON WD.CD_COMPANY = GH.CD_COMPANY AND WD.NO_GIR = GH.NO_GIR
		JOIN CZ_MA_DELIVERY MD ON MD.CD_COMPANY = WD.CD_COMPANY AND MD.CD_PARTNER = WD.CD_DELIVERY_TO
		JOIN MA_COMPANY MC ON MC.CD_COMPANY = GH.CD_COMPANY
		JOIN MA_EMP ME ON ME.CD_COMPANY = GH.CD_COMPANY AND ME.NO_EMP = GH.NO_EMP
		LEFT JOIN CZ_MA_HULL MH ON MH.NO_IMO = WD.NO_IMO
		LEFT JOIN (SELECT A.CD_COMPANY, A.NO_ISURCV,
						  MAX(A.NO_IO) AS NO_IO,
						  MAX(A.DT_IO) AS DT_IO,
						  STRING_AGG(A.NO_PO_PARTNER, ',') AS NO_PO_PARTNER,
						  MAX(A.NO_PO_PARTNER) AS NO_PO_PARTNER1
				   FROM (SELECT OL.CD_COMPANY, OL.NO_ISURCV, SL.NO_PO_PARTNER,
						 	    MAX(OL.DT_IO) AS DT_IO,
						 	    MAX(OL.NO_IO) AS NO_IO
						 FROM MM_QTIO OL
						 JOIN SA_SOL SL ON SL.CD_COMPANY = OL.CD_COMPANY AND SL.NO_SO = OL.NO_PSO_MGMT AND SL.SEQ_SO = OL.NO_PSOLINE_MGMT
						 GROUP BY OL.CD_COMPANY, OL.NO_ISURCV, SL.NO_PO_PARTNER) A
				   GROUP BY A.CD_COMPANY, A.NO_ISURCV) OL
		ON OL.CD_COMPANY = GH.CD_COMPANY AND OL.NO_ISURCV = GH.NO_GIR
		WHERE GH.CD_COMPANY = @P_CD_COMPANY
		AND GH.STA_GIR = 'C'
		AND ((WD.CD_MAIN_CATEGORY = '002' AND WD.CD_SUB_CATEGORY IN ('DIR', 'ADV')) OR
		     (WD.CD_MAIN_CATEGORY = '003' AND WD.CD_SUB_CATEGORY IN ('001', '002', '004', '005', '007')))
		AND ISNULL(NULLIF(WD.NO_DELIVERY_EMAIL, ''), MD.NO_EMAIL) <> ''
		AND NOT EXISTS (SELECT 1
						FROM MA_FILEINFO FI
						WHERE FI.CD_COMPANY = GH.CD_COMPANY
						AND FI.CD_MODULE = 'SA'
						AND FI.ID_MENU = 'P_CZ_SA_GIM_REG'
						AND FI.CD_FILE = GH.NO_GIR + '_' + GH.CD_COMPANY)
	),
	B AS
	(
		SELECT A.NM_COMPANY,
			   A.FROM_EMAIL,
			   A.NO_GIR,
			   A.LN_PARTNER,
			   A.NM_VESSEL,
			   A.TO_EMAIL,
			   A.NO_IO,
			   A.DT_IO,
			   A.NO_PO_PARTNER,
			   A.NO_PO_PARTNER1,
			   A.NM_KOR,
			   A.NO_TEL,
			   A.NO_EMAIL,
			   (A.QT_DIFF - A.QT_HOLIDAY) AS QT_DAY
	    FROM A
		WHERE (A.QT_DIFF - A.QT_HOLIDAY) >= 5
	)
	SELECT B.FROM_EMAIL,
		   B.TO_EMAIL,
		   B.NO_EMAIL,
		   (B.NM_COMPANY + ' ' + B.NO_PO_PARTNER1 + ' (' + B.NO_IO + ') ' + B.NM_VESSEL + ' 인수증 미접수 알림') AS DC_SUBJECT,
		   '수신 : ' + B.LN_PARTNER + CHAR(13) + CHAR(10) +
		   '발신 : ' + B.NM_COMPANY + CHAR(13) + CHAR(10) + CHAR(13) + CHAR(10) +
		   '- 송품 정보' + CHAR(13) + CHAR(10) +
		   ' 의뢰번호 : ' + B.NO_GIR + CHAR(13) + CHAR(10) +
		   ' 호선명 : ' + B.NM_VESSEL + CHAR(13) + CHAR(10) +
		   ' ORDER NO. : ' + B.NO_PO_PARTNER + CHAR(13) + CHAR(10) +
		   ' 선사 : ' + B.LN_PARTNER + CHAR(13) + CHAR(10) + CHAR(13) + CHAR(10) +
		   '- 담당자 정보' + CHAR(13) + CHAR(10) +
		   ' 담당자 : ' + B.NM_KOR + CHAR(13) + CHAR(10) +
		   ' 연락처 : ' + B.NO_TEL + CHAR(13) + CHAR(10) +
		   ' 메일주소 : ' + B.NO_EMAIL + CHAR(13) + CHAR(10) + CHAR(13) + CHAR(10) +
		   '※ 해당 건은 인수증을 받지 못한 건 입니다. receipt 메일로 인수증 송부 해주시기 바랍니다.' AS DC_BODY
	FROM B
	
	OPEN CUR
	FETCH NEXT FROM CUR INTO @V_FROM_EMAIL, @V_TO_EMAIL, @V_BCC_EMAIL, @V_DC_SUBJECT, @V_DC_BODY
	
	WHILE @@FETCH_STATUS = 0
	BEGIN
	
	EXEC msdb.dbo.sp_send_dbmail @PROFILE_NAME = @V_FROM_EMAIL,	
								 @RECIPIENTS = @V_TO_EMAIL,
								 --@COPY_RECIPIENTS = @V_FROM_EMAIL,
								 @BLIND_COPY_RECIPIENTS = @V_BCC_EMAIL, 
								 @SUBJECT = @V_DC_SUBJECT,	
								 @BODY = @V_DC_BODY
	
	FETCH NEXT FROM CUR INTO @V_FROM_EMAIL, @V_TO_EMAIL, @V_BCC_EMAIL, @V_DC_SUBJECT, @V_DC_BODY
	END
	
	CLOSE CUR
	DEALLOCATE CUR
END

GO