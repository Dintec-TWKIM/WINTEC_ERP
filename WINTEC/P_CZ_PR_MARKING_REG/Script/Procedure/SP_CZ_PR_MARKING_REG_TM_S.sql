USE [NEOE]
GO
/****** Object:  StoredProcedure [NEOE].[SP_CZ_PR_MARKING_REG_TM_S]    Script Date: 2023-06-15 오후 8:01:53 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_PR_MARKING_REG_TM_S]          
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_NO_SO			NVARCHAR(20),
	@P_CD_ITEM			NVARCHAR(20)
)
AS
   
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
SET NOCOUNT ON

CREATE TABLE #BOM
(
	CD_COMPANY	NVARCHAR(7), 
	CD_PLANT	NVARCHAR(7), 
	CD_ITEM_T	NVARCHAR(20),
	NO_SEQ		NUMERIC(5, 0),
	CD_ITEM		NVARCHAR(20), 
	CD_MATL		NVARCHAR(20), 
	LEVEL		INT, 
	PATH		NVARCHAR(100), 
	QT			NUMERIC(25, 10)
)

CREATE NONCLUSTERED INDEX BOM ON #BOM (CD_COMPANY, CD_PLANT, CD_ITEM_T);

WITH BOM 
(
	CD_COMPANY, 
	CD_PLANT, 
	CD_ITEM_T,
	NO_SEQ,
	CD_ITEM, 
	CD_MATL, 
	LEVEL, 
	PATH, 
	QT
) 
AS
(
	SELECT PB.CD_COMPANY,
		   PB.CD_PLANT,
		   PB.CD_ITEM AS CD_ITEM_T,
		   PB.NO_SEQ,
		   PB.CD_ITEM, 
		   PB.CD_MATL, 
		   LEVEL = 1, 
		   CONVERT(VARCHAR(1000), PB.CD_MATL),
		   PB1.QT_ITEM_NET
    FROM PR_ROUT_ASN PB
	JOIN PR_BOM PB1 ON PB1.CD_COMPANY = PB.CD_COMPANY AND PB1.CD_PLANT = PB.CD_PLANT AND PB1.CD_ITEM = PB.CD_ITEM AND PB1.CD_MATL = PB.CD_MATL AND PB1.DT_END >= CONVERT(CHAR(8), GETDATE(), 112)
	WHERE PB.CD_COMPANY = @P_CD_COMPANY
	AND PB.NO_OPPATH = '100'
	AND EXISTS (SELECT 1 
				FROM MA_PITEM MI 
				WHERE MI.CD_COMPANY = PB.CD_COMPANY 
				AND MI.CD_PLANT = PB.CD_PLANT 
				AND MI.CD_ITEM = PB.CD_MATL
				AND MI.TP_PROC = 'M'
				AND ISNULL(MI.YN_USE, 'N') = 'Y')
	AND EXISTS (SELECT 1 
		        FROM PR_ROUT_L RL
		        WHERE RL.CD_COMPANY = PB.CD_COMPANY
		        AND RL.CD_PLANT = PB.CD_PLANT
		        AND RL.CD_ITEM = PB.CD_ITEM
		        AND RL.NO_OPPATH = PB.NO_OPPATH
		        AND RL.TP_OPPATH = PB.TP_OPPATH
		        AND RL.CD_OP = PB.CD_OP
			    AND RL.YN_USE = 'Y')
	UNION ALL
	SELECT BM.CD_COMPANY,
		   BM.CD_PLANT,
		   BM.CD_ITEM_T,
		   PB.NO_SEQ,
		   PB.CD_ITEM, 
		   PB.CD_MATL, 
		   LEVEL + 1, 
		   CONVERT(VARCHAR(1000), PATH + ' -> ' + PB.CD_MATL),
		   PB1.QT_ITEM_NET
	FROM BOM BM
	JOIN PR_ROUT_ASN PB ON PB.CD_ITEM = BM.CD_MATL AND PB.NO_OPPATH = '100'
	JOIN PR_BOM PB1 ON PB1.CD_COMPANY = PB.CD_COMPANY AND PB1.CD_PLANT = PB.CD_PLANT AND PB1.CD_ITEM = PB.CD_ITEM AND PB1.CD_MATL = PB.CD_MATL AND PB1.DT_END >= CONVERT(CHAR(8), GETDATE(), 112)
	WHERE EXISTS (SELECT 1 
				  FROM MA_PITEM MI 
				  WHERE MI.CD_COMPANY = PB.CD_COMPANY 
				  AND MI.CD_PLANT = PB.CD_PLANT 
				  AND MI.CD_ITEM = PB.CD_MATL
				  AND MI.TP_PROC = 'M'
				  AND ISNULL(MI.YN_USE, 'N') = 'Y')
	AND EXISTS (SELECT 1 
		        FROM PR_ROUT_L RL
		        WHERE RL.CD_COMPANY = PB.CD_COMPANY
		        AND RL.CD_PLANT = PB.CD_PLANT
		        AND RL.CD_ITEM = PB.CD_ITEM
		        AND RL.NO_OPPATH = PB.NO_OPPATH
		        AND RL.TP_OPPATH = PB.TP_OPPATH
		        AND RL.CD_OP = PB.CD_OP
			    AND RL.YN_USE = 'Y')
)
INSERT INTO #BOM
SELECT CD_COMPANY, 
	   CD_PLANT, 
	   CD_ITEM_T,
	   NO_SEQ,
	   CD_ITEM, 
	   CD_MATL, 
	   LEVEL, 
	   PATH, 
	   QT 
FROM BOM
UNION ALL
SELECT PB.CD_COMPANY,
	   PB.CD_PLANT,
	   PB.CD_ITEM AS CD_ITEM_T,
	   PB.NO_SEQ,
	   PB.CD_ITEM, 
	   PB.CD_ITEM, 
	   LEVEL = 0, 
	   CONVERT(VARCHAR(1000), PB.CD_ITEM),
	   1
FROM PR_ROUT_ASN PB
JOIN MA_PITEM MI1 ON MI1.CD_COMPANY = PB.CD_COMPANY AND MI1.CD_PLANT = PB.CD_PLANT AND MI1.CD_ITEM = PB.CD_ITEM
WHERE PB.CD_COMPANY = @P_CD_COMPANY
AND PB.NO_OPPATH = '100'
AND EXISTS (SELECT 1 
		    FROM PR_ROUT_L RL
		    WHERE RL.CD_COMPANY = PB.CD_COMPANY
		    AND RL.CD_PLANT = PB.CD_PLANT
		    AND RL.CD_ITEM = PB.CD_ITEM
		    AND RL.NO_OPPATH = PB.NO_OPPATH
		    AND RL.TP_OPPATH = PB.TP_OPPATH
		    AND RL.CD_OP = PB.CD_OP
			AND RL.YN_USE = 'Y')
GROUP BY PB.CD_COMPANY, PB.CD_PLANT, PB.CD_ITEM, PB.NO_SEQ


SELECT 'N' AS S,
	   SL.NO_SO,
	   SL.SEQ_SO,
	   BM.NO_SEQ,
	   BM.CD_MATL,
	   MI.NM_ITEM,
	   MI.NO_DESIGN,
	   MI.STND_ITEM,
	   (SL.QT_SO * BM.QT) AS QT_SO,
	   ISNULL(TM1.QT_MARKING, 0) AS QT_MARKING,
	   (ISNULL(TM2.NO_LINE, 0) + 1) AS NO_LINE,
	   BM.CD_MATL AS NM_FILE
FROM SA_SOH SH
JOIN SA_SOL SL ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
JOIN #BOM BM ON BM.CD_COMPANY = SL.CD_COMPANY AND BM.CD_PLANT = SL.CD_PLANT AND BM.CD_ITEM_T = SL.CD_ITEM
LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = BM.CD_COMPANY AND MI.CD_PLANT = BM.CD_PLANT AND MI.CD_ITEM = BM.CD_MATL
LEFT JOIN (SELECT CD_COMPANY, NO_SO, SEQ_SO, CD_ITEM,
				  COUNT(1) AS QT_MARKING
		   FROM CZ_SA_TRUST_MARKING
		   GROUP BY CD_COMPANY, NO_SO, SEQ_SO, CD_ITEM) TM1
ON TM1.CD_COMPANY = SL.CD_COMPANY AND TM1.NO_SO = SL.NO_SO AND TM1.SEQ_SO = SL.SEQ_SO AND TM1.CD_ITEM = BM.CD_MATL
LEFT JOIN (SELECT CD_COMPANY, NO_SO, SEQ_SO, CD_ITEM,
				  MAX(NO_LINE) AS NO_LINE
		   FROM CZ_SA_TRUST_MARKING
		   GROUP BY CD_COMPANY, NO_SO, SEQ_SO, CD_ITEM) TM2
ON TM2.CD_COMPANY = SL.CD_COMPANY AND TM2.NO_SO = SL.NO_SO AND TM2.SEQ_SO = SL.SEQ_SO AND TM2.CD_ITEM = BM.CD_MATL
WHERE SL.CD_COMPANY = @P_CD_COMPANY
AND SL.TXT_USERDEF9 = 'Y'
AND MI.GRP_ITEM IN ('2VP01', '2VP02', '2VP05', '2VP06', 'GP01', '4VP06', '4PP06', 'GP04')
AND (ISNULL(@P_NO_SO, '') = '' OR SL.NO_SO = @P_NO_SO)
AND (ISNULL(@P_CD_ITEM, '') = '' OR MI.CD_ITEM = @P_CD_ITEM)
AND SL.QT_SO * BM.QT > ISNULL(TM1.QT_MARKING, 0)
AND EXISTS (SELECT 1
			FROM CZ_SA_SOL_TRUST_WINTEC TW
			WHERE TW.CD_COMPANY = SL.CD_COMPANY
			AND TW.NO_SO = SL.NO_SO
			AND TW.CD_ITEM = BM.CD_MATL)
ORDER BY SH.DT_SO
