USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_SO_MNG_SERIAL_WINTEC_JSON]    Script Date: 2017-01-05 ¿ÀÈÄ 5:48:55 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_SA_SO_MNG_SERIAL_WINTEC_JSON]          
(
	@P_JSON			NVARCHAR(MAX),
	@P_ID_USER		NVARCHAR(10)	
)  
AS
   
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DELETE SR
FROM CZ_SA_SOL_SERIAL_WINTEC SR
WHERE EXISTS (SELECT 1 
			  FROM OPENJSON(@P_JSON)
			  WITH
			  (
					CD_COMPANY      NVARCHAR(7),
					NO_SO			NVARCHAR(20),
					SEQ_SO			NUMERIC(5, 0),
					IDX				INT,
					JSON_FLAG		NVARCHAR(1)
			  ) JS
			  WHERE JS.JSON_FLAG = 'D'
			  AND JS.CD_COMPANY = SR.CD_COMPANY
			  AND JS.NO_SO = SR.NO_SO
			  AND JS.SEQ_SO = SR.SEQ_SO
			  AND JS.IDX = SR.IDX)

INSERT INTO CZ_SA_SOL_SERIAL_WINTEC
(
	CD_COMPANY,
	NO_SO,
	SEQ_SO,
	IDX,
	NO_SERIAL,
	ID_INSERT,
	DTS_INSERT
)
SELECT CD_COMPANY,
	   NO_SO,
	   SEQ_SO,
	   (SELECT ISNULL(MAX(IDX), 0) 
	    FROM CZ_SA_SOL_SERIAL_WINTEC
		WHERE CD_COMPANY = JS.CD_COMPANY
		AND NO_SO = JS.NO_SO
		AND SEQ_SO = JS.SEQ_SO) + ROW_NUMBER() OVER (PARTITION BY JS.CD_COMPANY, JS.NO_SO, JS.SEQ_SO ORDER BY JS.CD_COMPANY, JS.NO_SO, JS.SEQ_SO) AS IDX,
	   NO_SERIAL,
	   @P_ID_USER,
	   NEOE.SF_SYSDATE(GETDATE())
FROM OPENJSON(@P_JSON)
WITH
(
	CD_COMPANY      NVARCHAR(7),
	NO_SO			NVARCHAR(20),
	SEQ_SO			NUMERIC(5, 0),
	IDX				INT,
	NO_SERIAL		NVARCHAR(14),
	JSON_FLAG		NVARCHAR(1)
) JS
WHERE JS.JSON_FLAG = 'I'

UPDATE SR
SET SR.NO_SERIAL = JS.NO_SERIAL,
	SR.ID_UPDATE = @P_ID_USER,
	SR.DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
FROM CZ_SA_SOL_SERIAL_WINTEC SR
JOIN OPENJSON(@P_JSON)
WITH
(
	CD_COMPANY      NVARCHAR(7),
	NO_SO			NVARCHAR(20),
	SEQ_SO			NUMERIC(5, 0),
	IDX				INT,
	NO_SERIAL		NVARCHAR(14),
	JSON_FLAG		NVARCHAR(1)
) JS
ON JS.CD_COMPANY = SR.CD_COMPANY AND JS.NO_SO = SR.NO_SO AND JS.SEQ_SO = SR.SEQ_SO AND JS.IDX = SR.IDX
WHERE JS.JSON_FLAG = 'U'

GO