USE [NEOE]
GO
/****** Object:  StoredProcedure [NEOE].[SP_CZ_PR_LINK_MES_BATCH]    Script Date: 2021-03-08 오후 7:53:09 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROC [NEOE].[SP_CZ_PR_LINK_MES_BATCH]
(
	@P_CD_COMPANY	NVARCHAR(7)		= NULL,
	@P_CD_PLANT		NVARCHAR(7)		= NULL,
	@P_NO_MES		NVARCHAR(20)	= NULL
)
AS

DECLARE	@ERRMSG			NVARCHAR(255),
		@ERRNO			INT,
		@V_CD_COMPANY	NVARCHAR(7),   
		@V_CD_PLANT		NVARCHAR(7),  
		@V_NO_MES		NVARCHAR(20),
		@V_CD_EXC		NVARCHAR(4)

DECLARE CUR_PR_LINK_MES_BATCH CURSOR FOR  
	SELECT	CD_COMPANY, CD_PLANT, NO_MES
	FROM	PR_LINK_MES
	WHERE	(YN_ERR			IS NULL OR YN_ERR = 'N' OR YN_ERR = '')
	AND		(YN_END			IS NULL OR YN_END = 'N' OR YN_END = '')
	AND		(@P_CD_COMPANY	IS NULL OR @P_CD_COMPANY	= '' OR CD_COMPANY	= @P_CD_COMPANY)
	AND		(@P_CD_PLANT	IS NULL OR @P_CD_PLANT		= '' OR CD_PLANT	= @P_CD_PLANT)
	AND		(@P_NO_MES		IS NULL OR @P_NO_MES		= '' OR NO_MES		= @P_NO_MES)

OPEN CUR_PR_LINK_MES_BATCH
FETCH NEXT FROM CUR_PR_LINK_MES_BATCH  
	INTO @V_CD_COMPANY, @V_CD_PLANT, @V_NO_MES

WHILE (@@FETCH_STATUS = 0)  
BEGIN  
	SELECT	@V_CD_EXC	= CD_EXC 
	FROM	MA_EXC 
	WHERE	CD_COMPANY	= @V_CD_COMPANY 
	AND		EXC_TITLE	= 'MES연동'

	-- 연동옵션 : 000[사용안함], 100[자동연동], 200[수동연동], 300[배치+수동)]
	IF (@V_CD_EXC <> '300')
	BEGIN
		CLOSE CUR_PR_LINK_MES_BATCH     
		DEALLOCATE CUR_PR_LINK_MES_BATCH
		
		SELECT @ERRMSG = '[TI_PR_LINK_MES]통제환경설정에 [배치+수동]이 아닙니다.'
		GOTO ERROR
	END
    
	SET XACT_ABORT ON; 
	BEGIN TRY  
		BEGIN TRAN
		EXEC NEOE.SP_CZ_PR_LINK_MES_REG_I @V_CD_COMPANY, @V_CD_PLANT, @V_NO_MES
		COMMIT TRAN      
	END TRY    
	
	BEGIN CATCH  
		SET @ERRMSG = ERROR_MESSAGE()        
		SET @ERRNO =  ERROR_NUMBER()          
	  
		IF (XACT_STATE()) = -1        
		BEGIN            
			ROLLBACK TRAN                    
			PRINT 'ROLLBACK TRAN'        
		END        
		ELSE IF (XACT_STATE() = 1 )         
		BEGIN             
			COMMIT TRAN      
			PRINT 'COMMIT TRAN'        
		END     
		
		UPDATE PR_LINK_MES  
		SET    YN_ERR  = 'Y' 
		WHERE  CD_COMPANY = @V_CD_COMPANY  
		AND    CD_PLANT   = @V_CD_PLANT  
		AND    NO_MES	  = @V_NO_MES
		
		EXEC NEOE.UP_PR_LINK_MES_ERROR_I	@V_NO_MES		,
											@V_CD_COMPANY	,
											@V_CD_PLANT		,
											@ERRNO			,
											@ERRMSG			,
											''				,
											'MES_SCHEDULER'	
	END CATCH  
    
	FETCH NEXT FROM CUR_PR_LINK_MES_BATCH  
		INTO @V_CD_COMPANY, @V_CD_PLANT, @V_NO_MES
END  

CLOSE CUR_PR_LINK_MES_BATCH     
DEALLOCATE CUR_PR_LINK_MES_BATCH
RETURN

ERROR: RAISERROR (@ERRMSG, 18, 1)
ROLLBACK TRANSACTION
RETURN