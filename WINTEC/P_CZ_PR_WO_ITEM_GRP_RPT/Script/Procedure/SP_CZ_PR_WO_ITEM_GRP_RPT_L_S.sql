USE [NEOE]
GO
/****** Object:  StoredProcedure [NEOE].[SP_CZ_PR_WO_ITEM_GRP_RPT_L_S]    Script Date: 2021-02-18 오후 1:19:05 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_PR_WO_ITEM_GRP_RPT_L_S]
(  
    @P_CD_COMPANY       NVARCHAR(7),
    @P_NO_SO            NVARCHAR(20),
    @P_SEQ_SO           NUMERIC(5, 0),
	@P_NO_DESIGN		NVARCHAR(20)
)  
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

;WITH A AS
(
    SELECT SL.CD_COMPANY, SL.CD_PLANT, SL.NO_SO, SL.SEQ_SO, 
		   SW.CD_ITEM,
           WO.NO_WO,
           WR.CD_WC, WR.CD_OP, WR.QT_START, WR.QT_WIP, WR.YN_FINAL, WP.DC_RMK, 
           SW.QT_APPLY,
           WR1.QT_ROUT,
           SUM(SW.QT_APPLY) OVER (PARTITION BY WO.NO_WO, WR.CD_OP ORDER BY (CASE WHEN SL.QT_GIR > 0 THEN '1' ELSE '2' END), SL.DT_DUEDATE, SL.NO_SO, SL.SEQ_SO) AS QT_TOTAL 
    FROM SA_SOL SL
    JOIN CZ_PR_SA_SOL_PR_WO_MAPPING SW ON SW.CD_COMPANY = SL.CD_COMPANY AND SW.NO_SO = SL.NO_SO AND SW.NO_LINE = SL.SEQ_SO
    LEFT JOIN PR_WO WO ON WO.CD_COMPANY = SW.CD_COMPANY AND WO.NO_WO = SW.NO_WO
    LEFT JOIN PR_WO_ROUT WR ON WR.CD_COMPANY = WO.CD_COMPANY AND WR.NO_WO = WO.NO_WO
    LEFT JOIN PR_WCOP WP ON WP.CD_COMPANY = WR.CD_COMPANY AND WP.CD_PLANT = WR.CD_PLANT AND WP.CD_WCOP = WR.CD_WCOP AND WP.CD_WC = WR.CD_WC
    LEFT JOIN (SELECT WR.CD_COMPANY,
                      WR.NO_WO,
                      WR.NO_LINE,
                      (WR.QT_MOVE - (SUM(WR.QT_BAD) OVER (PARTITION BY WR.CD_COMPANY, WR.NO_WO ORDER BY WR.NO_LINE DESC) - WR.QT_BAD) + WR.QT_WIP) AS QT_ROUT
               FROM PR_WO_ROUT WR) WR1
    ON WR1.CD_COMPANY = WR.CD_COMPANY AND WR1.NO_WO = WR.NO_WO AND WR1.NO_LINE = WR.NO_LINE
    WHERE SL.CD_COMPANY = @P_CD_COMPANY
    AND EXISTS (SELECT 1 
                FROM MA_PITEM MI 
                WHERE MI.CD_COMPANY = SW.CD_COMPANY 
                AND MI.CD_PLANT = SW.CD_PLANT 
                AND MI.CD_ITEM = SW.CD_ITEM
                AND MI.NO_DESIGN = @P_NO_DESIGN)
),
B AS
(
    SELECT A.CD_COMPANY, A.CD_PLANT, A.NO_SO, A.SEQ_SO, 
		   A.CD_ITEM,
		   A.NO_WO,
		   A.CD_WC, A.CD_OP, A.DC_RMK,
		   A.QT_APPLY,
		   (CASE WHEN A.YN_FINAL = 'Y' AND (ISNULL(A.QT_ROUT, 0) - ISNULL(A.QT_WIP, 0)) >= A.QT_TOTAL THEN 'Y' ELSE 'N' END) AS YN_DONE,
           ROW_NUMBER() OVER (PARTITION BY A.NO_SO, A.SEQ_SO, A.CD_ITEM, A.NO_WO ORDER BY A.CD_OP DESC) AS IDX
    FROM A
    WHERE A.QT_ROUT >= A.QT_TOTAL
),
C AS
(
    SELECT QH.CD_COMPANY, QH.CD_PLANT, QL.NO_SO, QL.SEQ_SO, QL.CD_ITEM,
           '재고' AS NO_WO, 
		   '재고' AS CD_WC,
    	   '999' AS CD_OP,
		   MAX(MS.NM_SL) AS DC_RMK,
		   SUM(QL.QT_GI) AS QT_APPLY,
		   'Y' AS YN_DONE
    FROM MM_GIREQH QH
    JOIN MM_GIREQL QL ON QL.CD_COMPANY = QH.CD_COMPANY AND QL.NO_GIREQ = QH.NO_GIREQ
	LEFT JOIN MA_SL MS ON MS.CD_COMPANY = QL.CD_COMPANY AND MS.CD_SL = QL.CD_GRSL
    WHERE QH.CD_COMPANY = @P_CD_COMPANY
    AND QL.CD_SL = 'SL_STND'
    AND EXISTS (SELECT 1 
                FROM MA_PITEM MI 
                WHERE MI.CD_COMPANY = QH.CD_COMPANY 
                AND MI.CD_PLANT = QH.CD_PLANT 
                AND MI.CD_ITEM = QL.CD_ITEM
                AND MI.NO_DESIGN = @P_NO_DESIGN)
    GROUP BY QH.CD_COMPANY, QH.CD_PLANT, QL.NO_SO, QL.SEQ_SO, QL.CD_ITEM
    HAVING SUM(QL.QT_GI) > 0
    UNION ALL
    SELECT B.CD_COMPANY, B.CD_PLANT, B.NO_SO, B.SEQ_SO, B.CD_ITEM,
           B.NO_WO,
           B.CD_WC,
		   B.CD_OP,
		   (CASE WHEN B.YN_DONE = 'Y' THEN MS.NM_SL ELSE B.DC_RMK END) DC_RMK,
		   B.QT_APPLY,
		   B.YN_DONE
    FROM B
	LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = B.CD_COMPANY AND MI.CD_PLANT = B.CD_PLANT AND MI.CD_ITEM = B.CD_ITEM
	LEFT JOIN MA_SL MS ON MS.CD_COMPANY = MI.CD_COMPANY AND MS.CD_PLANT = MI.CD_PLANT AND MS.CD_SL = MI.CD_SL
    WHERE B.IDX = 1
)
SELECT ROW_NUMBER() OVER (PARTITION BY C.NO_SO, C.SEQ_SO, C.CD_ITEM ORDER BY C.YN_DONE, C.CD_OP) AS IDX,
       C.NO_WO,
       WC.NM_WC,
       C.DC_RMK,
       C.QT_APPLY
FROM SA_SOL SL
JOIN C ON C.CD_COMPANY = SL.CD_COMPANY AND C.CD_PLANT = SL.CD_PLANT AND C.NO_SO = SL.NO_SO AND C.SEQ_SO = SL.SEQ_SO
LEFT JOIN MA_WC WC ON WC.CD_COMPANY = C.CD_COMPANY AND WC.CD_PLANT = C.CD_PLANT AND WC.CD_WC = C.CD_WC
WHERE SL.NO_SO = @P_NO_SO
AND SL.SEQ_SO = @P_SEQ_SO
OPTION(RECOMPILE)

GO