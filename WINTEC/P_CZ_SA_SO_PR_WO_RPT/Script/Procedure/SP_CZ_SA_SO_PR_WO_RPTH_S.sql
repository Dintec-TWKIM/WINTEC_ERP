USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_SA_SO_PR_WO_RPTH_S]    Script Date: 2019-11-14 오후 3:11:38 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [NEOE].[SP_CZ_SA_SO_PR_WO_RPTH_S]        
(  
    @P_CD_COMPANY		NVARCHAR(7),
	@P_CD_PLANT			NVARCHAR(7),
	@P_NO_OPPATH		NVARCHAR(3),
	@P_DT_START			NVARCHAR(8),
	@P_DT_END			NVARCHAR(8),
	@P_NO_SO			NVARCHAR(20),
	@P_NO_WO			NVARCHAR(20),
	@P_CD_WC			NVARCHAR(1000),
	@P_CD_WCOP			NVARCHAR(1000),
	@P_CD_EQUIP			NVARCHAR(1000),
	@P_YN_EXPECT_GI		NVARCHAR(1),
	@P_CD_PARTNER		NVARCHAR(20),
	@P_CD_ITEM			NVARCHAR(20),
	@P_NO_DRAWING		NVARCHAR(20),
	@P_NM_ITEM			NVARCHAR(200),
	@P_YN_MOTHER		NVARCHAR(1)
)        
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

CREATE TABLE #BOM
(
	CD_COMPANY	NVARCHAR(7), 
	CD_PLANT	NVARCHAR(7), 
	CD_ITEM_T	NVARCHAR(20), 
	CD_ITEM		NVARCHAR(20), 
	CD_MATL		NVARCHAR(20), 
	LEVEL		INT, 
	PATH		NVARCHAR(100), 
	QT			NUMERIC(25, 10)
)

CREATE NONCLUSTERED INDEX BOM ON #BOM (CD_COMPANY, CD_PLANT, CD_ITEM_T);

WITH BOM 
(
	CD_COMPANY, 
	CD_PLANT, 
	CD_ITEM_T, 
	CD_ITEM, 
	CD_MATL, 
	LEVEL, 
	PATH, 
	QT
) 
AS
(
	SELECT PB.CD_COMPANY,
		   PB.CD_PLANT,
		   PB.CD_ITEM AS CD_ITEM_T,
		   PB.CD_ITEM, 
		   PB.CD_MATL, 
		   LEVEL = 1, 
		   CONVERT(VARCHAR(1000), PB.CD_MATL),
		   PB1.QT_ITEM_NET
    FROM PR_ROUT_ASN PB
	JOIN PR_BOM PB1 ON PB1.CD_COMPANY = PB.CD_COMPANY AND PB1.CD_PLANT = PB.CD_PLANT AND PB1.CD_ITEM = PB.CD_ITEM AND PB1.CD_MATL = PB.CD_MATL AND PB1.DT_END >= CONVERT(CHAR(8), GETDATE(), 112)
	WHERE PB.CD_COMPANY = @P_CD_COMPANY
	AND PB.CD_PLANT = @P_CD_PLANT 
	AND PB.NO_OPPATH = @P_NO_OPPATH
	AND EXISTS (SELECT 1 
				FROM MA_PITEM MI 
				WHERE MI.CD_COMPANY = PB.CD_COMPANY 
				AND MI.CD_PLANT = PB.CD_PLANT 
				AND MI.CD_ITEM = PB.CD_MATL
				AND MI.TP_PROC = 'M'
				AND ISNULL(MI.YN_USE, 'N') = 'Y')
	AND EXISTS (SELECT 1 
			    FROM SA_SOH SH
				JOIN SA_SOL SL ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
				WHERE SH.CD_COMPANY = @P_CD_COMPANY
				AND SL.CD_ITEM = PB.CD_ITEM
	            AND SL.DT_DUEDATE BETWEEN @P_DT_START AND @P_DT_END
	            AND (ISNULL(@P_NO_SO, '') = '' OR SH.NO_SO = @P_NO_SO)
	            AND (ISNULL(@P_CD_PARTNER, '') = '' OR SH.CD_PARTNER = @P_CD_PARTNER))
	AND EXISTS (SELECT 1 
		        FROM PR_ROUT_L RL
		        WHERE RL.CD_COMPANY = PB.CD_COMPANY
		        AND RL.CD_PLANT = PB.CD_PLANT
		        AND RL.CD_ITEM = PB.CD_ITEM
		        AND RL.NO_OPPATH = PB.NO_OPPATH
		        AND RL.TP_OPPATH = PB.TP_OPPATH
		        AND RL.CD_OP = PB.CD_OP
			    AND RL.YN_USE = 'Y')
	UNION ALL
	SELECT BM.CD_COMPANY,
		   BM.CD_PLANT,
		   BM.CD_ITEM_T,
		   PB.CD_ITEM, 
		   PB.CD_MATL, 
		   LEVEL + 1, 
		   CONVERT(VARCHAR(1000), PATH + ' -> ' + PB.CD_MATL),
		   PB1.QT_ITEM_NET
	FROM BOM BM
	JOIN PR_ROUT_ASN PB ON PB.CD_ITEM = BM.CD_MATL AND PB.NO_OPPATH = @P_NO_OPPATH
	JOIN PR_BOM PB1 ON PB1.CD_COMPANY = PB.CD_COMPANY AND PB1.CD_PLANT = PB.CD_PLANT AND PB1.CD_ITEM = PB.CD_ITEM AND PB1.CD_MATL = PB.CD_MATL AND PB1.DT_END >= CONVERT(CHAR(8), GETDATE(), 112)
	WHERE EXISTS (SELECT 1 
				  FROM MA_PITEM MI 
				  WHERE MI.CD_COMPANY = PB.CD_COMPANY 
				  AND MI.CD_PLANT = PB.CD_PLANT 
				  AND MI.CD_ITEM = PB.CD_MATL
				  AND MI.TP_PROC = 'M'
				  AND ISNULL(MI.YN_USE, 'N') = 'Y')
	AND EXISTS (SELECT 1 
		        FROM PR_ROUT_L RL
		        WHERE RL.CD_COMPANY = PB.CD_COMPANY
		        AND RL.CD_PLANT = PB.CD_PLANT
		        AND RL.CD_ITEM = PB.CD_ITEM
		        AND RL.NO_OPPATH = PB.NO_OPPATH
		        AND RL.TP_OPPATH = PB.TP_OPPATH
		        AND RL.CD_OP = PB.CD_OP
			    AND RL.YN_USE = 'Y')
)
INSERT INTO #BOM
SELECT CD_COMPANY, 
	   CD_PLANT, 
	   CD_ITEM_T, 
	   CD_ITEM, 
	   CD_MATL, 
	   LEVEL, 
	   PATH, 
	   QT 
FROM BOM;

WITH A AS
(
	SELECT SH.CD_COMPANY,
		   SH.NO_SO,
		   SH.NO_PO_PARTNER,
		   SH.DT_SO,
		   SH.CD_PARTNER,
		   SL.SEQ_SO,
		   SL.CD_PLANT,
		   SL.TXT_USERDEF6 AS NO_HULL,
		   SL.CD_USERDEF3 AS TP_ENGINE,
		   SL.DT_DUEDATE,
		   SL.CD_ITEM,
		   SL.CD_ITEM AS CD_MATL,
		   SL.QT_SO,
		   SL.QT_GIR,
		   SL.QT_GI,
		   SL.QT_SO AS QT_BOM,
		   SL.CD_MNGD1 AS DT_EXPECT,
		   0 AS LEVEL
	FROM SA_SOH SH
	JOIN SA_SOL SL ON SL.CD_COMPANY = SH.CD_COMPANY AND SL.NO_SO = SH.NO_SO
	WHERE SH.CD_COMPANY = @P_CD_COMPANY
	AND SL.DT_DUEDATE BETWEEN @P_DT_START AND @P_DT_END
	AND (ISNULL(@P_NO_SO, '') = '' OR SH.NO_SO = @P_NO_SO)
	AND (ISNULL(@P_CD_PARTNER, '') = '' OR SH.CD_PARTNER = @P_CD_PARTNER)
),
B AS
(
	SELECT * 
	FROM A
	UNION ALL
	SELECT A.CD_COMPANY,
		   A.NO_SO,
		   A.NO_PO_PARTNER,
		   A.DT_SO,
		   A.CD_PARTNER,
		   A.SEQ_SO,
		   A.CD_PLANT,
		   A.NO_HULL,
		   A.TP_ENGINE,
		   A.DT_DUEDATE,
		   A.CD_ITEM,
		   BM.CD_MATL,
		   A.QT_SO,
		   A.QT_GIR,
		   A.QT_GI,
		   (A.QT_SO * BM.QT) AS QT_BOM,
		   A.DT_EXPECT,
		   BM.LEVEL
	FROM A
	JOIN #BOM BM ON BM.CD_COMPANY = A.CD_COMPANY AND BM.CD_PLANT = A.CD_PLANT AND BM.CD_ITEM_T = A.CD_ITEM
	WHERE EXISTS (SELECT 1 
				  FROM CZ_PR_SA_SOL_PR_WO_MAPPING SW 
				  WHERE SW.CD_COMPANY = A.CD_COMPANY 
				  AND SW.CD_PLANT = A.CD_PLANT 
				  AND SW.NO_SO = A.NO_SO 
				  AND SW.NO_LINE = A.SEQ_SO 
				  AND SW.CD_ITEM = BM.CD_MATL)
)
SELECT B.NO_SO,
	   B.NO_PO_PARTNER,
	   B.DT_SO,
	   B.CD_PARTNER,
       MP.LN_PARTNER,
	   B.NO_HULL,
	   CD.NM_SYSDEF AS NM_TP_ENGINE,
	   B.SEQ_SO,
	   B.DT_DUEDATE,
	   OL.DT_IO,
	   B.CD_ITEM,
	   MI.NM_ITEM,
	   MI.NO_DESIGN,
	   B.CD_MATL,
	   MI1.NM_ITEM AS NM_MATL,
	   MI1.NO_DESIGN AS NO_DESIGN1,
	   B.QT_SO,
	   B.QT_GIR,
	   B.QT_GI,
	   B.QT_BOM,
	   SW.NO_WO,
	   SW.QT_APPLY,
	   WR.CD_WC,
	   WC.NM_WC,
	   WR.CD_WCOP,
	   OP.NM_OP,
	   WR.CD_EQUIP,
	   EQ.NM_EQUIP,
	   B.LEVEL,
	   WR.YN_FINAL,
	   WR.QT_WO,
	   WR.QT_START,
	   WR.QT_WORK,
	   (WR.QT_START - WR.QT_WORK) AS QT_REMAIN,
	   (CASE WHEN ISNULL(WR.QT_WO, 0) = 0 THEN 0.0 ELSE ROUND(ISNULL(WR.QT_WORK, 0) / ISNULL(WR.QT_WO, 0) * 100, 2) END) AS RT_WORK,
	   B.DT_EXPECT
FROM B
LEFT JOIN MA_PARTNER MP ON MP.CD_COMPANY = B.CD_COMPANY AND MP.CD_PARTNER = B.CD_PARTNER
LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = B.CD_COMPANY AND MI.CD_PLANT = B.CD_PLANT AND MI.CD_ITEM = B.CD_ITEM
LEFT JOIN MA_PITEM MI1 ON MI1.CD_COMPANY = B.CD_COMPANY AND MI1.CD_PLANT = B.CD_PLANT AND MI1.CD_ITEM = B.CD_MATL
LEFT JOIN CZ_PR_SA_SOL_PR_WO_MAPPING SW ON SW.CD_COMPANY = B.CD_COMPANY AND SW.CD_PLANT = B.CD_PLANT AND SW.NO_SO = B.NO_SO AND SW.NO_LINE = B.SEQ_SO AND SW.CD_ITEM = B.CD_MATL
LEFT JOIN PR_WO_ROUT WR ON WR.CD_COMPANY = SW.CD_COMPANY AND WR.NO_WO = SW.NO_WO
LEFT JOIN MA_WC WC ON WC.CD_COMPANY = WR.CD_COMPANY AND WC.CD_PLANT = WR.CD_PLANT AND WC.CD_WC = WR.CD_WC
LEFT JOIN PR_WCOP OP ON OP.CD_COMPANY = WR.CD_COMPANY AND OP.CD_PLANT = WR.CD_PLANT AND OP.CD_WC = WR.CD_WC AND OP.CD_WCOP = WR.CD_WCOP
LEFT JOIN PR_EQUIP EQ ON EQ.CD_COMPANY = WR.CD_COMPANY AND EQ.CD_PLANT = WR.CD_PLANT AND EQ.CD_EQUIP = WR.CD_EQUIP
LEFT JOIN MA_CODEDTL CD ON CD.CD_COMPANY = B.CD_COMPANY AND CD.CD_FIELD = 'CZ_WIN0003' AND CD.CD_SYSDEF = B.TP_ENGINE
LEFT JOIN (SELECT OL.CD_COMPANY, OL.NO_PSO_MGMT, OL.NO_PSOLINE_MGMT,
				  MAX(OH.DT_IO) AS DT_IO
		   FROM MM_QTIOH OH
		   JOIN MM_QTIO OL ON OL.CD_COMPANY = OH.CD_COMPANY AND OL.NO_IO = OH.NO_IO
		   WHERE OL.FG_PS = '2'
		   GROUP BY OL.CD_COMPANY, OL.NO_PSO_MGMT, OL.NO_PSOLINE_MGMT) OL
ON OL.CD_COMPANY = B.CD_COMPANY AND OL.NO_PSO_MGMT = B.NO_SO AND OL.NO_PSOLINE_MGMT = B.SEQ_SO
WHERE (SW.NO_WO IS NULL OR (WR.QT_START > WR.QT_WORK) OR (WR.QT_START > 0 AND WR.QT_START = WR.QT_WORK AND WR.YN_FINAL = 'Y'))
AND (ISNULL(@P_NO_WO, '') = '' OR SW.NO_WO = @P_NO_WO)
AND (@P_CD_WC = '' OR WR.CD_WC IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_WC)))
AND (@P_CD_WCOP = '' OR WR.CD_WCOP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_WCOP)))
AND (@P_CD_EQUIP = '' OR WR.CD_EQUIP IN (SELECT CD_STR FROM GETTABLEFROMSPLIT(@P_CD_EQUIP)))
AND (ISNULL(@P_YN_EXPECT_GI, 'N') = 'N' OR B.QT_SO > B.QT_GI)
AND (ISNULL(@P_CD_ITEM, '') = '' OR B.CD_ITEM = @P_CD_ITEM OR B.CD_MATL = @P_CD_ITEM)
AND (ISNULL(@P_NO_DRAWING, '') = '' OR MI.NO_DESIGN = @P_NO_DRAWING OR MI1.NO_DESIGN = @P_NO_DRAWING)
AND (ISNULL(@P_NM_ITEM, '') = '' OR MI1.NM_ITEM LIKE '%' + @P_NM_ITEM + '%')
AND (ISNULL(@P_YN_MOTHER, 'N') = 'Y' OR B.LEVEL <> 0)
ORDER BY B.DT_DUEDATE, B.NO_SO, B.SEQ_SO, B.LEVEL

GO