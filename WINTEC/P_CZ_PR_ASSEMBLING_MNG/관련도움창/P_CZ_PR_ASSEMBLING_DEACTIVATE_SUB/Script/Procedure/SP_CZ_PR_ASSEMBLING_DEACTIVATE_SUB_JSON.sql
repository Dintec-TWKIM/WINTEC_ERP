USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_PR_ASSEMBLING_DEACTIVATE_SUB_JSON]    Script Date: 2017-01-05 오후 5:48:55 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_PR_ASSEMBLING_DEACTIVATE_SUB_JSON]          
(
	@P_JSON			NVARCHAR(MAX),
	@P_ID_USER		NVARCHAR(10)	
)  
AS
   
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DELETE MD
FROM CZ_PR_ASSEMBLING_DEACTIVATE MD
WHERE EXISTS (SELECT 1 
			  FROM OPENJSON(@P_JSON)
			  WITH
			  (
					CD_COMPANY		NVARCHAR(7),
					CD_PLANT		NVARCHAR(7),
			  		NO_ID			NVARCHAR(20),
			  		JSON_FLAG		NVARCHAR(1)
			  ) JS
			  WHERE JS.JSON_FLAG = 'D'
			  AND JS.CD_COMPANY = MD.CD_COMPANY
			  AND JS.CD_PLANT = MD.CD_PLANT
			  AND JS.NO_ID = MD.NO_ID)

INSERT INTO CZ_PR_ASSEMBLING_DEACTIVATE
(
	CD_COMPANY,
	CD_PLANT,
	NO_ID,
	STA_DEACTIVATE,
	DC_RMK,
	ID_INSERT,
	DTS_INSERT
)
SELECT JS.CD_COMPANY,
	   JS.CD_PLANT,
	   JS.NO_ID,
	   JS.STA_DEACTIVATE,
	   JS.DC_RMK,
	   @P_ID_USER,
	   NEOE.SF_SYSDATE(GETDATE())
FROM OPENJSON(@P_JSON)
WITH
(
	CD_COMPANY		NVARCHAR(7),
	CD_PLANT		NVARCHAR(7),
	NO_ID			NVARCHAR(20),
	STA_DEACTIVATE	NVARCHAR(4),
	DC_RMK			NVARCHAR(500),
	JSON_FLAG		NVARCHAR(1)
) JS
WHERE JS.JSON_FLAG = 'I'

UPDATE MD
SET MD.STA_DEACTIVATE = JS.STA_DEACTIVATE,
	MD.DC_RMK = JS.DC_RMK,
	MD.ID_UPDATE = @P_ID_USER,
	MD.DTS_UPDATE = NEOE.SF_SYSDATE(GETDATE())
FROM CZ_PR_ASSEMBLING_DEACTIVATE MD
JOIN OPENJSON(@P_JSON)
     WITH (CD_COMPANY		NVARCHAR(7),
		   CD_PLANT		    NVARCHAR(7),
		   NO_ID			NVARCHAR(20),
		   STA_DEACTIVATE	NVARCHAR(4),
		   DC_RMK			NVARCHAR(500),
		   JSON_FLAG		NVARCHAR(1)) JS
ON JS.JSON_FLAG = 'U' 
AND JS.CD_COMPANY = MD.CD_COMPANY 
AND JS.CD_PLANT = MD.CD_PLANT 
AND JS.NO_ID = MD.NO_ID

GO