USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_PR_ASSEMBLING_MNG_ID_MATCH]    Script Date: 2017-01-05 오후 5:48:55 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_PR_ASSEMBLING_MNG_ID_MATCH]          
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_NO_SO			NVARCHAR(20),
	@P_CD_ITEM          NVARCHAR(20),
	@P_NO_ID_FROM		NVARCHAR(10),
	@P_NO_ID_TO		    NVARCHAR(10)
)  
AS
   
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT 'N' AS S,
	   PV.NO_WO,
	   PV.NO_LINE,
	   PV.SEQ_WO,
	   PV.NO_ID,
	   PV.[001],
	   PV.[002],
	   PV.[003],
	   PV.[004],
	   PV.[005],
	   MD1.NO_HEAT AS NO_HEAT1,
	   MD1.NO_LOT AS NO_LOT1,
	   MD2.NO_HEAT AS NO_HEAT2,
	   MD2.NO_LOT AS NO_LOT2,
	   MD3.NO_HEAT AS NO_HEAT3,
	   MD3.NO_LOT AS NO_LOT3,
	   MD4.NO_HEAT AS NO_HEAT4,
	   MD4.NO_LOT AS NO_LOT4,
	   MD5.NO_HEAT AS NO_HEAT5,
	   MD5.NO_LOT AS NO_LOT5,
	   PV.CD_ITEM,
	   @P_NO_SO AS NO_SO
FROM (SELECT MD.CD_COMPANY,
			 MD.CD_PLANT,
			 MD.NO_WO,
			 MD.NO_LINE,
			 MD.SEQ_WO,
			 WO.CD_ITEM,
			 MD.NO_ID,
			 MD.TP_POS,
			 MD.NO_ID_C
	  FROM CZ_PR_MATCHING_DATA MD
	  LEFT JOIN PR_WO WO ON WO.CD_COMPANY = MD.CD_COMPANY AND WO.CD_PLANT = MD.CD_PLANT AND WO.NO_WO = MD.NO_WO) MD
PIVOT(MAX(MD.NO_ID_C) FOR MD.TP_POS IN ([001], [002], [003], [004], [005])) AS PV
LEFT JOIN CZ_PR_MATCHING_DATA MD1 ON MD1.CD_COMPANY = PV.CD_COMPANY AND MD1.CD_PLANT = PV.CD_PLANT AND MD1.NO_ID = PV.NO_ID AND MD1.NO_ID_C = PV.[001]
LEFT JOIN CZ_PR_MATCHING_DATA MD2 ON MD2.CD_COMPANY = PV.CD_COMPANY AND MD2.CD_PLANT = PV.CD_PLANT AND MD2.NO_ID = PV.NO_ID AND MD2.NO_ID_C = PV.[002]
LEFT JOIN CZ_PR_MATCHING_DATA MD3 ON MD3.CD_COMPANY = PV.CD_COMPANY AND MD3.CD_PLANT = PV.CD_PLANT AND MD3.NO_ID = PV.NO_ID AND MD3.NO_ID_C = PV.[003]
LEFT JOIN CZ_PR_MATCHING_DATA MD4 ON MD4.CD_COMPANY = PV.CD_COMPANY AND MD4.CD_PLANT = PV.CD_PLANT AND MD4.NO_ID = PV.NO_ID AND MD4.NO_ID_C = PV.[004]
LEFT JOIN CZ_PR_MATCHING_DATA MD5 ON MD5.CD_COMPANY = PV.CD_COMPANY AND MD5.CD_PLANT = PV.CD_PLANT AND MD5.NO_ID = PV.NO_ID AND MD5.NO_ID_C = PV.[005]
WHERE PV.CD_COMPANY = @P_CD_COMPANY
AND PV.CD_ITEM = @P_CD_ITEM 
AND (ISNULL(@P_NO_ID_FROM, '')  = '' OR PV.NO_ID >= ISNULL(@P_NO_ID_FROM, '')) 
AND (ISNULL(@P_NO_ID_TO, '') = '' OR PV.NO_ID <= ISNULL(@P_NO_ID_TO, ''))
AND EXISTS (SELECT 1 
			FROM CZ_PR_WO_INSP WI
			WHERE WI.CD_COMPANY = PV.CD_COMPANY
			AND WI.NO_WO = PV.NO_WO 
			AND WI.NO_LINE = PV.NO_LINE 
			AND WI.SEQ_WO = PV.SEQ_WO
			AND WI.NO_SO = @P_NO_SO
			AND WI.NO_INSP = 997)
AND NOT EXISTS (SELECT 1 
                FROM CZ_PR_ASSEMBLING_DATA MD
                WHERE MD.CD_COMPANY = PV.CD_COMPANY
				AND MD.CD_PLANT = PV.CD_PLANT
                AND MD.NO_ID_C = PV.[001]) -- 등록된 품목 제외
AND NOT EXISTS (SELECT 1 
                FROM CZ_PR_ASSEMBLING_DEACTIVATE MD
                WHERE MD.CD_COMPANY = PV.CD_COMPANY
                AND MD.CD_PLANT = PV.CD_PLANT
                AND MD.NO_ID = PV.NO_ID) -- 대기 품목 제외
AND NOT EXISTS (SELECT 1 
                FROM CZ_PR_ASSEMBLING_SA_SOL SL
                WHERE SL.CD_COMPANY = PV.CD_COMPANY
                AND SL.CD_PLANT = PV.CD_PLANT
                AND SL.NO_ID = PV.NO_ID) -- 단품 판매 품목 제외

GO