USE [NEOE]
GO
/****** Object:  StoredProcedure [NEOE].[SP_CZ_PR_ASSEMBLING_MNG_S1]    Script Date: 2023-04-25 오후 4:49:10 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_PR_ASSEMBLING_MNG_S1]          
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_CD_PLANT			NVARCHAR(7),
    @P_CD_ITEM          NVARCHAR(20),
    @P_NO_SO            NVARCHAR(20)
)  
AS

DECLARE @V_COLUMN_PV NVARCHAR(MAX)
DECLARE @V_COLUMN_PV1 NVARCHAR(MAX)
DECLARE @V_COLUMN_PV2 NVARCHAR(MAX)
DECLARE @V_COLUMN NVARCHAR(MAX)
DECLARE @V_COLUMN1 NVARCHAR(MAX)
DECLARE @V_COLUMN2 NVARCHAR(MAX)
DECLARE @V_QUERY NVARCHAR(MAX)

;WITH tblA (CD_PITEM, QT_ITEM) AS
(
	SELECT AI.CD_PITEM, AI.QT_ITEM
    FROM CZ_PR_ASSEMBLING_ITEM AI
    WHERE AI.CD_COMPANY = @P_CD_COMPANY
    AND AI.CD_PLANT = @P_CD_PLANT
    AND AI.CD_ITEM = @P_CD_ITEM
)
, tblB (CD_PITEM, QT_ITEM, SEQ) AS
(
	SELECT 
		A.CD_PITEM
	,	A.QT_ITEM
	,	1
	FROM tblA A
	UNION ALL
	SELECT
		A.CD_PITEM
	,	A.QT_ITEM
	,	A.SEQ + 1
	FROM tblB A
	WHERE A.SEQ + 1 <= A.QT_ITEM
)
, A AS
(
	SELECT
		A.CD_PITEM + '_' + CONVERT(NVARCHAR, A.SEQ) AS CD_PITEM
	,	A.SEQ
	FROM tblB A
)
, B AS
(
	SELECT '[' + A.CD_PITEM + ']' AS CD_PITEM,
           '[' + A.CD_PITEM + '-1]' AS CD_PITEM1,
           '[' + A.CD_PITEM + '-2]' AS CD_PITEM2,
           'MAX([' + A.CD_PITEM + ']) AS [' + A.CD_PITEM + ']' AS CD_PITEM_MAX,
           'MAX([' + A.CD_PITEM + '-1]) AS [' + A.CD_PITEM + '-1]' AS CD_PITEM1_MAX,
           'MAX([' + A.CD_PITEM + '-2]) AS [' + A.CD_PITEM + '-2]' AS CD_PITEM2_MAX
	FROM A
)
SELECT @V_COLUMN = STRING_AGG(B.CD_PITEM, ','),
       @V_COLUMN1 = STRING_AGG(B.CD_PITEM1, ','),
       @V_COLUMN2 = STRING_AGG(B.CD_PITEM2, ','),
       @V_COLUMN_PV = STRING_AGG(B.CD_PITEM_MAX, ','),
       @V_COLUMN_PV1 = STRING_AGG(B.CD_PITEM1_MAX, ','),
       @V_COLUMN_PV2 = STRING_AGG(B.CD_PITEM2_MAX, ',')
FROM B

SET @V_QUERY = ';WITH A AS
(
    SELECT WO.CD_COMPANY,
		   WO.NO_WO,
		   AD.SEQ_WO,
		   AD.NO_ID,
		   WO.CD_ITEM,
		   AD.CD_PITEM +' + '''_''' + '+ CONVERT(NVARCHAR, AD.SEQ_ITEM) AS CD_PITEM,
           (AD.CD_PITEM +' + '''_''' + '+ CONVERT(NVARCHAR, AD.SEQ_ITEM) + ''-1'') AS CD_PITEM1,
           (AD.CD_PITEM +' + '''_''' + '+ CONVERT(NVARCHAR, AD.SEQ_ITEM) + ''-2'') AS CD_PITEM2,
		   AD.NO_ID_C,
           AD.NO_LOT,
           AD.NO_HEAT 
    FROM CZ_PR_ASSEMBLING_DATA AD
    LEFT JOIN PR_WO WO ON WO.CD_COMPANY = AD.CD_COMPANY AND WO.CD_PLANT = AD.CD_PLANT AND WO.NO_WO = AD.NO_WO
    WHERE AD.CD_COMPANY = ''' + @P_CD_COMPANY + '''
    AND AD.CD_PLANT = ''' + @P_CD_PLANT + '''
    AND WO.CD_ITEM = ''' + @P_CD_ITEM + '''
)
SELECT PV2.NO_WO,' +
	  'PV2.SEQ_WO,
	   PV2.NO_ID,
	   PV2.CD_ITEM,
	   MAX(WI.NO_SO) AS NO_SO,
       MAX(WI.TXT_OPENING) AS TXT_OPENING,
       MAX(WI.TXT_VENTING) AS TXT_VENTING,
       MAX(WI.TXT_SHIM) AS TXT_SHIM,
	   MAX(WI.DC_RMK) AS DC_RMK,'
	   + @V_COLUMN_PV + ','
       + @V_COLUMN_PV1 + ','
       + @V_COLUMN_PV2 +
'FROM A
 PIVOT(MAX(A.NO_ID_C) FOR A.CD_PITEM IN (' + @V_COLUMN + ')) AS PV
 PIVOT(MAX(PV.NO_LOT) FOR PV.CD_PITEM1 IN (' + @V_COLUMN1 + ')) AS PV1
 PIVOT(MAX(PV1.NO_HEAT) FOR PV1.CD_PITEM2 IN (' + @V_COLUMN2 + ')) AS PV2
 LEFT JOIN CZ_PR_WO_INSP WI ON WI.CD_COMPANY = PV2.CD_COMPANY AND WI.NO_WO = PV2.NO_WO AND WI.SEQ_WO = PV2.SEQ_WO AND WI.NO_INSP = 996
 WHERE (ISNULL(''' + @P_NO_SO + ''', '''') = '''' OR WI.NO_SO = ''' + @P_NO_SO + ''')
 GROUP BY PV2.NO_WO, PV2.SEQ_WO, PV2.NO_ID, PV2.CD_ITEM'

PRINT @V_QUERY

EXEC SP_EXECUTESQL @V_QUERY

