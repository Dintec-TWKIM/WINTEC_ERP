USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_PR_WO_SA_SOL_CHANGE_S2]    Script Date: 2021-03-02 오전 10:51:36 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [NEOE].[SP_CZ_PR_WO_SA_SOL_CHANGE_S2]
(
	@P_CD_COMPANY		NVARCHAR(7),
	@P_NO_SO			NVARCHAR(20),
	@P_SEQ_SO			NUMERIC(5, 0),
    @P_CD_MATL			NVARCHAR(20),
	@P_YN_EXPECT_GIR	NVARCHAR(1),
	@P_YN_EXPECT_GI		NVARCHAR(1),
	@P_NO_WO			NVARCHAR(20)
) 
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

SELECT @P_NO_SO AS NO_SO,
	   @P_SEQ_SO AS SEQ_SO,
       WO.NO_WO,
	   WO.CD_ITEM,
	   MI.NM_ITEM,
	   WO.QT_ITEM,
	   WR.CD_WC,
	   WC.NM_WC,
	   WR.CD_WCOP,
	   OP.NM_OP,
	   WR.QT_START,
	   WR.QT_WORK,
	   WR.QT_REJECT,
	   WM.QT_APPLY,
	   WM.DT_DUEDATE,
	   WO.NUM_USERDEF1 AS NUM_ORDER
FROM PR_WO WO
LEFT JOIN (SELECT WM.CD_COMPANY, WM.NO_WO,
				  SUM(WM.QT_APPLY) AS QT_APPLY,
				  MAX(SL.DT_DUEDATE) AS DT_DUEDATE,
				  SUM(SL.QT_SO) AS QT_SO,
				  SUM(SL.QT_GIR) AS QT_GIR,
				  SUM(SL.QT_GI) AS QT_GI
		   FROM CZ_PR_SA_SOL_PR_WO_MAPPING WM
		   LEFT JOIN SA_SOL SL ON SL.CD_COMPANY = WM.CD_COMPANY AND SL.NO_SO = WM.NO_SO AND SL.SEQ_SO = WM.NO_LINE 
		   GROUP BY WM.CD_COMPANY, WM.NO_WO) WM
ON WM.CD_COMPANY = WO.CD_COMPANY AND WM.NO_WO = WO.NO_WO
LEFT JOIN (SELECT WR.CD_COMPANY, WR.NO_WO,
				  WR.CD_WC,
				  WR.CD_WCOP,
				  WR.QT_START,
				  WR.QT_WORK,
				  SUM(WR.QT_REJECT) OVER (PARTITION BY WR.NO_WO ORDER BY WR.NO_LINE) AS QT_REJECT,
				  ROW_NUMBER() OVER (PARTITION BY WR.NO_WO ORDER BY WR.NO_LINE DESC) AS IDX
		   FROM PR_WO_ROUT WR
		   WHERE WR.QT_START > 0) WR
ON WR.CD_COMPANY = WO.CD_COMPANY AND WR.NO_WO = WO.NO_WO AND WR.IDX = 1
LEFT JOIN MA_WC WC ON WC.CD_COMPANY = WR.CD_COMPANY AND WC.CD_WC = WR.CD_WC
LEFT JOIN PR_WCOP OP ON OP.CD_COMPANY = WO.CD_COMPANY AND OP.CD_PLANT = WO.CD_PLANT AND OP.CD_WC = WR.CD_WC AND OP.CD_WCOP = WR.CD_WCOP
LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = WO.CD_COMPANY AND MI.CD_PLANT = WO.CD_PLANT AND MI.CD_ITEM = WO.CD_ITEM
WHERE WO.CD_COMPANY = @P_CD_COMPANY
AND (ISNULL(@P_YN_EXPECT_GIR, 'N') = 'N' OR (ISNULL(WO.QT_ITEM, 0) - ISNULL(WR.QT_REJECT, 0)) > ISNULL(WM.QT_APPLY, 0) OR ISNULL(WM.QT_SO, 0) > ISNULL(WM.QT_GIR, 0))
AND (ISNULL(@P_YN_EXPECT_GI, 'N') = 'N' OR (ISNULL(WO.QT_ITEM, 0) - ISNULL(WR.QT_REJECT, 0)) > ISNULL(WM.QT_APPLY, 0) OR ISNULL(WM.QT_SO, 0) > ISNULL(WM.QT_GI, 0))
AND WO.CD_ITEM = @P_CD_MATL
AND (ISNULL(@P_NO_WO, '') = '' OR WO.NO_WO = @P_NO_WO)
ORDER BY WM.DT_DUEDATE DESC

GO