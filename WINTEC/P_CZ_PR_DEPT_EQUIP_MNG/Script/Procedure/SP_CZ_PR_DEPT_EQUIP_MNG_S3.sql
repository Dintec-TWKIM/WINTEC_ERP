USE [NEOE]
GO

/****** Object:  StoredProcedure [NEOE].[SP_CZ_PR_DEPT_EQUIP_MNG_S3]    Script Date: 2019-03-26 오전 11:26:39 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [NEOE].[SP_CZ_PR_DEPT_EQUIP_MNG_S3]
(
    @P_CD_COMPANY   NVARCHAR(7),
	@P_CD_DEPT		NVARCHAR(12)
)
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

WITH A AS
(
	SELECT EQ.CD_COMPANY,
		   EQ.CD_EQUIP,
		   EQ.NM_EQUIP
	FROM PR_EQUIP EQ
	LEFT JOIN MA_CODEDTL MC ON MC.CD_COMPANY = EQ.CD_COMPANY AND MC.CD_FIELD = 'PR_E000003' AND MC.CD_SYSDEF = EQ.CD_EQIP_GRP
	WHERE EQ.CD_COMPANY = @P_CD_COMPANY
	AND MC.CD_FLAG1 = @P_CD_DEPT
),
B AS
(
	SELECT WO.CD_COMPANY,
		   WR.CD_EQUIP,
		   ROW_NUMBER() OVER (PARTITION BY WR.CD_EQUIP ORDER BY ISNULL(NULLIF(WR.CD_USERDEF1, ''), '9999') ASC, WO.DT_REL) AS IDX_WORK,
		   WO.NO_WO,
		   WO.DT_REL,
	       WO.CD_ITEM,
	       MI.NM_ITEM,
	       MI.NO_DESIGN,
		   MI.STND_ITEM,
		   WR.CD_OP,
	       OP.NM_OP,
		   WO.QT_ITEM,
		   WR.QT_WORK,
		   WR.QT_WIP
	FROM PR_WO WO
	JOIN PR_WO_ROUT WR ON WR.CD_COMPANY = WO.CD_COMPANY AND WR.NO_WO = WO.NO_WO
	JOIN PR_WCOP OP ON OP.CD_COMPANY = WR.CD_COMPANY AND OP.CD_PLANT = WR.CD_PLANT AND OP.CD_WC = WR.CD_WC AND OP.CD_WCOP = WR.CD_WCOP
	LEFT JOIN MA_PITEM MI ON MI.CD_COMPANY = WO.CD_COMPANY AND MI.CD_PLANT = WO.CD_PLANT AND MI.CD_ITEM = WO.CD_ITEM
	WHERE WO.CD_COMPANY = @P_CD_COMPANY
	AND WR.QT_WIP > 0
	AND WR.CD_OP <> 'C'
)
SELECT A.CD_EQUIP,
	   A.NM_EQUIP,

	   B.NO_WO,
	   B.CD_ITEM,
	   B.NM_ITEM,
	   B.NO_DESIGN,
	   B.STND_ITEM,
	   B.NM_OP,
	   B.QT_ITEM,
	   B.QT_WIP,
	   B.QT_WORK,
	   (CASE WHEN B.QT_WORK > 0 THEN '진행중' 
		     WHEN B.NO_WO IS NOT NULL THEN '셋팅중' END) AS NM_STATE,
	   ME.NM_KOR AS NM_EMP,
	   
	   B1.NO_WO AS NO_WO_1,
	   B1.DT_REL AS DT_REL_1,
	   B1.CD_ITEM AS CD_ITEM_1,
	   B1.NM_ITEM AS NM_ITEM_1,
	   B1.NO_DESIGN AS NO_DESIGN_1,
	   B1.STND_ITEM AS STND_ITEM_1,
	   B1.NM_OP AS NM_OP_1,
	   B1.QT_WIP AS QT_WIP_1,
	   B1.QT_ITEM AS QT_ITEM_1,
	   
	   B2.NO_WO AS NO_WO_2,
	   B2.DT_REL AS DT_REL_2,
	   B2.CD_ITEM AS CD_ITEM_2,
	   B2.NM_ITEM AS NM_ITEM_2,
	   B2.NO_DESIGN AS NO_DESIGN_2,
	   B2.STND_ITEM AS STND_ITEM_2,
	   B2.NM_OP AS NM_OP_2,
	   B2.QT_WIP AS QT_WIP_2,
	   B2.QT_ITEM AS QT_ITEM_2,

	   B3.NO_WO AS NO_WO_3,
	   B3.DT_REL AS DT_REL_3,
	   B3.CD_ITEM AS CD_ITEM_3,
	   B3.NM_ITEM AS NM_ITEM_3,
	   B3.NO_DESIGN AS NO_DESIGN_3,
	   B3.STND_ITEM AS STND_ITEM_3,
	   B3.NM_OP AS NM_OP_3,
	   B3.QT_WIP AS QT_WIP_3,
	   B3.QT_ITEM AS QT_ITEM_3
FROM A
LEFT JOIN B ON B.CD_COMPANY = A.CD_COMPANY AND B.CD_EQUIP = A.CD_EQUIP AND B.IDX_WORK = 1
LEFT JOIN B B1 ON B1.CD_COMPANY = A.CD_COMPANY AND B1.CD_EQUIP = A.CD_EQUIP AND B1.IDX_WORK = 2
LEFT JOIN B B2 ON B2.CD_COMPANY = A.CD_COMPANY AND B2.CD_EQUIP = A.CD_EQUIP AND B2.IDX_WORK = 3
LEFT JOIN B B3 ON B3.CD_COMPANY = A.CD_COMPANY AND B3.CD_EQUIP = A.CD_EQUIP AND B3.IDX_WORK = 4
LEFT JOIN (SELECT WK.CD_COMPANY, 
		   	      WK.NO_WO,
		          WK.CD_OP,
		   	      WK.NO_EMP,
		   	      ROW_NUMBER() OVER (PARTITION BY WK.CD_COMPANY, WK.NO_WO, WK.CD_OP ORDER BY WK.DT_WORK DESC) AS IDX
		   FROM PR_WORK WK) WK 
ON WK.CD_COMPANY = A.CD_COMPANY AND WK.NO_WO = B.NO_WO AND WK.CD_OP = B.CD_OP AND WK.IDX = 1
LEFT JOIN MA_EMP ME ON ME.CD_COMPANY = WK.CD_COMPANY AND ME.NO_EMP = WK.NO_EMP
ORDER BY A.CD_EQUIP

GO

